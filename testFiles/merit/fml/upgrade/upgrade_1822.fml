:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_1822.fml
:: Utworzony: 05.01.2018
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 18.02 na 18.22
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - realizacja zakwalifikowana jako wykonana
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Główna formuła upgrade-ów 18.22
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('color',,,'ZWS',18);
__UPG.add_task('FIRMA',,,'ZWS');
__UPG.add_task('upgrade_act',,,'ZPR',18);
__UPG.add_task('funpar',,,'ZWS',3);
__UPG.add_task('stalesys',,,'ZWS',3);
__UPG.add_task('DOKUM',,,'ZWS',3);
__UPG.add_task('B_KEYREF',,,'ZPR');
__UPG.add_task('rubatr',,,'PPL',8);
__UPG.add_task('KH_OSOB',,,'ZWS');
__UPG.add_task('CDTAB',,,'ZWS');
__UPG.add_task('za_nota',,,'PBA');
__UPG.add_task('del140',,,'ZWS');
__UPG.add_task('MATERIAL',,,'ZWS');
__UPG.add_task('areatitle',,,'ZWS',7);
__UPG.add_task('eancAKT',,,'LMG');
__UPG.add_task('stawkaDZ',,,'PPL',3);
__UPG.add_task('cit25',,,'FKS');
__UPG.add_task('sp',,,'FKS');
__UPG.add_task('KH_DOD_SP',,,'ZWS');
__UPG.add_task('RBK_VAT',,,'ZWS');
__UPG.add_task('matkan',,,'ZWS',3);
__UPG.add_task('N_NDOK_do_CU',,,'PKD',1);
__UPG.add_task('rubryka7079',,,'PPL');
__UPG.add_task('umowy_zc',,,'PPL',1);
:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('actualMG',,'N','ZWS',,,,1);
__UPG.add_task('TOPER',,'N','TTE',,,,1);
__UPG.add_task('FO_DEF',,'N','ZWS',2);
__UPG.add_task('FO_USR',,'N','ZWS',2);
__UPG.add_task('UAT',,'N','TTE',,,,1);
__UPG.add_task('StanPROD',,'N','LMG',3,,,1);
__UPG.add_task('faks_kh_kraj',,'N','LSP',,,,1);
__UPG.add_task('PB_SP',,'N','HBN',,,,1);
__UPG.add_task('PW_SP',,'N','HBN',,,,1);
__UPG.add_task('RD_OKR_zalacz',,'N','PKD',,,,1);
__UPG.add_task('DATY',,'N','ZWS',,,,1);
__UPG.add_task('rola_rod',,'N','ZWS',,,,1);
__UPG.add_task('rap_zew',,'N','PKD',,,,1);
__UPG.add_task('pb_rhp',,,'HBN',,,,1);
__UPG.add_task('erase_pb',,,'HBN',,,,1);
__UPG.add_task('RD_slowniki',,'N','PKD',,,,1);
__UPG.add_task('listopad_12','N','N','PRC',,,,1,'T');
~~


\FIRMA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli FIRMA
::----------------------------------------------------------------------------------------------------------------------
_result:=FIRMA.for_each("{? FIRMA.A='' || FIRMA.A:='T'; FIRMA.put() ?}",1);
{? _result
|| __UPG.msg('Zaktualizowano pole określające aktywność firmy.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji kartoteki firm.')
?};
_result


\FIRMA_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli FIRMA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli firm.'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',0)<>0
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.');
   1
?}


\stalesys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Aktualizacja definicji stałych systemu.
::----------------------------------------------------------------------------------------------------------------------
_licz:=
   (exec('add_acr','#stalesys','AIP_PASS',,'N','N','T','XINFO','ROD')<>0)+
   (exec('add_acr','#stalesys','AIP_SYS',,'N','N','T','XINFO','ROD')<>0)+
   (exec('add_acr','#stalesys','AIP_URL',,'N','N','T','XINFO','ROD')<>0)+
   (exec('add_acr','#stalesys','AIP_USR',,'N','N','T','XINFO','ROD')<>0)+
   (exec('add_acr','#stalesys','EM_IDO',,'N','N','T','XINFO','ROD')<>0)+
   (exec('add_acr','#stalesys','NA_IDO',,'N','N','T','XINFO','ROD')<>0)+
   (exec('add_acr','#stalesys','IM_IDO',,'N','N','T','XINFO','ROD')<>0);

__UPG.msg('Zweryfikowano definicje stałych. Liczba modyfikacji: %1'[$_licz]);

1


\stalesys_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Aktualizacja definicji stałych systemu - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji stałych systemu'


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\actualMG
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizuje znacznik MG.IL
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
MG.cntx_psh();
MG.prefix();
_res:=MG.for_each("
   _put:=0;
   __lrec+=1;
   {? MG.IL='' || _put:=1; MG.IL:='N' ?};
   {? _put || {? MG.put() || __lmod+=1 ?} ?}
");
MG.cntx_pop();
__UPG.msg('Zaktualizowano znacznik Magazyn ilościowy dla definicji magazynu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\actualMG_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizuje znacznik MG.IL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika Magazyn ilościowy dla definicji magazynu.'


\FO_DEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizuje pola w tabeli FO_DEF
::----------------------------------------------------------------------------------------------------------------------
FO_DEF.cntx_psh();
FO_DEF.index('TAB');
FO_DEF.prefix();
_can_continue:=1;
_result:=0;
{? FO_DEF.first()
|| {!
   |? {? FO_DEF.WDR=''
      ||
         _put:=1;
         FO_DEF.cntx_psh();
         FO_DEF.index('NR');
         FO_DEF.prefix(FO_DEF.KIND,'N',FO_DEF.NR);
         {? FO_DEF.first()
         || _put:=0
         ?};
         FO_DEF.cntx_pop();

         {? _put>0
         || FO_DEF.WDR:='N';
            _can_continue:=FO_DEF.put()
         ?}
      ?};
      FO_DEF.next() & _can_continue>0
   !}
?};
{? _can_continue>0
|| _result:=1;
   __UPG.msg('Zaktualizowano uniwersalne parametry FO (aplikacyjne).')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji uniwersalnych parametrów FO (aplikacyjnych).')
?};
FO_DEF.cntx_pop();
_result


\FO_DEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizuje pola w tabeli FO_DEF - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja mechanizmu uniwersalnych parametrów FO (parametry aplikacyjne)'


\FO_USR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizuje pola w tabeli FO_USR
::----------------------------------------------------------------------------------------------------------------------
FO_USR.cntx_psh();
FO_USR.index('TAB');
FO_USR.prefix();
_can_continue:=1;
_result:=0;
{? FO_USR.first()
|| {!
   |? {? FO_USR.WDR=''
      ||
         _put:=1;
         FO_USR.cntx_psh();
         FO_USR.index('USERS');
         FO_USR.prefix(FO_USR.USERS,'N',FO_USR.NR);
         {? FO_USR.first()
         || _put:=0
         ?};
         FO_USR.cntx_pop();

         {? _put>0
         || FO_USR.WDR:='N';
            _can_continue:=FO_USR.put()
         ?}
      ?};
      FO_USR.next() & _can_continue>0
   !}
?};
{? _can_continue>0
|| _result:=1;
   __UPG.msg('Zaktualizowano uniwersalne parametry FO (użytkownika).')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji uniwersalnych parametrów FO (użytkownika).')
?};
FO_USR.cntx_pop();
_result


\FO_USR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizuje pola w tabeli FO_USR - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja mechanizmu uniwersalnych parametrów FO (parametry użytkownika)'


\TOPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Aktualizacja operacji Tpz
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_names:=TOPER.names();

TOPER.cntx_psh();
{? _names.first()
|| {!
   |? TOPER.index('TPZ');
      TOPER.prefix('T');
      {? TOPER.first()
      || {!
         |? TOPER.cntx_psh();
            TOPER.index('UNROP');
            TOPER.prefix(TOPER.TPZREF);
            {? TOPER.first()
            || _act:=TOPER.ACT;
               _od:=TOPER.OD;
               _cause:=TOPER.CAUSE;
               _user:=TOPER.USER;
               _mod_time:=TOPER.MOD_TIME;
               TOPER.cntx_pop();
               _put:=0;
               __lrec+=1;
               {? TOPER.ACT<>_act || TOPER.ACT:=_act; _put:=1 ?};
               {? TOPER.OD<>_od || TOPER.OD:=_od; _put:=1 ?};
               {? TOPER.CAUSE<>_cause || TOPER.CAUSE:=_cause; _put:=1 ?};
               {? TOPER.USER<>_user || TOPER.USER:=_user; _put:=1 ?};
               {? TOPER.MOD_TIME<>_mod_time || TOPER.MOD_TIME:=_mod_time; _put:=1 ?};
               {? _put || {? TOPER.put() || __lmod+=1 || _res:=0 ?} ?}
            || TOPER.cntx_pop()
            ?};
            TOPER.next()
         !}
      ?};
      _names.next()
   !}
?};

TOPER.cntx_pop();

__UPG.msg('Zaktualizowano operacje Tpz.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\TOPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Aktualizacja operacji Tpz - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja operacji Tpz'


\DOKUM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   _put:=0;
   {? DOKUM.RECEIVED='' || DOKUM.RECEIVED:='N'; _put:=1 ?};
   {? DOKUM.BSEND='' || DOKUM.BSEND:='N'; _put:=1 ?};
   {? DOKUM.STAT_REJ='' || DOKUM.STAT_REJ:='N'; _put:=1 ?};
   {? _put || DOKUM.put() ?}
";
_result:=DOKUM.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w kartotece dokumentów.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji kartoteki dokumentów.')
?};
_result


\DOKUM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli DOKUM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli dokumentów.'


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.PKD.PKW.PPL.TTE.WYP.ZWS.FST.FKS.KON.OBE.OBG','.');
_dom:=spli_str('ZWS.ZPR.LMG.PPL.PKD.ROD.HBN','.');
_len:=obj_len(_dom);
FUN.prg_start(_len+2,'Aktualizacja czynności.'@,,,1);

:: Uzupełnienie domen (zawsze)
FUN.prg_next();
exec('add_domain','#b_action');
__UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
FUN.prg_next();
exec('fill_tab','#b_type',0);
__UPG.msg('Zaktualizowano typy danych.');

{! _i.._len
|! FUN.prg_next();
   _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
   {? _domain<>null()
   || exec('import_needed_dom','#b_design',_domain,0);
      __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
   ?}
!};
FUN.prg_stop();
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\UAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.22]
:: OPIS: Aktualizacja atrybutów technologii
::----------------------------------------------------------------------------------------------------------------------
_result:=UAT.for_each("{? UAT.ACT='' || UAT.ACT:='T'; UAT.put() ?}",1);
{? _result
|| __UPG.msg('Zaktualizowano pole określające aktywność atrybutu technologii.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji atrybutów technologii.')
?};
_result


\UAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.22]
:: OPIS: Aktualizacja atrybutów technologii - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja atrybutów technologii'


\B_KEYREF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizacja rekordów kluczowych
::----------------------------------------------------------------------------------------------------------------------
B_KEYREF.cntx_psh();
B_KEYREF.index('UID_REF');

_result:=0;
_can_continue:=1;

B_KEYREF.prefix();
{? B_KEYREF.first()
|| {!
   |?
::    Uwaga, cała funkcja sprowadza się do przeputowania wszystkich rekordów
::    bo nawijanie brakujących pól jest w triggerze
      _can_continue:=B_KEYREF.put();
      B_KEYREF.next() & _can_continue>0
   !}
?};
B_KEYREF.cntx_pop();
{? _can_continue>0
|| _result:=1;
   __UPG.msg('Zaktualizowano rekordy kluczowe.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji rekordów kluczowych.')
?};
_result


\B_KEYREF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizacja rekordów kluczowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rekordów kluczowych'


\StanPROD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizacja stanów w produkcji
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_buf:=tab_tmp(2,'MAT','STRING[16]','','MAG','STRING[16]','');
_stoddz:=ST.ODDZ_KOD;
_stodd2:=ST.ODDZ;
ODDZ.cntx_psh();
SM.cntx_psh();
ZL.cntx_psh();
ODDZ.index('KOD');
{? ODDZ.first()
|| {!
   |? _lrec:=0;
      {? form(ODDZ.KOD)<>''
      || ST.ODDZ_KOD:=ODDZ.KOD;
         ST.ODDZ:=ODDZ.KOD;
         SM.use('stm__'+ODDZ.KOD+'zb');
         SM.index('SM');
         SM.prefix();
         ZL.index('SMPROD');
         ZL.prefix('O',ODDZ.KOD,'Z','P','Z');
         {? ZL.first()
         || {!
            |? {? ZL.ILPDOK>0 & ZL.KTM<>null() & ZL.MG<>null() & (_buf.clear(); ~_buf.find_key($ZL.KTM,$ZL.MG))
               || _lrec+=1;
                  _buf.blank();
                  _buf.MAT:=$ZL.KTM;
                  _buf.MAG:=$ZL.MG;
                  _buf.add(1);
                  exec('obl_stan','magazyn_stan',ZL.KTM,1,ZL.MG)
               ?};
               ZL.next()
            !}
         ?};
         ZL.prefix('O',ODDZ.KOD,'Z','P','P');
         {? ZL.first()
         || {!
            |? {? ZL.ILPDOK>0 & ZL.KTM<>null() & ZL.MG<>null() & (_buf.clear(); ~_buf.find_key($ZL.KTM,$ZL.MG))
               || _lrec+=1;
                  _buf.blank();
                  _buf.MAT:=$ZL.KTM;
                  _buf.MAG:=$ZL.MG;
                  _buf.add(1);
                  exec('obl_stan','magazyn_stan',ZL.KTM,1,ZL.MG)
               ?};
               ZL.next()
            !}
         ?};
         __UPG.msg('Zaktualizowano Stan w produkcji dla magazynów oddziału %1.'@[ODDZ.KOD]);
         __UPG.msg('Przetworzono: %1 zapisów.'[$_lrec])
      ?};
      _res & ODDZ.next()
   !}
?};
ODDZ.cntx_pop();
SM.cntx_pop();
ZL.cntx_pop();
obj_del(_buf);
ST.ODDZ_KOD:=_stoddz;
ST.ODDZ:=_stodd2;
_res


\StanPROD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizacja stanów w produkcji - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola Stan w produkcji dla stanów magazynowych'


\rubatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [18.22]
:: OPIS: Uzupełnienie atrybutów rubryk.
::----------------------------------------------------------------------------------------------------------------------
:: Poprawka SPIS ER/WRT/XP/12.41/1610/0096 - błędne wyliczenie kwot dla składników okreswowych
exec('add_attr','rubatr',6522,65221,'Składniki wynagrodzenia za okresy miesięczne',1,
   'Pozycja 11 zaświadczenia.',,
   135,175,177,436,437,438,439,442,443,444,7031
);
exec('add_attr','rubatr',6522,65222,'Składniki wynagrodzenia za inne okresy',1,
   'Pozycja 14 zaświadczenia.',,
   7006,7041,7051,7053,7055,7059
);
:: Poprawka SPIS ER/WRT/XP/12.51/1710/0043 - Wynagrodzenie za urlop a ZUS Z3
exec('add_attr','rubatr',65221,652211,'Składniki wynagrodzenia miesięczne - urlop',1,
   'Pozycja 11 zaświadczenia.',,
   436,437,438,439,442,443);
__UPG.msg('Dodano atrybuty sprawozdania ZUS-Z3');
:: Poprawka SPIS ER/WRT/XP/12.51/1802/0004 - Składnik wyrównania do wynagrodzenia minimalnego, nie jest uwzględniany
::  w podstawie wynagrodzenia chorobowego
__UPG.msg(
   {? +exec('import','rubryki','g','499',1,0)
   || 'Poprawiono rubrykę składnika płacowego o nr 499.'
   || 'Nie poprawiono rubryki składnika płacowego o nr 499.'
   ?}
);
:: Poprawka SPIS ER/WRT/XP/12.51/1705/0002 - Brak informacji o ćwiczeniach wojskowych na świadectwie pracy
{? +exec('import','rubryki','g','7068',1,0)
|| __UPG.msg('Nie dodano rubryki 7068 - Ćwiczenia wojskowe i atrybutów z nią związanych.')
|| exec('add_attr','rubatr',1323,13231,'Służba wojskowa',1,'');
   exec('add_use','rubatr',13231,15);
   exec('add_attr','rubatr',1323,13232,'Ćwiczenia wojskowe',1,'Urlop bezpłatny udzielany na okres ćwiczeń wojskowych');
   exec('add_uses','rubatr',7068,1,11,13,15,132,134,152,1323,13232);
   exec('add_uses','rubatr',7068,14,14151);
   __UPG.msg('Dodano rubrykę 7068 - Ćwiczenia wojskowe oraz atrybuty z nią związane.')
?};
:: Poprawka SPIS ER/WRT/XP/12.51/1801/0014
exec('add_attr','rubatr',211,2116,'W całości potrącany na roszczenia niealimentacyjne',1,
      'Rubryki stanowiące w całości podstawę do wszystkich potrąceń oprócz alimentacyjnych');
__UPG.msg('Dodano atrybut 2116 - W całości potrącany na roszczenia niealimentacyjne.');

:: Aktualizacja - Ograniczenie handlu w niedzielę
{? +exec('import','rubryki','g','7034,7061',1,0)
|| __UPG.msg('Nie dodano rubryk 7034 i 7061.')
|| exec('add_uses','rubatr',7034,1,13,16,161,131,19,194,199,1942,19421);
   exec('add_attr','rubatr',131,1313,'Dopełnienie do wymiaru - zakaz handlu',1,
      'Na podstawie Art. 9. 1. ustawy z dnia 10 stycznia 2018 r. o ograniczeniu handlu w niedziele i święta '
      'dotyczącym zmniejszenia wymiaru czasu pracy w wigilię i wiel. sobotę.'
   );
   exec('add_use','rubatr',1313,7034);
   exec('add_uses','rubatr',7061,4,48,6,65,652,100,1002,6522,10023,65221,100231);
   __UPG.msg('Dodano rubryki rubryk 7034 i 7061.')
?};
:: Aktualizacja - dodanie możliwości rejestracji podstawy FP w innych firmach zlecone przez firma Banach
{? +exec('import','rubryki','g','7069',1,0)
|| __UPG.msg('Nie dodano rubryki 7069 - Podst. FP inna fir.')
|| __UPG.msg('Dodano rubrykę 7069 - Podst. FP inna fir.')
?};

::Poprawka SPIS ER/WRT/XP/12.51/1803/0004
exec('__RUB','object');
R.cntx_psh();
R.clear();
__RUB.fill();
exec('add_attr','rubatr',4,401,'Rozliczenie godzin pracy',1,'');
exec('add_use','rubatr',401,123,7006,7050,7051,7040,7041,7052,7053,7058,7059,7054,7055,125,126,135,120,121);
exec('add_attr','rubatr',401,4011,'Nie wliczane do minimalnego wynagrodzenia',1,
   'Wynagrodzenia i dodatki za przepracowane nadgodziny, które nie są doliczane przy wyliczeniu '
   'dopełnienia do minimalnego wynagrodzenia');
exec('add_use','rubatr',4011,123,7006,7050,7051,7040,7041,7052,7053,7058,7059,7054,7055,125,126,135,120,121);
RA_DEF.cntx_psh();
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
{? RA_DEF.find_key(4011)
|| exec('use_add','rubatr',RA_DEF.ref(),__RUB.ref(122),date(2016,1,1));
   __UPG.msg('Dodano atrybut 4011 - Rozliczenie godzin pracy.')
|| __UPG.msg('Nie dodano atrybutu 4011 - Rozliczenie godzin pracy.')
?};
RA_DEF.cntx_pop();
__RUB.fill();
R.cntx_pop();
1


\rubatr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [18.22]
:: OPIS: Uzupełnienie atrybutów rubryk - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie atrybutów rubryk'


\KH_OSOB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli KH_OSOB
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   _put:=0;
   {? KH_OSOB.BL='' || KH_OSOB.BL:='N'; _put:=1 ?};
   {? KH_OSOB.BL_RESET='' || KH_OSOB.BL_RESET:='N'; _put:=1 ?};
   {? _put || KH_OSOB.put() ?}
";
_result:=KH_OSOB.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w kartotece osób kontaktowych kontrahenta.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w kartotece osób kontaktowych kontrahenta.')
?};
_result


\KH_OSOB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli KH_OSOB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli osób kontaktowych kontrahenta.'


\CDTAB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia tabelę CDTAB
::----------------------------------------------------------------------------------------------------------------------
CDTAB.cntx_psh();
CDTAB.index('CDTAB');
CDTAB.prefix();
_ok:=0;
{? ~CDTAB.find_key('KH',)
|| CDTAB.blank();
   CDTAB.AKRONIM:='KH';
   {? CDTAB.add() || _ok+=1 ?}
|| _ok+=1
?};
{? ~CDTAB.find_key('SRSR',)
|| CDTAB.blank();
   CDTAB.AKRONIM:='SRSR';
   {? CDTAB.add() || _ok+=1 ?}
|| _ok+=1
?};
CDTAB.cntx_pop();
{? _ok=2 || _result:=1 || _result:=0 ?};
{? _result
|| __UPG.msg('Zaktualizowano listę tabel z obsługą cech dodatkowych.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji listy tabel z obsługą cech dodatkowych.')
?};
_result


\CDTAB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia tabelę CDTAB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy tabel z obsługą cech dodatkowych.'


\za_nota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.22]
:: OPIS: Uzupełnienie pola NOTA tabeli ZA_NOTA na podstawie Adnotacji z ZA_NOTA.ZZ_DOK.
::----------------------------------------------------------------------------------------------------------------------
ZZ_DOK.cntx_psh();
ZZ_DOK.prefix();
ZA_NOTA.cntx_psh();
ZA_NOTA.prefix();
_loop:=ZA_NOTA.first();
_lrec:=0;
_lmod:=0;
{!
|? _loop
|! _lrec+=1;
   _firma:=$ZA_NOTA.ZA_TEST().ZA_ZEST().FIRMA;
   ZZ_DOK.use('zz_do'+(_firma+1));
   {? ZZ_DOK.seek(ZA_NOTA.ZZ_DOK)
   || {? +ZZ_DOK.memo_txt(,1,'NOTA')
      || _txt:=ZZ_DOK.memo_txt(,1,'NOTA');
         ZA_NOTA.memo_set(_txt,'NOTA');
         {? ZA_NOTA.memo_put(,'NOTA')
         || ZZ_DOK.memo_set('','NOTA');
            ZZ_DOK.memo_put(,'NOTA');
            _lmod+=1
         ?}
      ?}
   ?};
   _loop:=ZA_NOTA.next()
!};
ZA_NOTA.cntx_pop();
ZZ_DOK.cntx_pop();

__UPG.msg('Zaktualizowano odpowiedzi opisowe z ankiet na podstawie zapisów z dokumentów kadrowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego przepisano: %2 zapisów.'[$_lrec,$_lmod]);
1


\za_nota_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.22]
:: OPIS: Uzupełnienie pola NOTA tabeli ZA_NOTA na podstawie Adnotacji z ZA_NOTA.ZZ_DOK - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie odpowiedzi opisowych z ankiet.'


\del140
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Usuwa parametr 140 systemu
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_ustaw:=exec('get_par','#parametr',140)='T';
{? exec('del','#parametr',,140)
|| __UPG.msg('Usunięto parametr systemu 140.')
|| _res:=-1;
   __UPG.msg('Nie usunięto parametru systemu 140.')
?};
{? var_pres('_ustaw')=type_of(0) & _ustaw
|| M_ATR.cntx_psh();
   M_ATR.index('TYP');
   M_ATR.prefix('B',);
   {? M_ATR.first()
   || _lrec:=M_ATR.size();
      {!
      |? M_ATR.ADD:=2;
         M_ATR.put(1);
         M_ATR.next()
      !}
   ?};
   M_ATR.cntx_pop();
   __UPG.msg('Zaktualizowano definicje wzorców atrybutów indeksu.');
   __UPG.msg('Przetworzono: %1 zapisów.'[$_lrec])
?};
_res


\del140_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Usuwa parametr 140 systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie parametru: 140 - Logistyka i produkcja: indeks dla materiałów i usług tworzony wg słowników.'


\MATERIAL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Uzupełnia pola w tabeli M
::----------------------------------------------------------------------------------------------------------------------
_formula:="
:: Wymagane pola nawijane są triggerem, więc wystarczy przeputować rekordy
   M.put()
";
M.prefix();
_result:=M.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w kartotece materiałowej.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w kartotece materiałowej.')
?};
_result


\MATERIAL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Uzupełnia pola w tabeli M - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki materiałowej.'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\eancAKT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Ustawienie znacznika aktywności urządzeń mobilnych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_lrec:=0;
_lmod:=0;
EANC.cntx_psh();
EANC.index('IDADD');
EANC.prefix();
{? EANC.first()
|| _lrec:=EANC.size();
   {!
   |? {? EANC.AKT='T' & EANC.IDTERM=''
      || EANC.AKT:='N';
         {? EANC.put(1) || _lmod+=1 ?}
      ?};
      EANC.next()
   !}
?};
EANC.cntx_pop();
__UPG.msg('Zaktualizowano znacznik aktywności urządzeń mobilnych.');
__UPG.msg('Przetworzono: %1 zapisów. Zaktualizowano %2.'[$_lrec,$_lmod]);
_res


\eancAKT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Ustawienie znacznika aktywności urządzeń mobilnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika aktywności urządzeń mobilnych.'


\stawkaDZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MJ] [18.22]
:: OPIS: Nowa rubryka stawka dzienna dla rozliczanych dziennie umów cywilnoprawnych
::----------------------------------------------------------------------------------------------------------------------
_return:=-1;
{? +exec('import','rubryki','g','7070',1,0)
|| __UPG.msg('Nie dodano rubryki 7070 - Stawka dzienna nominalna.')
|| __UPG.msg('Dodano rubrykę 7070 - Stawka dzienna nominalna.');
   _return:=1
?};
_return


\stawkaDZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MJ] [18.22]
:: OPIS: Nowa rubryka stawka dzienna dla rozliczanych dziennie umów cywilnoprawnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Nowa rubryka (7070 Stawka dzienna nominalna) dla umów cywilnoprawnych rozliczanych wg stawki dziennej'


\faks_kh_kraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.22]
:: OPIS: Aktualizuje FAKS.KH_KRAJ, FAKS.O_KRAJ na dokumentach
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('czytaj','#stalesys',,INFO,'NAROD');
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKS.names();
FAKS.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("
         __lrec+=1;
         _put:=0;
         {? FAKS.KH & FAKS.KH_KRAJ=''
         ||
            FAKS.KH_KRAJ:=FAKS.KH().KRAJ;
            _put:=1
         ?};
         {? FAKS.KH_ODB & FAKS.O_KRAJ=''
         ||
            FAKS.O_KRAJ:=FAKS.KH_ODB().KRAJ;
            _put:=1
         ?};
         {? _put & FAKS.put() || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();

__UPG.msg('Zaktualizowano kraj kontrahenta, odbiorcy do wydruku dla dokumentów sprzedaży.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\faks_kh_kraj_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.22]
:: OPIS: Aktualizuje FAKS.KH_KRAJ, FAKS.O_KRAJ na dokumentach - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola kraj na dokumentach.'


\cit25
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.22]
:: OPIS: ER/WRT/XP/12.51/1804/0003 CIT (25) wiersze 103 i 104
::       ER/WRT/XP/12.51/1803/0038 Błędny opis na sprawozdaniu finansowym i deklaracji CIT-8/O wersja 25
::----------------------------------------------------------------------------------------------------------------------
GR_SIK.cntx_psh(); DEFW.cntx_psh();
GR_SIK.index('SYSSKROT'); GR_SIK.prefix(REF.FIRMA,'FKS');
{? GR_SIK.find_key('CIT-8/O_25') & GR_SIK.AKC<>'T'
|| DEFW.index('GRUPA');
   DEFW.prefix(GR_SIK.ref(),'25');
   {? DEFW.first
   || DEFW.NAZWA:='Dotacje otrzymane z budżetu państwa - wolne od podatku na podst.art.17ust.1pkt47';
      DEFW.put()
   ?}
?};
GR_SIK.cntx_pop(); DEFW.cntx_pop();
exec('fiks_30a','upgrade_1822',103,'exec(\'poz_edek_plus\',\'xml\',103)');
exec('fiks_30a','upgrade_1822',104,'exec(\'poz_edek_plus\',\'xml\',104)');
__UPG.msg('Zaktualizowano sprawozdania CIT.');
1


\cit25_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.22]
:: OPIS: Poprawki spisowe do CIT (25)
::----------------------------------------------------------------------------------------------------------------------
'Formuła naprawcza do CIT (25)'


\sp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Aktualizacja danych związanych z podzielymi płatnościami (Split Payment)
::----------------------------------------------------------------------------------------------------------------------
AktFun:=obj_new('akt','msg','is');
AktFun.akt:=AktFun.is:=0;
AktFun.msg:="
   {? AktFun.akt
   || AktFun.is:=1;
      __UPG.msg('Zaktualizowano pole dla '+_a+'.');
      AktFun.akt:=0
   ?}
";
KS.prefix();
KS.for_each("
   {? KS.SP='' || KS.SP:='N'; AktFun.akt:=1; KS.put() ?}
");
AktFun.msg('kont księgowych');
DOK_REJ.prefix();
DOK_REJ.for_each("
   {? DOK_REJ.SP_PRZEL='' || DOK_REJ.SP_PRZEL:='N'; AktFun.akt:=1; DOK_REJ.put() ?}
");
AktFun.msg('rodzajów dokumentów');
OP.cntx_psh();
ZAP_OP.cntx_psh();
_op:=OP.names();
{? _op.first()
|| {!
   |? echo('Aktualizacja rozrachunków: %1'@[_op.NAME]);
      OP.use(_op.NAME);
      OP.prefix();
      OP.for_each("
         {? OP.SP='' || OP.SP:='N'; AktFun.akt:=1; OP.put() ?}
      ");
      _op.next()
   !}
?};
AktFun.msg('rozrachunków');
_zo:=ZAP_OP.names();
{? _zo.first()
|| {!
   |? echo('Aktualizacja zapisów rozrachunków: %1'@[_zo.NAME]);
      OP.use('operac'+(_zo.NAME+2));
      ZAP_OP.use(_zo.NAME);
      ZAP_OP.prefix();
      ZAP_OP.for_each("
         {? ZAP_OP.SP_TYP='' || ZAP_OP.SP_TYP:='NN'; AktFun.akt:=1; ZAP_OP.put() ?}
      ");
      _zo.next()
   !}
?};
AktFun.msg('zapisów rozrachunków');
ZAP_OP.cntx_pop();
OP.cntx_pop();
_poz:=POZ.names();
{? _poz.first()
|| POZ.cntx_psh();
   DOK.cntx_psh();
   {!
   |? echo('Aktualizacja pozycji dokumentów księgowych: %1'@[_poz.NAME]);
      POZ.use(_poz.NAME);
      DOK.use('doku'+(_poz.NAME+4));
      POZ.prefix();
      POZ.for_each("
         {? POZ.SP_KS='' || POZ.SP_KS:='NN'; AktFun.akt:=1; POZ.put() ?}
      ");
      _poz.next()
   !};
   DOK.cntx_pop();
   POZ.cntx_pop()
?};
AktFun.msg('pozycji dokumentów księgowych');
echo();
{? AktFun.is=0
|| __UPG.msg('Aktualizacja nie była wymagana.')
?};
VAR_DEL.delete('AktFun');
1


\sp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Aktualizacja danych związanych z podzielymi płatnościami (Split Payment) - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych na potrzeby podzielnej płatności'


\fiks_30a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.22]
:: OPIS: Formula pomocnicza do cit25
::   WE: _a - nr pozycji deklaracji
::       _b - tresc formuly
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','CIT8','CIT8',25);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('P_'+$_a,)
      || ISTDEFS.REGULY:=_b;
         ISTDEFS.put()
      ?}
   ?}
?};
__UPG.msg('Zaktualizowano sprawozdanie CIT.');
1


\PB_SP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pole PB.SP (split-payment)
::----------------------------------------------------------------------------------------------------------------------
_formula:="{? PB.SP='' || PB.SP:='N'; PB.put() ?}";
PB.cntx_psh();
_maska:=PB.names();
_result:=1;
{? _maska.first()
|| {! |?
      PB.use(_maska.NAME);
      PB.prefix();
      _result:=PB.for_each(_formula,1);
      _result & _maska.next()
   !}
?};
{? _result
|| __UPG.msg('Zaktualizowano nowe pole w kartotece przelewów elektronicznych.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowego pola w kartotece przelewów elektronicznych.')
?};
PB.cntx_pop();
_result


\PB_SP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pola w tabeli PB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki przelewów elektronicznych.'


\PW_SP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pole PW.SP (split-payment)
::----------------------------------------------------------------------------------------------------------------------
_formula:="{? PW.SP='' || PW.SP:='N'; PW.put() ?}";
PW.cntx_psh();
_maska:=PW.names();
_result:=1;
{? _maska.first()
|| {! |?
      PW.use(_maska.NAME);
      PW.prefix();
      _result:=PW.for_each(_formula,1);
      _result & _maska.next()
   !}
?};
{? _result
|| __UPG.msg('Zaktualizowano nowe pole w kartotece pozycji wyciągów bankowych.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowego pola w kartotece pozycji wyciągów bankowych.')
?};
PW.cntx_pop();
_result


\PW_SP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pola w tabeli PW - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki pozycji wyciągów bankowych.'


\KH_DOD_SP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pole KH_DOD.SP (split-payment)
::----------------------------------------------------------------------------------------------------------------------
_formula:="{? KH_DOD.SP='' || KH_DOD.SP:='N'; KH_DOD.put() ?}";
KH_DOD.prefix();
_result:=KH_DOD.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pole w kartotece danych kontrahentów.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowego pola w kartotece danych kontrahentów.')
?};
_result


\KH_DOD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pola w tabeli KH_DOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki danych kontrahentów.'


\RBK_VAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pole SKID_RBK.VAT (VAT dla split-payment)
::----------------------------------------------------------------------------------------------------------------------
_formula:="{? SKID_RBK.VAT='' || SKID_RBK.VAT:='N'; SKID_RBK.put() ?}";
SKID_RBK.prefix();
_result:=SKID_RBK.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pole w kartotece rachunków bankowych licencjobiorcy.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowego pola w kartotece rachunków bankowych licencjobiorcy.')
?};
_result


\RBK_VAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pola w tabeli SKID_RBK.VAT - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki rachunków bankowych licencjobiorcy.'


\RD_OKR_zalacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Dodanie nowego typu załącznika kadrowego i jego uzupełnienie tabeli ZALACZ wartościami z tabeli DOKUM.
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_lrec:=_lmod:=_ldel:=0;
_firma:=exec('ref_firma','ustawienia');
_typ:=exec('slo_naz','ext_slo','ZAL','Wniosek o urlop rodzicielski');

:: Definicja miejsca użycia załączników do urlopów rodzicielskich.
Cntx.psh(SLO_NAZ,ZAOTM,ZAOH,ZAOTS);
SLO_NAZ.index('NAZWA');
ZAOH.index(ZAOH.ndx_tmp(,1,'NAZWA',,));
ZAOTS.prefix();
SLO_NAZ.prefix(exec('slo_typ','ext_slo','ZAL'),'Wniosek o urlop rodzicielski');
{? SLO_NAZ.first()
|| ZAOTM.blank();
   ZAOTM.MIEJSCE:='RD_OKR';
   ZAOTM.SLO_NAZ:=SLO_NAZ.ref();
   {? ZAOTM.add(1)
   || ZAOH.prefix('Urlopy macierzyńskie, ojcowskie, wychowawcze',);
      {? ZAOH.first()
      || ZAOTS.blank();
         ZAOTS.ZAOH:=ZAOH.ref();
         ZAOTS.SLO_NAZ:=SLO_NAZ.ref();
         ZAOTS.add(1)
      ?}
   ?};
   _result:=1
?};
ZAOH.ndx_drop();
Cntx.pop(SLO_NAZ,ZAOTM,ZAOH,ZAOTS);
{? _result=1
|| __UPG.msg('Utworzenie miejsca przechowywania wniosków o urlop rodzicielski zakończone powodzeniem.')
|| __UPG.msg('Utworzenie miejsca przechowywania wniosków o urlop rodzicielski zakończone niepowodzeniem.');
   __UPG.msg('Przeniesienie wniosków nie jest możliwe.');
   return(_result)
?};
:: Przepisanie załączników do wniosków o urlop rodzicielski z tabeli DOKUM do tabeli ZALACZ.
Cntx.psh(RD_OKR,OSOBA,ZALACZ,DOKUM);
RD_OKR.prefix();
{? RD_OKR.first()
|| OSOBA.prefix();
   ZALACZ.index('NAG');
   ZALACZ.prefix();
   DOKUM.index('DOKUM');
   {!
   |? DOKUM.prefix(_firma,$RD_OKR.ref(),);
      _lrec:=DOKUM.size();
      {? DOKUM.first()
      || do();
         {!
         |? ZALACZ.blank();
            ZALACZ.ZAL:=DOKUM.DOKUM;
            ZALACZ.DATA:=DOKUM.DATA;
            ZALACZ.ZAL_NAME:=DOKUM.KR_OP;
            ZALACZ.TYP_ZAL:=_typ;
            ZALACZ.RODZAJ:=2-!RD_OKR;
            ZALACZ.OSOBA:=RD_OKR.OSOBA;
            ZALACZ.P:=RD_OKR.P;
            ZALACZ.NAG:=RD_OKR.uidref();
            {? ZALACZ.add() || _lmod+=1 ?};
            _uid:=DOKUM.uidref(); _rr:=DOKUM.del(); exec('dokumlog_del','zbl_dok',_uid);
            _rr
         !};
         end()
      ?};
      RD_OKR.next()
   !};
   _ldel:=DOKUM.size()
?};
Cntx.pop(RD_OKR,OSOBA,ZALACZ,DOKUM);
{? _lrec
|| {? _lrec-_lmod
   || __UPG.msg('Przeniesiono %1 z %2 załączników do wniosków o urlop rodzicielski z tabeli DOKUM.'[$_lmod,$_lrec]);
      _result:=-1
   || __UPG.msg('Przeniesiono wszystkie załączniki do wniosków o urlop rodzicielski z tabeli DOKUM.')
   ?};
   {? _ldel
   || __UPG.msg('Nie usunięto %1 z %2 załączników do wniosków o urlop rodzicielski z tabeli DOKUM.'[$_ldel,$_lrec]);
      _result:=-1
   ?}
|| __UPG.msg('Brak załączników do przeniesienia.')
?};
_result


\RD_OKR_zalacz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Dodanie nowego typu załącznika kadrowego i jego uzupełnienie tabeli ZALACZ wartościami z tabeli DOKUM - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie tabeli ZALACZ wartościami z tabeli DOKUM (Wniosek o urlop rodzicielski)'


\DATY
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Dodanie pola ROK_T do tabeli DATY
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_lrec:=_lmod:=0;
DATY.cntx_psh();
DATY.index('DATA');
DATY.prefix();
{? DATY.first()
|| {!
   |? _lrec+=1;
      {? DATY.ROK_T=0
      || DATY.ROK_T:=(DATY.DATA-DATY.DZIEN_T+4)~1;
         {? DATY.put() || _lmod+=1 ?}
      ?};
      DATY.next()
   !}
?};
DATY.cntx_pop();
__UPG.msg('Zaktualizowano tabelę DATY.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
_res


\DATY_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Dodanie pola ROK_T do tabeli DATY - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli DATY'


\matkan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Sprawdzenie występowania w danych nazwiska matki pracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

{? var_pres('MATKAN',OSOBA)<0
|| __UPG.msg('Tabela OSOBA nie zawiera kolumny "Nazwisko matki".')
|| _TAB1:=sql('select count(*) ILE from OSOBA where OSOBA.MATKAN<>\'\'');
   {? _TAB1.first() & _TAB1.ILE>0
   || __UPG.msg('Liczba wierszy w tabeli OSOBA z wypełnionym polem "Nazwisko matki": %1.' [$_TAB1.ILE]);
      __UPG.msg('Dane te (po konsultacji z klientem) powinny być usunięte.');
      _ret:=-1
   || __UPG.msg('Tabela OSOBA nie zawiera danych w kolumnie "Nazwisko matki".')
   ?}
?};

{? var_pres('MATKAN',RD)<0
|| __UPG.msg('Tabela RD nie zawiera kolumny "Nazwisko matki".')
|| _TAB2:=sql('select count(*) ILE from RD where RD.MATKAN<>\'\'');
   {? _TAB2.first() & _TAB2.ILE>0
   || __UPG.msg('Liczba wierszy w tabeli RD z wypełnionym polem "Nazwisko matki": %1.' [$_TAB2.ILE]);
      __UPG.msg('Dane te (po konsultacji z klientem) powinny być usunięte.');
      _ret:=-1
   || __UPG.msg('Tabela RD nie zawiera danych w kolumnie "Nazwisko matki".')
   ?}
?};

_ret


\matkan_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Sprawdzenie występowania w danych nazwiska matki pracownika - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Sprawdzenie występowania w danych nazwiska matki pracownika'


\rola_rod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Utworzenie roli 'Specjalista ds. bezpieczeństwa danych'.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
:: sprawdzenie licencji
{? exec('lic','#b_domain','ROD')
|| _firma:=exec('ref_firma','ustawienia');
   _rola:='Specjalista ds. bezpieczeństwa danych';

   B_ROLE.cntx_psh();
   B_ROLE.index('UNIK');
   B_ROLE.prefix(_firma,_rola);
   {? B_ROLE.first()
   || __UPG.msg('Rola ''%1'' już istnieje.' [_rola])
   || B_ROLE.blank();
      B_ROLE.FIRMA:=_firma;
      B_ROLE.NAME:=_rola;
      {? B_ROLE.add()
      || __UPG.msg('Rola ''%1'' została utworzona.' [_rola])
      || __UPG.msg('Roli ''%1'' nie udało się utworzyć.' [_rola]);
         _ret:=-1
      ?}
   ?};
   B_ROLE.cntx_pop();

   {? _ret=1
   || _uid:=spli_str('ROD_REQ_CALL.ROD_REQ_RHIS.ROD_REQ_RRET','.');
      {! _lp:=1 .. obj_len(_uid)
      |! {? exec('add_actrol_one','#b_role',_rola,_uid[_lp])
         || __UPG.msg('Nadano roli uprawnienia do czynności %1.' [_uid[_lp]])
         || __UPG.msg('Nadanie roli uprawnień do czynności %1 nie powiodło się.' [_uid[_lp]]);
            _ret:=-1
         ?}
      !}
   ?}

|| __UPG.msg('Brak licencji dla obszaru ROD.')
?};
_ret


\rola_rod_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Utworzenie roli 'Specjalista ds. bezpieczeństwa danych' - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Utworzenie roli ''Specjalista ds. bezpieczeństwa danych'' i nadanie jej uprawnień do czynności:\n'
'ROD_REQ_CALL - RODO: Wywołanie żądania\n'
'ROD_REQ_RHIS - RODO: Żądania do odpowiedzi\n'
'ROD_REQ_RRET - RODO: Żądania retencji'


\rap_zew
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Aktualizacja definicji raportów zewnętrznych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('reg_rep','rap_zew','PKD','zaswzatr','Zaświadczenie o zatrudnieniu i zarobkach')
|| __UPG.msg('Zaktualizowano definicje raportów zewnętrznych.');
   1
|| __UPG.msg('Aktualizacja raportów zewnętrznych nie powiodła się.');
   0
?}


\rap_zew_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Aktualizacja definicji raportów zewnętrznych - opis.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji raportów zewnętrznych'


\N_NDOK_do_CU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Przepisanie wartości w tabeli N z pola NDKO do CU dla dla urlopu wychowawczego i urlopów rodzicielskich.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lrec:=_lmod:=0;

exec('__RUB','object');
Cntx.psh(R,N);
R.clear();
__RUB.fill();
_kody:=__RUB.sys_sql(114)+','+__RUB.sys_sql(1227);

N.clear();
{? N.f_active() || N.f_clear() ?};
N.f_set(,'join R','R.RN in (:_a)',_kody);
{? N.f_first()
|| {!
   |? {? #N.NDOK
      || _lrec+=1;
         {? ~N.CU & +N.NDOK=1
         || N.CU:=#N.NDOK;
            N.NDOK:='';
            _lmod+=N.put(1)
         ?}
      ?};
      N.f_next()
   !}
?};
Cntx.pop(R,N);

{? _lrec
|| {? _lrec-_lmod
   || __UPG.msg('Dla nieobecności o kodach %1 zmodyfikowano %2 z %3 wierszy w tabeli N.'[_kody,$_lmod,$_lrec]);
      _result:=-1
   || __UPG.msg('Dla nieobecności o kodach %1 zmodyfikowano %2 wierszy w tabeli N.'[_kody,$_lrec])
   ?}
|| __UPG.msg('Brak danych do modyfikacji.')
?};
_result


\N_NDOK_do_CU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Przepisanie wartości w tabeli N z pola NDKO do CU dla dla urlopu wychowawczego i urlopów rodzicielskich - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola CU z tabeli N wartościami z pola NDOK dla urlopów rodzicielskich i urlopu wychowawczego'


\pb_rhp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Aktualizacja poleceń przelewów dla rachunków od umów cywilno-prawnych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_MASK:=PB.names();

PB.cntx_psh();
RH.cntx_psh();
RHP.cntx_psh();
RHP.prefix();

_loop:=RHP.first();
{!
|? _loop
|! _mdb:='pb%1'[4+$RHP.RH().DWY];
   _kid:=form(#RHP.ref(),-7,0,'99');
   _loop:=(_MASK.find_le(_mdb) | _MASK.first());
   {!
   |? _loop
   |! PB.use(_MASK.NAME);
      PB.index('PBKO');
      PB.prefix(_kid,);
      _loop:=PB.first();
      {!
      |? _loop
      |! {? (-PB.KD='kali: umowa' | -PB.KD='ppl: umowy') & RHP.RH().DWY<=PB.DP
         || PB.KID:=gsub(RHP.IDADD,' ','');
            PB.cntx_psh();
            PB.prefix();
            PB.put();
            PB.cntx_pop()
         ?};
         _loop:=PB.next()
      !};
      _loop:=_MASK.next()
   !};
   _loop:=RHP.next()
!};

RHP.cntx_pop();
RH.cntx_pop();
PB.cntx_pop();

__UPG.msg('Aktualizacja poleceń przelewów zakończona.');

1


\pb_rhp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Aktualizacja poleceń przelewów dla rachunków od umów cywilno-prawnych - opis.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja poleceń przelewów dla rachunków od umów cywilno-prawnych'


\erase_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Usunięcie przelewów ze zbioru technicznego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PB.cntx_psh();
PB.use('pb____');
PB.index('PB_S');
PB.prefix('PPL:');
{? PB.first()
|| {! |? PB.del() !}
?};
PB.cntx_pop();

__UPG.msg('Usunięcie przelewów ze zbioru technicznego zakończone.');

1


\erase_pb_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Usunięcie przelewów ze zbioru technicznego - opis.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie przelewów ze zbioru technicznego'


\umowy_zc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: ER/WRT/XP/18.22/1809/004
::       Błąd importu umów zlecenia, przypisanie umów do pierwszego zleceniobiorcy (pole ZC.OSOBA)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lmod:=_lerr:=0;

ZC.cntx_psh();
ZC.index('ZLECPRAC');
ZC.prefix();
ZC.trig_off('*','*');

{? ZC.first()
|| FUN.prg_start(ZC.size(),'Aktualizacja pola Osoba w tabeli umów cywilnoprawnych.'@,,,1);
   {!
   |? FUN.prg_next();
      {? ZC.OSOBA<>ZC.P().OSOBA
      || _lmod+=1;
         ZC.OSOBA:=P.OSOBA;
         {? ~ZC.put(1)
         || _lerr+=1;
            _result:=-1
         ?}
      ?};
      ZC.next()
   !};
   FUN.prg_stop()
?};
ZC.trig_on('*','*');
ZC.cntx_pop();

{? _lmod
|| {? _result>0
   || __UPG.msg('Naprawiono dane w tabeli umów cywilnoprawnych.');
      __UPG.msg('Liczba poprawionych rekordów: %1'[$_lmod])
   || __UPG.msg('Nie udało się naprawić wszystkich danych w tabeli umów cywilnoprawnych.');
      __UPG.msg('Liczba nienaprawionych rekordów: %1 z %2, które należało poprawić.'[$_lerr,$_lmod])
   ?}
|| __UPG.msg('Tabela umów cywilnoprawnych nie wymagała naprawy.')
?};

_result


\umowy_zc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: ER/WRT/XP/18.22/1809/004
::       Błąd importu umów zlecenia, przypisanie umów do pierwszego zleceniobiorcy (pole ZC.OSOBA) - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Formuła naprawcza danych po imporcie do tabeli umów cywilnoprawnych'


\rubryka7079
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PK] [18.22]
:: OPIS: Nowa rubryka płacowa "Korekta składki zdrowotnej odliczonej"
::----------------------------------------------------------------------------------------------------------------------
_return:=-1;
{? +exec('import','rubryki','g','7079',1,0)
|| __UPG.msg('Nie dodano rubryki płacowej 7079 - Korekta składki zdrowotnej odliczonej.')
|| __UPG.msg('Dodano rubrykę płacową 7079 - Korekta składki zdrowotnej odliczonej.');
   _return:=1
?};
_return


\rubryka7079_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PK] [18.22]
:: OPIS: Nowa rubryka płacowa "Korekta składki zdrowotnej odliczonej" - opis
::----------------------------------------------------------------------------------------------------------------------
'Nowa rubryka płacowa (7079 Korekta składki zdrowotnej odliczonej)'


\RD_slowniki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Uzupełnienie słownika podstaw prawnych i słownika rodzajów nieobecności do wniosków o urlop rodzicielski.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Uzupełnienie słownika podstaw prawnych do wniosków o urlop rodzicielski.
exec('podstawa_prawna','urlop_rodzicielski');
__UPG.msg('Słownik podstaw prawnych do wniosków o urlop rodzicielski został zaktualizowany.');
:: Uzupełnienie słownika rodzajów nieobecności do wniosków o urlop rodzicielski.
exec('nieobecnosci','urlop_rodzicielski');
__UPG.msg('Słownik rodzajów nieobecności do wniosków o urlop rodzicielski został zaktualizowany.');
1


\RD_slowniki_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Uzupełnienie słownika podstaw prawnych i słownika rodzajów nieobecności do wniosków o urlop rodzicielski - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słowników podstaw prawnych i rodzajów nieobecności do wniosków o urlop rodzicielski'


\listopad_12
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Nowe święto 12 listopada 2018 r.
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
_tab:=exec('p_05','napraw_p');
{? type_of(_tab)=118
|| {? _tab.first()
   || _return:=1;
      {!
      |? {? _tab.PARENT=0
         || {? _tab.DONE='T'
            || __UPG.msg('Wykonano zadanie - %1'[_tab.TEXT])
            || {? _tab.SEL='T'
               || __UPG.msg('Brak danych dla zadania - %1'[_tab.TEXT])
               || __UPG.msg('Nie przetwarzano zadania - %1'[_tab.TEXT])
               ?}
            ?}
         ?};
         _tab.next()
      !}
   || _return:=-1
   ?}
?};
_return


\listopad_12_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Nowe święto 12 listopada 2018 r.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja związana z wprowadzeniem nowego święta na 12 listopada 2018 r. '
'związanego z 100-leciem Odzyskania Niepodległości'

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:12 bc266a24ec951a3a0420a1f544ad53cf40ed28158d3e920fd28ff6eec451223533696e380ab29ccc30fc527d54c6d8faa8fecf5edefec044b4c895c695210564c4c24cc73f02140b02e6ebb137c5980680a89787d78259f990f8f55bb8b644eb4db41cbdd960f8eb8ba69d3491551e07af36769e7b4a56d4b4c32c486a4d5309

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2137_02.fml
:: Utworzony: 03.12.2021
:: Autor: DG
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.14 na 21.37
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Główna formuła aktualizacji 21.37
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('rubryki_placowe',,,'PPL',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('kryterium_listy_plac',,'N','PPL',,,,1,'T');
:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
__UPG.add_task('formuly_placowe_Polski_Lad','N','N','PPL',,,,1,'T');
~~


\rubryki_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie rubryk płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

{? +exec('import','rubryki','g','7149,7170,7171,7172',1,0)
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);

_result


\rubryki_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie rubryk płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk płacowych.'


\formuly_placowe_Polski_Lad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie formuł płacowych.
::----------------------------------------------------------------------------------------------------------------------
FM.index('FORMNAZ');
FM.prefix(exec('ref_firma','ustawienia'),'P');
{? FM.first
|| __UPG.msg('Znaleziono formuły obliczeniowe dla listy korekt podatku, typ: \'P\'.\n'
      'Automatyczna aktualizacja formuł obliczeniowych typu \'P\', nie będzie wykonana.');
   return(-1)
?};

exec('__RUB','object');

_rs:='950,990,994,995,996,999,7170';
_rn:=spli_str(_rs,',');
_tp:=spli_str('P',',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_err:=exec('formuly_import',form_stack(0).file,_tp,_rn,'formuly_txt');

{? _err>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| __UPG.msg('Wprowadzono formuły typu \'P\' do obliczenia listy korekt podatku KPD.');
   1
?}


\formuly_placowe_Polski_Lad_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie formuł płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuł płacowych dla zmian: Polski Ład'


\formuly_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.42]
:: OPIS: Funkcja do aktualizacji formuł płacowych.
::   WE: _a [ARRAY] - Tablica aktualizowanych typów formuł płacowych.
::       _b [ARRAY] - Tablica kodów aktualizowanych składników płacowych.
::       _c [STRING] - Nazwa formuły do importu formuł płacowych.
::   WY: _err (0/1) - Liczba błędów, które wystąpiły podczas importu.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;
_formula:=_c;
_err:=0;

FM.cntx_psh();
{! _ni:=1..obj_len(_tp)
|! FM.index('FORMNAZ');
   FM.prefix(exec('ref_firma','ustawienia'),_tp[_ni]);
   {? FM.size()=0
   || {! _mi:=1..obj_len(_rn)
      |! _fm:=form(exec(_formula,form_stack(0).file,_tp[_ni],#_rn[_mi]));
         {? _fm<>''
         || _ok:=1;
            _org:='';
            _found:=FM.find_tab(,'R','RN','=',#_rn[_mi]);
            {? _found
            || _org:=FM.memo_txt(,1,'F')
            || FM.blank();
               FM.TP:=_tp[_ni];
               FM.R:=__RUB.ref(#_rn[_mi]);
               FM.W:={? 'U,F,D'*_tp[_ni] || 'T' || 'N' ?};
               {? ~FM.add()
               || _err+=0;
                  _ok:=0
               ?}
            ?};
            {? _found & _fm=_org
            || __UPG.msg('Formuła typu "%1" dla składnika %2 nie wymaga aktualizacji.'[_tp[_ni],_rn[_mi]])
            |? _ok
            || FM.memo_set(_fm,'F');
               {? FM.memo_put(,'F')
               || {? _found
                  || __UPG.msg('Zaktualizowano formułę typu "%1" dla składnika %2.'[_tp[_ni],_rn[_mi]])
                  ?}
               |? _found
               || __UPG.msg('Aktualizacja formuły typu "%1" dla składnika %2 nie powiodła się.'[_tp[_ni],_rn[_mi]]);
                  _err+=1
               ?}
            ?}
         ?}
      !}
   ?}
!};
FM.cntx_pop();
_err


\formuly_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

:: ==================
{? _rn=950
|| "
FUNKCJE.L(797,799)"
:: ==================
|? _rn=990
|| "
DoList.x:=FUNKCJE.L(500)+FUNKCJE.L(740)+FUNKCJE.L(747)-FUNKCJE.L(950)+FUNKCJE.L(7092);
DoList.m[1]:=DoList.x"
:: ==================
|? _rn=994
|| "
DoList.p[1]:=DoList.p[2]:=DoList.p[3]:=0;
DoList.p[1]:=exec('kwprzel_kali','lista_licz',994)"
:: ==================
|? _rn=995
|| "
DoList.p[2]:=exec('kwprzel_kali','lista_licz',995)"
:: ==================
|? _rn=996
|| "
DoList.p[3]:=exec('kwprzel_kali','lista_licz',996)"
:: ==================
|? _rn=999
|| "
DoList.m[1]"
:: ==================
|? _rn=7170
|| "
exec('korekta_zal_pod','lista_licz')"
|| ""
?}


\kryterium_listy_plac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [21.37]
:: OPIS: Kryterium listy płac.
::----------------------------------------------------------------------------------------------------------------------
_cnt:=0;
exec('__F_ZATR','object');
exec('__RUB','object');
__RUB.fill();
:exec('czytaj','formuly','P',FM,1);
KT.cntx_psh();
KT.index('F_ZATR');
KT.prefix(exec('ref_firma','ustawienia'));
{? (_f_zatr:=__F_ZATR.find('P'))
|| {? ~KT.find_key(_f_zatr,'KPD')
   || KT.blank();
      KT.T:='KPD';
      KT.N:='Korekta podatku';
      KT.K:='FUNKCJE.LP_SYS(9611)';
      KT.F:='P';
      KT.L:='*';
      KT.F_ZATR:=_f_zatr;
      _cnt+=KT.add(1)
   ?}
?};
_uz:=PAR_SKID.get(190);
{? (_f_zatr:=__F_ZATR.find('Z'))
|| {? ~KT.find_key(_f_zatr,'ZLK')
   || KT.blank();
      KT.T:='ZLK';
      KT.N:='Lista korekcyjna';
      KT.F:=_uz;
      KT.L:='*';
      KT.F_ZATR:=_f_zatr;
      _cnt+=KT.add(1)
   ?}
?};
KT.cntx_pop();

{? _cnt>0
|| __UPG.msg('Dodano nowe kryteria dla list płac.')
?};

1


\kryterium_listy_plac_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Kryterium listy płac - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie kryterium listy płac.'

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:25 47ff7f8835594804a37a9f8b9c589c416ffb2600a26cea9126bc1ebae366b2ca4b5cbbec57f33c28350b235b322fabf8c42b1dd62832f4008efb587a2f5f35feacf57278bfcf3153546bd9d70903381a0b84937972cbe6ea5a77b1a074fce119bce7743e73d2c7b1f85f4ef19f4499b8d76d4b9cb2a00691f0bd8358f30bd996

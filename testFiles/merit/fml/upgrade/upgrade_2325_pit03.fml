:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2325_pit03.fml
:: Utworzony: 21.11.2023
:: Autor: DAROKR
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 22.26 na 23.25
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [23.25]
:: OPIS: Główna formuła aktualizacji 23.25_PIT03
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2023,11,21),time(0,0,0));

:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('akt_2325_pit03',,,'PPL',1);
__UPG.add_task('rep_tran',,,'PPL');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('akt_LIM_SKZW',,'N','PPL',,,,1);
:: Zadania manualne
~~


\akt_2325_pit03
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [23.25]
:: OPIS: Dodanie nowych e-deklaracji obowiązujących w 2023 dotyczące przychodów od 01.01.2023
::----------------------------------------------------------------------------------------------------------------------
_dod_ver:="
   VAT_VER.index('VER_OD'); VAT_VER.prefix(_a,_c,_c,_b,_d);
   _jest:=VAT_VER.first();
   {? ~_jest
   || _old:={? _a='FKS' || 'FIKS' |? _a='PPL' || 'KALI' || '' ?};
      VAT_VER.prefix(_old,_c,_c,_b,_d);
      _jest:=VAT_VER.first()
   ?};
   {? _jest
   || _add:=0
   || _add:=1;
      VAT_VER.blank();
      VAT_VER.SYSTEM:=_a;
      VAT_VER.OD:=_b;
      VAT_VER.NAZWA:=_c;
      VAT_VER.NR:=_d
   ?};
   {? _<6 || _f:='' ?};
   {? _<5 || _e:=0 ?};
   _put:=0;
   {? VAT_VER.NRF<>_e || VAT_VER.NRF:=_e; _put+=1 ?};
   {? VAT_VER.WIERSZ<>_f || VAT_VER.WIERSZ:=_f; _put+=1 ?};
   VAT_VER.prefix();
   {? _add || VAT_VER.add(1) |? _put || VAT_VER.put(1) ?}
";
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
{? _dod_ver('PPL',date(2023,1,1),'PIT4R',13,13,',2,3,4,6,7,9,')
|| __UPG.msg('Dodano 13 wersja e-deklaracji PIT-4R(13) obowiązująca od dnia 2023.11.07 dla przychodów od 2023.01.01')
|| __UPG.msg('Brak aktualizacji, 13 wersja e-deklaracji PPIT-4R(13 już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-4R wersja 13
{? exec('edek_stru','edeklar','PIT4R',13,'pit-4r13_v1-0.dfg')
|| __UPG.msg('Dodano schemat 13 wersji e-deklaracji PIT-4R(13).')
|| __UPG.msg('Brak aktualizacji, schemat 13 wersji e-deklaracji PIT-4R już istnieje.')
?};
_wakt:=',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,41,';
{? _dod_ver('PPL',date(2023,1,1),'PIT8AR',14,13,_wakt)
|| __UPG.msg('Dodano 14 wersja e-deklaracji PIT-8AR(13) obowiązująca od dnia 2023.11.08 dla przychodów od 2023.01.01')
|| __UPG.msg('Brak aktualizacji, 14 wersja e-deklaracji PIT-8AR(13) już istnieje.')
?};
::ER/WRT/XP/23.25/2312/0053 AKT 23.25_PIT03-PIT-8AR(13) jeżeli dokonano odliczeń podatku CIT w bloku D
::w xml nie jest zapisana wartość pola P_478 dotycząca mc XII
::kasowanie istniejacych struktur e-deklaracji waznych za 2023-14 wersję e-deklaracji dla 13 wersji formularza PIT-8AR
exec('edek_stru_del','edeklar','PIT8AR',14);
::import poprawnych struktur e-deklaracji PIT8AR wersja 14 dla 13 wersji formularza
{? exec('edek_stru','edeklar','PIT8AR',14,'pit-8ar14_v1-0.dfg')
|| __UPG.msg('Dodano schemat 14 wersji e-deklaracji PIT-8AR(13).')
|| __UPG.msg('Brak aktualizacji, schemat 14 wersji e-deklaracji PIT-8AR(13) już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-11(29) wersja 14 dla 2 numeru schemy od 2023.07.11
{? exec('edek_stru','edeklar','PIT11',14,'pit-11_29_v1-2.dfg',date(2023,7,11))
|| __UPG.msg('Dodano schemat 14 wersji 2 numeru schemy e-deklaracji PIT-11(29).')
|| __UPG.msg('Brak aktualizacji, schemat 14 wersji 2 numeru schemy e-deklaracji PIT-11(29) już istnieje.')
?};
BPMN.SYM_DOM:=_dom;

::https://www.gov.pl/web/kas/niskiepodatki-dwa-razy-wyzsze-wynagrodzenie-dla-platnikow-pobierajacych-pit
::Podwyżka wynagrodzenia obowiązywać będzie w okresie od  1 lipca 2022 r. do  31 grudnia 2023 r.
{? var_pres('PW_TWP',KST)>0
|| _dodano:=exec('kst_war_add','#stalesys','KST.PW_TWP','0.30',date(2024,1,1));
   {? _dodano
   || __UPG.msg('Dodano informację o procencie wynagrodzenia 0.3% za terminową wpłatę podatków od 01.01.2024.')
   ?}
?};
1


\akt_2325_pit03_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [23.25]
:: OPIS: Dodanie nowych e-deklaracji obowiązujących w 2023 dotyczące przychodów od 01.01.2023
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych e-deklaracji PIT-4R(13), PIT-8AR(13) obowiązujących w 2023 dotyczące przychodów od 01.01.2023\n'
'Aktualizacja wartości pola KST.PW_TWP na 0.3% od 01.01.2024.'


\rep_tran
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [23.25]
:: OPIS: Kompilacja wydruków.
::----------------------------------------------------------------------------------------------------------------------
{? rep_tran('ppl*')<>0
|| __UPG.msg('Zaktualizowano wydruki dziedziny PPL.');
   1
|| __UPG.msg('Aktualizacja wydruków dziedziny PPL nie powiodła się.');
   -1
?}


\rep_tran_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [23.25]
:: OPIS: Kompilacja wydruków dziedziny PPL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków dziedziny PPL.'


\akt_LIM_SKZW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [23.25]
:: OPIS: Aktualizacja wartości pola KST.LIM_SKZW na 840 zł od 01.09.2023 ale dotyczy przychodów od 1 stycznia 2023.
::----------------------------------------------------------------------------------------------------------------------
:: Dodanie wartości pola określającego limit odliczenia od dochodu składek członkowskich od września 2023 kwota 840zł.
{? var_pres('LIM_SKZW',KST)>0
|| _dodano:=exec('kst_war_add','#stalesys','KST.LIM_SKZW','840.00',date(2023,9,1));
   {? _dodano
   || __UPG.msg('Dodano informację o limicie 840 zł odliczenia od dochodu składek członkowskich od 01.01.2023.');
      __UPG.msg('Dotyczy przychodów od 01.01.2023.')
   ?}
?};
1


\akt_LIM_SKZW_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [23.25]
:: OPIS: Aktualizacja wartości pola KST.LIM_SKZW na 840 zł od 01.09.2023 ale dotyczy przychodów od 1 stycznia 2023.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wartości pola KST.LIM_SKZW na 840 zł od 01.09.2023 ale dotyczy przychodów od 1 stycznia 2023.'

:Sign Version 2.0 jowisz:1045 2023/12/19 09:09:21 a4aad62a8a3d6cb75faf684d1bbaf317e96c2cd4b9e32053375c0548a829bc1f95c8278e7291541a18cf73f383bc3e8167d08f51e6d44765c29aa5d0ca23fcbf47e278a3d1a1b1deab3eb4a3bb9565bdafb04506f59628c6c882511160704ce235363e4bd4106597d207e56dc21e683e0298ccd6401a1b93b9399560e5bdd0d6

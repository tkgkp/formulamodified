:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2137_14.fml
:: Utworzony: 04.02.2022
:: Autor: TS
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.14 na 21.37
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37_14]
:: OPIS: Główna formuła aktualizacji 21.37_14
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('stalesys_ini',,,'ZWS',,,,,'T');
__UPG.add_task('upgrade_act',,,'ZPR',1,,,,'T');
__UPG.add_task('funpar',,,'ZWS',,,,,'T');
__UPG.add_task('report',,,'ZWS',,,,,'T');
__UPG.add_task('color',,,'ZWS',2,,,,'T');
__UPG.add_task('areatitle',,,'ZWS',1);
__UPG.add_task('KH_ODB',,,'ZWS',,,,,'T');
__UPG.add_task('USERSF',,,'ZWS',,,,,'T');
__UPG.add_task('KH_DOD',,,'ZWS',,,,,'T');
__UPG.add_task('kontr_st_zwol',,,'FKS',,,,,'T');
__UPG.add_task('dok_proc_upd',,,'FKS',,,,,'T');
__UPG.add_task('dok_proc_upd_tt',,,'FKS',,,,,'T');
__UPG.add_task('jpk_fa_upd',,,'FKS',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('FO_DEF',,'N','ZWS',,,,,'T');
__UPG.add_task('FAKS',,'N','LSP',,,,1,'T');
__UPG.add_task('fakskh',,'N','LSP',,,,1,'T');
__UPG.add_task('TYPYSP',,'N','ZWS',,,,1,'T');
__UPG.add_task('WYDRUKIL',,'N','ZWS',,,,1,'T');
:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
~~


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA','.');
_dom:=spli_str('ZPR.ZWS.FKS','.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',1)
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.')
?}


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [JK] [21.37]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [JK] [21.37]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\KH_ODB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Wypełnia pola w tabeli KH_ODB
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
KH_ODB.cntx_psh();
KH_ODB.prefix();
_res:=KH_ODB.for_each("
   __lrec+=1;
   _put:=0;
   {? KH_ODB.KH_LNK=null()
   || {? KH_ODB.JST<>'N'
      || KH_ODB.JST:='N';
         _put:=1
      ?}
   || {? KH_ODB.JST<>'N' & KH_ODB.JST<>'T'
      || KH_ODB.JST:='T';
         _put:=1
      ?}
   ?};
   {? _put>0
   || {? KH_ODB.put()
      || __lmod+=1
      ?}
   ?}
");
KH_ODB.cntx_pop();
__UPG.msg('Zaktualizowano odbiorców kontrahentów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\KH_ODB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Wypełnia pola w tabeli KH_ODB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja odbiorców kontrahentów'


\USERSF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja użytkowników w firmach
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
USERSF.trig_off('*','*');
USERSF.cntx_psh();
USERSF.prefix();
USERSF.for_each("
   __lrec+=1;
   _put:=0;
   {? USERSF.KSEF_DL='' || USERSF.KSEF_DL:='N'; _put:=1 ?};
   {? USERSF.KSEF_UL='' || USERSF.KSEF_UL:='N'; _put:=1 ?};
   {? _put
   || {? USERSF.put(1) || __lmod+=1 ?}
   ?}",
   1
);
USERSF.cntx_pop();
USERSF.trig_on('*','*');
__UPG.msg('Zaktualizowano użytkowników w firmach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\USERSF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja użytkowników w firmach - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja użytkowników w firmach'


\FAKS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje pól tabeli FAKS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKS.names();
KH.cntx_psh();
KH_ODB.cntx_psh();
FAKS.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("
         __lrec+=1;
         _put:=0;
         {? FAKS.STATKSEF='' || _put:=1 ?};
         _kpocz:=FAKS.KH().KPOCZ; {? _kpocz<>'' & FAKS.KPOCZ='' || _put:=1; FAKS.KPOCZ:=_kpocz ?};
         _kpocz:=FAKS.KH_ODB().KPOCZ; {? _kpocz<>'' & FAKS.O_KPOCZ='' || _put:=1; FAKS.O_KPOCZ:=_kpocz ?};
:: FAKS.EDOKUMEN i przy okacji FAKS.DRUKUJ ustawiane w triggerze
         {? FAKS.EDOKUMEN='' || _put:=1 ?};
         {? _put & FAKS.put() || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();
KH_ODB.cntx_pop();
KH.cntx_pop();

__UPG.msg('Zaktualizowano pola w nagłówku dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\FAKS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.37]
:: OPIS: Aktualizuje pól tabeli FAKS opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola w nagłówku dokumentów sprzedaży i zakupu.'


\fakskh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Uzupełnia pola tabeli FAKSKH
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_formula:="
KH.cntx_psh();
FAKSKH.cntx_psh();
FAKSKH.prefix();
{? FAKSKH.first()
|| {!
   |? __lrec+=1;
      _put:=0;
      {? FAKSKH.SYS='' || _put:=1; FAKSKH.SYS:='N' ?};
      _kpocz:=FAKSKH.KH().KPOCZ; {? _kpocz<>'' & FAKSKH.KPOCZ='' || _put:=1; FAKSKH.KPOCZ:=_kpocz ?};
      {? _put
      || {? FAKSKH.put(1)
         || __lmod+=1
         ?}
      ?};
      FAKSKH.next()
   !}
?};
FAKSKH.cntx_pop();
KH.cntx_pop();
1
";
FAKSKH.cntx_psh();
FAKSKH.prefix();
_result:=FAKS.for_each(_formula,1);
FAKSKH.cntx_pop();

_result:=exec('for_each_mask','#table',FAKSKH,_formula,,,,1,1);

{? _result
|| __UPG.msg('Zaktualizowano dodatkowe podmiotów dokumentu sprzedaży.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji dodatkowych podmiotów dokumentu sprzedaży.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\fakskh_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Uzupełnia pola w tabeli FAKSKH - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dodatkowych podmiotów dokumentu sprzedaży.'


\KH_DOD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Uzupełnia pola w tabeli KH_DOD
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   _put:=0;
   {? KH_DOD.KSEFKH='' || KH_DOD.KSEFKH:='N'; _put:=1 ?};
   {? KH_DOD.KSEFLI='' || KH_DOD.KSEFLI:='N'; _put:=1 ?};
   {? _put || KH_DOD.put() ?}
";
KH_DOD.cntx_psh();
KH_DOD.prefix();
_result:=KH_DOD.for_each(_formula,1);
KH_DOD.cntx_pop();
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w kartotece kontrahentów (dane dodatkowe dla firmy).')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w kartotece kontrahentów (dane dodatkowe dla firmy).')
?};
_result


\KH_DOD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Uzupełnia pola w tabeli KH_DOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli kontrahentów (dane dodatkowe dla firmy).'


\TYPYSP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizacja tabeli TYPYSP - typy dokumentów
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;
TYPYSP.prefix();
TYPYSP.for_each("
   __lrec+=1;
   _put:=0;
   {? TYPYSP.KSEF='' || TYPYSP.KSEF:='N'; _put:=1 ?};
   {? TYPYSP.KSEFZDOK='' || TYPYSP.KSEFZDOK:='N'; _put:=1 ?};
   {? _put & TYPYSP.put(1) || __lmod+=1 ?}
");
__UPG.msg('Zaktualizowano dane typów dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\TYPYSP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizacja tabeli TYPYSP - typy dokumentów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych typów dokumentów sprzedaży i zakupu'


\WYDRUKIL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizacja tabeli WYDRUKIL - parametry wydruków
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;
WYDRUKIL.prefix();
WYDRUKIL.for_each("
   __lrec+=1;
   _put:=0;
   {? WYDRUKIL.DP='' || WYDRUKIL.DP:='N'; _put:=1 ?};
   {? _put & WYDRUKIL.put(1) || __lmod+=1 ?}
");
__UPG.msg('Zaktualizowano dane parametrów wydruków.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\WYDRUKIL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizacja tabeli WYDRUKIL - parametry wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych parametrów wydruków'


\FO_DEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Aktualizacja parametrów FO aplikacyjnych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_res:=exec('upgrade_apl','#params');

__UPG.msg('Zaktualizowano parametry aplikacyjne');
VAR_DEL.delete('__lrec','__lmod');
_res


\FO_DEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Aktualizacja parametrów FO aplikacyjnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametów aplikacyjnych'


\kontr_st_zwol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Dodaje formułę początkową formuły kontrolnej sprawdzającej podanie powodu użycia stawki zwolnionej VAT
::----------------------------------------------------------------------------------------------------------------------
_name:='ST_ZWOL';
_ret:=1;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G',_name,);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:=_name;
   FORMULA.NAZWA:='Formuła początkowa dla kontroli uzupełnienia powodu użycia zwolnionej stawki VAT';
   FORMULA.FORMULA:=$"exec('st_zwol','%fks_spr_dok')";
   _ret:=FORMULA.add();
   {? _ret
   || __UPG.msg('Dodano formułę %1.'[_name])
   || __UPG.msg('Nie udało się dodać formuły %1.'[_name])
   ?}
|| __UPG.msg('Istnieje już formuła %1.'[_name])
?};
FORMULA.cntx_pop();
_ret


\kontr_st_zwol_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Dodaje formułę początkową formuły kontrolnej sprawdzającej podanie powodu użycia stawki zwolnionej VAT - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły ST_ZWOL.\n'
'Jest to początkowa formuła formuły kontrolnej dla sprawdzenia uzupełnienia powodu użycia zwolnionej stawki VAT.'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\dok_proc_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Aktualizacja pola dla procedury szczególnej DOK.PROC
::  OLD: \dok_proc_upd/upgrade_rrxx.fml
::   WE: _a - 1/0 - czy formuła testująca, domyślnie 0
::  OLD: \dok_proc_upd/ak_148.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;

DOK.cntx_psh();
_maskAr:=DOK.names();
{? _maskAr.first()
|| {!
   |? __UPG.msg('Aktualizacja dokumentów z maski: '+_maskAr.NAME);
      DOK.use(_maskAr.NAME);
      DOK.prefix();
      DOK.for_each("
         _put:=0;
         {? DOK.JPK_PROC<>'' & DOK.PROC=null
         || _valsAr:=spli_str(DOK.JPK_PROC,',');
            {! _i:=1..obj_len(_valsAr)
            |! {? DOK.PROC=null
               || DOK.PROC:=exec('FindInSet','#table','JPK_SLO','KOD',_valsAr[_i],1,,1,'PROC_FAKS',null())
               ?}
            !};
            {? DOK.PROC
            || _put:=1
            ?}
         ?};
         {? _put
         || DOK.put()
         ?}
      ");
      _maskAr.next()
   !}
?};
DOK.cntx_pop();

__UPG.msg('Zaktualizowano pole dla procedury szczególnej.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\dok_proc_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Aktualizacja pola dla procedury szczególnej DOK.PROC
::  OLD: \dok_proc_upd_desc/upgrade_rrxx.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola dla procedury szczególnej'


\dok_proc_upd_tt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37_14]
:: OPIS: Aktualizacja procedur szczególnych DOK.JPK_PROC
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;

DOK.cntx_psh();
DOKPOLA.cntx_psh();
_maski:=DOKPOLA.names();
{? _maski.first()
|| {!
   |? __UPG.msg('Aktualizacja danych dodatkowych z maski: '+_maski.NAME);
      _maska:=_maski.NAME+4;
      DOKPOLA.use('dokp'+_maska);
      _i2:=DOKPOLA.ndx_tmp('',1,'TT',,0);
      DOKPOLA.index(_i2);
      DOKPOLA.prefix('T',);
      {? DOKPOLA.first()
      || {!
         |? _dokm:=ref_name(DOKPOLA.REK);
            DOK.use(_dokm);
            DOK.prefix();
            {? DOK.seek(DOKPOLA.REK)
            || VPOZ.cntx_psh();
               VPOZ.use('pozv'+(_dokm+4));
               VPOZ.index('VDOK');
               VPOZ.prefix(DOK.ref());
               {? VPOZ.first()
               || _str:=DOK.JPK_PROC;
                  {!
                  |? {? VPOZ.RVAT().RVAT().KVAT().SYM='_WWspNat'
                     || _str:=exec('add_str','dok_ksef',_str,'TT_WNT')
                     |? KVAT.SYM='_WWspDot'
                     || _str:=exec('add_str','dok_ksef',_str,'TT_D')
                     ?};
                     VPOZ.next()
                  !};
                  {? DOK.JPK_PROC<>_str
                  || DOK.JPK_PROC:=_str;
                     DOK.put()
                  ?}
               ?};
               VPOZ.cntx_pop()
            ?};
            DOKPOLA.next()
         !}
      ?};
      DOKPOLA.index('UNIK');
      DOKPOLA.ndx_drop(_i2);
      _maski.next()
   !}
?};
DOKPOLA.cntx_pop();
DOK.cntx_pop();

__UPG.msg('Zaktualizowano pole dla procedury szczególnej.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\dok_proc_upd_tt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37_14]
:: OPIS: Aktualizacja pola dla procedury szczególnej DOK.JPK_PROC
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola dla procedury szczególnej'


\jpk_fa_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Aktualizacja struktury pliku JPK_FA
::----------------------------------------------------------------------------------------------------------------------
_fml:="exec('jpk_fa_tt','jpk')";
_zmiana:=-1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','FA','JPK_FA(3)_v1-0',);
{? ISTDEF.first()
|| _zmiana:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref(),'P_23');
   {? ISTDEFS.first() & ISTDEFS.REGULY<>$_fml
   || ISTDEFS.REGULY:=$_fml;
      _zmiana:=ISTDEFS.put()
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
{? _zmiana=1
|| __UPG.msg('Zaktualizowano schemat JPK_FA (3).')
|? _zmiana=-1
|| __UPG.msg('Nie znaleziono schematu JPK_FA (3).')
|| __UPG.msg('Aktualizacja schematu JPK_FA (3) nie była wymagana.')
?};
1


\jpk_fa_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Aktualizacja struktury pliku JPK_FA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu JPK_FA (3)\n'
'Zmiana formuły dla pola JPK\\Faktura\\P_23'


\FO_USR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Aktualizacja parametrów FO użytkowników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_res


\FO_USR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Aktualizacja parametrów FO użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametów użytkowników'

:Sign Version 2.0 jowisz:1048 2023/06/23 14:16:37 463f779966d68f201d421109f58f27740a5b666ddc9ea99a868c9ce3df8fbb638c8e0fac6e3a57332d95984c98ebb746a7d30780d299711d5f537268d004b029bfa3000aea253e7087943bfb8f3056ead5851a0367adb3bfb9f1295c730596372ad840a7066695ebcc643759181c4d0da7d135e829effd2125adf37fa334c798

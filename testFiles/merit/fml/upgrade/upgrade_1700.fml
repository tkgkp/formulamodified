:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_1700.fml
:: Utworzony: 17.03.2017
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 12.41 na 17.14
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalfikowana jako wykonana
::               -1 - całość przeszła z uwagami  - realizacja zakwalfikowana jako wykonana
::                0 - został stwierdzony błąd    - realizacja zakwalfikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Główna formuła upgrade-ów 17.00
::----------------------------------------------------------------------------------------------------------------------
_nofirm:={? REF.WFIRM || 0 || 1 ?};
:: zadania manualne
__UPG.add_task('users_imp','N','N','ZWS',0,'T',,_nofirm);
__UPG.add_task('transfer_all','N',,'ZWS',,'T');
__UPG.add_task('transfer_firm','N','N','ZWS',,'T');
~~


\transfer_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: wskazanie na formuły potransferowe wielofirmowe
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('transfer_all','transfer');
_res


\transfer_all_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Opis dla formuł potransferowych wielofirmowych
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja systemu z wersji Xpertis ERP - zadania wspólne'


\transfer_firm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: wskazanie na formuły potransferowe jednofirmowe
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('transfer','transfer');
_res


\transfer_firm_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Opis dla formuł potransferowych wielofirmowych
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja systemu z wersji Xpertis ERP - zadania dla każdej firmy'


\users_std
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Import domyślnych ról, użytkowników i ról dla użytkowników
::----------------------------------------------------------------------------------------------------------------------
_funpar:=obj_new(1);
_funpar[1]:='ZWS_USERS';
exec('import_init','#excel_imex',_funpar,,1,'Import użytkowników domyślnych (merit\\transfer\\usersstd)');
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['users_std'])
|| _result:=1
|| _result:=-1
?};
_result


\users_std_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Import domyślnych ról, użytkowników i ról dla użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Import domyślnych ról, użytkowników i ról dla użytkowników'


\users_xp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Import ról, użytkowników i ról dla użytkowników z Xpertis
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? FUN.ask('Czy importować role, użytkowników i role dla nich z Xpertis?'@)
|| _funpar:=obj_new(1);
   _funpar[1]:='ZWS_USERS';
   exec('import_init','#excel_imex',_funpar,,1,'Import użytkowników z Xpertis (merit\\transfer\\usersxp)')
?};
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['users_xp'])
|| _result:=1
|| _result:=-1
?};
_result


\users_xp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Import ról, użytkowników i ról dla użytkowników z Xpertis - opis
::----------------------------------------------------------------------------------------------------------------------
'Import ról, użytkowników i ról dla użytkowników z Xpertis'


\users_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.42]
:: OPIS: Import ról, użytkowników i ról dla użytkowników
::----------------------------------------------------------------------------------------------------------------------
exec('sort','transfer');
_funpar:=obj_new(1);
_funpar[1]:='ZWS_USERS';
exec('import_init','#excel_imex',_funpar,,1,'Import użytkowników(merit\\transfer\\usersimp)',,,'users.xlsx');
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['users_imp'])
|| _result:=1
|| _result:=-1
?};
:: dodanie ról dla admin-a - potrzebne do wykonania innych zadań aktualizacyjnych transfer
{? _result>0 & OPERATOR.USER().KOD='admin'
|| _buffer:=exec('buffer','#b_usrrol');
   _rola:=obj_new(2);
   _rola[1]:='Administrator'; _rola[2]:='Właściciel procesów';
   _refr:=obj_new(2);
   {! _ii:=1..2
   |! _refr[_ii]:=exec('ref','#b_role',_rola[_ii]);
      {? _refr[_ii]=null()
      || {? (_refr[_ii]:=exec('ref','#b_role',_rola[_ii],,1))<>null()
         || __UPG.msg('Rola ''%1'' została utworzona.' [_rola[_ii]])
         ?}
      ?};
      {? _refr[_ii]<>null()
      || FIRMA.cntx_psh();
         FIRMA.index('SYMBOL');
         {? FIRMA.first()
         || {!
            |? {? (FIRMA.TYP='S' | FIRMA.SYMBOL='000') & FIRMA.Z='N'
               || _buffer.FIRMA:=FIRMA.ref();
                  _buffer.USERS:=OPERATOR.USER;
                  _buffer.B_ROLE:=_refr[_ii];
                  {? _buffer.FIRMA<>null & _buffer.USERS<>null & _buffer.B_ROLE<>null
                  || B_USRROL.cntx_psh();
                     B_USRROL.index('UNIK');
                     B_USRROL.prefix(_buffer.FIRMA,_buffer.B_ROLE,_buffer.USERS);
                     _oki:=~B_USRROL.first();
                     B_USRROL.cntx_pop();
                     {? ~_oki
                     || _msg:='Użytkownik: %1 '
                              'już przypisany do roli: %2 '
                              'w firmie: %3'[OPERATOR.USER().KOD,_rola[_ii],FIRMA.SYMBOL];
                        __UPG.msg(_msg)
                     |? exec('add','#b_usrrol',_buffer)<>null()
                     || _msg:='Przypisano użytkownika: %1 '
                              'do roli: %2 '
                              'w firmie: %3'[OPERATOR.USER().KOD,_rola[_ii],FIRMA.SYMBOL];
                        __UPG.msg(_msg)
                     || _msg:='Nie udało się przypisać użytkownika: %1 '
                              'do roli: %2 '
                              'w firmie: %3'[OPERATOR.USER().KOD,_rola[_ii],FIRMA.SYMBOL];
                        __UPG.msg(_msg)
                     ?}
                  ?}
               ?};
               FIRMA.next()
            !}
         ?};
         FIRMA.cntx_pop()
      || __UPG.msg('Roli ''%1'' nie udało się utworzyć.'[_rola[_ii]])
      ?}
   !};
   obj_del(_rola);
   obj_del(_refr);
   obj_del(_buffer)
?};
_result


\users_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.42]
:: OPIS: Import ról, użytkowników i ról dla użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Import ról, użytkowników i ról dla użytkowników'

:Sign Version 2.0 jowisz:1045 2023/09/05 11:32:35 b6fd74fc9ebabe76574429cb883cb5aa5f69c5ec3d268c819f0237f299f52618bcdd535932c82c14d0a9304448f91a7cf3dd8a7337c755790cf8527069af4b4bf174a5415d364a343dbd2c460a3ad46db478fbe2275a6406f0825bfdb1f5dc9167dca1d5999a9694bad4b5f385c5e5e62c080bd78bae37f3af96bf287a4899f1

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_1922_03.fml
:: Utworzony: 19.08.2019
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych na wersję 19.22_03
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22_03]
:: OPIS: Główna formuła upgrade-ów 19.22_03
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('color',,,'ZWS',,,,,'T');
__UPG.add_task('funpar',,,'ZWS',,,,,'T');
__UPG.add_task('sp_l',,,'FKS',,,,,'T');
__UPG.add_task('M',,,'ZWS',,,,,'T');
__UPG.add_task('MPKW',,,'ZWS',,,,,'T');
:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('plugins',,'N','ZWS',,,,1,'T');
__UPG.add_task('sp_g',,'N','FKS',,,,1,'T');
:: Zadania manualne
__UPG.add_task('trans_sp','N','N','ZWS',,'N',,1,1);
~~


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',0)
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.')
?}


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\plugins
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Uzupełnia definicję wtyczek wdrożeniowych
::----------------------------------------------------------------------------------------------------------------------
exec('init','plugins');
__UPG.msg('Uzupełniono definicję wtyczek wdrożeniowych.');
1


\plugins_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Uzupełnia definicję wtyczek wdrożeniowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie definicji wtyczek wdrożeniowych:\n'
'FAKS_SP_001\n'


\M
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22_03]
:: OPIS: Uzupełnia M.SP_WYM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   _put:=0;
   {? M.SP_WYM='' || M.SP_WYM:='N'; _put:=1 ?};
   {? _put & M.put() || __lmod+=1 ?}
";
M.prefix();
_result:=M.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w kartotece materiałowej i usługowej.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w kartotece materiałowej i usługowej.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\M_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22_03]
:: OPIS: Uzupełnia M.SP_WYM - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki materiałowej i usługowej.'


\MPKW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22_03]
:: OPIS: Uzupełnia MPKW.SP_WYM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   _put:=0;
   {? MPKW.SP_WYM='' || MPKW.SP_WYM:='N'; _put:=1 ?};
   {? _put & MPKW.put() || __lmod+=1 ?}
";
MPKW.prefix();
_result:=MPKW.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w kartotece PKWiU.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w kartotece PKWiU.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\MPKW_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22_03]
:: OPIS: Uzupełnia MPKW.SP_WYM - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki PKWiU.'


\sp2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22_03]
:: OPIS: Uaktualnienie pól związanych z wymagalnością split payment
::   WE: _a - 1 - uruchomienie w dowolnej firmie
::            2 - uruchomienie w każdej firmie
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lmod,__lrec');
__lrec:=__lmod:=0;
_result:=1;
{? var_press('SP_WYM',DOK)<=0
|| __UPG.msg('Dane dotyczące Split Payment nie zostały uaktualnione. Brak naniesionej aktualizacji (19.22_03).');
   VAR_DEL.delete('__lmod,__lrec');
   return(0)
|| _tryb:=_a;
   {? _tryb=1
   || FIRMA.cntx_psh();
      FIRMA.index('SYMBOL2'); FIRMA.prefix('S');
      {? FIRMA.first()
      || {!
         |? {? FIRMA.Z='N'
            || ROK_F.index('ROKPOCZ'); ROK_F.prefix(FIRMA.ref());
               {? ROK_F.find_le(date(2019,1,1))
               || {!
                  |? OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
                     {? OKRO_F.first()
                     || {!
                        |? _maska:=ROK_F.KOD+form(OKRO_F.NR,-2);
                           DOK.cntx_psh();
                           DOK.use('doku'+_maska);
                           echo('Uaktualnienie dokumentów księgowych: %1'@[DOK.name()]);
                           DOK.prefix();
                           {? ~DOK.for_each("
                                 __lrec+=1;
                                 {? DOK.SP_WYM='' || DOK.SP_WYM:='N'; {? DOK.put() || __lmod+=1 ?} ?};
                                 ~~
                              ",1)
                           || __UPG.msg('Błąd podczas aktualizacji dokumentów księgowych: %1'[DOK.name()]);
                              _result:=0
                           ?};
                           VPOZ.cntx_psh();
                           VPOZ.use('pozv'+_maska);
                           echo('Uaktualnienie pozycji VAT dokumentów: %1'@[VPOZ.name()]);
                           VPOZ.prefix();
                           {? ~VPOZ.for_each("
                                 __lrec+=1;
                                 {? VPOZ.SP_WYM='' || VPOZ.SP_WYM:='T'; {? VPOZ.put() || __lmod+=1 ?} ?};
                                 ~~
                              ",1)
                           || __UPG.msg('Błąd poczas aktualizacji pozycji VAT dokumentów: %1'[VPOZ.name()]);
                              _result:=0
                           ?};
                           VPOZ.cntx_pop();
                           POZ.cntx_psh();
                           POZ.use('pozy'+_maska);
                           echo('Uaktualnienie pozycji dokumentów: %1'@[POZ.name()]);
                           POZ.prefix();
                           {? ~POZ.for_each("
                                 __lrec+=1;
                                 {? POZ.SP_WYM=''
                                 || POZ.SP_WYM:={? POZ.SP_KS='TT' || 'T' || 'N' ?};
                                    {? POZ.put() || __lmod+=1 ?}
                                 ?};
                                 ~~
                              ",1)
                           || __UPG.msg('Błąd podczas aktualizacji pozycji dokumentów: %1'[POZ.name()]);
                              _result:=0
                           ?};
                           POZ.cntx_pop();
                           DOK.cntx_pop();
                           OKRO_F.next()
                        !}
                     ?};
                     _maska:=ROK_F.KOD;
                     OP.cntx_psh();
                     ZAP_OP.cntx_psh();
                     POZ.cntx_psh();
                     DOK.cntx_psh();
                     ZAP_OP.use('rozzap'+_maska);
                     OP.use('operac'+_maska);
                     OP.prefix();
                     {? ~OP.for_each("
                           __lrec+=1;
                           {? OP.DSP=date(0,0,0)
                           || ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
                              {? ZAP_OP.first()
                              || {!
                                 |? _mask:=8+$ZAP_OP.POZDOK;
                                    POZ.use(_mask);
                                    DOK.use('doku'+(_mask+4));
                                    {? ZAP_OP.POZDOK().DOK().RVAT
                                    || OP.DSP:=DOK.DTO;
                                       {? OP.put() || __lmod+=1 ?}
                                    ?};
                                    OP.DSP=date(0,0,0) & ZAP_OP.next()
                                 !}
                              ?}
                           ?};
                           ~~
                        ",1)
                     || __UPG.msg('Błąd podczas aktualizacji rozrachunków: %1'[OP.name()]);
                        _result:=0
                     ?};
                     POZ.cntx_pop();
                     echo('Uaktualnienie zapisów rozrachunków: %1'@[ZAP_OP.name()]);
                     ZAP_OP.prefix();
                     {? ~ZAP_OP.for_each("
                           __lrec+=1;
                           {? ZAP_OP.SP_WYM=''
                           || ZAP_OP.SP_WYM:=1+ZAP_OP.SP_TYP;
                              {? ZAP_OP.put() || __lmod+=1 ?}
                           ?};
                           ~~
                        ",1)
                     || __UPG.msg('Błąd podczas aktualizacji zapisów rozrachunków: %1'[ZAP_OP.name()]);
                        _result:=0
                     ?};
                     ZAP_OP.cntx_pop();
                     OP.cntx_pop();
                     DVAT.cntx_psh();
                     DVAT.use('dvat__'+_maska);
                     echo('Uaktualnienie dokumentów VAT: %1'@[DVAT.name()]);
                     DVAT.prefix();
                     {? ~DVAT.for_each("
                           __lrec+=1;
                           {? DVAT.SP_WYM='' || DVAT.SP_WYM:='N'; {? DVAT.put() || __lmod+=1 ?} ?};
                           ~~
                        ",1)
                     || __UPG.msg('Błąd podczas aktualizacji dokumentów VAT: %1'[DVAT.name()]);
                        _result:=0
                     ?};
                     DVAT.cntx_pop();
                     EDOKUM.cntx_psh();
                     EDOKUM.use('skid_v'+_maska);
                     echo('Uaktualnienie dokumentów w obiegu: %1'@[EDOKUM.name()]);
                     EDOKUM.prefix();
                     {? ~EDOKUM.for_each("
                           __lrec+=1;
                           {? EDOKUM.SP_WYM='' || EDOKUM.SP_WYM:='N'; {? EDOKUM.put() || __lmod+=1 ?} ?};
                           ~~
                        ",1)
                     || __UPG.msg('Błąd poczas aktualizacji dokumentów w obiegu: %1'[EDOKUM.name()]);
                        _result:=0
                     ?};
                     EDOKUM.cntx_pop();
                     DOK.cntx_pop();
                     ROK_F.next()
                  !}
               ?}
            ?};
            FIRMA.next()
         !}
      ?};
      FIRMA.cntx_pop();
      echo('Uaktualnienie typów dokumentów w obiegu ...'@);
      ETYPY.cntx_psh();
      ETYPY.prefix();
      {? ~ETYPY.for_each("
            __lrec+=1;
            {? ETYPY.SP_WYM='' || ETYPY.SP_WYM:='N'; {? ETYPY.put() || __lmod+=1 ?} ?};
            ~~
         ",1)
      || __UPG.msg('Błąd podczas aktualizacji typów dokumentów w obiegu.');
         _result:=0
      ?};
      ETYPY.cntx_pop();
      {? _result
      || __UPG.msg('Uaktualniono dane dotyczące Split Payment - formuły wspólne dla wszystkich firm.')
      || __UPG.msg('Wystąpiły błędy podczas aktualizacji danych dotyczących Split Payment - formuły wspólne dla wszystkich firm.')
      ?}
   |? _tryb=2
   || ROK_F.cntx_psh();
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
      {? ROK_F.find_le(date(2019,1,1))
      || {!
         |? PB.cntx_psh();
            PB.use('pb'+(4+$ROK_F.POCZ_ROK));
            echo('Uaktualnienie przelewów: %1'@[PB.name()]);
            PB.prefix();
            {? ~PB.for_each("
                  __lrec+=1;
                  {? PB.SP_WYM='' || PB.SP_WYM:='N'; {? PB.put() || __lmod+=1 ?} ?};
                  ~~
               ",1)
            || __UPG.msg('Błąd poczas aktualizacji przelewów: %1'[PB.name()]);
               _result:=0
            ?};
            PB_OP.cntx_psh();
            ROK_F.cntx_psh();
            PB_OP.use('pop'+(4+$ROK_F.POCZ_ROK));
            PB_OP.prefix();
            echo('Uaktualnienie rozrachunków przelewów: %1'@[PB_OP.name()]);
            {? ~PB_OP.for_each("
                  __lrec+=1;
                  {? PB_OP.DSP=date(0,0,0)
                  || OP.cntx_psh();
                     {? exec('find_op','rozrach_przel')
                     || PB_OP.DSP:=OP.DSP; {? PB_OP.put() || __lmod+=1 ?}
                     ?};
                     OP.cntx_pop()
                  ?}
               ")
            || __UPG.msg('Błąd podczas aktualizacji rozrachunków przelewów: %1'[PB_OP.name()]);
               _result:=0
            ?};
            PB_OP.cntx_pop();
            PB.cntx_pop();
            ROK_F.cntx_pop();
            ROK_F.next()
         !}
      ?};
      ROK_F.cntx_pop();
      ROZLVAT.cntx_psh();
      echo('Uaktualnienie zawieszonych dokumentów VAT ...'@);
      ROZLVAT.prefix();
      {? ~ROZLVAT.for_each("
            __lrec+=1;
            {? ROZLVAT.SP_WYM='' || ROZLVAT.SP_WYM:='N'; {? ROZLVAT.put() || __lmod+=1 ?} ?};
            ~~
         ",1)
      || __UPG.msg('Błąd podczas aktualizacji zawieszonych dokumentów VAT.');
         _result:=0
      ?};
      ROZLVAT.cntx_pop();
      PB.cntx_psh();
      PB.use('pbxxxx');
      echo('Uaktualnienie przelewów: %1'@[PB.name()]);
      PB.prefix();
      {? ~PB.for_each("
            __lrec+=1;
            {? PB.SP_WYM='' || PB.SP_WYM:='N'; {? PB.put() || __lmod+=1 ?} ?};
            ~~
         ",1)
      || __UPG.msg('Błąd podczas aktualizacji przelewów: %1'[PB.name()]);
         _result:=0
      ?};
      PB_OP.cntx_psh();
      PB_OP.use('popxxxx');
      PB_OP.prefix();
      echo('Uaktualnienie rozrachunków przelewów: %1'@[PB_OP.name()]);
      {? ~PB_OP.for_each("
            __lrec+=1;
            {? PB_OP.DSP=date(0,0,0)
            || OP.cntx_psh();
               {? exec('find_op','rozrach_przel')
               || PB_OP.DSP:=OP.DSP; {? PB_OP.put() || __lmod+=1 ?}
               ?};
               OP.cntx_pop()
            ?}
         ")
      || __UPG.msg('Błąd podczas aktualizacji rozrachunków przelewów: %1'[PB_OP.name()]);
         _result:=0
      ?};
      PB_OP.cntx_pop();
      PB.cntx_pop();
      {? _result
      || __UPG.msg('Uaktualniono dane dotyczące Split Payment - formuły dla firmy.')
      || __UPG.msg('Wystąpiły błędy podczas aktualizacji danych dotyczących Split Payment - formuły dla firmy.')
      ?}
   ?};
   echo()
?};
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lmod,__lrec');
_result


\sp_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22_03]
:: OPIS: Uaktualnienie pól związanych z wymagalnością split payment, wszystkie firmy
::----------------------------------------------------------------------------------------------------------------------
exec('sp2','upgrade_1922_03',2)


\sp_g_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22_03]
:: OPIS: Uaktualnienie pól związanych z wymagalnością split payment, wszystkie firmy - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie pól związanych z wymagalnością split payment - wszystkie firmy.'


\sp_l
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22_03]
:: OPIS: Uaktualnienie pól związanych z wymagalnością split payment, bieżąca firma
::----------------------------------------------------------------------------------------------------------------------
exec('sp2','upgrade_1922_03',1)


\sp_l_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22_03]
:: OPIS: Uaktualnienie pól związanych z wymagalnością split payment, bieżąca firma - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie pól związanych z wymagalnością split payment - bieżąca firma.'


\trans_sp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [19.42]
:: OPIS: Tłumaczenie "Split payment" dla języka angielskiego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_opc:=1;
_opc:=FUN.choice('Należy dodać tłumaczenie "Split payment" dla języka angielskiego.\n'
                    'W (Ustawienia i parametryzacja -> Zakładka Parametryzacja -> pozycja  Tłumaczenia w gałęzi Wspólne -> Tłumaczenia wydruków sprzedaży i korygujących -> pozycja słownika tekstów tłumaczeń kod 1000170).\n'
                    'Czy dodano tłumaczenie do systemu?',,'Tak','Nie');
      {? _opc=1
      ||__UPG.msg('Dodano tłumaczenie "Split payment" dla kodu 1000170')
      ||{? _opc=2
        || __UPG.msg('Nie dodano tłumaczenia "Split payment" dla kodu 1000170');
           _opc:=0
        || __UPG.msg('Zrezygnowano z doania tłumaczenia "Split payment" dla kodu 1000170');
           _opc:=-1
        ?}
      ?};
_opc


\trans_sp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [19.42]
:: OPIS: Tłumaczenie "Split payment" dla języka angielskiego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dodać tłumaczenie "Split payment" dla języka angielskiego'

:Sign Version 2.0 jowisz:1045 2020/04/03 17:16:50 22911558b4a568d959f81120d2d4422ae5b3818f9e41a1a4b9de2ba36e8c3f4ffe3fa56faa7e063946488845db3892f54740c661257b19afff766f542a1b96d1b197405dd137fbda073bbb2c9dc4d764c63e761864fc1f164d9acab17aa51cd2f17298104c1b54d8e71c1e90ba87b8b71b14e7b4c1493f92f40e35e4091c128d

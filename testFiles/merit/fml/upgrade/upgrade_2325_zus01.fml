:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2325_zus01.fml
:: Utworzony: 02.08.2023
:: Autor: RN
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 22.26 na 23.25
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Główna formuła aktualizacji 23.25_ZUS01
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2023,8,3),time(0,0,0));

:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('atrybuty_placowe',,,'PPL',1,,,,'T');
__UPG.add_task('del_data',,,'PPL',1);
:: Zadania wykonywane dla każdej firmy osobno
:: Zadania manualne wykonywane raz dla całego systemu
:: Zadania manualne wykonywane dla każdej firmy osobno
__UPG.add_task('kody_zus_import','N','N','ZWS',,,,1,'T');
:: Wyłączenie zadań z poprzednich wersji
__UPG.off_task('atrybuty_placowe','=','22.26_zus01');
__UPG.off_task('kody_zus_import','=','22.26_zus01');
__UPG.off_task('del_data','=','22.26_zus01');
~~


\atrybuty_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Aktualizacja atrybutów płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('__RUB','object');
__RUB.fill();
:: Aktualizacja atrybutów rubryk. -------------------------------------------------------------------------------------

:: Dodanie atrybutów dla pracy zadalnej
exec('atrplaimex_decl','rubatr');
_atr:=obj_new(@.CLASS.AtrPlaImEx);
_atr.add_attr('g','19421,14131,14319',1);

RA_DEF.cntx_psh();
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
_dtUst:=date(2023,8,5);
{? RA_DEF.find_key(14319)
|| exec('use_add','rubatr',RA_DEF.ref(),__RUB.ref(32),_dtUst,'N');
   exec('use_add','rubatr',RA_DEF.ref(),__RUB.ref(33),_dtUst,'N')
?};
RA_DEF.cntx_pop();

__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\atrybuty_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Dodanie rubryk i atrybutów płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja atrybutów płacowych:\n'
' * 14319 "319 - urlop macierzyński w trakcie rodzicielskiego":\n'
'  ** dodanie wyłączonego użycia rubryki 32 "Urlop rodzicielski" od 05.08.2023.\n'
'  ** dodanie wyłączonego użycia rubryki 33 "Urlop rodzic. praca" od 05.08.2023.\n'
'Dodanie atrybutów płacowych:\n'
' * 14131 "131 - urlop opiekuńczy".'


\kody_zus_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Import Słowniki kodów ZUS.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('import_init','#excel_imex',spli_str('ZWS_PAR_PZUS',','));
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['kody_zus_import'])
|| __UPG.msg('Zaktualizowano element "Słowniki kodów ZUS".');
   _result:=1
|| _result:=-1
?};
_result


\kody_zus_import_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Import Słowniki kodów ZUS - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import aktualnych kodów zus do kartoteki "Słowniki kodów ZUS" uwzględniający\n'
'Rozporządzenie Ministra Rodziny i Polityki Społecznej z dnia 3 lipca 2023 r., zmieniające rozporządzenie '
'w sprawie określenia wzorów zgłoszeń do ubezpieczeń społecznych i ubezpieczenia zdrowotnego, imiennych '
'raportów miesięcznych i imiennych raportów miesięcznych korygujących, zgłoszeń płatnika składek, deklaracji '
'rozliczeniowych i deklaracji rozliczeniowych korygujących, zgłoszeń danych o pracy w szczególnych warunkach '
'lub o szczególnym charakterze, raportów informacyjnych, oświadczeń o zamiarze przekazania raportów '
'informacyjnych, informacji o zawartych umowach o dzieło oraz innych dokumentów.\n'
'Stan prawny na dzień: 5.08.2023 r.'


\del_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Naprawa rubryki 7215 w atrybucie 19421. ER/WRT/XP/23.25/2310/0040
::   WE:
::   WY: 1/-1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('__RUB','object');
__RUB.fill();

:: sprawdzenie czy rubryka istnieje
R.cntx_psh();
R.index('RUBKOD');
R.prefix(7215);
{? ~R.first() | ~__RUB.sys_attr(7215,19421,date(2023,8,1)) || _result:=-1 ?};
:: usuwanie daty jeśli istnieje
{? _result>0
|| _dtUst:=date(2023,8,5);
   RA_USE.cntx_psh();
   RA_USE.index('DEF_RN');
   RA_USE.prefix('S',19421,7215,_dtUst);
   {? RA_USE.first()
   || RA_USE.del()
   ?};
   RA_USE.cntx_pop()
?};
R.cntx_pop();

{? _result>0
|| __UPG.msg('Zakończono aktualizację rubryki 7215 w atrybucie 19421.')
|| __UPG.msg('Aktualizacja nie jest potrzebna - brak rubryki 7215 w atrybucie 19421.')
?};
_result


\del_data_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Naprawa rubryki 7215 w atrybucie 19421 - opis. ER/WRT/XP/23.25/2310/0040
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Zadanie usuwa zbędnie dodaną datę (2023/08/05) wyłączonego użycia rubryki 7215 "Urlop opiekuńczy" '
'(co powodowało błędne działanie mechanizmu generującego deklarację ZUS RSA) w '
'atrybucie 19421 "Warunkowo pomijane w zapytaniach sql".'


:Sign Version 2.0 jowisz:1045 2023/10/17 11:09:39 db977a86a2a5582af03ef3e149cff80d1434fd53f3a099400685c4d4a783dce51c550c17d4495879d183d14101941654c571eb7ac89c492cc90c51ce5e7fe4a623b83de9ea590b3c7629bbaa53efa74d26d1bec22f5a120c35ef62a6d73f7c3ec71a67a45fec06c2fd7df54bdb2b0a22ec40b1f63ed57d13a1bcfbdd4e3e168b

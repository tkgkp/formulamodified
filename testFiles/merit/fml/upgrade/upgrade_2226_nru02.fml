:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2226_nru02.fml
:: Utworzony: 14.07.2023
:: Autor: RWR [22.26]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.37 na 22.26
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Główna formuła aktualizacji 22.26_pzd02
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2023,8,25),time(0,0,0));
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('resend_r',,'N','POR',,,,1,'T');
:: Zadania manualne
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów.
::   WE: [_a] - dziedziny
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności (ciekawe, czy uda się alfabetycznie ...)
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.BIQ','.');
_dom_str:={? var_pres('_a')=type_of('')
          || _a
          || 'OBE'
          ?};
_dom:=spli_str(_dom_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\resend_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wymuszenie wysyłki definicji typów nieobecności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_run:="
   _ok:=1;
   _TR:=__RUB.sys_rub(_a);
   {? _TR.first()
   || {!
      |? {? ~R.find_key(_TR.RN)
         || __UPG.msg('Odnalezienie składnika płacowego o kodzie %1 nie powiodło się.'[$_TR.RN]);
            _ok:=0
         |? ~R.put(,1)
         || __UPG.msg('Zmiana znacznika modyfikacji składnika płacowego %1 - \"%2\" nie powiodła się.'[$R.RN,R.RT]);
            _ok:=0
         || __UPG.msg(
               'Zmiana znacznika modyfikacji składnika płacowego %1 - \"%2\" zakończyła się sukcesem.'[$R.RN,R.RT]
            )
         ?};
         _TR.next()
      !}
   || __UPG.msg('Brak składników płacowych z przypisanym atrybutem %1.'[$_a]);
      _ok:=0
   ?};
   _ok
";

exec('__RUB','object');

R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? _run(1181)*_run(1191)
|| _ret:=1;
   _new:=exec('portalwu_init','portal_nieobecnosci',1);
   {? _new<>''
   || __UPG.msg('-'*255);
      __UPG.msg('Lista kodów składników płacowych, dla których wymagana jest weryfikacja parametrów wniosków o urlop');
      __UPG.msg('(Ustawieniai prametryzacja → [zakładka] Parametryzacja → ');
      __UPG.msg(' HR Portal → Parametry działania → Parametry wniosków o urlop):');
      __UPG.msg(_new)
   ?}
|| _ret:=-1
?};
R.cntx_pop();

_ret


\resend_r_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wymuszenie wysyłki definicji typów nieobecności - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wymuszenie wysyłki definicji typów nieobecności dla:\n'
' ● urlopu opiekuńczego\n'
' ● siły wyższej'


:Sign Version 2.0 jowisz:1045 2023/08/25 11:56:17 776859f004216ae165965f10da18011055309c2de5bed39c57528e8b636488e0515b956ecaccaa14700792a97804a7ae04e1a30d64c519b62be0d35633656b706d692909499fc4765033ae7d0b1322d9e4f3a37ca478ffbfc79eff3c2e81dc547636b50a155ad26b15a76a672fa8e4cd621a18642737c4d2918204c4fdb46145

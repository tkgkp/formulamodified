:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2325_apo01.fml
:: Utworzony: 08.08.2023
:: Autor: IgPtasze [23.25]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 22.26 na 23.25
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Główna formuła aktualizacji 23.25_apo01
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2023,8,25),time(0,0,0));
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',2,,,,'T');
__UPG.add_task('eatr_upd_memo',,,'POR',,,,,'T');
__UPG.add_task('funpar',,,'ZWS',,,,,'T');
__UPG.add_task('id_sync',,,'ZWS',,,,1,'T');
__UPG.add_task('rd_url_op',,,'POR',,,,,'T');
__UPG.add_task('s_zus_r_11',,,'POR',,,,,'T');
__UPG.add_task('ppsft_er23100029',,,'POR',,,,,'T');

:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('role',,'N','ZWS',,,,1,'T');
__UPG.add_task('r_por_ww_por_nr',,'N','POR',,,,1,'T');
__UPG.add_task('resend_r',,'N','POR',1,,,1,'T');

:: Zadania manualne
__UPG.add_task('procesy_pzd03','N','N','POR',,,,1,'T');
__UPG.add_task('excel_wnioski_portalowe','N',,'ZWS',,,,1,'T');
__UPG.add_task('excel_wymiana_danych','N',,'ZWS',,,,1,'T');

:: Wyłączenie zadań z poprzednich wersji
__UPG.off_task('upgrade_act','=','22.26_pzd03');
__UPG.off_task('eatr_upd_memo','=','22.26_pzd03');
__UPG.off_task('funpar','=','22.26_pzd03');
__UPG.off_task('id_sync','=','22.26_pzd03');
__UPG.off_task('role','=','22.26_pzd03');
__UPG.off_task('r_por_ww_por_nr','=','22.26_pzd03');
__UPG.off_task('procesy_pzd03','=','22.26_pzd03');
__UPG.off_task('excel_wnioski_portalowe','=','22.26_pzd03');
__UPG.off_task('excel_wymiana_danych','=','22.26_pzd03');
__UPG.off_task('upgrade_act','=','22.26_nru02');
__UPG.off_task('resend_r','=','22.26_nru02');
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów.
::   WE: [_a] - dziedziny
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności (ciekawe, czy uda się alfabetycznie ...)
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.BIQ','.');
_dom_str:={? var_pres('_a')=type_of('')
          || _a
          || 'POR.ZWS.OBE'
          ?};
_dom:=spli_str(_dom_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami.
::----------------------------------------------------------------------------------------------------------------------
exec('role','upgrade_2226_pzd03')


\role_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami - opis.
::----------------------------------------------------------------------------------------------------------------------
exec('role_desc','upgrade_2226_pzd03')


\procesy_pzd03
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Import procesów zmienionych w aktualizacji 22.26_PZD03.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','POR_OOPZ_WWW,POR_PZDO_WWW','POR_')

\procesy_pzd03_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import procesów zmienionych w aktualizacji 22.26_PZD03 - opis.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować nowy proces dziedziny POR:\n'
' ● POR_OOPZ_WWW - Oświadczenie o pracy zdalnej (wersja 22.26)\n'
' ● POR_PZDO_WWW - Wniosek o świadczenie pracy zdalnej (wersja 22.26).\n'


\excel_wnioski_portalowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import wniosków portalowych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',,,0,,,1,'TAT_WP,ETYPPROC,ETYP_ATR_WP,ETYPY_WP,PORSLO,PORSLOIT');

_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack().name])
|| __UPG.msg('Zaktualizowano definicje wniosków portalowych.');
   _result:=1
|| _result:=-1
?};
_result


\excel_wnioski_portalowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import wniosków portalowych - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import wniosków portalowych:\n'
' ● OswiadczeniePracaZdalna - Oświadczenie o pracy zdalnej\n'
' ● ZdarzeniaDotPracy - Wniosek o świadczenie pracy zdalnej\n'


\excel_wymiana_danych
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import definicji wymiany danych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_DOK_SYNCHRO',','),,1);

_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack().name])
|| __UPG.msg('Zaktualizowano definicje wymiany danych.');
   _result:=1
|| _result:=-1
?};
_result


\excel_wymiana_danych_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import definicji wymiany danych - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import definicji wymiany dancyh:\n'
' ● Definicja PORTAL\n'
' ● Definicje synchronizacji z zewnętrznymi API\n'


\eatr_upd_memo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Uaktualnia pole domyślne na nowe dla atrybutów wielolinijkowych
::----------------------------------------------------------------------------------------------------------------------
exec('eatr_upd_memo','upgrade_2226_pzd03')


\eatr_upd_memo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Uaktualnia pole domyślne na nowe dla atrybutów wielolinijkowych - opis
::   WE: _a [NUMBER] - czy test? (0/1)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('eatr_upd_memo_desc','upgrade_2226_pzd03')


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Aktualizacja obszaru "Ustawienia i parametryzacja".
::----------------------------------------------------------------------------------------------------------------------
exec('funpar','upgrade_2226_pzd03')


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
exec('funpar_desc','upgrade_2226_pzd03')


\id_sync
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Dodaje definicje identyfikatorów rekordów
::----------------------------------------------------------------------------------------------------------------------
exec('id_sync','upgrade_2226_pzd03')


\id_sync_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Dodaje definicje identyfikatorów rekordów - opis
::----------------------------------------------------------------------------------------------------------------------
exec('id_sync_desc','upgrade_2226_pzd03')


\r_por_ww_por_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Uzupełnia nowe pole POR_NR w tabeli R_POR_WW
::----------------------------------------------------------------------------------------------------------------------
exec('r_por_ww_por_nr','upgrade_2226_pzd03')


\r_por_ww_por_nr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: Uzupełnia nowe pole POR_NR w tabeli R_POR_WW - opis
::----------------------------------------------------------------------------------------------------------------------
exec('r_por_ww_por_nr_desc','upgrade_2226_pzd03')


\resend_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: Wymuszenie wysyłki definicji wybranych typów nieobecności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_run:="
   _ok:=1;
   _TR:=__RUB.sys_rub(_a);
   {? _TR.first()
   || {!
      |? {? ~R.find_key(_TR.RN)
         || __UPG.msg('Odnalezienie składnika płacowego o kodzie %1 nie powiodło się.'[$_TR.RN]);
            _ok:=0
         |? ~R.put(,1)
         || __UPG.msg('Zmiana znacznika modyfikacji składnika płacowego %1 - \"%2\" nie powiodła się.'[$R.RN,R.RT]);
            _ok:=0
         || __UPG.msg(
               'Zmiana znacznika modyfikacji składnika płacowego %1 - \"%2\" zakończyła się sukcesem.'[$R.RN,R.RT]
            )
         ?};
         _TR.next()
      !}
   || __UPG.msg('Brak składników płacowych z przypisanym atrybutem %1.'[$_a]);
      _ok:=0
   ?};
   _ok
";

exec('__RUB','object');

R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? _run(1181)*_run(1191)*_run(1111)*_run(1112)*_run(1162)*_run(1161)*_run(1113)
:: SW         U.opiek.   U.n.żąd.    U.wypocz. Art.188    U.okol.    U.dodat.
|| _ret:=1;
   _new:=exec('portalwu_init','portal_nieobecnosci',1);
   {? _new<>''
   || __UPG.msg('-'*255);
      __UPG.msg('Lista kodów składników płacowych, dla których wymagana jest weryfikacja parametrów wniosków o urlop');
      __UPG.msg('(Ustawieniai prametryzacja → [zakładka] Parametryzacja → ');
      __UPG.msg(' HR Portal → Parametry działania → Parametry wniosków o urlop):');
      __UPG.msg(_new)
   ?}
|| _ret:=-1
?};
R.cntx_pop();

_ret


\resend_r_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: Wymuszenie wysyłki definicji wybranych typów nieobecności - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wymuszenie wysyłki definicji wybranych typów nieobecności.'


\rd_url_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: ER/WRT/XP/23.25/2309/0017: Portal: poprawka zbiorcza (urlop opiekuńczy, prezentacja limitów, praca zdalna).
::       Wymuszenie ponownej wysyłki danych członków rodziny, na których może zostać udzielony urlop opiekuńczy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
RD.cntx_psh();
RD.prefix();
RD.f_set('OSOBA'
   ,'join "S_ZUS" as "STPK" using("RD".STPK,"STPK".REFERENCE)'
   ,'"RD".OSOBA in (select P.OSOBA from P where P.PORTAL=\'T\') and "STPK".KOD in (\'01\',\'11\',\'30\',\'31\')'
);
{? RD.f_first()
|| _ile:=0;
   {!
   |? {? RD.put(,1)
      || _ile+=1
      || _ret:=-1
      ?};
      _ret & RD.f_next()
   !};
   {? _ret
   || __UPG.msg('Liczba rekordów wytypowanych do ponownej wysyłki na portal: %1.'[$_ile])
   || __UPG.msg('Zmiana znacznika modyfikacji nie powiodła się. Operację należy powtórzyć.')
   ?}
|| __UPG.msg('Nie znaleziono rekordów wymagających ponownej wysyłki.')
?};
RD.f_clear();
RD.cntx_pop();
_ret


\rd_url_op_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: ER/WRT/XP/23.25/2309/0017: Portal: poprawka zbiorcza (urlop opiekuńczy, prezentacja limitów, praca zdalna).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wymuszenie ponownej wysyłki danych członków rodziny, na których może zostać udzielony urlop opiekuńczy.'


\s_zus_r_11
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: ER/WRT/XP/23.25/2309/0017: Portal: poprawka zbiorcza (urlop opiekuńczy, prezentacja limitów, praca zdalna).
::       Wymuszenie wysyłki definicji stopnia pokrewieństwa dla kodu 11.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
S_ZUS.cntx_psh();
S_ZUS.index('S_ZUS');
S_ZUS.prefix('R','11',);
{? S_ZUS.first()
|| {? S_ZUS.put(,1)
   || __UPG.msg('Zmiana znacznika modyfikacji zakończyła się sukcesem. Rekord zaznaczony do ponownej wysyłki.')
   || __UPG.msg('Zmiana znacznika modyfikacji nie powiodła się. Operację należy powtórzyć.');
      _ret:=-1
   ?}
|| __UPG.msg('Słownik kodów ZUS stopnia pokrewieństwa nie zawiera kodu 11. Ponowna wysyłka nie jest wymagana.')
?};
S_ZUS.cntx_pop();
_ret


\s_zus_r_11_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: ER/WRT/XP/23.25/2309/0017: Portal: poprawka zbiorcza (urlop opiekuńczy, prezentacja limitów, praca zdalna).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wymuszenie wysyłki definicji stopnia pokrewieństwa dla kodu 11.'


\ppsft_er23100029
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: ER/WRT/XP/23.25/2310/0029: Problem z rejestracja wniosków i WE/WY dla typów pracy, gdzie wymagane jest
::       oświadczenie (zmiana założeń dla obsługi metody chr_WorkRelatedEventTypeModify).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
PPSFT.cntx_psh();
PPSFT.index('TYP');
PPSFT.prefix();
{? PPSFT.first()
|| PPSFT.trig_off('*','*');
   {!
   |? _ret*=PPSFT.put(,1);
      PPSFT.next()
   !};
   PPSFT.trig_on('*','*');
   {? _ret
   || __UPG.msg('Zmiana znaczników modyfikacji zakończyła się sukcesem. Rekordy zaznaczone do ponownej wysyłki.')
   || __UPG.msg('Zmiana znaczników modyfikacji nie powiodła się. Operację należy powtórzyć.');
      _ret:=-1
   ?}
|| __UPG.msg('Słownik typów pracy poza siedzibą firmy jest pusty. Ponowna wysyłka nie jest wymagana.')
?};
PPSFT.cntx_pop();
_ret


\ppsft_er23100029_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: ER/WRT/XP/23.25/2310/0029: Problem z rejestracja wniosków i WE/WY dla typów pracy, gdzie wymagane jest
::       oświadczenie (zmiana założeń dla obsługi metody chr_WorkRelatedEventTypeModify).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/23.25/2310/0029:\n'
'Problem z rejestracja wniosków i WE/WY dla typów pracy, gdzie wymagane jest\n'
'oświadczenie (zmiana założeń dla obsługi metody chr_WorkRelatedEventTypeModify).'

:Sign Version 2.0 jowisz:1045 2023/10/18 08:50:50 d99d75327fe973572e6b126e6b6e23faeabdba10c4057ba2ad1eb71e6796149e9234fb65c6699946a776509fc95978051f5d930ab8789206b8af77b4dc9b56c822cbb393454db8b111a6e5c0fb8ad56ee4f8fb97221a2f78cac5fb61d8417ae5abcf21b0baa753bb402daef71549bf6fd70247f3aca85fbad80d75117fc7129c

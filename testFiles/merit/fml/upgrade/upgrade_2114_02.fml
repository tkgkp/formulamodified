:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2114_02.fml
:: Utworzony: 15.04.2021
:: Autor: [darokr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 20.42 na 21.14
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [DAROKR] [21.14]
:: OPIS: Główna formuła aktualizacji 21.14_02
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('edeklaracje_2114_02',,,'PPL',,,,,'T');
__UPG.add_task('rep_tran',,,'PPL',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('vat_dek_2114_02',,'N','PPL',,,,1,'T');
:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
~~


\edeklaracje_2114_02
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.14]
:: OPIS: Dodanie nowych e-deklaracji obowiązujących w 2021 dotyczące przychodów od 01.01.2021
::       oraz nowych rodzajów umow zleceń 'v' - 'staże uczniowskie' i 'y' - 'praktyki absolwenckie'
::----------------------------------------------------------------------------------------------------------------------
::sprawdzenie czy są rodzaje 'v' i 'y' umów zleceń w słowniku RU
_uz_il:=0;
_typuz:=exec('slo_typ','ext_slo','UMZLEC');
{? _typuz<>null()
|| RU.cntx_psh();
   RU.index('K');
   RU.prefix(_typuz);
   _kod:='y';
   _opis:='praktyki absolwenckie';
   {? ~RU.find_key(_kod,_kod)
   || RU.blank();
      RU.TYP:=_typuz;
      RU.K:=_kod;
      RU.O:=_opis;
      RU.PKU:=0;
      RU.PPD:=17;
      RU.ZWPOD:='T';
      {? RU.add(1)
      || _uz_il+=1;
         __UPG.msg('Dodano nowy rodzaj umowy cywilnoprawnej: %1 - %2.'[_kod,_opis])
      || __UPG.msg('Nie udało się dodać nowego rodzaju umowy cywilnoprawnej: %1 - %2.'[_kod,_opis])
      ?}
   || __UPG.msg('Rodzaj umowy cywilnoprawnej: %1 - %2 już istnieje. Należy zweryfikować dane.'[RU.K,RU.O])
   ?};
   _kod:='v';
   _opis:='staże uczniowskie';
   {? ~RU.find_key(_kod,_kod)
   || RU.blank();
      RU.TYP:=_typuz;
      RU.K:=_kod;
      RU.O:='staże uczniowskie';
      RU.PKU:=0;
      RU.PPD:=17;
      RU.ZWPOD:='T';
      {? RU.add(1)
      || _uz_il+=1;
         __UPG.msg('Dodano nowy rodzaj umowy cywilnoprawnej: %1 - %2.'[_kod,_opis])
      || __UPG.msg('Nie udało się dodać nowego rodzaju umowy cywilnoprawnej: %1 - %2.'[_kod,_opis])
      ?}
   || __UPG.msg('Rodzaj umowy cywilnoprawnej: %1 - %2 już istnieje. Należy zweryfikować dane.'[RU.K,RU.O])
   ?};
   RU.cntx_pop()
?};
_dod_ver:="
   VAT_VER.index('VER_OD'); VAT_VER.prefix(_a,_c,_c,_b,_d);
   _jest:=VAT_VER.first();
   {? ~_jest
   || _old:={? _a='FKS' || 'FIKS' |? _a='PPL' || 'KALI' || '' ?};
      VAT_VER.prefix(_old,_c,_c,_b,_d);
      _jest:=VAT_VER.first()
   ?};
   {? _jest
   || _add:=0
   || _add:=1;
      VAT_VER.blank();
      VAT_VER.SYSTEM:=_a;
      VAT_VER.OD:=_b;
      VAT_VER.NAZWA:=_c;
      VAT_VER.NR:=_d
   ?};
   {? _<6 || _f:='' ?};
   {? _<5 || _e:=0 ?};
   _put:=0;
   {? VAT_VER.NRF<>_e || VAT_VER.NRF:=_e; _put+=1 ?};
   {? VAT_VER.WIERSZ<>_f || VAT_VER.WIERSZ:=_f; _put+=1 ?};
   VAT_VER.prefix();
   {? _add || VAT_VER.add(1) |? _put || VAT_VER.put(1) ?}
";
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
{? _dod_ver('PPL',date(2021,3,4),'PIT11',12,27,',3,4,5,7,8,10,')
|| __UPG.msg('Dodano 12 wersja e-deklaracji PIT-11 obowiązująca od dnia 2021.03.04 dla przychodów od 2021.01.01')
|| __UPG.msg('Brak aktualizacji, 12 wersja e-deklaracji PIT-11 już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-11 wersja 12
{? exec('edek_stru','edeklar','PIT11',12,'pit-11_27_v1.dfg')
|| __UPG.msg('Dodano schemat 12 wersji e-deklaracji PIT-11.')
|| __UPG.msg('Brak aktualizacji, schemat 12 wersji e-deklaracji PIT-11 już istnieje.')
?};
{? _dod_ver('PPL',date(2021,4,2),'PIT4R',12,12,',2,3,4,6,7,9,')
|| __UPG.msg('Dodano 12 wersja e-deklaracji PIT-4R obowiązująca od dnia 2021.4.2 dla przychodów od 2021.01.01')
|| __UPG.msg('Brak aktualizacji, 12 wersja e-deklaracji PIT-4R już istnieje.')
?};
_wakt:=',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,';
{? _dod_ver('PPL',date(2021,4,2),'PIT8AR',12,11,_wakt)
|| __UPG.msg('Dodano 12 wersja e-deklaracji PIT-8AR obowiązująca od dnia 2021.4.2 dla przychodów od 2021.01.01')
|| __UPG.msg('Brak aktualizacji, 12 wersja e-deklaracji PIT-8AR już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-4R wersja 12
{? exec('edek_stru','edeklar','PIT4R',12,'pit-4r12_v1.dfg')
|| __UPG.msg('Dodano schemat 12 wersji e-deklaracji PIT-4R.')
|| __UPG.msg('Brak aktualizacji, schemat 12 wersji e-deklaracji PIT-4R już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-8AR wersja 12
{? exec('edek_stru','edeklar','PIT8AR',12,'pit-8ar12_v1.dfg')
|| __UPG.msg('Dodano schemat 12 wersji e-deklaracji PIT-8AR.')
|| __UPG.msg('Brak aktualizacji, schemat 12 wersji e-deklaracji PIT-8AR już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
1


\edeklaracje_2114_02_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.42]
:: OPIS: Dodanie nowych e-deklaracji obowiązujących w 2021 dotyczące przychodów od 01.01.2021
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych e-deklaracji obowiązujących w 2021 dotyczące przychodów od 01.01.2021'


\rep_tran
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.14]
:: OPIS: Kompilacja wydruków dziedziny PPL.
::----------------------------------------------------------------------------------------------------------------------
{? rep_tran('ppl*')<>0
|| __UPG.msg('Zaktualizowano wydruki dziedziny PPL.');
   1
|| __UPG.msg('Aktualizacja wydruków dziedziny PPL nie powiodła się.');
   -1
?}


\rep_tran_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.14]
:: OPIS: Kompilacja wydruków dziedziny PPL - opis
::----------------------------------------------------------------------------------------------------------------------
'Kompilacja wydruków dziedziny PPL.'


\vat_dek_2114_02
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.14]
:: OPIS: Uzupełnienie nowych pól w tabeli VAT_DEK - formuła naprawcza.
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_stat:=obj_new('poprawiono','niepoprawiono','niewymaga');
   {! _lp:=1..obj_len(_stat)
   |! _stat[_lp]:=0
   !};
params_set('stat',_stat);
VAT_DEK.trig_off('*','*');
VAT_DEK.clear();
VAT_DEK.for_each("_stat:=params_get();
  _put:=0;
  {? VAT_DEK.PDOF1='' || VAT_DEK.PDOF1:='N'; _put+=1 ?};
  {? VAT_DEK.PDOF2='' || VAT_DEK.PDOF2:='N'; _put+=1 ?};
  {? VAT_DEK.PDOF3='' || VAT_DEK.PDOF3:='N'; _put+=1 ?};
  {? VAT_DEK.PDOF4='' || VAT_DEK.PDOF4:='N'; _put+=1 ?};
  {? VAT_DEK.PDOF5='' || VAT_DEK.PDOF5:='N'; _put+=1 ?};
  {? VAT_DEK.PDOF6='' || VAT_DEK.PDOF6:='N'; _put+=1 ?};
  {? _put
  || {? VAT_DEK.put()
     || _stat.stat.poprawiono+=1
     || _stat.stat.niepoprawiono+=1
     ?}
  || _stat.stat.niewymaga+=1
  ?}",1
);
VAT_DEK.trig_on('*','*');
__UPG.msg('Liczba wierszy wymagających poprawienia: %1.' [$(_stat.poprawiono+_stat.niepoprawiono)]);
__UPG.msg('Liczba wierszy poprawionych: %1.' [$_stat.poprawiono]);
__UPG.msg('Liczba wierszy, których nie udało się poprawić: %1.' [$_stat.niepoprawiono]);
__UPG.msg('Liczba wierszy niewymagających poprawienia: %1.' [$_stat.niewymaga]);
_ret:={? _stat.niepoprawiono>0 || -1 || 1 ?};
_ret


\vat_dek_2114_02_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.14]
:: OPIS: Uzupełnienie nowych pól w tabeli VAT_DEK - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie nowych pól w tabeli VAT_DEK: VAT_DEK.PDOF1 do VAT_DEK.PDOF6 w aktualizacja Merit 2114_02.'

:Sign Version 2.0 jowisz:1045 2021/06/29 07:48:42 e0f078c05ff55e06cf7e74b6b52ffd29e6689980369d8569ad6c9c64dbf0461e7dcf26bd1961accb04c9290e28b1048f4f1a67f6f86e90a82775504cf7de1a7d0ab891f7b0d9f83d0257cf557099815bb7300938d028715947f3f6ef62e2fa04de753b5a2fc62094c6352d28fc918bacec85da634e049827e0060bc39e5a24b3

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2014.fml
:: Utworzony: 16.10.2019
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 19.42 na 20.14
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Główna formuła upgrade-ów 20.14
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',2,,,,'T');
__UPG.add_task('color',,,'ZWS',2,,,,'T');
__UPG.add_task('funpar',,,'ZWS',2,,,,'T');
__UPG.add_task('abstore_m',,,'ZWS');
__UPG.add_task('stalesys_ini',,,'ZWS',2,,,,'T');
__UPG.add_task('xinfo_ter_widok',,,'ZWS');
__UPG.add_task('b_event',,,'ZWS');
__UPG.add_task('id_sync',,,'ZWS');
__UPG.add_task('kh',,,'ZWS');
__UPG.add_task('kst_kus',,,'ZWS');
__UPG.add_task('report',,,'ZWS');
__UPG.add_task('areatitle',,,'ZWS',2);
__UPG.add_task('kh_osob',,,'ZWS');
__UPG.add_task('zdarzt',,,'ZWS');
__UPG.add_task('dokum',,,'ZWS');
__UPG.add_task('KH_ODB',,,'ZWS');
__UPG.add_task('upd_par76',,,'ZWS');
__UPG.add_task('SK_CHO_ZUS_Z3',,,'PPL');
__UPG.add_task('del_par',,,'ZWS');
__UPG.add_task('tlumaczenia',,,'ZWS');
__UPG.add_task('fiks_66',,,'FKS',1,,,,'T');
__UPG.add_task('fiks_67',,,'FKS',1,,,,'T');
__UPG.add_task('akt_2014_04',,,'PPL');
__UPG.add_task('fiks_71',,,'FKS',1,,,,'T');
__UPG.add_task('fiks_68',,,'FKS',1,,,,'T');
__UPG.add_task('fiks_70',,,'FKS',1,,,,'T');
__UPG.add_task('akt_2014_09',,,'PPL');
__UPG.add_task('szablony_adres',,,'ZWS',,,,,'T');
__UPG.add_task('akt_2014_12',,,'FKS');
__UPG.add_task('szablony_akt_zes',,,'ZWS',,,,,'T');
__UPG.add_task('zmiana_R_7093',,,'PPL',,,,,'T');
__UPG.add_task('vat_ue_f',,,'FKS',,,,'T');
__UPG.add_task('szablony_akt_zes2',,,'ZWS',,,,,'T');
__UPG.add_task('kal_def_typws',,,'ZWS',,,,,'T');
__UPG.add_task('rubryka_7124',,,'PPL',,,,,'T');
__UPG.add_task('szablony_akt_zes3',,,'ZWS',,,,,'T');
__UPG.add_task('fiks_73',,,'FKS',1,,,,'T');
__UPG.add_task('edeklaracje_2020_07',,,'PPL',,,,,'T');
__UPG.add_task('attr_use_RP7',,,'PPL',,,,,'T');
__UPG.add_task('edokos_wpr',,,'ZWS',,,,,'T');
__UPG.add_task('rubryka_150_CHO',,,'PPL',,,,,'T');
:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('REM_ZGL',,'N','TRE',,,,1);
__UPG.add_task('potwZAM',,'N','LZK',,,,1);
__UPG.add_task('bado',,'N','TTE',,,,1);
__UPG.add_task('zlim',,'N','TTE',,,,1);
__UPG.add_task('tmat',,'N','TTE',,,,1);
__UPG.add_task('zws_par_kkot',,'N','ZWS',,,,1);
__UPG.add_task('zws_par_kbad',,'N','ZWS',,,,1);
__UPG.add_task('KPO',,'N','LUO',,,,1);
__UPG.add_task('kkh_kon_pkon',,'N','ZWS',,,,1);
__UPG.add_task('kkh_kon_drko',,'N','ZWS',,,,1);
__UPG.add_task('dokum_kh_osob',,'N','ZWS',,,,1);
__UPG.add_task('tte_wyk_dwzl',,'N','TTE',,,,1);
__UPG.add_task('pkd_zes_inne',,'N','PKD',,,,1);
__UPG.add_task('KR2AR',,'N','TTE',,,,1);
__UPG.add_task('TR_NZL',,'N','LTR',,,,1);
__UPG.add_task('SYNC_DEF',,'N','ZWS',,,,1);
__UPG.add_task('FAKS',,'N','LSP',,,,1);
__UPG.add_task('FAP',,'N','LSP',,,,1);
__UPG.add_task('ND',,'N','LMG',,,,1);
__UPG.add_task('logo',,'N','ZWS',,,,0);
__UPG.add_task('PL_WZOR',,'N','TPP',,,,1);
__UPG.add_task('PL_OPIS',,'N','TPP',,,,1);
__UPG.add_task('PL_WZORU',,'N','TPP',,,,1);
__UPG.add_task('ZLP',,'N','LUM',,,,1);
__UPG.add_task('TTOPER',,'N','TTE',,,,1);
__UPG.add_task('TOPER',,'N','TTE',,,,1);
__UPG.add_task('ZGP',,'N','TTE',,,,1);
__UPG.add_task('LUM_ZGL_PZGL',,'N','ZWS',,,,1);
__UPG.add_task('UMLOJN_zak_kon',,'N','PKD',,,,1);
__UPG.add_task('rok_f_akt_roz',,'N','ZWS',,,,1);
__UPG.add_task('REM_ZAS',,'N','TRE',,,,1);
__UPG.add_task('f_zatra_add',,'N','ZWS',,,,1);
__UPG.add_task('role_personel',,'N','PKD',,,,1);
__UPG.add_task('PROD_REJ_OPIS',,'N','TTE',,,,1);
__UPG.add_task('FAKS_ERR_2006_0027',,'N','LSP',,,,1);
__UPG.add_task('zal_ucp',,'N','ZWS',,,,1,'T');
__UPG.add_task('ppk_06',,'N','PPK',,,,1,'T');
:: Zadania manualne
__UPG.add_task('szablony','N',,'ZWS',7,,,1,'T');
__UPG.add_task('formuly_PRZESTOJ','N','N','PPL',1,,,1,'T');
__UPG.add_task('kal_def_dataw','N','N','ZWS',2,,,1,'T');
~~


\potwZAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Uzupełnia zapisy dla ZDP_POZ oraz ZDP_NAG
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__lrecZ','__lmodZ');

__lrec:=__lmod:=__lrecZ:=__lmodZ:=0;
_oddz:=ST.ODDZ;
_msk:=ZDP_NAG.names();
exec('openzd_psh','open_tab');
_msk.clear();
{? _msk.first()
|| {!
   |? exec('openzd','open_tab',_msk.NAME+3);
      ST.ODDZ:=1+(_msk.NAME+3);
      ZDP_NAG.prefix();
      ZDP_NAG.for_each("
         _put:=0;
         __lrecZ+=1;
         {? ZDP_NAG.R=0 || _put:=1; ZDP_NAG.R:=ZDP_NAG.D_WYS~1 ?};
         {? ZDP_NAG.M=0 || _put:=1; ZDP_NAG.M:=ZDP_NAG.D_WYS~2 ?};
         {? _put || {? ZDP_NAG.put() || __lmodZ+=1 ?} ?};
         ZDP_POZ.index('ZDP_NAG');
         ZDP_POZ.prefix(ZDP_NAG.ref());
         {? ZDP_POZ.first()
         || {!
            |? _put:=0;
               __lrec+=1;
               {? ZDP_POZ.KH=null()
               || _put:=1; ZDP_POZ.KH:=exec('FindAndGet','#table',ZD_POZ,ZDP_POZ.ZD_POZ,,\"KH\",null())
               ?};
               {? ZDP_POZ.KH_MSC=null()
               || _put:=1; ZDP_POZ.KH_MSC:=exec('FindAndGet','#table',ZD_POZ,ZDP_POZ.ZD_POZ,,\"KH_MSC\",null())
               ?};
               {? ZDP_POZ.M=null()
               || _put:=1; ZDP_POZ.M:=exec('FindAndGet','#table',ZD_POZ,ZDP_POZ.ZD_POZ,,\"M\",null())
               ?};
               {? ZDP_POZ.JM=null() || _put=1; ZDP_POZ.JM:=ZDP_POZ.M().J ?};
               {? _put || {? ZDP_POZ.put() || __lmod+=1 ?} ?};
               ZDP_POZ.next()
            !}
         ?}
      ");
      _msk.next()
   !}
?};
exec('openzd_pop','open_tab');
__UPG.msg('Zaktualizowano zapisy dla nagłówków potwierdzeń zamówień dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrecZ,$__lmodZ]);
__UPG.msg('Zaktualizowano zapisy dla pozycji potwierdzeń zamówień dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);
ST.ODDZ:=_oddz;

VAR_DEL.delete('__lrec','__lmod','__lrecZ','__lmodZ');
_res


\potwZAM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Uzupełnia zapisy dla ZDP_POZ oraz ZDP_NAG - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów dla pozycji i nagłówków potwierdzeń zamówień dostaw.'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',1)
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.')
?}


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\bado
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli BADO
::----------------------------------------------------------------------------------------------------------------------
_result:=BADO.for_each("
   {? BADO.ZGP<>null()
   || ZGP.cntx_psh();
      BADO.ZGP();
      BADO.ILP:=BADO.IL_BRAK-ZGP.IL_BRAK-ZGP.DEK_BR;
      BADO.put();
      ZGP.cntx_pop()
   ?}
",1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola rozpiski badań do niezarejestrowanych wykonań.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól rozpiski badań do niezarejestrowanych wykonań.')
?};
_result


\bado_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli BADO - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli badań do niezarejestrowanych wykonań.'


\abstore_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli M
::----------------------------------------------------------------------------------------------------------------------
_result:=M.for_each("
   {? M.ABSTORE<>''
   || M.cntx_psh();
      M.ABSTOREC:=M.ABSTORE;
      M.put();
      M.cntx_pop()
   ?}
",1);
{? _result
||
   _result:=MGR.for_each("
      {? MGR.ABSTORE<>''
      || MGR.cntx_psh();
         MGR.ABSTOREC:=MGR.ABSTORE;
         MGR.put();
         MGR.cntx_pop()
      ?}
   ",1)
?};
{? _result
|| __UPG.msg('Zaktualizowano w kartotece materiałowej nowe pola integracyjne z AbStore.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól integracyjnych z AbStore w kartotece materiałowej.')
?};
_result


\abstore_m_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli M - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli kartoteki materiałowej, uzupełnienie nowych pól związanych z integracją z AbStore.'


\REM_ZGL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Wypełnia pola w tabeli REM_ZGL
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
REM_ZGL.cntx_psh();
REM_ZGL.prefix();
_res:=REM_ZGL.for_each("
   __lrec+=1;
   _put:=0;
   {? REM_ZGL.PL_DOST=''
   || REM_ZGL.PL_DOST:='B';
      _put:=1
   ?};

   {? REM_ZGL.PLAN_PX=''
   || REM_ZGL.PLAN_PX:='N';
      _put:=1
   ?};

   {? REM_ZGL.PLAN_PL=''
   || REM_ZGL.PLAN_PL:='N';
      _put:=1
   ?};

   {? REM_ZGL.PL_TRYB=''
   || REM_ZGL.PL_TRYB:='A';
      _put:=1
   ?};

   {? _put>0
   || {? REM_ZGL.put()
      || __lmod+=1
      ?}
   ?}
");
REM_ZGL.cntx_pop();
__UPG.msg('Zaktualizowano zgłoszenia remontowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\REM_ZGL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Wypełnia pola w tabeli REM_ZGL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zgłoszeń remontowych'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\zlim
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli ZLIM
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_msk:=ZLIM.names();
_msk.clear();
ZLIM.trig_off('*', '*');
{? _msk.first()
||
   {!
   |? ZLIM.cntx_psh();
      ZLIM.use(_msk.NAME);
      ZLIM.prefix();
      ZLIM.for_each("
      {? ZLIM.ROZ<>'T'
      || ZLIM.ROZ:='N';
         ZLIM.put()
      ?}
      ",1);
      ZLIM.cntx_pop();
      _msk.next()
   !}
?};
ZLIM.trig_on('*', '*');
__UPG.msg('Zaktualizowano nowe pola limitów surowców zleceń.');
_result


\zlim_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli ZLIM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli limitów surowców zleceń.'


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [20.14]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.OBE','.');
::_dom:=spli_str('LSP.LZK.PKD.PPL.TRE.PBA.LTR.TTE.ZWS.PED.LMG.LUM.PRC.HBN.OBE','.');
_str:={? var_pres('_a')>0 || _a || 'ZUI.TTE.ZWS.LTR.KKH.PKD.PKW.LMG.LSP.LZK.ZPR.LMO.OBE.OBG' ?};
_dom:=spli_str(_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.'@,,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [20.14]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\tmat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli TMAT
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_msk:=TMAT.names();
_msk.clear();
TMAT.trig_off('*', '*');
{? _msk.first()
||
   {!
   |? TMAT.cntx_psh();
      TMAT.use(_msk.NAME);
      TMAT.prefix();
      TMAT.for_each("
      {? TMAT.ROZ<>'T'
      || TMAT.ROZ:='N';
         TMAT.put()
      ?}
      ",1);
      TMAT.cntx_pop();
      _msk.next()
   !}
?};
TMAT.trig_on('*', '*');
__UPG.msg('Zaktualizowano nowe pola surowców technologii.');
_result


\tmat_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli TMAT - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli surowców technologii.'


\xinfo_ter_widok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Aktualizacja stałych systemu: Konfiguracja TERYT widoczna w historii stałych systemu
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('xinfo_ter_widok','teryt');
__UPG.msg('Konfiguracja TERYT jest widoczna w historii stałych systemu.');
_result


\xinfo_ter_widok_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Aktualizacja stałych systemu: Konfiguracja TERYT widoczna w historii stałych systemu
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu: Konfiguracja TERYT widoczna w historii stałych systemu.'


\b_event
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Uzupełnia definicje zdarzeń modelera o nowe elementy
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('fill_tab','#b_event');
__UPG.msg('Zaktualizowano definicje zdarzeń modelera o nowe elementy.');
_res


\b_event_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Uzupełnia definicje zdarzeń modelera o nowe elementy - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia definicje zdarzeń modelera o nowe elementy.'


\zws_par_kkot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła dodaje czynność ZWS_PAR_KKOT do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZWS_PAR_KKKA','ZWS_PAR_KKHR'";
_uid:='ZWS_PAR_KKOT';

_ROLE:=sql(
   'select distinct B_ROLE.NAME '
   'from B_ACTROL join B_ACTION using (B_ACTROL.B_ACTION,B_ACTION.REFERENCE) '
                 'join B_ROLE using (B_ACTROL.B_ROLE,B_ROLE.REFERENCE) '
   'where B_ACTROL.FIRMA=:_a and B_ACTION.UID in (:_b) '
   'order by 1',
   exec('ref_firma','ustawienia'),_cr
);

{? type_of(_ROLE)=type_of(~~)
|| __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
   return(0)
?};

{? _ROLE.first()
|| _ret:=1;
   {!
   |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_ROLE.NAME])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_ROLE.NAME]);
         _ret:=-1
      ?};
      _ROLE.next()
   !};
   _ret
|| __UPG.msg('W firmie %1 brak ról z uprawnieniami do czynności %2.'[REF.FIRMA().SYMBOL,_cr]);
   1
?}


\zws_par_kkot_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła dodaje czynność ZWS_PAR_KKOT do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_KKOT - Typy osób kontaktowych" do odpowiednich ról'


\id_sync
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów
::----------------------------------------------------------------------------------------------------------------------
_init:=exec('init','sync_id');
_result:=_init.RESULT;
_args:=_init.ARGS;
{? _result
|| __UPG.msg('Zaktualizowano definicje identyfikatorów rekordów.')
|| __UPG.msg('Wystąpił problem podczas aktualizacji definicji identyfikorów rekordów %1 - %2 do systemu %3.'[_args.KOD,_args.OPIS,_args.SYSTEM])
?};
_result


\id_sync_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie definicji identyfikatorów rekordów:\n'
'MWA_ND_SYM\n'
'MWA_FAKS_SYM\n'
'MWA_DOKUMENT_SYM\n'
'BDO_KH_ID\n'
'BDO_KH_ODB_ID\n'
'PORTAL_PORTALK_ID\n'
'PORTAL_P_ID\n'
'PORTAL_OS_ADRES_ID\n'
'PORTAL_RD_ID\n'
'PORTAL_PKO_N_ID\n'
'PORTAL_PKO_K_ID\n'
'PORTAL_OS_SZKOL_ID\n'
'PORTAL_P_INFO_ID\n'
'PORTAL_HUM_ID\n'
'PORTAL_H_ST_ID\n'
'PORTAL_H_WYD_ID\n'
'PORTAL_H_MIEJSCE\n'
'PORTAL_H_ID\n'
'PORTAL_ZC_ID\n'
'PORTAL_K_P_ID\n'
'PORTAL_K_W_ID\n'
'PORTAL_KRE_HIS_P_ID\n'
'PORTAL_KRE_HIS_W_ID\n'
'PORTAL_BNFTT_ID\n'
'PORTAL_GSPN_ID\n'
'PORTAL_P_DODINF_ID\n'
'PORTAL_USERS\n'
'PORTAL_BIPKAT_KDR_ID\n'
'PORTAL_BIPPYTN_KDR_ID\n'
'PORTAL_BIPPYTP_KDR_ID\n'
'PORTAL_KART_DOD_WYR_KAR_ID\n'
'PORTAL_R_N_ID\n'
'PORTAL_N_ID\n'
'PORTAL_ZJ_ID\n'
'PORTAL_BDO_ID\n'
'PORTAL_SZK_PRAC_ID\n'
'PORTAL_PPK_UZA_ID\n'
'PORTAL_PPK_UCZ_ID\n'


\KPO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja pól tabeli KPO
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_msk:=KPO.names();
_msk.clear();
KPO.trig_off('*','*');
KPO.cntx_psh();
{? _msk.first()
|| {!
   |? KPO.use(_msk.NAME);
      KPO.prefix();
      KPO.for_each("
         _put:=0;
         {? KPO.TYP=''
         || {? KPO.R<>'G'
            || KPO.TYP:=exec('typ_kpo','odpady');
               _put:=1
            || KPO.TYP:=exec('typ_kpok_tra','odpady');
               _put:=1
            ?}
         ?};
         {? KPO.KORW=''
         || KPO.KORW:='K';
            _put:=1
         ?};
         {? KPO.OB=''
         || KPO.OB:='T';
            _put:=1
         ?};
         {? _put || KPO.put() ?}
      ",1);
      _msk.next()
   !}
?};
KPO.cntx_pop();
KPO.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola kart przekazania odpadów.');
_result


\KPO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja pól tabeli KPO - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli kart przekazania odpadów.'


\kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja pól tabeli KH
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KH.prefix();
_result:=KH.for_each("
   _zm:=0;
   {? KH.POPIS=''
   ||
      KH.POPIS:=exec('kh_popis','kontrahent',KH.P);
      {? KH.POPIS<>'' || _zm:=1 ?}
   ?};
   {? KH_OSOB.ARCH=''
   || KH_OSOB.ARCH:='N';
      _zm:=1
   ?};
   {? _zm
   || KH.put()
   ?}
",1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola kartoteki kontrahenta.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji kartoteki kontrahenta.')
?};
_result


\kh_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja pól tabeli KH - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli kontrahenta.'


\kkh_kon_pkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła dodaje czynność KKH_KON_PKON do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZWS_PAR_KKKW'";
_uid:='KKH_KON_PKON';

_ROLE:=sql(
   'select distinct B_ROLE.NAME '
   'from B_ACTROL join B_ACTION using (B_ACTROL.B_ACTION,B_ACTION.REFERENCE) '
                 'join B_ROLE using (B_ACTROL.B_ROLE,B_ROLE.REFERENCE) '
   'where B_ACTROL.FIRMA=:_a and B_ACTION.UID in (:_b) '
   'order by 1',
   exec('ref_firma','ustawienia'),_cr
);

{? type_of(_ROLE)=type_of(~~)
|| __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
   return(0)
?};

{? _ROLE.first()
|| _ret:=1;
   {!
   |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_ROLE.NAME])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_ROLE.NAME]);
         _ret:=-1
      ?};
      _ROLE.next()
   !};
   _ret
|| __UPG.msg('W firmie %1 brak ról z uprawnieniami do czynności %2.'[REF.FIRMA().SYMBOL,_cr]);
   1
?}


\kkh_kon_pkon_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła dodaje czynność KKH_KON_PKON do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "KKH_KON_PKON - Przegląd kontaktów kontrahenta" do odpowiednich ról'


\kkh_kon_drko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła dodaje czynność KKH_KON_DRKO do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZWS_PAR_KKKD'";
_uid:='KKH_KON_DRKO';

_ROLE:=sql(
   'select distinct B_ROLE.NAME '
   'from B_ACTROL join B_ACTION using (B_ACTROL.B_ACTION,B_ACTION.REFERENCE) '
                 'join B_ROLE using (B_ACTROL.B_ROLE,B_ROLE.REFERENCE) '
   'where B_ACTROL.FIRMA=:_a and B_ACTION.UID in (:_b) '
   'order by 1',
   exec('ref_firma','ustawienia'),_cr
);

{? type_of(_ROLE)=type_of(~~)
|| __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
   return(0)
?};

{? _ROLE.first()
|| _ret:=1;
   {!
   |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_ROLE.NAME])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_ROLE.NAME]);
         _ret:=-1
      ?};
      _ROLE.next()
   !};
   _ret
|| __UPG.msg('W firmie %1 brak ról z uprawnieniami do czynności %2.'[REF.FIRMA().SYMBOL,_cr]);
   1
?}


\kkh_kon_drko_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła dodaje czynność KKH_KON_DRKO do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "KKH_KON_DRKO - Rejestracja kontaktów kontrahenta" do odpowiednich ról'


\dokum_kh_osob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła uzupełnia w pole KH_OSOB w tabeli DOKUM na podstawie pola OSOBA.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_nn:=0;

DOKUM.cntx_psh();
KH_OSOB.cntx_psh();

DOKUM.index('KHO');
KH_OSOB.index('KH');
KH_OSOB.prefix();
{? KH_OSOB.first()
|| {!
   |? {? KH_OSOB.KH<>null() & KH_OSOB.NAZWISKO<>''
      || DOKUM.prefix(REF.FIRMA,KH_OSOB.KH,KH_OSOB.NAZWISKO+' '+KH_OSOB.IMIE);
         {? DOKUM.first()
         || {!
            |? {? DOKUM.KH_OSOB=null()
               || DOKUM.KH_OSOB:=KH_OSOB.ref();
                  {? DOKUM.put(1)
                  || _nn+=1
                  ?}
               ?};
               DOKUM.next()
            !}
         ?}
      ?};
      KH_OSOB.next()
   !}
?};

KH_OSOB.cntx_pop();
DOKUM.cntx_pop();

__UPG.msg('Zaktualizowano pole "osoba kontaktowa" tabeli kontaktów - liczba zmian: '+$_nn+'.');
_result


\dokum_kh_osob_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła uzupełnia w pole KH_OSOB w tabeli DOKUM na podstawie pola OSOBA - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola "osoba kontaktowa" tabeli kontaktów.'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\kst_kus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Dodanie stałej systemu KST.KUS - Koszty uzyskania przychodu - szczególne
::----------------------------------------------------------------------------------------------------------------------
_dodano:=-1;
{? exec('add_acr','#stalesys','KUS','Koszty uzyskania przychodu - szczególne','T','T','T','KST','PPL','PKD','PRC')
   |
   exec('upd_acr','#stalesys','KUS','Koszty uzyskania przychodu - szczególne','T','T','T','KST','PPL','PKD','PRC')
|| _dodano:=1;
   exec('kst_war_add','#stalesys','KST.KUS','111.25',date(0,0,0));
   exec('kst_war_add','#stalesys','KST.KUS','250',date(2019,10,1))
?};
_txt:={? _dodano
      || 'Wykonano aktualizację historii stałych systemu dla KST.KUS - Koszty uzyskania przychodu - szczególne'
      || 'Błąd. Nie udało się dodać/zaktualizować stałej systemu KST.KUS - Koszty uzyskania przychodu - szczególne'
      ?};
__UPG.msg(_txt);

_dodano


\kst_kus_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Dodanie stałej systemu KST.KUS - Koszty uzyskania przychodu - szczególne - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie stałej systemu KST.KUS - Koszty uzyskania przychodu - szczególne'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\tte_wyk_dwzl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja uprawnień\ról związana z czynnością TTE_WYK_DWZL (rejestracja wykonań zleceń)
::----------------------------------------------------------------------------------------------------------------------
_status:=1;
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_add:="
   _rola:=_a;
   _lista:=_b;
   _ok:=1;
   _uid:=spli_str(_lista,'.');
   {! _lp:=1 .. obj_len(_uid)
   |! {? exec('add_actrol_one','#b_role',_rola,_uid[_lp])
      || __UPG.msg('Roli ''%1'' nadano uprawnienia do czynności ''%2''.' [_rola,_uid[_lp]])
      || __UPG.msg('Nadanie roli ''%1'' uprawnień do czynności ''%2'' nie powiodło się.' [_rola,_uid[_lp]]);
         _ok:=0
      ?}
   !};
   _ok";
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='TTE_WYK_DWZL';
_rola:='Produkcja';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
::Roli Rejestrujący wykonania produkcji przydzielam użytkowników: wszystkich, którzy dotychczas posiadajali rolę Produkcja
_buffer:=exec('buffer','#b_usrrol');
_rola:='Rejestrujący wykonania produkcji';
_ref:=exec('ref','#b_role',_rola);
_firma:=REF.FIRMA;
{? _ref=null()
||
::   Jak nie ma roli to ją dodaję
   {? (_ref:=exec('ref','#b_role',_rola,,1))<>null()
   || __UPG.msg('Rola ''%1'' została utworzona.' [_rola])
   ?}
?};
{? _ref=null()
|| __UPG.msg('Roli ''%1'' nie udało się utworzyć.' [_rola]);
   _status:=0
||
   _USERS:=sql(
      'select DISTINCT USERS.REFERENCE AS REF '
      'from USERS join B_USRROL using (B_USRROL.USERS,USERS.REFERENCE) '
                 'join B_ROLE using (B_USRROL.B_ROLE,B_ROLE.REFERENCE) '
      'where B_ROLE.NAME=\':_a\' '
      'order by 1',
      'Produkcja'
   );
   {? _USERS.first()
   || {!
      |?
         _buffer.FIRMA:=_firma;
         _buffer.USERS:=exec('FindAndGet','#table',USERS,_USERS.REF,,"USERS.ref()",null);
         _buffer.B_ROLE:=_ref;
         {? _buffer.FIRMA<>null & _buffer.USERS<>null & _buffer.B_ROLE<>null
         || {? exec('add','#b_usrrol',_buffer)<>null()
            || _msg:='Przypisano użytkownika: %1 do roli: %2 w firmie: %3' [exec('FindAndGet','#table',USERS,_buffer.USERS,,"USERS.KOD",'') ,_rola,B_PROC.FIRMA().SYMBOL];
               __UPG.msg(_msg)
            || _msg:='Nie udało się przypisać użytkownika: %1 do roli: %2 w firmie: %3' [exec('FindAndGet','#table',USERS,_buffer.USERS,,"USERS.KOD",''),_rola,B_PROC.FIRMA().SYMBOL];
               __UPG.msg(_msg)
            ?}
         ?};
         _USERS.next
      !}
   ?}
?};
_status


\tte_wyk_dwzl_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja uprawnień związana z czynnością TTE_WYK_DWZL - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień związana z czynnością rejestracji wykonań zleceń.'


\KR2AR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli KR2AR
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
KR2AR.cntx_psh();
KR2AR.for_each("
   _put:=0;
   {? KR2AR.DSP=''
   || KR2AR.DSP:='T';
      _put:=1
   ?};
   {? _put || KR2AR.put() ?}
",1);
KR2AR.cntx_pop();
__UPG.msg('Zaktualizowano nowe pola tabeli mapowania rubryk kalkulacyjnych na analityczne.');
_result


\KR2AR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja pól tabeli KR2AR - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli mapowania rubryk kalkulacyjnych na analityczne.'


\TR_NZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Wypełnia pola w tabeli TR_NZL i TR_ZL
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__lrecp','__lmodp');
__lrec:=__lmod:=__lrecp:=__lmodp:=0;
_msk:=TR_NZL.names();
TR_NZL.cntx_psh();
TR_ZL.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? TR_NZL.use(_msk.NAME);
      TR_ZL.use('trzl'+(_msk.NAME+4));
      TR_ZL.prefix();
      TR_ZL.for_each("
        __lrecp+=1;
        {? TR_ZL.M().RODZ='T' & TR_ZL.IL_OP=0
        || TR_ZL.IL_OP:=1;
           {? TR_ZL.ZIL>0 || TR_ZL.ZIL_OP:=TR_ZL.IL_OP ?};
           {? TR_ZL.put(1) || __lmodp+=1 ?}
        ?}");
      TR_NZL.prefix();
      TR_NZL.for_each("
        __lrec+=1;
        {? TR_NZL.STMG=''
        || TR_NZL.STMG:='no';
           {? TR_NZL.put(1) || __lmod+=1 ?}
        ?}");
      _msk.next()
   !}
?};
TR_NZL.cntx_pop();
TR_ZL.cntx_pop();
__UPG.msg('Zaktualizowano dyspozycje transportu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
__UPG.msg('Zaktualizowano pozycje dyspozycji transportu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrecp,$__lmodp]);
VAR_DEL.delete('__lrec','__lmod','__lrecp','__lmodp');
_res


\TR_NZL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Wypełnia pola w tabeli TR_NZL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dyspozycji transportu'


\SYNC_DEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AKUL] [20.14]
:: OPIS: Wypełnia pola w tabeli SYNC_DEF
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
SYNC_DEF.cntx_psh();
SYNC_DEF.clear();
SYNC_DEF.for_each("
  __lrec+=1;
  {? SYNC_DEF.WGIDPUT=''
  || SYNC_DEF.WGIDPUT:='N';
     {? SYNC_DEF.put(1) || __lmod+=1 ?}
  ?}
");
SYNC_DEF.cntx_pop();
__UPG.msg('Zaktualizowano definicje synchronizacji z innymi systemami.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\SYNC_DEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AKUL] [20.14]
:: OPIS: Wypełnia pola w tabeli SYNC_DEF - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji synchronizacji z innymi systemami'


\FAKS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje FAKS.INTRAKC
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKS.names();
FAKS.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("
         __lrec+=1;
         _put:=0;
         {? FAKS.INTRAKC=''
         ||
            FAKS.INTRAKC:={? FAKS.KZ='' & (FAKS.IST_TYP='' | FAKS.AKC='N') || 'N' || 'T' ?};
            _put:=1
         ?};
         {? _put & FAKS.put() || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();

__UPG.msg('Zaktualizowano pola w nagłówku dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\FAKS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje FAKS.INTRAKC - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola w nagłówku dokumentów sprzedaży i zakupu.'


\ND
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje ND.INTRAKC
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ND.names();
ND.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? ND.use(_msk.NAME);
      ND.prefix();
      ND.for_each("
         __lrec+=1;
         _put:=0;
         {? ND.INTRAKC=''
         ||
            ND.INTRAKC:={? ND.IST_TYP='' | ND.Z='N' || 'N' || 'T' ?};
            _put:=1
         ?};
         {? _put & ND.put() || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
ND.cntx_pop();

__UPG.msg('Zaktualizowano pola w nagłówku dokumentów magazynowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\ND_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje ND.INTRAKC - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola w nagłówku dokumentów magazynowych.'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\kh_osob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła aktualizuje pola w tabeli KH_OSOB.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_nn:=0;

KH_OSOB.cntx_psh();

KH_OSOB.index('KH');
KH_OSOB.prefix();
{? KH_OSOB.first()
|| {!
   |? _zm:=0;
      {? KH_OSOB.OS_ZARZ=''
      || KH_OSOB.OS_ZARZ:='N';
         _zm:=1
      ?};
      {? KH_OSOB.OS_ODP=''
      || KH_OSOB.OS_ODP:='N';
         _zm:=1
      ?};
      {? KH_OSOB.OS_DEC=''
      || KH_OSOB.OS_DEC:='N';
         _zm:=1
      ?};
      {? KH_OSOB.REL=''
      || KH_OSOB.REL:='N';
         _zm:=1
      ?};
      {? KH_OSOB.ARCH=''
      || KH_OSOB.ARCH:='N';
         _zm:=1
      ?};
      {? _zm
      || KH_OSOB.put();
         _nn+=1
      ?};
      KH_OSOB.next()
   !}
?};

KH_OSOB.cntx_pop();

__UPG.msg('Zaktualizowano nowe pola tabeli osób kontaktowych - liczba zmian: '+$_nn+'.');
_result


\kh_osob_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła aktualizuje pola w tabeli KH_OSOB.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli osób kontaktowych.'


\zdarzt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła aktualizuje pola w tabeli ZDARZT.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_nn:=0;

ZDARZT.cntx_psh();

ZDARZT.index('ZDARZT');
ZDARZT.prefix('N');
{? ZDARZT.first()
|| {!
   |? {? ZDARZT.RODZ='' & ZDARZT.RODZINF().NAZ='Dokument prosty'
      || _zm:=0;
         _naz:=ZDARZT.ZDARZ;
         _rd:='';
         {? _naz='EMAIL'
         || _rd:='M'
         |? _naz='SPOTKANIE'
         || _rd:='S'
         |? _naz='TELEFON'
         || _rd:='T'
         ?};
         {? _rd<>''
         || ZDARZT.RODZ:=_rd;
            _zm:=1
         ?};
         {? _zm
         || ZDARZT.put();
           _nn+=1
         ?}
      ?};
      ZDARZT.next()
   !}
?};

ZDARZT.cntx_pop();

__UPG.msg('Zaktualizowano nowe pola tabeli typów kontaktów z kontrahentem - liczba zmian: '+$_nn+'.');
_result


\zdarzt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła aktualizuje pola w tabeli ZDARZT.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli typów kontaktów z kontrahentem.'


\FAP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje FAP.MX, FAP.NX w masce hs
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAP.names();
FAP.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? {?  (_msk.NAME+2)='hs'
      ||
         FAP.use(_msk.NAME);
         FAP.prefix();
         FAP.for_each("
            __lrec+=1;
            _put:=0;
            {? FAP.MX=''
            ||
               FAP.MX:=FAP.M().KTM;
               FAP.NX:=FAP.M().N;
               _put:=1
            ?};
            {? _put & FAP.put() || __lmod+=1 ?}
         ")
      ?};
      _msk.next()
   !}
?};
FAP.cntx_pop();

__UPG.msg('Zaktualizowano pola w pozycjach dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\FAP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje FAP.MX, FAP.NX w masce hs - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól w pozycji dokumentów sprzedaży i zakupu.'


\PL_WZOR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Wypełnia pola w tabeli PL_WZOR
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PL_WZOR.cntx_psh();
PL_WZOR.index('NAZWA');
PL_WZOR.prefix();
{? PL_WZOR.first()
|| _lp:=1;
   {!
   |?
      __lrec+=1;
      _put:=0;
      {? PL_WZOR.AKT=''
      || PL_WZOR.AKT:='T';
         _put:=1
      ?};
      {? PL_WZOR.LP=0
      || PL_WZOR.LP:=_lp;
         _lp+=1;
         _put:=1
      ?};

      {? _put>0
      || {? PL_WZOR.put()
         || __lmod+=1
         ?}
      ?};
      PL_WZOR.next()
   !}
?};
PL_WZOR.cntx_pop();
__UPG.msg('Zaktualizowano widoki planu operacyjnego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\PL_WZOR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Wypełnia pola w tabeli PL_WZOR - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja widoków planu operacyjnego'


\PL_OPIS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Wypełnia pola w tabeli PL_OPIS
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PL_OPIS.cntx_psh();
PL_OPIS.index('PL_WZOR');
PL_WZOR.cntx_psh();
PL_WZOR.index('NAZWA');
PL_WZOR.prefix();

_can_continue:=1;

:: Pierwsza petla przesuwa wszystko w minusy
{? PL_WZOR.first()
|| {!
   |? PL_OPIS.prefix(PL_WZOR.ref());
      {? PL_OPIS.first()
      || {!
         |? PL_OPIS.NUMER:=-(PL_OPIS.NUMER);
            _can_continue:=PL_OPIS.put();
            PL_OPIS.next() & _can_continue>0
         !}
      ?};
      PL_WZOR.next() & _can_continue>0
   !}
?};

:: Druga pętla naprawia numerację
PL_OPIS.index('PL_OPIS');
{? _can_continue>0 & PL_WZOR.first()
|| _lp:=1;
   {!
   |? PL_OPIS.prefix(PL_WZOR.ref());
      {? PL_OPIS.last()
      || _lp:=1;
         {!
         |? __lrec+=1;
            _next:=0;
            _ref_nxt:=null();
            PL_OPIS.cntx_psh();
            {? PL_OPIS.prev()
            || _ref_nxt:=PL_OPIS.ref()
            ?};
            PL_OPIS.cntx_pop();

            PL_OPIS.NUMER:=_lp;
            _can_continue:=PL_OPIS.put();
            {? _can_continue>0
            || __lmod+=1
            ?};
            _lp+=1;

            {? _ref_nxt<>null()
            || _next:=PL_OPIS.seek(_ref_nxt)
            ?};
            _next>0 & _can_continue>0
         !}
      ?};
      PL_WZOR.next() & _can_continue>0
   !}
?};


PL_WZOR.cntx_pop();
PL_OPIS.cntx_pop();
__UPG.msg('Zaktualizowano zasoby w widokach planu operacyjnego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
{? _can_continue<=0
|| _result:=-1
?};
_result


\PL_OPIS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Wypełnia pola w tabeli PL_OPIS - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zasobów w widokach planu operacyjnego'


\PL_WZORU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Inicjalizacja użytkowników widoków planu operacyjnego
::   WY: 0/1/-1
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_can_continue:=1;
_tab_role:=tab_tmp(2,
:: 'POLE','TYP','Nazwa w oknie',
   'B_ROLE','STRING[16]','Rola',
   'UPR','STRING[1]','Czy redakcja czy podgląd',
   'OPIS','STRING[20]','opis'
);
B_ACTROL.cntx_psh();
B_ACTROL.index('ACTION');
_action:=exec('ref','#b_action','TPP_PPO_DPPL');
B_ACTROL.prefix(_action);
{? B_ACTROL.first()
|| {!
   |? _tab_role.prefix($B_ACTROL.B_ROLE,);
      {? _tab_role.first()=0
      || _tab_role.blank();
         _tab_role.B_ROLE:=$B_ACTROL.B_ROLE;
         _tab_role.UPR:='R';
         _tab_role.OPIS:='redakcja'@;
         _can_continue:=_tab_role.add()
      ?};
      B_ACTROL.next() & _can_continue>0
   !}
?};
_action:=exec('ref','#b_action','TPP_PPO_PPPL');
B_ACTROL.prefix(_action);
{? B_ACTROL.first()
|| {!
   |? _tab_role.prefix($B_ACTROL.B_ROLE,);
      {? _tab_role.first()=0
      || _tab_role.blank();
         _tab_role.B_ROLE:=$B_ACTROL.B_ROLE;
         _tab_role.UPR:='P';
         _tab_role.OPIS:='podgląd'@;
         _can_continue:=_tab_role.add()
      ?};
      B_ACTROL.next() & _can_continue>0
   !}
?};
B_ACTROL.cntx_pop();

PL_WZOR.cntx_psh();
PL_WZOR.index('NAZWA');
PL_WZOR.prefix();
PL_WZORU.cntx_psh();
PL_WZORU.index('PL_WZOR');
B_USRROL.cntx_psh();
B_USRROL.index('B_ROLE');
_tab_role.prefix();
{? _tab_role.first()
|| {!
   |? _b_role:=exec('FindAndGet','#table',B_ROLE,_tab_role.B_ROLE,,,null());
      {? _b_role<>null()
      || B_USRROL.prefix(_b_role);
         {? B_USRROL.first()
         || {!
            |?
               {? PL_WZOR.first()
               || {!
                  |?
                     PL_WZORU.prefix(PL_WZOR.ref(),B_USRROL.USERS);
                     {? PL_WZORU.first()=0
                     || PL_WZORU.blank();
                        PL_WZORU.PL_WZOR:=PL_WZOR.ref();
                        PL_WZORU.USERS:=B_USRROL.USERS;
                        PL_WZORU.UPR:=_tab_role.UPR;
                        _can_continue:=PL_WZORU.add();
                        {? _can_continue>0
                        || _usr:=exec('record','#to_string',B_USRROL.USERS);
                           __UPG.msg('Dodano użytkownikowi: %1 uprawnienie do widoku planu: %2 (%3)'@[_usr,PL_WZOR.NAZWA,_tab_role.OPIS])
                        ?}
                     ?};
                     PL_WZOR.next() & _can_continue>0
                  !}
               ?};
               B_USRROL.next() & _can_continue>0
            !}
         ?}
      ?};
      _tab_role.next() & _can_continue>0
   !}
?};
B_USRROL.cntx_pop();
PL_WZORU.cntx_pop();
PL_WZOR.cntx_pop();
{? _can_continue<=0
|| _result:=-1
?};
_result


\PL_WZORU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Inicjalizacja użytkowników widoków planu operacyjnego - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Inicjalizacja użytkowników widoków planu operacyjnego.'


\logo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja miejsca przechowywania logo dla wysyłanych wiadomości
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('slo_kod','dane_startowe');
_slo_typ:=exec('FindInSet','#table','SLO_TYP','SYMBOL','TYPBLOBF',,,1,,null());
{? _slo_typ=null()
|| __UPG.msg('Nie zaktualizowano miejsca przechowywania logo dla wysyłanych wiadomości - brak słownika TYPBLOBF.');
   _res:=0
|| __UPG.msg('Zaktualizowano miejsca przechowywania logo dla wysyłanych wiadomości.');
   SKID_BLB.cntx_psh(); IMG_BLOB.cntx_psh();
   SKID_BLB.index('ID'); IMG_BLOB.index('KOD');

   SKID_BLB.prefix('fo_700011',);
   {? SKID_BLB.first()
   || _blob:=SKID_BLB.BLOB;
      {? _blob
      || IMG_BLOB.prefix('F',null(),exec('ref_firma','#firma'),'logo_h',);
         {? IMG_BLOB.first()
         || IMG_BLOB.OBRAZ:=_blob;
            IMG_BLOB.put()
         || IMG_BLOB.RODZ:='F';
            IMG_BLOB.USER:=null();
            IMG_BLOB.FIRMA:=exec('ref_firma','#firma');
            IMG_BLOB.SLO_KOD:=exec('FindInSet','#table','SLO_KOD','KOD','logo_h',_slo_typ,,1,,null());
            IMG_BLOB.OBRAZ:=_blob;
            IMG_BLOB.add()
         ?}
      || IMG_BLOB.prefix('F',null(),exec('ref_firma','#firma'),'logo_h',);
         {? IMG_BLOB.first()
         || IMG_BLOB.del()
         ?}
      ?}
   || IMG_BLOB.prefix('F',null(),exec('ref_firma','#firma'),'logo_h',);
      {? IMG_BLOB.first()
      || IMG_BLOB.del()
      ?}
   ?};

   SKID_BLB.prefix('fo_700014',);
   {? SKID_BLB.first()
   || _blob:=SKID_BLB.BLOB;
      {? _blob
      || IMG_BLOB.prefix('F',null(),exec('ref_firma','#firma'),'logo_f',);
         {? IMG_BLOB.first()
         || IMG_BLOB.OBRAZ:=_blob;
            IMG_BLOB.put()
         || IMG_BLOB.RODZ:='F';
            IMG_BLOB.USER:=null();
            IMG_BLOB.FIRMA:=exec('ref_firma','#firma');
            IMG_BLOB.SLO_KOD:=exec('FindInSet','#table','SLO_KOD','KOD','logo_f',_slo_typ,,1,,null());
            IMG_BLOB.OBRAZ:=_blob;
            IMG_BLOB.add()
         ?}
      || IMG_BLOB.prefix('F',null(),exec('ref_firma','#firma'),'logo_f',);
         {? IMG_BLOB.first()
         || IMG_BLOB.del()
         ?}
      ?}
   || IMG_BLOB.prefix('F',null(),exec('ref_firma','#firma'),'logo_f',);
      {? IMG_BLOB.first()
      || IMG_BLOB.del()
      ?}
   ?};

   SKID_BLB.cntx_pop(); IMG_BLOB.cntx_pop()
?};
_res


\logo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja miejsca przechowywania logo dla wysyłanych wiadomości - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja miejsca przechowywania logo dla wysyłanych wiadomości'


\zws_par_kbad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.14]
:: OPIS: Formuła dodaje czynność ZWS_PAR_KBAD do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_uid:='ZWS_PAR_KBAD';
_lista:=''+"'Produkcja','Logistyk'";

_ROLE:=sql(
   'select distinct B_ROLE.NAME '
   'from B_ROLE '
   'where B_ROLE.NAME in (:_a) '
   'order by 1',
   _lista
);

{? type_of(_ROLE)=type_of(~~)
|| __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
   return(0)
?};

{? _ROLE.first()
|| _ret:=1;
   {!
   |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_ROLE.NAME])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_ROLE.NAME]);
         _ret:=-1
      ?};
      _ROLE.next()
   !};
   _ret
|| __UPG.msg('Brak ról do przypisania dla czynności %1.'[_uid]);
   1
?}


\zws_par_kbad_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.14]
:: OPIS: Formuła dodaje czynność ZWS_PAR_KBAD do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_KBAD - Kwalifikacja dostaw i badania" do odpowiednich ról'


\dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła uzupełnia nowe pola tabeli DOKUM.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_nn:=0;

DOKUM.cntx_psh();
DOKUMZ.cntx_psh();
DOKUMZ.index('DISP');
DOKUM.index('KHF');
DOKUM.prefix();
{? DOKUM.first()
|| {!
   |? _zm:=0;
      DOKUMZ.prefix(DOKUM.ref());
      {? DOKUMZ.first()
      || _zal:='T'
      || _zal:='N'
      ?};
      {? DOKUM.ZAL<>_zal
      || DOKUM.ZAL:=_zal;
         _zm:=1
      ?};
      {? DOKUM.P_POL=''
      || DOKUM.P_POL:='N';
         _zm:=1
      ?};
      {? DOKUM.P_POT=''
      || DOKUM.P_POT:='N';
         _zm:=1
      ?};
      {? _zm & DOKUM.put(1)
      || _nn+=1
      ?};
      DOKUM.next()
   !}
?};
DOKUMZ.cntx_pop();
DOKUM.cntx_pop();

__UPG.msg('Zaktualizowano nowe pola tabeli kontaktów - liczba zmian: '+$_nn+'.');
_result


\dokum_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Formuła uzupełnia nowe pola tabeli DOKUM - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli kontaktów.'


\KH_ODB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Wypełnia pola w tabeli KH_ODB
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
KH_ODB.cntx_psh();
KH_ODB.prefix();
_res:=KH_ODB.for_each("
   __lrec+=1;
   _put:=0;
   {? KH_ODB.SKR=''
   || KH_ODB.SKR:=KH_ODB.KOD;
      _put:=1
   ?};
   {? _put>0
   || {? KH_ODB.put()
      || __lmod+=1
      ?}
   ?}
");
KH_ODB.cntx_pop();
__UPG.msg('Zaktualizowano odbiorców kontrahentów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\KH_ODB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Wypełnia pola w tabeli KH_ODB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja odbiorców kontrahentów'


\ZLP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Uzupełnia pola w tabeli ZLP
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_msk:=ZLP.names();
_msk.clear();
ZLP.trig_off('*','*');
ZLP.cntx_psh();
{? _msk.first()
|| {!
   |?
      __lrec+=1;
      ZLP.use(_msk.NAME);
      ZLP.prefix();
      ZLP.for_each("
         _put:=0;
         {? ZLP.TKPO=''
         || ZLP.TKPO:=exec('typ_kpo','odpady');
            _put:=1
         ?};
         {? ZLP.SENT_KPO=''
         || ZLP.SENT_KPO:='N';
            _put:=1
         ?};
         {? _put
         || __lmod+=1;
            ZLP.put()
         ?}
      ",1);
      _msk.next()
   !}
?};
ZLP.cntx_pop();
ZLP.trig_on('*','*');

__UPG.msg('Zaktualizowano zgłoszenia jednorazowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\ZLP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Uzupełnia pola w tabeli ZLP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól zgłoszeń jednorazowych'


\TTOPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja pól tabeli TTOPER
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
TTOPER.trig_off('*','*');
TTOPER.cntx_psh();
TTOPER.prefix();
TTOPER.for_each("
   __lrec+=1;
   {? TTOPER.FIX_NORM=''
   || TTOPER.FIX_NORM:='N';
      {? TTOPER.put(1) || __lmod+=1 ?}
   ?}
",1);
TTOPER.cntx_pop();
TTOPER.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola taryfikatora operacji technologii.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\TTOPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja pól tabeli TTOPER - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli taryfikatora operacji technologii.'


\TOPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja pól tabeli TOPER
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=TOPER.names();
_msk.clear();
TOPER.trig_off('*','*');
{? _msk.first()
||
   {!
   |? TOPER.cntx_psh();
      TOPER.use(_msk.NAME);
      TOPER.prefix();
      TOPER.for_each("
         __lrec+=1;
         {? TOPER.FIX_NORM=''
         || TOPER.FIX_NORM:='N';
            {? TOPER.put(1) || __lmod+=1 ?}
         ?}
      ",1);
      TOPER.cntx_pop();
      _msk.next()
   !}
?};
TOPER.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola operacji technologii.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\TOPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja pól tabeli TOPER - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli operacji technologii.'


\ZGP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja pól tabeli ZGP
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZGP.trig_off('*','*');
ZGP.cntx_psh();
ZGP.prefix();
ZGP.for_each("
   __lrec+=1;
   {? ZGP.FIX_NORM=''
   || ZGP.FIX_NORM:='N';
      {? ZGP.put(1) || __lmod+=1 ?}
   ?}
",1);
ZGP.cntx_pop();
ZGP.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola przewodników produkcyjnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZGP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Aktualizacja pól tabeli ZGP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli pozycji przewodników produkcyjnych.'


\LUM_ZGL_PZGL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.42]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnością przeglądania zgłoszeń jednorazowych (LUM_ZGL_PZGL)
::----------------------------------------------------------------------------------------------------------------------
_status:=1;
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_add:="
   _rola:=_a;
   _lista:=_b;
   _ok:=1;
   _uid:=spli_str(_lista,'.');
   {! _lp:=1 .. obj_len(_uid)
   |! {? exec('add_actrol_one','#b_role',_rola,_uid[_lp])
      || __UPG.msg('Roli ''%1'' nadano uprawnienia do czynności ''%2''.' [_rola,_uid[_lp]])
      || __UPG.msg('Nadanie roli ''%1'' uprawnień do czynności ''%2'' nie powiodło się.' [_rola,_uid[_lp]]);
         _ok:=0
      ?}
   !};
   _ok";
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='LUM_ZGL_PZGL';
_rola:='Specjalista ds. umów cyklicznych';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_rola:='Specjalista ds. odpadów';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_status


\LUM_ZGL_PZGL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Aktualizacja uprawnień związana z czynnością LUM_ZGL_PZGL - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień związana z czynnością przeglądania zgłoszeń jednorazowych.'


\UMLOJN_zak_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Utworzenie nowego typu załącznika "Umowa o zakazie konkurencji", dodanie miejsca jego użycia w UMLOJN.
::       Wypełnienie nowych pól tabeli UMLOJN w dotychczasowych danych wartościami domyślnymi.
::----------------------------------------------------------------------------------------------------------------------
:: Dodanie nowego typu załącznika i miejsca użycia:
_typ:=exec('slo_naz','ext_slo',exec('slo_typ','ext_slo','ZAL'),'Umowa o zakazie konkurencji');
_txt:='';
_res:=1;

:: Uzupełnienie pozycji slownika SLO_KOD:
_update:="
   {? ~(_put:=SLO_KOD.find_key(_a,_b,))
   || SLO_KOD.blank(1);
      SLO_KOD.SLO_TYP:=_a;
      SLO_KOD.KOD:=_b
   ?};
   _zmiana:=0;
   {? SLO_KOD.NAZWA<>_c
   || SLO_KOD.NAZWA:=_c;
      _zmiana+=1
   ?};
   {? var_pres('_d')=type_of('') & SLO_KOD.SYSTEM<>_d
   || SLO_KOD.SYSTEM:=_d;
      _zmiana+=1
   ?};
   {? SLO_KOD.SYSTEM<>'N' & SLO_KOD.SYSTEM<>'T'
   || SLO_KOD.SYSTEM:='N';
      _zmiana+=1
   ?};
   {? _put
   || {? _zmiana
      || SLO_KOD.put()
      ?}
   || SLO_KOD.add()
   ?}
";
SLO_KOD.cntx_psh();
SLO_KOD.index('KOD');
SLO_KOD.prefix();
_update(exec('slo_typ','ext_slo','ZAOM'),'UMLOJN','Umowa lojalnościowa','T');
SLO_KOD.cntx_pop();

:: Dodanie miejsca użycia w ZAOTM:
{? _typ
|| ZAOTM.cntx_psh();
   ZAOTM.index('MIEJSCER');
   {? SLO_NAZ.seek(_typ,,1)
   || _msc:='UMLOJN';
      ZAOTM.prefix(_msc,SLO_NAZ.ref());
      {? ZAOTM.first()
      || _txt:='Nowy typ załącznika (%1) powiązano z miejscem użycia.' [SLO_NAZ.NAZWA]
      || ZAOTM.blank();
         ZAOTM.MIEJSCE:=_msc;
         ZAOTM.SLO_NAZ:=SLO_NAZ.ref();
         ZAOTM.add()
      ?}
   ?};
   ZAOTM.cntx_pop()
|| _res:=-1
?};

:: Wypełnienie nowych pól tabeli UMLOJN w dotychczasowych danych wartościami domyślnymi:
UMLOJN.cntx_psh();
UMLOJN.prefix();
{? UMLOJN.first()
|| UMLOJN.trig_off('*','*');
   {!
   |? {? UMLOJN.TYP<>'Z'
      || UMLOJN.TYP:='L';
         UMLOJN.PO:='N';
         UMLOJN.DTZ:=date(0,0,0);
         {? ~UMLOJN.put()
         || _txt:='Nie udało się poprawić rekordu tabeli UMLOJN.'@;
            _res:=-1
         ?}
      ?};

      UMLOJN.next()
   !};
   UMLOJN.trig_on('*','*');
   {? _res>0 || _txt+=' Poprawiono rekordy tabeli UMLOJN.'@ ?}
?};
UMLOJN.cntx_pop();
__UPG.msg(_txt);

_res


\UMLOJN_zak_kon_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Utworzenie nowego typu załącznika "Umowa o zakazie konkurencji", dodanie miejsca jego użycia w UMLOJN.
::       Wypełnienie nowych pól tabeli UMLOJN w dotychczasowych danych wartościami domyślnymi. - opis
::----------------------------------------------------------------------------------------------------------------------
'Nowy typ załącznika umów lojalnościowych - "Umowa o zakazie konkurencji"'


\rok_f_akt_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Uzupełnia zawartość pola AKT_ROZ w tabeli ROK_F
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
ROK_F.prefix();
VAR_DEL.delete('__par76');
__par76:=PAR_SKID.get(76);
__prec:=0;
_result:=ROK_F.for_each("{? ROK_F.FIRMA=REF.FIRMA & ROK_F.AKT_ROZ=''
                         || {? __par76='N' || ROK_F.AKT_ROZ:='N' || ROK_F.AKT_ROZ:='T' ?};
                            ROK_F.put();
                            __prec+=1
                         ?}",1);
__UPG.msg('Zaktualizowano lata obrachunkowe.'@);
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$ROK_F.size(),$__prec]);
VAR_DEL.delete('__prec','__par76');
ROK_F.cntx_pop();
_result


\rok_f_akt_roz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Uzupełnia zawartość pola AKT_ROZ w tabeli ROK_F - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pole AKT_ROZ - Znacznik, czy aktualizować rozrachunki w roku bilansowym? (T/N)'


\upd_par76
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Aktualizacja parametru 76
::----------------------------------------------------------------------------------------------------------------------
PARAMS.cntx_psh();
PARAMS.index('UNIQ'); PARAMS.prefix(76);
{? PARAMS.first()
|| {!
   |? {? PARAMS.TRESC='1'
      || PARAMS.TRESC:='T';
        PARAMS.put()
      ?};
      PARAMS.next()
   !}
?};
PARAMS.cntx_pop();
__UPG.msg('Zaktualizowano parametr numer 76.'@);
1


\upd_par76_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Aktualizacja parametru numer 76 - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametru 76 zmiana treści z ''1'' na ''T''.'


\REM_ZAS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.14]
:: OPIS: Przekopiowanie wartości numeru fabrycznego ze środka trwałego do numeru fabrycznego zasobu remontowego
::----------------------------------------------------------------------------------------------------------------------
REM_ZAS.cntx_psh();
SRSR.cntx_psh();
REM_ZAS.prefix();
SRSR.prefix();
{? REM_ZAS.first()
||
   {!|?
      {? REM_ZAS.SRSR<>null() & REM_ZAS.NF=''& SRSR.seek($REM_ZAS.SRSR,ref_name($REM_ZAS.SRSR))
      ||
         REM_ZAS.NF:= REM_ZAS.SRSR().NF;
         REM_ZAS.put()
      ?};
      REM_ZAS.next()
   !}
?};
REM_ZAS.cntx_pop();
SRSR.cntx_pop();
1


\REM_ZAS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.14]
:: OPIS: Przekopiowanie wartości numeru fabrycznego ze środka trwałego do numeru fabrycznego zasobu remontowego
::----------------------------------------------------------------------------------------------------------------------
'Przekopiowanie wartości numeru fabrycznego ze środka trwałego do numeru fabrycznego zasobu remontowego.'


\SK_CHO_ZUS_Z3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: ER/WRT/XP/12.51/2003/0026 Zaświadczenie płatnika składek ZUS Z-3 - błąd w przypadku urlopu
::       w jednym miesiącu nieprawidłowe odejmowanie urlopu od miesięcznego wynagrodzenia brutto
::       dla pracownika o stawka godzinowa
::----------------------------------------------------------------------------------------------------------------------
::Poprawka emitowana w 2018 roku, dlatego startowa maska 2018
::pole SK_CHO.SK_NR=0 i ma opis Średnia urlopowa w polu SK_CHO.SKLADNIK
::zostanie uzupełniony wartość numeru rubryki 438
_start:=2018;
_rok_b:=date~1;
_where:='where (SK_CHO.REFERENCE like \'s_ch'+$_start+'%\'';
_start+=1;
{!
|? _start<=(_rok_b)
|! _where+=' or SK_CHO.REFERENCE like \'s_ch'+$_start+'%\'';
   _start+=1
!};
_where+=') ';
_where+=' and SK_CHO.SK_NR=0 and SK_CHO.LT=\'URLOP\'';
_sql:='select SK_CHO.REFERENCE as REF, substr(SK_CHO.REFERENCE,5,4) as ROK from @SK_CHO ';
_sql+=_where;
_sql+='order by 2';
_tab:=sql(_sql);
_lrec:=_tab.size();
_lmod:=0;
{? _tab.first()
|| SK_CHO.cntx_psh();
   _rok:=_tab.ROK;
   SK_CHO.use('s_ch'+_rok);
   SK_CHO.prefix();
   {!
   |? {? _rok<>_tab.ROK
      || SK_CHO.use('s_ch'+_tab.ROK);
         SK_CHO.prefix()
      ?};
      {? SK_CHO.seek(_tab.REF,)
      || SK_CHO.SK_NR:=438;
         _lmod+=SK_CHO.put(1)
      ?};
      _rok:=_tab.ROK;
      _tab.next()
   !};
   SK_CHO.cntx_pop()
?};
__UPG.msg('Zaktualizowano brakujące kody składników urlopowych w rozliczeniu wynagrodzeń/zasiłków chorobowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
1


\SK_CHO_ZUS_Z3_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: ER/WRT/XP/12.51/2003/0026 Zaświadczenie płatnika składek ZUS Z-3 - błąd w przypadku urlopu
::       w jednym miesiącu nieprawidłowe odejmowanie urlopu od miesięcznego wynagrodzenia brutto
::       dla pracownika o stawka godzinowa
::----------------------------------------------------------------------------------------------------------------------
'Zaktualizowano brakujące kody składników urlopowych w rozliczeniu wynagrodzeń/zasiłków chorobowych.'


\del_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.14]
:: OPIS: Usunięcie parametru ZWS_KDOST_BAD - Kwalifikacja dostaw i badania
::----------------------------------------------------------------------------------------------------------------------
:Identyfikator parametru do usunięcia (pole ID z tabeli FP_DEF)
_id:='ZWS_KDOST_BAD';

FP_STR.cntx_psh();
FP_DEF.cntx_psh();
FP_WYK.cntx_psh();
FP_FIR.cntx_psh();
FP_AKT.cntx_psh();
FP_INS.cntx_psh();

::Usuwanie danych z tabeli FP_DEF
FP_DEF.index('ID');
FP_DEF.prefix('T',_id,);

{? FP_DEF.first() ||
   _def:=FP_DEF.ref;
   FP_DEF.del(1);

:: Usuwanie danych z tabeli FP_STR
   FP_STR.index(FP_STR.ndx_tmp(,1,'FP_DEF',,,'ID',,));
   FP_STR.prefix(_def,);
   {? FP_STR.first() || FP_STR.del(1) ?};

:: Usuwanie powiązanych danych z tabeli FP_WYK
   FP_WYK.index(FP_WYK.ndx_tmp(,1,'FP_DEF',,,'FP_AKT',,));
   FP_WYK.prefix(_def,);
   {? FP_WYK.first() ||
      {! |?
         FP_WYK.del(1);
         FP_WYK.next()
      !}
   ?};

:: Usuwanie powiązanych danych z tabeli FP_FIR
   FP_FIR.index('UNIQUE');
   FP_FIR.prefix(_def,);
   {? FP_FIR.first() ||
      {! |?

::       Usuwanie powiązanych danych z tabeli FP_INS
         FP_INS.index('UNIQUE');
         FP_INS.prefix(FP_FIR.ref,);
         {? FP_INS.first() ||
            {! |?
               FP_INS.del(1);
               FP_INS.next()
            !}
         ?};

         FP_FIR.del(1);
         FP_FIR.next()
      !}
   ?};

:: Usuwanie powiązanych danych z tabeli FP_AKT
   FP_AKT.index(FP_AKT.ndx_tmp(,1,'FP_DEF',,,'FP_WER',,));
   FP_AKT.prefix(_def,);
   {? FP_AKT.first() ||
      {! |?
         FP_AKT.del(1);
         FP_AKT.next()
      !}
   ?};
   __UPG.msg('Parametr o identyfikatorze ''%1'' został usunięty z systemu.' [_id]);
   _status:=1
||
   __UPG.msg('Usunięcie parametru o identyfikatorze ''%1'' nie powiodło się.' [_id]);
   _status:=-1
?};

FP_STR.cntx_pop();
FP_DEF.cntx_pop();
FP_WYK.cntx_pop();
FP_FIR.cntx_pop();
FP_AKT.cntx_pop();
FP_INS.cntx_pop();
_status


\del_par_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.14]
:: OPIS: Usunięcie parametru ZWS_KDOST_BAD - Kwalifikacja dostaw i badania
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie parametru ZWS_KDOST_BAD - Kwalifikacja dostaw i badania.'


\pkd_zes_inne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesosnow [20.14]
:: OPIS: Formuła dodaje czynność PKD_ZES_INNE do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_uid:='PKD_ZES_INNE';
_lista:=''+"'Specjalista ds. kadr','Specjallista ds. zleceniobiorców','Przeglądający Kadry','Dyrektor HR','Przeglądający Zleceniobiorców'";

_ROLE:=sql(
   'select distinct B_ROLE.NAME '
   'from B_ROLE '
   'where B_ROLE.NAME in (:_a) '
   'order by 1',
   _lista
);

{? type_of(_ROLE)=type_of(~~)
|| __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
   return(0)
?};

{? _ROLE.first()
|| _ret:=1;
   {!
   |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_ROLE.NAME])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_ROLE.NAME]);
         _ret:=-1
      ?};
      _ROLE.next()
   !};
   _ret
|| __UPG.msg('Brak ról do przypisania dla czynności %1.'[_uid]);
   1
?}


\pkd_zes_inne_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesosnow [20.14]
:: OPIS: Formuła dodaje czynność PKD_ZES_INNE do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "PKD_ZES_INNE - zaświadczenia dla innych form współpracy" do odpowiednich ról'


\f_zatra_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.14]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

B_ACTION.cntx_psh();
B_ACTION.f_clear();
B_ACTION.index('UNIK');
B_ACTION.prefix();

:: Lista czynności, dla których przypisywane zostana formy współpracy. Założenia:
:: 1. Czynności są oddzielone przecinkami.
:: 2. W ramach czynności przekazywana jest również informacja o kodach form współpracy (oddzielonych znakiem dwukropka).
::    Zakładamy, że duże litery dotyczą form docelowych.
_lista:=spli_str('PKD_EZK_OPDI:P:K:T,PKD_EZK_ORDI:P:K:T',',');
_lenl:=obj_len(_lista);

{! _lp:=1 .. _lenl
|! _acz:=spli_str(_lista[_lp],':');
   _uid:=_acz[1];
   {? B_ACTION.find_key(_uid,)
   || _lena:=obj_len(_acz);
      {! _lf:=2 .. _lena
      |! _fz:=_acz[_lf];
         __UPG.msg('Forma współpracy: %1 | Czynność: %2 - %3' [_fz,_uid,B_ACTION.NAME]);
         _add:=exec('f_zatra_add','f_zatr',{? -_fz=_fz || 'N' || 'T' ?},_fz,_uid);
         {? _add=''
         || __UPG.msg(' - uprawnienia nadane')
         || __UPG.msg(_add);
            _ret:=0
         ?}
      !}
   || __UPG.msg('"%1" - nieprawidłowy kod czynności.' [_uid]);
      _ret:=0
   ?};
   obj_del(_acz)
!};

B_ACTION.cntx_pop();

_ret


\f_zatra_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.14]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Nadanie czynnościom uprawnień do form współpracy'


\role_personel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.14]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi.
::----------------------------------------------------------------------------------------------------------------------
_status:=1;
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_add:="
   _rola:=_a;
   _lista:=_b;
   _ok:=1;
   _uid:=spli_str(_lista,'.');
   {! _lp:=1 .. obj_len(_uid)
   |! {? exec('add_actrol_one','#b_role',_rola,_uid[_lp])
      || __UPG.msg('Roli ''%1'' nadano uprawnienia do czynności ''%2''.' [_rola,_uid[_lp]])
      || __UPG.msg('Nadanie roli ''%1'' uprawnień do czynności ''%2'' nie powiodło się.' [_rola,_uid[_lp]]);
         _ok:=0
      ?}
   !};
   _ok";
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='PKD_EZK_OPDI';
_rola:='Dyrektor HR';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='PKD_EZK_OPDI';
_rola:='Przeglądający Kadry';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='PKD_EZK_ORDI';
_rola:='Specjalista ds. kadr';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='PKD_EZK_ORDI';
_rola:='Specjalista ds. płac';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='ZWS_PAR_ASDD';
_rola:='Administrator';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='ZWS_PAR_ASDZ';
_rola:='Administrator';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='ZWS_PSD_APKD';
_rola:='Administrator';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_act:='ZWS_PSD_APPL';
_rola:='Administrator';
_ref:=exec('ref','#b_role',_rola);
{? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
|| __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_act]);
   _status:=-1
|| {? ~_add(_rola,_act)
   || _status:=-1
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_status


\role_personel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.14]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ról/uprawnień związana z czynnościami personelowymi.'


\tlumaczenia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja tłumaczeń
::----------------------------------------------------------------------------------------------------------------------
exec('init','tlumaczenia');

exec('czytaj','#stalesys',,XINFO,'SLJEZYK');

_kod:='Wydruki dokumentów sprzedaży i korekt';
TRANS_GR.cntx_psh();
TRANS_GR.index('KOD');
TRANS_GR.prefix(_kod,);
{? TRANS_GR.first()
||
   _kod:='1000218';
   TRANS_PL.cntx_psh();
   TRANS_PL.index('GR');
   TRANS_PL.prefix(TRANS_GR.ref(),_kod,);
   {? TRANS_PL.first()
   ||
      _jezyki:='ANG|POL|';
      SLO.cntx_psh();
      SLO.index('SL');
      {!
      |? _jezyki*'|'
      |!
         _wsk:=_jezyki*'|';
         _jezyk:=(_wsk-1)+_jezyki;
         _jezyki:=_wsk-_jezyki;
         SLO.prefix(XINFO.SLJEZYK,_jezyk,);
         {? SLO.first()
         ||
            TRANSLAT.cntx_psh();
            TRANSLAT.blank();
            TRANSLAT.TRANS_PL:=TRANS_PL.ref();
            TRANSLAT.T:='CN';
            TRANSLAT.JEZYK:=SLO.ref();
            TRANSLAT.add(1);
            TRANSLAT.cntx_pop()
         ?}
      !};
      SLO.cntx_pop()
   ?};
   TRANS_PL.cntx_pop()
?};
TRANS_GR.cntx_pop();
__UPG.msg('Aktualizacja tłumaczeń powiodła się.'@);
1


\tlumaczenia_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja tłumaczeń - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tłumaczeń.'


\plugins
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Uzupełnia definicję wtyczek wdrożeniowych
::----------------------------------------------------------------------------------------------------------------------
exec('init','plugins');
__UPG.msg('Uzupełniono definicję wtyczek wdrożeniowych.');
1


\plugins_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Uzupełnia definicję wtyczek wdrożeniowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie definicji wtyczek wdrożeniowych:\n'
'DOK_KS_P_001\n'
'DOK_KS_K_001\n'


\PROD_REJ_OPIS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Uzupełnia opis operacji grupowych w oknie rejestracji produkcji
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

PROD_REJ.trig_off('*', '*');
PROD_REJ.cntx_psh();
PROD_REJ.prefix();
GROP.cntx_psh();
GROP.prefix();
_res:=PROD_REJ.for_each("
   __lrec+=1;
   _put:=0;
   {? PROD_REJ.SRODZ='ZL' & PROD_REJ.GROP<>null()
   || PROD_REJ.GROP();
      exec('fields_4rej','zl_grop');
      _put:=1
   |? PROD_REJ.SRODZ='PL' & PROD_REJ.PL_OGR<>null()
   || exec('fields_4rej','po_wyk');
      _put:=1
   ?};

   {? _put>0
   || {? PROD_REJ.put()
      || __lmod+=1
      ?}
   ?}
");
GROP.cntx_pop();
PROD_REJ.cntx_pop();
PROD_REJ.trig_on('*', '*');
__UPG.msg('Zaktualizowano opisy operacji grupowych w oknie rejestracji produkcji.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\PROD_REJ_OPIS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Uzupełnia opis operacji grupowych w oknie rejestracji produkcji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie opisu operacji grupowych w oknie rejestracji produkcji'


\szablony
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Import zestawów danych i definicji szablonów.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_ASDZ,ZWS_PAR_ASDD',','));
__UPG.msg('Zaktualizowano zestawy danych i definicje szablonów.');
1


\szablony_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Import zestawów danych i definicji szablonów - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import zestawów danych i definicji szablonów.'


\fiks_67
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.14]
:: OPIS: kod - ER/WRT/XP/20.14/2004/0029 : E-Bilans mała
::----------------------------------------------------------------------------------------------------------------------
_s_bil:='eBilansM'; _ret:=0;
_sql:=sql('select REFERENCE as REF from GR_SIK where SKROT=\':_a\'',_s_bil);
{? _sql.first()
|| GR_SIK.cntx_psh();
   GR_SIK.prefix();
   {!
   |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
      || _r1:=exec('fiks_67a','upgrade_2014',GR_SIK.ref(),'Aktywa_B_III_A','Aktywa_B_III_A','A_B_III_A',
                   'a)krótkoterminowe aktywa finansowe, w tym:'
                   );
         _r2:=exec('fiks_67a','upgrade_2014',GR_SIK.ref(),'Pasywa_B_II','Pasywa_B_II','P_B_II',
                   'Zobowiązania długoterminowe, w tym:'
                   );
         _r3:=exec('fiks_67a','upgrade_2014',GR_SIK.ref(),'Pasywa_B_II_1','Pasywa_B_II_1','P_B_II_1',
                   '- z tytułu kredytu i pożyczek'
                   );
         {? (_r1 | _r2 | _r3) & GR_SIK.AKC<>'N'
         || GR_SIK.AKC:='N'; GR_SIK.put()
         ?};
         {? _r1 | _r2 | _r3 || _ret:=1 ?};
         ~~
      ?};
      _sql.next()
   !};
   GR_SIK.cntx_pop()
?};
{? _ret
|| __UPG.msg('Wykonane działania naprawcze na sprawozdaniu E-bilans mała')
|| __UPG.msg('Brak potrzeby wykonania działania naprawczego na sprawozdaniu E-bilans mała')
?};
&_sql;
1


\fiks_67_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.14]
:: OPIS: kod - ER/WRT/XP/20.14/2004/0029 : E-Bilans mała
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Sprawozdanie E-Bilans mała - poprawa błędnych opisów w wierszach'


\fiks_67a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.14]
:: OPIS: kod - ER/WRT/XP/20.14/2004/0029 : E-Bilans mała
::   WE: _a - sprawozdanie
::       _b - e-Kod stary
::       [_c] - e-Kod nowy
::       [_d] - kod nowy
::       [_e] - nowa nazwa
::       [_f] - lp
::       [_g] - nazwa dla kontroli
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
DEFW.cntx_psh();
DEFW.index('E_KOD'); DEFW.prefix(_a,_b,);
{? DEFW.first()
|| _kod:=DEFW.KOD;
   _naz:=DEFW.NAZWA;
   _zm:='';
   {? var_press('_c')>0 & DEFW.E_KOD<>_c || _zm+='eKod na: '+_c+', '; DEFW.E_KOD:=_c ?};
   {? var_press('_d')>0 & DEFW.KOD<>_d || _zm+='kod na: '+_d+', '; DEFW.KOD:=_d ?};
   {? var_press('_e')>0 & DEFW.NAZWA<>_e || _zm+='nazwę na: '+_e+', '; DEFW.NAZWA:=_e ?};
   {? var_press('_f')>0 & DEFW.LP<>_f || _zm+='lp na: '+$_f+', '; DEFW.LP:=_f ?};
   DEFW.cntx_psh();
   DEFW.prefix();
   {? (var_press('_g')<=0 | _g=DEFW.NAZWA) & _zm<>''
   || _ret:=DEFW.put(1)
   ?};
   DEFW.cntx_pop()
?};
DEFW.cntx_pop();
_ret



\fiks_66
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.14]
:: OPIS: kod - ER/WRT/XP/20.14/2004/0028
::       JPK_FA(2) i JPK_FA(3) brak NIP UE
::----------------------------------------------------------------------------------------------------------------------
_r1:=_r2:=_r3:=_r4:=_ret:=0;
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','FA',null,date(2019,7,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'P_5B');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[50]';
      _r1:=ISTDEFS.put()
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'P_4B');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[50]';
      _r2:=ISTDEFS.put()
   ?}
?};

ISTDEF.prefix('FKS','J','FA',null,date(2019,11,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'P_5B');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[50]';
      _r3:=ISTDEFS.put()
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'P_4B');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[50]';
      _r4:=ISTDEFS.put()
   ?}
?};
{? _r1 | _r2 | _r3 | _r4 || _ret:=1 ?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
{? _ret
|| __UPG.msg('Wykonane działania naprawcze na specyfikacji JPK_FA(2) i JPK_FA(3)')
|| __UPG.msg('Brak potrzeby wykonania działania naprawczego na specyfikacji JPK_FA(2) i JPK_FA(3)')
?};
1


\fiks_66_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.14]
:: OPIS: kod - ER/WRT/XP/20.14/2004/0028 JPK_FA(2) i JPK_FA(3) brak NIP UE - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'JPK_FA(2) i JPK_FA(3) brak NIP UE'


\akt_2014_04
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: Aktualizacja 20.14_04: Przestój, kwarantanna, zasiłek dodatkowy w 2020 pandemia koronowirusa
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';

:: Aktualizacja opisów składników płacowych.
{? +exec('import','rubryki','g','7118,7119,7120,7121,7122,7123',1,0)
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);

:: Definicje atrybutów.
exec('__RUB','object');
__RUB.fill();
exec('add_attr','rubatr',1225,12253,'Zasiłek dodatkowy',1,,,7118);
exec('add_uses','rubatr',7118,14,15,16,19,123,129,151,162,191,192,195);
exec('add_uses','rubatr',7118,196,1223,1293,1922,12231,14312);
exec('add_attr','rubatr',131,1314,'Przestój',1,,,7119);
exec('add_attr','rubatr',45,453,'Przestój',1,,,7120,7121);
   exec('add_attr','rubatr',453,4531,'Wynagrodzenie za przestój',1,,,7120);
   exec('add_attr','rubatr',453,4532,'Korekta wynagrodzenia za przestój',1,,,7121);

exec('add_attr','rubatr',3,34,'Stałe składn. wyn. wypłacane podczas przestoju',1,
   'Stałe składniki wynagrodzenia z przebiegu zatrudnienia wypłacane podczas przestoju.');

exec('add_attr','rubatr',45,454,'Stałe składniki wynagrodzenia',1);
   exec('add_attr','rubatr',454,4541,'Dodatki funkcyjne',1);
      exec('add_attr','rubatr',4541,45411,'Dodatek funkcyjny',1,,,101);
      exec('add_attr','rubatr',4541,45412,'Dodatek brygadzistowski',1,,,102);
   exec('add_attr','rubatr',454,4542,'Dodatki stałe',1);
      exec('add_attr','rubatr',4542,45421,'Dodatek stały',1,,,110);
::W Xpertis 12.51_83 dodany został atrybut 137-Nieobecności niestanowiące obniżenia min. wyn.
::natomiast w Merit ten atrybut był już używany 137-Służba wojskowa
::zmiana numeru atrybutu na 138-Służba wojskowa
_zm_137:=1;
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
{? RA_DEF.find_key(137)
|| {? RA_DEF.NAZWA='Służba wojskowa'
   || RA_DEF.SYMBOL:=138;
      _zm_137:=RA_DEF.put(1)
   ?}
?};
{? ~_zm_137
|| __UPG.msg('Nie udała się aktualizacja symbolu atrybutu 137 - Służba wojskowa na symbol 138.')
?};
exec('add_attr','rubatr',13,137,'Nieobecności niestanowiące obniżenia min. wyn.',1,
   'Nieobecności niestanowiące obniżenia minimalnego wynagrodzenia',,7119);
exec('add_uses','rubatr',7119,14,152,161,192,199,1923,19421,14350);
exec('add_uses','rubatr',7120,6313107,48);
exec('add_uses','rubatr',7121,6313107,48);


:: aktualizacja atrybutów rubryk
exec('add_attr','rubatr',1211,12113,'Kwarantanna',1,,,7123);
exec('add_uses','rubatr',7122,14,15,16,19,123,126,129,151,162,191,192);
exec('add_uses','rubatr',7122,195,196,1223,1293,1921,12231,14313);

exec('add_attr','rubatr',12211,122113,'Kwarantanna',1,,,7122);
exec('add_uses','rubatr',7123,14,15,16,19,100,126,129,151,161,191,192);
exec('add_uses','rubatr',7123,195,196,1001,1293,1921,10011,14331,100112);

:: usunięcie nieprawidłowych atrybutów
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
{? RA_DEF.find_key(1213)
|| exec('cascadel','#table',RA_DEF)
?};
{? RA_DEF.find_key(12214)
|| exec('cascadel','#table',RA_DEF)
?};

_wyn:=_zas:=null();
R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? R.find_key(7122)
|| _zas:=R.ref()
?};
{? R.find_key(7123)
|| _wyn:=R.ref()
?};
R.cntx_pop();

RA_USE.index('RA_USE');
{? RA_DEF.find_key(12111) & _wyn
|| RA_USE.prefix(RA_DEF.ref,_wyn);
   {? RA_USE.first() || {! |? RA_USE.del() !} ?}
?};
{? RA_DEF.find_key(122111) & _zas
|| RA_USE.prefix(RA_DEF.ref,_zas);
   {? RA_USE.first() || {! |? RA_USE.del() !} ?}
?};

:: Aktualizacja tabeli kodów nieobecności chorobowych
KST_CHOR.cntx_psh();
KST_CHOR.index('KST_CHOR');
KST_CHOR.prefix();
{? _wyn & _zas & ~KST_CHOR.find_key(7123)
|| KST_CHOR.WYN:=_wyn;
   KST_CHOR.ZAS:=_zas;
   KST_CHOR.add()
?};
KST_CHOR.cntx_pop();
__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\akt_2014_04_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: Aktualizacja: Przestój, kwarantanna, zasiłek dodatkowy w 2020 pandemia koronowirusa
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja: Przestój, kwarantanna, zasiłek dodatkowy w 2020'


\formuly_PRZESTOJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: Aktualizacja 20.14_04 formuł płacowych o naliczanie składników Przestój, kwarantanna, dodatkowa opieka.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='465,510,529,7118,7119,7120,7121,7122,7123';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,R,%1'[%255],',');
_err:=exec('formuly_import','upgrade_2014',_tp,_rn,'formuly_txt');

{? _err>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err]);
   -1
|| 1
?}


\formuly_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: Funkcja do aktualizacji formuł płacowych.
::   WE: _a [ARRAY] - Tablica aktualizowanych typów formuł płacowych.
::       _b [ARRAY] - Tablica kodów aktualizowanych składników płacowych.
::       _c [STRING] - Nazwa formuły do importu formuł płacowych.
::   WY: _err (0/1) - Liczba błędów, które wystąpiły podczas importu.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;
_formula:=_c;
_err:=0;

FM.cntx_psh();
{! _ni:=1..obj_len(_tp)
|! FM.index('FORMNAZ');
   FM.prefix(exec('ref_firma','ustawienia'),_tp[_ni]);
   {? FM.size()>0
   || {! _mi:=1..obj_len(_rn)
      |! _fm:=form(exec(_formula,'upgrade_2014',_tp[_ni],#_rn[_mi]));
         {? _fm<>''
         || _ok:=1;
            _org:='';
            _found:=FM.find_tab(,'R','RN','=',#_rn[_mi]);
            {? _found
            || _org:=FM.memo_txt(,1,'F')
            || FM.blank();
               FM.TP:=_tp[_ni];
               FM.R:=__RUB.ref(#_rn[_mi]);
               FM.W:={? 'U,F,D'*_tp[_ni] || 'T' || 'N' ?};
               {? ~FM.add()
               || __UPG.msg('Utworzenie formuły typu "%1" dla składnika %2 nie powiodło się.'[_tp[_ni],_rn[_mi]]);
                  _err+=0;
                  _ok:=0
               ?}
            ?};
            {? _found & _fm=_org
            || __UPG.msg('Formuła typu "%1" dla składnika %2 nie wymaga aktualizacji.'[_tp[_ni],_rn[_mi]])
            |? _ok
            || FM.memo_set(_fm,'F');
               {? FM.memo_put(,'F')
               || {? _found
                  || __UPG.msg('Zaktualizowano formułę typu "%1" dla składnika %2.'[_tp[_ni],_rn[_mi]])
                  || __UPG.msg('Utworzono formułę typu "%1" dla składnika %2.'[_tp[_ni],_rn[_mi]])
                  ?}
               |? _found
               || __UPG.msg('Aktualizacja formuły typu "%1" dla składnika %2 nie powiodła się.'[_tp[_ni],_rn[_mi]]);
                  _err+=1
               ?}
            ?}
         ?}
      !}
   ?}
!};
FM.cntx_pop();
_err


\formuly_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: Zwraca treść formuły płacowej.
::   WE: _a [STRING] - typ formuły
::       _b [INTEGER] - numer rubryki
::   WY: treść formuły
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_tp:=_a;
_rn:=_b;

{? _rn=465 & _tp='U'
:: ==================
:: Wynagr.za chorobę
|| "
{? FUNKCJE.L_SYS(1211)>0
|| exec('wynchor','!ppl_pll_nals',9,'u')
?}"

|? _rn=465 & _tp='F'
:: ==================
:: Wynagr.za chorobę
|| "
{? FUNKCJE.L_SYS(1211)>0
|| exec('wynchor','!ppl_pll_nals',9,'f')
?}"

|? _rn=510 & _tp='U'
:: =================
:: Zas.chorobowy
|| "
{? FUNKCJE.L_SYS(12211)>0
|| exec('wynchor','!ppl_pll_nals',7,'u')
?}"

|? _rn=510 & _tp='F'
:: =================
:: Zas.chorobowy
|| "
{? FUNKCJE.L_SYS(12211)>0
|| exec('wynchor','!ppl_pll_nals',7,'f')
?}"

|? _rn=529 & _tp='U'
:: =======
:: Zasiłek opiekuńczy
|| "
{? FUNKCJE.L_SYS(1225)>0
|| exec('wynchor','!ppl_pll_nals',6,'u')
?}"

|? _rn=529 & _tp='F'
:: =======
:: Zasiłek opiekuńczy
|| "
{? FUNKCJE.L_SYS(1225)>0
|| exec('wynchor','!ppl_pll_nals',6,'f')
?}"

|? _rn=7118 & _tp<>'R'
:: =================
:: Opieka zas. dod.
|| "
FUNKCJE.NK(7118)"

|? _rn=7119 & _tp<>'R'
:: =======
:: Nieob. przestojowa
|| "
FUNKCJE.NG(7119)"

|? _rn=7120 & _tp<>'R'
:: ==================
:: Wyn. przestój
|| "
exec('przestoj','!ppl_pll_nals')"

|? _rn=7121 & _tp<>'R'
:: ============
:: Kor. przestój
|| "
DoList.c[12]$2"

|? _rn=7122 & _tp<>'R'
:: ==================
:: Kwarantanna zas.
|| "
FUNKCJE.NK(7122)"

|? _rn=7123 & _tp<>'R'
:: ==================
:: Kwarantanna wynagr.
|| "
FUNKCJE.NK(7123)"

|? _rn=7118 & _tp='R'
:: =================
:: Opieka zas. dod.
|| "
'wyliczenie znajduje sie w formule: zasilki, wywołanie w 200';~~"

|? _rn=7122 & _tp='R'
:: =================
:: Kwarantanna zas.
|| "
'wyliczenie znajduje sie w formule: zasilki, wywołanie w 200';~~"

|| ""
?}


\formuly_PRZESTOJ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: Aktualizacja formuł płacowych o naliczanie składników Przestój, kwarantanna, dodatkowa opieka  - opis.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie formuł płacowych o naliczanie składników Przestój, kwarantanna, dodatkowa opieka'


\fiks_71
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.14]
:: OPIS: kod: ER/WRT/XP/20.14/2004/0033
::       Nieprawidłowo wyliczana kwota VAT split payment
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_start:=date(2019,1,1);
_ar:=SSTALE.AR;
_ao:=SSTALE.AO;
ROK_F.cntx_psh();
ROK_F.index('ROKPOCZ');
ROK_F.prefix();
{? ROK_F.first()
|| OKRO_F.cntx_psh();
   OKRO_F.index('ROK');
   {!
   |? {? ROK_F.ZAM<>'T' & ROK_F.POCZ_ROK>=_start
      || SSTALE.AR:=ROK_F.ref();
         OKRO_F.prefix(SSTALE.AR);
         {? OKRO_F.first()
         || {!
            |? SSTALE.AO:=OKRO_F.ref();
               exec('open_tabele','open_tab');
               echo('Naprawa dokumentów - Firma: '+ROK_F.FIRMA().SYMBOL+', rok: '+ROK_F.NAZ+', okres: '+OKRO_F.NAZ);
               DOK.prefix();
               _sql:=sql(
                  'select DOK.REFERENCE as ref, count(*) as size '+
                  'from DOK join VPOZ where DOK.A=\'T\' '+
                  'group by DOK.REFERENCE, DOK.NK '+
                  'HAVING count(*)>1 '+
                  'order by 1'
               );
               {? _sql.first()
               || {!
                  |? {? DOK.seek(BIT.sqlint(_sql.REF),)
                     || exec('upd_dok','fks_sp',"exec('update_ks1','fks_sp')");
         _ret:=1
                     ?};
                     _sql.next()
                  !}
               ?};
               &_sql;
               OKRO_F.next()
            !}
         ?}
      ?};
      ROK_F.next()
   !};
   OKRO_F.cntx_pop()
?};
ROK_F.cntx_pop();
{? SSTALE.AO<>_ao | SSTALE.AR<>_ar
|| SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   exec('open_tabele','open_tab')
?};
echo();
{? _ret=1
|| __UPG.msg('Poprawiono błąd nieprawidłowo wyliczanych kwot VAT split payment')
|| __UPG.msg('Nie znaleziono błędu z nieprawidłowo wyliczanymi kwotami VAT split payment')
?};
1


\fiks_71_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.14]
:: OPIS: kod: ER/WRT/XP/20.14/2004/0033
::       Nieprawidłowo wyliczana kwota VAT split payment
::----------------------------------------------------------------------------------------------------------------------
'Poprawiono błąd nieprawidłowo wyliczanych kwot VAT split payment'


\fiks_68
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.14]
:: OPIS: kod: ER/WRT/XP/20.14/2004/0030
::       JPK_SF:Błędne wyświetlanie opisów dla P_5A i P_5B
::----------------------------------------------------------------------------------------------------------------------
_r1:=_r2:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF','Jednostka');
{? ISTDEF.first()
|| {!
   |? {? ISTDEF.VER+4='v1-2'
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS');
         ISTDEFS.prefix(ISTDEF.ref(),'tns:P_5A',);
         _ile:=ISTDEFS.size();
         {? ISTDEFS.first()
         || _lp:={? _ile=1 || 2 || 1 ?};
            {!
            |? ISTDEFS.memo_set(
                  {? _lp=1
                  || 'Wskazanie, czy sprawozdanie finansowe zostało sporządzone przy założeniu kontynuowania działalności gospodarczej przez jednostkę w dającej się przewidzieć przyszłości: true - sprawozdanie sporządzone przy założeniu kontynuowania działalności, false - sprawozdanie zostało sporzadzone przy zalożeniu, że działalność nie będzie kontynuowana'
                  || 'Wskazanie, czy sprawozdanie finansowe zostało sporządzone przy założeniu kontynuowania działalności gospodarczej przez jednostkę w dającej się przewidzieć przyszłości'
                  ?},
                  'ANOTACJA'
               );
               ISTDEFS.memo_put(,'ANOTACJA');
               ISTDEFS.put();
          _r1:=1;
               _lp+=1;
               ISTDEFS.next()
            !}
         ?};
         ISTDEFS.prefix(ISTDEF.ref(),'tns:P_5B',);
         {? ISTDEFS.first()
         || {!
            |? ISTDEFS.memo_set(
                  'Wskazanie, czy nie istnieją okoliczności wskazujące na zagrożenie kontynuowania przez nią działalności: true - Brak okoliczności wskazujących na zagrożenie kontynuowania działalności; false - Wystąpiły okoliczności wskazujące na zagrożenie kontynuowania działalności',
                  'ANOTACJA'
               );
               ISTDEFS.memo_put(,'ANOTACJA');
               _r2:=1;
          ISTDEFS.put();
               ISTDEFS.next()
            !}

         ?};
         ISTDEFS.cntx_pop()
      ?};
      ISTDEF.next()
   !}
?};
ISTDEF.cntx_pop();
{? _r1=1
|| {? _r2=1
   || _msg:='Naprawiono błędne wyświetlanie opisów dla P_5A i P_5B'
   || _msg:='Naprawiono błędne wyświetlanie opisów dla P_5A, nie znaleziono błędów dla P_5B'
   ?}
|| {? _r2=1
   || _msg:='Naprawiono błędne wyświetlanie opisów dla P_5B, nie znaleziono błędów dla P_5A'
   || _msg:='Nie znaleziono błędów związanych z wyświetlaniem opisów dla P_5A i P_5B'
   ?}
?};
__UPG.msg(_msg);
1


\fiks_68_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.14]
:: OPIS: kod: ER/WRT/XP/20.14/2004/0030
::----------------------------------------------------------------------------------------------------------------------
'Poprawka błędu JPK_SF:Błędne wyświetlanie opisów dla P_5A i P_5B'


\fiks_70
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.14]
:: OPIS: kod: ER/WRT/XP/20.14/2004/0031
::       Błąd przy jpk-sf 1.2 dla jednostki małej w  tys
::----------------------------------------------------------------------------------------------------------------------
_r1:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','SF','JednostkaMalaWTysiacach(1)_v1-2',);
{? ISTDEF.first()
|| ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'jma:F_I_1',);
   {? ISTDEFS.first()
   || _start:=_next:=0;
      ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.TREE,);
      {? ISTDEFS.next()
      || _next:=ISTDEFS.LP
      ?};
      ISTDEFS.cntx_pop();
      ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),#ISTDEFS.ref());
      {? ISTDEFS.first()
      || {!
         |? {? ISTDEFS.OPIS='jma:PozycjaUszczegolawiajaca_1'
            || ISTDEFS.OPIS:='jma:PozycjaUszczegolawiajaca';
               _r1:=1;
               ISTDEFS.put()
            |? ISTDEFS.OPIS='jma:G_I_A_1'
            || _start:=ISTDEFS.LP
            ?};
            _start=0 & ISTDEFS.next()
         !}
      ?};
      ISTDEFS.cntx_pop();
      {? _next & _start
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('LP'); ISTDEFS.prefix(ISTDEF.ref());
         {! _ii:=_next-1//-1.._start
         |! {? ISTDEFS.find_key(_ii)
            || ISTDEFS.del()
            ?}
         !};
         {? ISTDEFS.find_key(_next)
         || {!
            |? ISTDEFS.LP:=_start;
               ISTDEFS.put();
               _start+=1;
               ISTDEFS.next()
            !}
         ?};
         ISTDEFS.cntx_pop()
      ?}
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
{? _r1=1
|| __UPG.msg('Poprawiono błąd przy jpk-sf 1.2 dla jednostki małej w  tys')
|| __UPG.msg('Nie znaleziono błędów przy jpk-sf 1.2 dla jednostki małej w  tys')
?};
1


\fiks_70_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.14]
:: OPIS: kod: ER/WRT/XP/20.14/2004/0031
::       Błąd przy jpk-sf 1.2 dla jednostki małej w  tys
::----------------------------------------------------------------------------------------------------------------------
'Poprawka błędu przy jpk-sf 1.2 dla jednostki małej w  tys'


\FAKS_ERR_2006_0027
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje FAKS.DZ, FAKS.DA_ZAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKS.names();
TYPYSP.cntx_psh();
FAKS.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("
         _uidref:=FAKS.uidref();
         __lrec+=1;
         _put:=0;
         {? FAKS.T().KOR='N' & FAKS.DZ<>date(0,0,0)
         ||
            FAKS.DA_ZAL:=FAKS.DZ;
            FAKS.DZ:=date(0,0,0);
            __UPG.msg('Przenieziono wartość pola FAKS.KZ do FAKS.DA_ZAL. [%1,%2]'[FAKS.SYM,_uidref]);
            _put:=1
         ?};
         {? _put & FAKS.put() || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();
TYPYSP.cntx_pop();

__UPG.msg('Zaktualizowano pola w nagłówku dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\FAKS_ERR_2006_0027_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje FAKS.DZ, FAKS.DA_ZAL - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól w nagłówku dokumentów sprzedaży i zakupu.'


\akt_2014_09
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.14]
:: OPIS:  Aktualizacja atrybutów składników wykorzystywanych w potrąceniach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');
__RUB.fill();

exec('add_attr','rubatr',211,2117,'Zwiększenie ochrony minimalnego wynagrodzenia.',1,,,825,827);

__UPG.msg('Zakończono aktualizację atrybutów.');

_result:=-1;
_symbol:='PODW_OCH';

KART_DEF.cntx_psh();
KART_DEF.clear();
KART_DEF.index('SYMBOL');
{? KART_DEF.find_key(_symbol,_symbol)
|| __UPG.msg('Kartoteka dodatkowa "Zwiększenie ochrony minimalnego wynagr." już istnieje.');
   _result:=1
|| KART_DEF.blank(1);
   KART_DEF.SYMBOL:=_symbol;
   KART_DEF.NAZWA:='Zwiększenie ochrony minimalnego wynagr.';
   KART_DEF.WER:='ODDO';
   KART_DEF.OPIS:='Powód zwiększenia';
   KART_DEF.OD:='Data od';
   KART_DEF.ODW:=1;
   KART_DEF.WARTOSC:='Procent zwiększenia';
   KART_DEF.CZYWART:='_';
   KART_DEF.DO:='Data do';

   {? KART_DEF.add(1)
   || __UPG.msg('Kartoteka dodatkowa "Zwiększenie ochrony minimalnego wynagr." została dodana.');
      _result:=1
   || __UPG.msg('Dodanie kartoteki dodatkowej "Zwiększenie ochrony minimalnego wynagr." zakończone niepowodzeniem.')
   ?}
?};
KART_DEF.cntx_pop();
_result


\akt_2014_09_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.14]
:: OPIS: Aktualizacja atrybutów składników wykorzystywanych w potrąceniach - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja - tarcza antykryzysowa - zwiększenie ochrony minimalnego wynagrodzenia.'


\kal_def_dataw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: ER/WRT/XP/20.14/2006/0061
::       Aktualizacja kolumny "Data wyjścia" w definicji kalendarza.
::----------------------------------------------------------------------------------------------------------------------
:: statystyki
_liczba:=0;
_zmiany:=0;
_bledy:=0;

KAL_DEF.trig_off('put','*');
KAL_NAZW.cntx_psh();
KAL_NAZW.index('KAL_CZES');
KAL_NAZW.prefix(exec('ref_firma','ustawienia'),'N');
_loop:=KAL_NAZW.first();
{!
|? _loop
|! KAL_ROK.cntx_psh();
   KAL_ROK.index('KAL_ROK');
   KAL_ROK.prefix(KAL_NAZW.ref());
   _loop:=KAL_ROK.first();
   {!
   |? _loop
   |! KAL_DEF.cntx_psh();
      KAL_DEF.index('KAL_DEF');
      KAL_DEF.prefix(KAL_ROK.ref());
      _loop:=KAL_DEF.first();
      {!
      |? _loop
      |! _liczba+=1;
         {? KAL_DEF.DATA<>KAL_DEF.DATAW
         || {? KAL_DEF.POCZATEK<=KAL_DEF.KONIEC & KAL_DEF.CZAS<time(24,0,0)
            || KAL_DEF.DATAW:=KAL_DEF.DATA;
               {? KAL_DEF.put(1)
               || _zmiany+=1
               || _bledy+=1
               ?}
            ?}
         ?};
         _loop:=KAL_DEF.next()
      !};
      KAL_DEF.cntx_pop();
      _loop:=KAL_ROK.next()
   !};
   KAL_ROK.cntx_pop();
   _loop:=KAL_NAZW.next()
!};
KAL_NAZW.cntx_pop();
KAL_DEF.trig_on('put','*');

__UPG.msg(
   'Przetworzono: %1 zapisów.\n'
   'Zmodyfikowano: %2 zapisów.\n'
   'Wystąpiło %3 błędów.'
   [$_liczba,$_zmiany,$_bledy]
);

{? _bledy=0 || 1 || -1 ?}


\kal_def_dataw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Aktualizacja kolumny "Data wyjścia" w definicji kalendarza - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kolumny "Data wyjścia" w definicji kalendarza.'


\szablony_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.14]
:: OPIS: Aktualizacja zestawu danych szablonów dotyczącego adresu - usunięcie pola QTEST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
SD_WKOL.cntx_psh();
SD_WKOL.index('SYMBOL');
SD_WKOL.prefix('QTEST',);
{? SD_WKOL.first()
|| {? ~SD_WKOL.del(1)
   || __UPG.msg('Zestaw danych "Adres" dla szablonów został zaktualizowany.');
      _result:=1
   || __UPG.msg('Aktualizacja zestawu danych "Adres" dla szablonów zakończona niepowodzeniem.')
   ?}
|| __UPG.msg('Zestaw danych "Adres" dla szablonów nie wymagał aktualizacji.');
   _result:=1
?};
SD_WKOL.cntx_pop();
_result


\szablony_adres_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.14]
:: OPIS: Aktualizacja zestawu danych szablonów dotyczącego adresu - usunięcie pola QTEST - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja - zestaw danych szablonów - Adres.'


\akt_2014_12
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Aktualizacja 20.14_12 - VAT-UE/K (5)
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{! _i:=1..2
|! _typ:={? _i=1 || 'POD' || 'PODK' ?};
   _fn:={? _i=1 || '' || 'k' ?};
   _imp:=-1;
   {? exec('add_ver','xml','FKS',date(2020,6,1),_typ,5)
   || _imp:=exec('upg_imp','xml','FKS','D',_typ,5,date(2020,6,1),'vat-ue'+_fn+'_5.dfg')
   ?};

   _txt:='e-Deklaracja VAT-UE'+(~_fn)+' (5) obowiązująca od 01.06.2020';

   {? _imp=1
   || __UPG.msg(_txt+' została dodana.')
   |? _imp=0
   || __UPG.msg(_txt+' już istnieje.')
   || __UPG.msg(_txt+' nie została dodana.');
      _ok:=0
   ?}
!};
_ok


\akt_2014_12_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Aktualizacja 20.14_12 - VAT-UE/K (5) - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja 20.14_12 - VAT-UE/K (5)'


\szablony_akt_zes
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.14]
:: OPIS: Aktualizacja zestawów danych szablonów dotyczących przebiegu zatrudnienia - usunięcie pól:
::       H_PRZYCZYNA_ZWOLNIENIA
::       H_RODZAJ_UMOWY
::       ZASWUMAN_DATA
::       ZASWUMAN_DATA_ZAWARCIA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_delNext:="{? SD_WKOL.first() || {? ~SD_WKOL.del(1) || 1 || 0 ?} || 1 ?}";
_recs:=obj_new('H_PRZYCZYNA_ZWOLNIENIA','H_RODZAJ_UMOWY','ZASWUMAN_DATA','ZASWUMAN_DATA_ZAWARCIA');
_recsN:=obj_ntab_names(_recs);
_size:=obj_len(_recsN);
_ok:=0;
SD_WKOL.cntx_psh();
SD_WKOL.index('SYMBOL');
{! _ii:=1.._size
|! SD_WKOL.prefix(_recsN[_ii],);
   _ok+=_delNext()
!};
SD_WKOL.cntx_pop();
{? _ok=_size
|| __UPG.msg('Zestawy danych dla szablonów zostały zaktualizowane.');
   _result:=1
|| __UPG.msg('Aktualizacja zestawów danych dla szablonów zakończona niepowodzeniem.')
?};
_result


\szablony_akt_zes_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.14]
:: OPIS: Aktualizacja zestawu danych szablonów dotyczących przebiegu zatrudnienia - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja - zestawy danych szablonów.'


\zmiana_R_7093
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: ER/WRT/XP/12.51/2003/0044 Brak rozliczenia przychodu z PPK podczas zwrotu przy pierwszym oblicz
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? R.find_key(7109)
|| _lp:=R.LP
|| _lp:=0
?};
{? _lp & R.find_key(7093)
|| R.LP:=_lp;
   {? R.put()
   || __UPG.msg('Zmieniono kolejność obliczania składnika płacowego nr 7093 "Nadpłata PPK fir."')
   ?};
   exec('lp_put_a','!zws_par_pskl')
?};
R.cntx_pop();
1


\zmiana_R_7093_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: ER/WRT/XP/12.51/2003/0044 Brak rozliczenia przychodu z PPK podczas zwrotu przy pierwszym oblicz - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiana w kolejności obliczeń składnika płacowego nr 7093 "Nadpłata PPK fir."'


\vat_ue_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Uzupełnia schemat e-Deklaracji VAT-UE/K (5) o pola związane z przemieszczeniem towarów
::       między magazynami CALL-OF STOCK
::  OLD: \vat_ue_f/napraw_f.fml
::  OLD: \vat_ue_f/upgrade_2014_1.fml
::----------------------------------------------------------------------------------------------------------------------
_zm:='POD,PODK';

_add:="
   _ref:=null;
   ISTDEFS.cntx_psh();
   ISTDEFS.prefix();
   ISTDEFS.blank();
   ISTDEFS.ISTDEF:=ISTDEF.ref();
   ISTDEFS.TREE:=#_a;
   ISTDEFS.LP:=_b;
   ISTDEFS.OPIS:=_c;
   ISTDEFS.REGULY:=_d;
   ISTDEFS.FORMULA:=_e;
   ISTDEFS.FORM_XML:=_f;
   ISTDEFS.WYM:=_g;
   ISTDEFS.HIDDEN:=_h;
   {? ISTDEFS.add()
   || _ref:=ISTDEFS.ref()
   ?};
   ISTDEFS.cntx_pop();
   _ref
";

_el:=spli_str(_zm,',');
{! _iter:=1..obj_len(_el)
|! _rodzaj:=_el[_iter];
   _status:=0;
   VAT_VER.cntx_psh();
   VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_rodzaj,_rodzaj,5);
   _ver:={? VAT_VER.first() || VAT_VER.ref() || null ?};
   VAT_VER.cntx_pop();
   {? _ver
   || ISTDEF.cntx_psh();
      ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_rodzaj,_ver);
      {? ISTDEF.first()
      || _status:=1;
         _nr:=0;
         _last:=null;
         ISTDEFS.cntx_psh();
         ISTDEFS.index('LP');
         ISTDEFS.prefix(ISTDEF.ref());
         {? ISTDEFS.last()
         || _nr:=ISTDEFS.LP-1;
            _last:=ISTDEFS.ref()
         ?};
         ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'PozycjeSzczegolowe',);
         {? ISTDEFS.first()
         || _ref:=ISTDEFS.ref();
            ISTDEFS.index('DRZEWO');
            ISTDEFS.prefix(ISTDEF.ref(),_ref);
            {? ISTDEFS.last() & ISTDEFS.OPIS<>'Grupa4' & _nr<>0
            || ISTDEFS.prefix();
               {? ISTDEFS.seek(_last)
               || ISTDEFS.LP+={? _rodzaj='POD' || 5 || 11 ?};
                  ISTDEFS.put();
                  {? _rodzaj='POD'
                  || _ref2:=_add(_ref,_nr+=1,'Grupa4','',$"exec('pod_f_petla','fks_ved',_a)",$"exec('pod_f_petla','fks_ved',2)",'N','N');
                     {? _ref2
                     || _add(_ref2,_nr+=1,'P_Ca',$"PodObj.KRAJ",'','','T','N');
                        _add(_ref2,_nr+=1,'P_Cb',$"PodObj.NIP1",'','','T','N');
                        _add(_ref2,_nr+=1,'P_Cc',$"PodObj.NIP2",'','','N','N');
                        _add(_ref2,_nr+=1,'P_Cd',$"{? PodObj.PP=-1 || '' || $PodObj.PP ?}",'','','N','N')
                     ?}
                  || _ref:=_add(_ref,_nr+=1,'Grupa4','',$"exec('pod_f_petla','fks_ved',_a)",$"exec('pod_f_petla','fks_ved',2)",'T','N');
                     {? _ref
                     || _ref2:=_add(_ref,_nr+=1,'Grupa',$"exec('grupa_if','xml')",'','','N','T');
                        {? _ref2
                        || _add(_ref2,_nr+=1,'P_CBa',$"PodObj.KRAJ",'','','T','N');
                           _add(_ref2,_nr+=1,'P_CBb',$"PodObj.NIP1",'','','T','N');
                           _add(_ref2,_nr+=1,'P_CBc',$"PodObj.NIP2",'','','N','N');
                           _add(_ref2,_nr+=1,'P_CBd',$"{? PodObj.PP=-1 || '' || $PodObj.PP ?}",'','','N','N')
                        ?};
                        _ref2:=_add(_ref,_nr+=1,'Grupa',$"exec('grupa_if','xml')",'','','N','T');
                        {? _ref2
                        || _add(_ref2,_nr+=1,'P_CJa',$"{? _ || exec('pod_f_petla','fks_ved',2) ?}; PodObj.KRAJ",'','','T','N');
                           _add(_ref2,_nr+=1,'P_CJb',$"PodObj.NIP1",'','','T','N');
                           _add(_ref2,_nr+=1,'P_CJc',$"PodObj.NIP2",'','','N','N');
                           _add(_ref2,_nr+=1,'P_CJd',$"{? PodObj.PP=-1 || '' || $PodObj.PP ?}",'','','N','N')
                        ?}

                     ?}
                  ?};
                  _status:=2
               ?}
            ?}
         ?};
         ISTDEFS.cntx_pop()
      ?};
      ISTDEF.cntx_pop()
   ?};
   _ver:={? _rodzaj='POD' || 'VAT-UE (5)' || 'VAT-UEK (5)' ?};
   {? _status=0
   || __UPG.msg('Nie znaleziono schematu e-Deklaracji %1'[_ver])
   |? _status=1
   || __UPG.msg('Schematu e-Deklaracji %1 jest już uzupełniony.'[_ver])
   || __UPG.msg('Schematu e-Deklaracji %1 został uzupełniony.'[_ver])
   ?}
!};
1


\vat_ue_f_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Uzupełnia schemat e-Deklaracji VAT-UE/K (5) o pola związane z przemieszczeniem towarów
::       między magazynami CALL-OF STOCK - opis
::  OLD: \vat_ue_f_desc/upgrade_2014_1.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia schemat e-Deklaracji VAT-UE/K (5) o pola związane z przemieszczeniem towarów między magazynami CALL-OF STOCK'


\szablony_akt_zes2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.14]
:: OPIS: Aktualizacja zestawów danych szablonów dotyczących świadectwa pracy - usunięcie formuł "po redagowaniu "pól:
::       OJCIEC_W, RODZIC_W, OKR, NRE, WKP, ODW, URLOPW
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_delNext:="{? SD_WFML.first() || {? ~SD_WFML.del(1) || 1 || 0 ?} || 1 ?}";
_zesName:='ZASWSW_';
_kind:='_AE';
_recs:=obj_new('OJCIEC_W','RODZIC_W','OKR','NRE','WKP','UOD','UDO','URLOPW');
_recsN:=obj_ntab_names(_recs);
_size:=obj_len(_recsN);
_ok:=0;
SD_WFML.cntx_psh();
SD_WFML.index('KOD');
{! _ii:=1.._size
|! _formName:=_zesName+_recsN[_ii]+_kind;
   SD_WFML.prefix(_formName,);
   _ok+=_delNext()
!};
SD_WFML.cntx_pop();
{? _ok=_size
|| __UPG.msg('Zestawy danych dla szablonów zostały zaktualizowane.');
   _result:=1
|| __UPG.msg('Aktualizacja zestawów danych dla szablonów zakończona niepowodzeniem.')
?};
_result


\szablony_akt_zes2_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.14]
:: OPIS: Aktualizacja zestawu danych szablonów dotyczących przebiegu zatrudnienia - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja - zestaw danych szablonów "Świadectwo pracy".'


\rubryka_7124
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: ER/WRT/XP/20.14/2008/0002 Oddelegowanie, opodatkowanie poza PL, rozliczenia z ZUS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

{? +exec('import','rubryki','g','7124',1,0)
|| _txt:='Dodanie składnika płacowego 7124 do oddelegowań zakończyło się niepowodzeniem.';
   _result:=-1
|| _txt:='Dodanie składnika płacowego 7124 do oddelegowań zakończone powodzeniem.'
?};
__UPG.msg(_txt);
exec('add_attr','rubatr',901,9014,'Kwota przychodu opodatkowanego za granicą',1,
  'Kwota przychodu opodatkowanego za granicą',,7081);
exec('add_attr','rubatr',901,9015,'Skladki ZUS od przychodu opodatkowanego za granicą',1,
  'Skladki ZUS płacone przez pracownika od przychodu opodatkowanego za granicą, nie są wykazywane na PIT-11',,7124);
__UPG.msg('Zakończono aktualizację atrybutów dla oddelegowań opodatkowanych za granicą.');
_result


\rubryka_7124_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: ER/WRT/XP/20.14/2008/0002 Oddelegowanie, opodatkowanie poza PL, rozliczenia z ZUS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dodanie składnika płacowego 7124 do oddelegowań oraz atrybutów'


\zal_ucp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.14]
:: OPIS: Zmiany w typach załączników: rezygnacja z "Umowa-zlecenie" na rzecz "Umowa cywilnoprawna".
::       ER/WRT/XP/20.14/2008/0055
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
SLO_TYP.cntx_psh();
SLO_TYP.index('SYMBOL');
SLO_TYP.prefix('ZAL',);
{? SLO_TYP.first()
|| SLO_NAZ.cntx_psh();
   SLO_NAZ.index('NAZWA');
   SLO_NAZ.prefix(SLO_TYP.ref());
   _tz:=obj_new('z','na');
   {! _lp:=1 .. 2
   |! _tz[_lp]:=obj_new('nazwa','ref','count');
      _tz[_lp].nazwa:={? _lp=1 || 'Umowa-zlecenie' || 'Umowa cywilnoprawna' ?};
      {? SLO_NAZ.find_key(_tz[_lp].nazwa,)
      || _tz[_lp].ref:=SLO_NAZ.ref();
         _tz[_lp].count:=SLO_NAZ.count()
      || _tz[_lp].ref:=null();
         _tz[_lp].count:=0
      ?}
   !};
   {? _tz.z.ref=null()
::    Jeżeli _tz.z.ref=null(), to właściwie nic nie musimy robić. Moglibyśmy założyć _tz.na (jeżeli nie istnieje), ale
::    zostanie to zrobione w ramach danych startowych.
   || __UPG.msg('Zmiany w typach załączników nie były konieczne.');
      _ret:=1

   |? _tz.na.ref=null()
::    Przypadek prostszy - nie ma "nowego" typu - wystarczy zmienić nazwę.
   || {? ~SLO_NAZ.seek(_tz.z.ref)
      || __UPG.msg('Odnalezienie typu załącznika nie powiodło się.');
         _ret:=-1
      |? SLO_NAZ.NAZWA:=_tz.na.nazwa;
         ~SLO_NAZ.put()
      || __UPG.msg('Zmiana nazwy typu załącznika z "%1" na "%2" nie powiodła się.' [_tz.z.nazwa,_tz.na.nazwa]);
         _ret:=-1
      || __UPG.msg('Zmiana nazwy typu załącznika z "%1" na "%2" zakończyła się sukcesem.' [_tz.z.nazwa,_tz.na.nazwa]);
         _ret:=1
      ?}

::    Przypadek najtrudniejszy - istnieją obydwa typy.
   || do();
      {? _tz.z.count<_tz.na.count
      || _src:=_tz.z.ref;
         _dest:=_tz.na.ref
      || _src:=_tz.na.ref;
         _dest:=_tz.z.ref
      ?};
::    Pamiętajmy, że SLO_NAZ jest tabelą globalną, ale wykorzystywaną w tabelach lokalnych. Postarajmy się wyeliminować
::    ten z typów, który ma mniej dowiązań. Najpierw w tabeli ZALACZ.
      ZALACZ.cntx_psh();
      ZALACZ.prefix();
      ZALACZ.f_set('TYP_ZAL',,'"ZALACZ".TYP_ZAL=:_a',_src);
      {? ZALACZ.f_first()
      || {!
         |? ZALACZ.TYP_ZAL:=_dest;
            ZALACZ.put();
            ZALACZ.f_next()
         !}
      ?};
      ZALACZ.f_clear();
      ZALACZ.cntx_pop();

::    ZAOTS - tutaj jest jednak indeks unikalny. Może się zdarzyć, że "podmiana" nie byłaby możliwa.
      ZAOTS.cntx_psh();
      ZAOTS.index('SLO_NAZ');
      ZAOTS.prefix();
      ZAOTS.f_set('SLO_NAZ',,'"ZAOTS".SLO_NAZ=:_a',_src);
      {? ZAOTS.f_first()
      || {!
         |? ZAOTS.cntx_psh();
            _jest:=ZAOTS.find_key(_dest,ZAOTS.G,);
            ZAOTS.cntx_pop();
            {? _jest
            || ZAOTS.del()
            || ZAOTS.SLO_NAZ:=_dest;
               ZAOTS.put()
            ?};
            ZAOTS.f_next()
         !}
      ?};
      ZAOTS.f_clear();
      ZAOTS.cntx_pop();

::    ZAOTM - tutaj też jest indeks tymczasowy.
      ZAOTM.cntx_psh();
      ZAOTM.index('MIEJSCER');
      ZAOTM.prefix();
      ZAOTM.f_set('SLO_NAZ',,'"ZAOTM".SLO_NAZ=:_a',_src);
      {? ZAOTM.f_first()
      || {!
         |? ZAOTM.cntx_psh();
            _jest:=ZAOTM.find_key(ZAOTM.MIEJSCE,_dest);
            ZAOTM.cntx_pop();
            {? _jest
            || ZAOTM.del()
            || ZAOTM.SLO_NAZ:=_dest;
               ZAOTM.put()
            ?};
            ZAOTM.f_next()
         !}
      ?};
      ZAOTM.f_clear();
      ZAOTM.cntx_pop();

::    Finalizacja
      _status:='';
      {? SLO_NAZ.seek(_src)
      || {? SLO_NAZ.count()=0
::          Czy można usunąć _src?
         || SLO_NAZ.del();
            {? _src=_tz.na.ref & SLO_NAZ.seek(_tz.z.ref)
::             Czy potrzebna jest zmiana nazwy?
            || SLO_NAZ.NAZWA:=_tz.na.nazwa;
               SLO_NAZ.put()
            ?};
            _status:='KONIEC'
         || _RECS:=SLO_NAZ.testlink();
            _status:={? _RECS.first() || 'DEL-' || 'DEL+' ?}
         ?}
      ?};

      {? end()
      || _ret:=1;
         {? _status='KONIEC'
         || __UPG.msg('Wszystkie zamierzone zmiany w typach załączników zostały wykonane i zakończyły się sukcesem.')
         |? _status='DEL+'
         || __UPG.msg('Zmiany w typach załączników w bieżącej firmie zostały wykonane.')
         |? _status='DEL-'
         || __UPG.msg(
               'Zmian w typach załączników w bieżącej firmie NIE udało się przeprowadzić '
               '(typy załączników wykorzystywane w niestandardowy sposób?).'
            );
            _ret:=-1
         ?}
      || __UPG.msg('Zmiany w typach załączników nie ');
         _ret:=-1
      ?}
   ?};
   SLO_NAZ.cntx_pop()

:: Nie ma SLO_TYP-a. Raczej niemozliwe, ale jeżeli nawet, to nic do robienia.
|| _ret:=1
?};
SLO_TYP.cntx_pop();
_ret


\zal_ucp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.14]
:: OPIS: Zmiany w typach załączników: rezygnacja z "Umowa-zlecenie" na rzecz "Umowa cywilnoprawna" - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiany w typach załączników: rezygnacja z "Umowa-zlecenie" na rzecz "Umowa cywilnoprawna".'


\kal_def_typws
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Aktualizacja kolumny TypWS w definicji kalendarza.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_tws:=',R,RN,RS,SN,SW,W5,WH,WN,WS,';
exec('__KAL','object');
KAL_ROK.cntx_psh();
KAL_DEF.cntx_psh();
KAL_DEF.trig_off('*','*');
_MDB:=KAL_DEF.names();
_loop:=_MDB.first();
{!
|? _loop
|! _cnt:=0;
   KAL_DEF.use(_MDB.NAME);
   KAL_DEF.index('KAL_DATA');
   KAL_DEF.prefix();
   _loop:=KAL_DEF.first();
   {!
   |? _loop & _res
   |! {? _tws*(','+KAL_DEF.TYP+KAL_DEF.TYPWS+',')=0
      || KAL_DEF.TYPWS:=exec('getTypWS','kaledit',KAL_DEF.TYP,KAL_DEF.DATA,KAL_DEF.ROK().WZORZEC);
         {? ~KAL_DEF.put(1)
         || _res:=0
         ?};
         _cnt+=1
      ?};
      _loop:=KAL_DEF.next()
   !};
   {? _res<>0
   || __UPG.msg('Przetworzono zbiór "%1". Liczba zaktualizowanych wierszy: %2'[_MDB.NAME,$_cnt])
   || __UPG.msg('Wystąpił błąd zapisu podczas przetwarzania zbioru "%1".'[_MDB.NAME])
   ?};
   _loop:=(_res<>0 & _MDB.next())
!};
KAL_DEF.trig_on('*','*');
KAL_DEF.cntx_pop();
KAL_ROK.cntx_pop();
_res


\kal_def_typws_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Aktualizacja kolumny TypWS w definicji kalendarza - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kolumny TypWS w definicji kalendarza.'


\ppk_06
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: PR/WRT/XP/12.51/2007/0038 - Brak uwzględnienia deklaracji złożonych między blokadą a wznowieniem grupowym.
::       Usunięcie wpisów procentowych wartości wpłat spowodowanych blokadą ekonomiczną.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: Usunięcie składników z wniosków DOWU25, WB25W i WB25B:
_wrd:=spli_str('DOWU25,WB25W,WB25B',',');
PPK_WST.cntx_psh();
PPK_WST.index('UNIQUE');
{! _ii:=1..obj_len(_wrd)
|! _ref:=exec('szukaj','ppk_wrd','U',_wrd[_ii]);
   PPK_WST.prefix(_ref);
   {? PPK_WST.first()
   || {!
      |? PPK_WST.del(1)
      !}
   ?}
!};
PPK_WST.cntx_pop();
obj_del(_wrd);

:: Usunięcie wpisów z PPK_WPW (w efekcie kasowane w wyzwalaczach również PPK_PNW i PPK_PWP)
:: dotyczących deklaracji DOWU25, WB25W i WB25B:
_query:=
   'select PPK_WPW.REFERENCE as REF '+
   'from PPK_WPW '+
   'join PPK_WNU using(PPK_WPW.PPK_WNU, PPK_WNU.REFERENCE) '+
   'join PPK_WRD using(PPK_WNU.PPK_WRD, PPK_WRD.REFERENCE) '+
   'where PPK_WRD.SYMBOL=\'DOWU25\' or PPK_WRD.SYMBOL=\'WB25W\' or PPK_WRD.SYMBOL=\'WB25B\'';
_WPW:=sql(_query);
{? _WPW.first()
|| PPK_WPW.cntx_psh();
   PPK_WPW.prefix();
   {!
   |? {? PPK_WPW.seek(_WPW.REF)
      || PPK_WPW.del(1)
      ?};

      _WPW.next()
   !};
   PPK_WPW.cntx_pop()
?};

:: Przemianowanie na 'O' wpisów z PPK_PWP utworzonych z listy (bez połączenia z wnioskiem),
:: a błędnie oznaczonych literką 'W':
_query:=
   'select PPK_PWP.REFERENCE as REF '+
   'from PPK_PWP '+
   'where PPK_PWP.SR=\'W\' and PPK_PWP.REFERENCE not in (select PPK_PNW.PPK_PWP from PPK_PNW)';
_PWP:=sql(_query);
{? _PWP.first()
|| PPK_PWP.cntx_psh();
   PPK_PWP.prefix();
   {!
   |? {? PPK_PWP.seek(_PWP.REF)
      || PPK_PWP.SR:='O';
         PPK_PWP.put(1)
      ?};

      _PWP.next()
   !};
   PPK_PWP.cntx_pop()
?};

1


\ppk_06_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Usunięcie wpisów procentowych wartości wpłat spowodowanych blokadą ekonomiczną - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie wpisów procentowych wartości wpłat spowodowanych blokadą ekonomiczną.'


\szablony_akt_zes3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.14]
:: OPIS: Aktualizacja zestawu danych szablonów dotyczącego adresu osoby (OS_ADRES) - zmiana wartości znacznika LISTA na
::       wartość 'N'.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
SD_WREK.cntx_psh();
SD_WREK.index('KOD');
SD_WREK.prefix('PXX_OS_ADRES',);
SD_WREK.trig_off('put','*');
_ok:={? SD_WREK.first() || SD_WREK.LISTA:='N'; SD_WREK.put(1) || 1 ?};
SD_WREK.trig_on('put','*');
SD_WREK.cntx_pop();
{? _ok
|| __UPG.msg('Zestaw danych dla szablonów: OS_ADRES został zaktualizowany.');
   _result:=1
|| __UPG.msg('Aktualizacja zestawu danych: OS_ADRES dla szablonów zakończona niepowodzeniem.')
?};
_result


\szablony_akt_zes3_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.14]
:: OPIS: Aktualizacja zestawu danych szablonów dotyczącego adresu osoby (OS_ADRES) - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja - zestaw danych szablonów OS_ADRES.'


\fiks_73
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2009/0002 - JPK_V7: Brak możliwości oznaczenia pozycji w wierszu KorektaPodstawyOpodt dla
::            sprzedaży
::  OLD: \fiks_73/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','V7');
{? ISTDEF.first()
|| {!
   |? ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'KorektaPodstawyOpodt',);
      {? ISTDEFS.first() & ISTDEFS.REGULY=$"{? VAT_PS.TYP='S' || '1' || '' ?}"
      || ISTDEFS.REGULY:=$"{? VAT_PS.TYP='N' || '1' || '' ?}";
         {? ISTDEFS.put()
         || _jest:=1;
            __UPG.msg('Poprawiono schemat: %1'[ISTDEF.VER])
         ?}
      ?};
      ISTDEFS.cntx_pop();
      ISTDEF.next()
   !}
?};
ISTDEF.cntx_pop();
{? _jest=0
|| __UPG.msg('Brak schematów JPK_V7M/K wymagających naprawy.')
?};
1


\fiks_73_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2009/0002 - JPK_V7: Brak możliwości oznaczenia pozycji w wierszu KorektaPodstawyOpodt dla
::            sprzedaży
::----------------------------------------------------------------------------------------------------------------------
'Naprawa schematów JPK_V7M/K.\nBrak możliwości oznaczenia faktury tagiem \'KorektaPodstawyOpodt\'.'


\edeklaracje_2020_07
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: Dodanie nowych struktur e-deklaracji obowiązujących od lipca 2020 zmiana wersji schemy z 1-0E na 1-1E, 1-2E
::----------------------------------------------------------------------------------------------------------------------
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
::import poprawnych struktur e-deklaracji PIT-4R wersja 9 schema 1-2E
{? exec('edek_stru','edeklar','PIT4R',9,'pit-4r9_v1-2.dfg',date(2020,8,19))
|| __UPG.msg('Dodano schemat 9 wersji schema 1-2E e-deklaracji PIT-4R.')
|| __UPG.msg('Brak aktualizacji, schemat 9 wersji schema 1-2E e-deklaracji PIT-4R już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-11 wersja 10 schema 1-1E
{? exec('edek_stru','edeklar','PIT11',10,'pit-11_25_v1-1.dfg',date(2020,7,10))
|| __UPG.msg('Dodano schemat 10 wersji schema 1-1E e-deklaracji PIT-11.')
|| __UPG.msg('Brak aktualizacji, schemat 10 wersji schema 1-1E e-deklaracji PIT-11 już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT8C wersja 10 schema 1-1E
{? exec('edek_stru','edeklar','PIT8C',10,'pit-8c-10v1-1.dfg',date(2020,7,8))
|| __UPG.msg('Dodano schemat 10 wersji schema 1-1E e-deklaracji PIT-8C.')
|| __UPG.msg('Brak aktualizacji, schemat 10 wersji schema 1-1E e-deklaracji PIT-8C już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
1


\edeklaracje_2020_07_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: Dodanie nowych e-deklaracji - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych wersji e-deklaracji 1-1E, 1-2E wersja schemy 2020'


\attr_use_RP7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.14]
:: OPIS: Aktualizacja atrybutów dla rubryk płacowych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

exec('__RUB','object');
__RUB.fill();
exec('add_uses','rubatr',7120,100231);
exec('add_uses','rubatr',7121,100232);
__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');
_result


\attr_use_RP7_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.14]
:: OPIS: Aktualizacja atrybutów dla rubryk płacowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja atrybutów dla rubryk płacowych.'


\edokos_wpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: kod -ER/WRT/XP/20.14/2009/0044 - formuła usuwająca nadmiarowe statusy EDOKOS utworzone podczas przydzielania
::       zwalniania zadań podczas wprowadzania
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKOS.cntx_psh();
_maski2:=EDOKUM.names();
{? _maski2.first()
|| {!
   |? EDOKUM.use(_maski2.NAME); EDOKUM.prefix();
      {? EDOKUM.first()
      || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         EDOKOS.index('SZUK12');
         {!
         |? EDOKOS.prefix(EDOKUM.ref,'W','T');
            {? EDOKOS.first()
            || EDOKOS.prefix(EDOKUM.ref,'W','N');
               {? EDOKOS.first()
               || {! |? EDOKOS.del !}
               ?}
            |? EDOKUM.USERS<>null
            || EDOKOS.prefix(EDOKUM.ref,'W','N');
               {? EDOKOS.first()
               || {! |? {? EDOKOS.USERS<>EDOKUM.USERS || EDOKOS.del() || EDOKOS.next() ?} !}
               ?}
            ?};
            EDOKUM.next()
         !}
      ?};
      _maski2.next()
   !}
?};
obj_del(_maski2);
EDOKUM.cntx_pop(); EDOKOS.cntx_pop();
__UPG.msg('Usunięto nadmiarowe statusy EDOKOS dotyczące wprowadzania dokumentu.');
1


\edokos_wpr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: kod -ER/WRT/XP/20.14/2009/0044 - formuła usuwająca nadmiarowe statusy EDOKOS utworzone podczas przydzielania
::       zwalniania zadań podczas wprowadzania - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie nadmiarowych rekordów tabeli EDOKOS utworzonych podczas wprowadzania dokumentu.'


\rubryka_150_CHO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: ER/WRT/XP/20.14/2010/0009 Niezgodne z opisem działanie składnika płac: 150-Premia jeżeli chodzi
::       o wliczanie do zasiłku chorobowego.
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? R.find_key(150)
|| {? R.CHO='N'
   || PAR_POKR.cntx_psh();
      PAR_POKR.index('R0');
      PAR_POKR.prefix(R.ref());
      {? PAR_POKR.first()
      || {? PAR_POKR.RZ='T'
         || __UPG.msg('Składnik okresowy 150 "Premia" jest wliczany do podstaw wynagrodzenia/zasiłków chorobowych.')
         || __UPG.msg('Składnik okresowy 150 "Premia" nie jest wliczany do podstaw wynagrodzenia/zasiłków chorobowych.')
         ?}
      || R.CHO:='T';
         R.memo_get(,'NOTA');
         _nota:=
         '150. Premia'
         '\n'
         '\nAlgorytm'
         '\nSkładnik może zostać wprowadzony przez operatora ręcznie '
         '\nprzy użyciu funkcji Dołącz bezpośrednio na liście płac'
         '\noraz poprzez wybranie jednej z opcji grupowego wprowadzania danych, dotyczącej wprowadzania'
         '\nkwoty składników na listę płac.'
         '\n'
         '\nŹródło danych:'
         '\nLista płac'
         '\n'
         '\nDane modyfikowane'
         '\nMa wpływ na wysokość podstawy naliczenia zasiłków i wynagrodzeń chorobowych'
         '\n(rubryki 465, 468, 510, 511, 512, 516, 523, 529, 531).'
         '\n'
         '\nUwagi'
         '\nW standardowej wersji programu Merit premia nie jest wliczana do podstawy'
         '\nnaliczania ekwiwalentu za niewykorzystany urlop.';
         R.memo_set(_nota,'NOTA');
         {? R.put() & R.memo_put(,'NOTA')
         || __UPG.msg('Składnik 150 "Premia" włączony do podstaw wynagrodzenia/zasiłków chorobowych.')
         || __UPG.msg('Aktualizacja składnika 150 "Premia" zakończona niepowodzeniem.')
         ?}
      ?};
      PAR_POKR.cntx_pop()
   || __UPG.msg('Składnik 150 "Premia" poprawnie jest wliczany do wynagrodzenia/zasiłków chorobowych.')
   ?}
?};
R.cntx_pop();
1


\rubryka_150_CHO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.14]
:: OPIS: ER/WRT/XP/20.14/2010/0009 Niezgodne z opisem działanie składnika płac: 150-Premia jeżeli chodzi
::       o wliczanie do zasiłku chorobowego.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka włączenia do podstawy wynagrodzenia/zasiłków chorobowych składnika 150 "Premia".'

:Sign Version 2.0 jowisz:1048 2020/10/16 15:22:59 b7901d6dae3abf097bdf7241325976b95f264dfdb6793a0313ecd5850df9b49689d36567c726f2bcd8f44c75d83f772dcc4ee8227d0623d04d2a2782d880e4f35a277576db77480736e79fca7548c83818c4a486f5bc4d5801697567d7301a199b31d562d7272c3e57d3799ed4662affdbaae6dd6f0971eeee42ca93ff85ef20

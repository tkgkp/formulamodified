:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_1714.fml
:: Utworzony: 14.02.2016
:: Autor: TS
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 17.00 na 17.14
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - realizacja zakwalifikowana jako wykonana
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Główna formuła upgrade-ów 17.14
::----------------------------------------------------------------------------------------------------------------------
:: zadania automatyczne
:: WIELOFIRMOWE
__UPG.add_task('upgrade_act',,,'ZPR',1);
__UPG.add_task('paramdef_act',,,'ZWS');
__UPG.add_task('edeklaracje',,,'ZWS');
__UPG.add_task('jpk',,,'ZWS');
__UPG.add_task('wz_proj',,,'ZWS');
__UPG.add_task('USERS',,,'ZWS');
__UPG.add_task('par_skid',,,'ZWS');
__UPG.add_task('B_SIGNAL',,,'ZPR');
__UPG.add_task('rubatr',,,'ZWS',1);
__UPG.add_task('vat7_sch',,,'FKS');
__UPG.add_task('color',,,'ZWS');
__UPG.add_task('typy',,,'ZWS');
__UPG.add_task('jpk_01',,,'ZWS');
__UPG.add_task('fiks_27',,,'ZWS');
:: JEDNOFIRMOWE
__UPG.add_task('PL_RES',,'N','ZWS');
__UPG.add_task('ZLBR',,'N','ZWS');
__UPG.add_task('u2uwagi',,'N','LSP');
__UPG.add_task('PL_OPER',,'N','TPP');
__UPG.add_task('PX_POZ',,'N','TPP');
__UPG.add_task('rp7_os',,'N','PPL');
__UPG.add_task('rp7_kw',,'N','PPL');
__UPG.add_task('ZTP',,'N','ZWS');
__UPG.add_task('PX_WYK',,'N','TPP');
:: zadania manualne
__UPG.add_task('sch_imp','N','N','OBG');
~~


\USERS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli USERS
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
USERS.cntx_psh();
USERS.prefix();
_formula:="
   _put:=0;
   {? USERS.EKIOSK=''
   || USERS.EKIOSK:='N';
      _put:=1
   ?};
   {? _put || USERS.put(1) ?}
";
_result:=USERS.for_each(_formula,1);
USERS.cntx_pop();
_result


\USERS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli USERS - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli użytkowników o znacznik obsługiwania E-Kiosków.'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\PL_RES
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_RES
::   WY: -1/0/1
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

PL_RES.cntx_psh();
PL_RES.index('SYM');
PL_RES.prefix();
_fml:="
   _put:=0;
   {? PL_RES.LAYERS=''
   || {? PL_RES.PARALLEL='T'
      || PL_RES.LAYERS:='T'
      || PL_RES.LAYERS:='N'
      ?}
   ?};
   {? _put>0
   || PL_RES.put()
   ?};
   ~~
";
_result:=PL_RES.for_each(_fml,1);
PL_RES.cntx_pop();
_result


\PL_RES_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_RES - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli zasobów planu operacyjnego o znacznik rysowania w wielu wartstwach.\n'
'Może się nie udać, tylko jeżeli pole tabeli PL_RES zostało zablokowane przez innego użytkownika'


\ZLBR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełania pola w tabeli ZLBR
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
ZLBR.cntx_psh();
ZLBR.prefix();
_formula:="
   _put:=0;
   {? ZLBR.AKT=''
   || {? #ZLBR.DO & ZLBR.DO<date()
      || ZLBR.AKT:='N'
      || ZLBR.AKT:='T'
      ?};
      _put:=1
   ?};
   {? _put || ZLBR.put(1) ?}
";
_result:=ZLBR.for_each(_formula,1);
ZLBR.cntx_pop();
_result


\ZLBR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli ZLBR - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli brygad o znacznik aktywności.'


\par_skid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.14]
:: OPIS: Aktualizacja listy parametrów
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');
exec('del','upgrade_1714',,64);
exec('del','upgrade_1714',,65);
PAR_SKID.set(64,5,1,'','KH.KOD','Sposób generowania subkonta',
                      '''KH.KOD - wg kodu kontrahenta'',
                       ''AUTONR - kolejny numer''');
1


\par_skid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.14]
:: OPIS: Aktualizacja listy parametrów - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy parametrów.'


\del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła usuwa wskazane parametry. Do zastosowań transferowych.
::   WE:  [_a] [REFERENCE] - Wskazanie firmy, w której będą usuwane parametry. Pominięcie parametru oznacza, że
::                           parametry będą usuwane ze wszystkich firm.
::         _b  [NUMBER]    - Numer usuwanego parametru.
::        [_c] [NUMBER]    - Kolejny numer usuwanego parametru.
::        [_d] [NUMBER]    - Kolejny numer usuwanego parametru.
::        [_e] [NUMBER]    - Kolejny numer usuwanego parametru.
::        [_f] [NUMBER]    - Kolejny numer usuwanego parametru.
::        [_g] [NUMBER]    - Kolejny numer usuwanego parametru.
::   WY: Status operacji:
::       0 - Błędne parametry.
::       1 - Operacja wykonana prawidłowo.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=FIRMA
|| _jf:=1;
   _firma:=_a
|| _jf:=0
?};
{? _<2
|| return(0)
?};

_err:=0;
_number:=type_of(0);
PARAMS.cntx_psh();
PARAMS.index('UNIQ');
{! _lp:=2 .. _
|! {? type_of(_[_lp])<>_number
   || _err+=1
   || {? _jf
      || PARAMS.prefix(_[_lp],_firma)
      || PARAMS.prefix(_[_lp])
      ?};
      {? PARAMS.first()
      || {!
         |? PARAMS.del()
         !}
      ?}
   ?}
!};
PARAMS.cntx_pop();
_err=0


\B_SIGNAL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli B_SIGNAL
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
B_SIGNAL.cntx_psh();
B_SIGNAL.prefix();
_formula:="
   _put:=0;
   {? B_SIGNAL.FIRM=''
   || B_SIGNAL.FIRM:='N';
      _put:=1
   ?};
   {? _put || B_SIGNAL.put(1) ?}
";
_result:=B_SIGNAL.for_each(_formula,1);
B_SIGNAL.cntx_pop();
_result


\B_SIGNAL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli B_SIGNAL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli sygnałów o znacznik wielofirmowości.'


\PL_OPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_OPER
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
PL_OPER.cntx_psh();
PL_OPER.prefix();
_formula:="
   _put:=0;
   {? PL_OPER.PROBLEM=''
   || PL_OPER.PROBLEM:='N';
      _put:=1
   ?};
   {? _put || PL_OPER.put(1) ?}
";
_result:=PL_OPER.for_each(_formula,1);
PL_OPER.cntx_pop();
_result


\PL_OPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_OPER - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli pozycji planu operacyjnego o znacznik problemu.'


\PX_POZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PX_POZ
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
PX_POZ.cntx_psh();
PX_POZ.prefix();
_formula:="
   _put:=0;
   {? PX_POZ.PROBLEM=''
   || PX_POZ.PROBLEM:='N';
      _put:=1
   ?};
   {? _put || PX_POZ.put(1) ?}
";
_result:=PX_POZ.for_each(_formula,1);
PX_POZ.cntx_pop();
_result


\PX_POZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PX_POZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli pozycji planu strategicznego o znacznik problemu.'


\u2uwagi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Przypisuję zawartość pola FAP.U do pola FAP.UWAGI
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_mask:=FAKS.names();
FAKS.cntx_psh();
FAP.cntx_psh();
_mask.clear();
{? _mask.first()
|| {!
   |? FAKS.use(_mask.NAME);
      FAP.use('fakpo'+(_mask.NAME+3));
      FAKS.prefix();
      FAP.prefix();
      FAP.for_each("{? FAP.memo_lin('UWAGI')='\n' & +form(FAP.U)
                    || FAP.memo_get();
                       FAP.memo_set(FAP.U);
                       FAP.memo_put(,'UWAGI')
                    ?}",1);
      _mask.next()
   !}
?};
FAKS.cntx_pop();
FAP.cntx_pop();
_result


\u2uwagi_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Przypisuję zawartość pola FAP.U do pola FAP.UWAGI - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uwag dla pozycji dokumentów sprzedaży i dokumentów zakupu.'


\rubatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Aktualizacja atrybutów rubryk płacowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('add_use','rubatr',191,3);

exec('add_attr','rubatr',100,1002,'Kwalifikacja świadczeń do RP-7',1,
     'Składniki, które powinny być uwzględnione przy przygotowaniu danych do RP-7.',
     ,100,101,102,103,105,106,107,110,124,200);
exec('add_use','rubatr',1002,120,121,122,123,125,126,150);
exec('add_use','rubatr',1002,436,437,438,439,442,443,444,7031);
exec('add_use','rubatr',1002,440);
exec('add_use','rubatr',1002,465,467,468,470,510,511,512,513,514,515,516,517,523,524,529,530,531,533,537);
exec('add_use','rubatr',1002,7017,7018);
exec('add_attr','rubatr',1002,10021,'Składniki stałe',1,
     'Składniki stałe, uwzględnione przy przygotowaniu danych do RP-7.',,100,101,102,103,105,106,107,110,124);
exec('add_attr','rubatr',10021,100211,'Wynagrodzenia stałe wypłacone',1,
     'Wynagrodzenia stałe wypłacone, uwzględnione do przygotowania danych do RP-7.',
     ,100,101,102,103,110,124);
exec('add_attr','rubatr',10021,100212,'Wyrównania wynagrodzenia stałego',1,
     'Wyrównania wypłaconego wynagrodzenia stałego, uwzględnione do przygotowania danych do RP-7.',
     ,105,106,107);
exec('add_attr','rubatr',1002,10022,'Składniki zmienne',1,
     'Składniki zmienne, uwzględnione przy przygotowaniu danych do RP-7.',,120,121,122,123,125,126,150);
exec('add_attr','rubatr',1002,10023,'Pozostałe składniki wynagrodzenia',1,
     'Pozostałe składniki wynagrodzenia, uwzględnione przy przygotowaniu danych do RP-7.',
     ,200,436,437,438,439,442,443,444);
   exec('add_use','rubatr',10023,7031);
exec('add_attr','rubatr',10023,100231,'Wypłacone pozostałe składniki wynagrodzenia',1,
     'Wypłacone pozostałe składniki wynagrodzenia, uwzględnione przy przygotowaniu danych do RP-7.',
     ,200,436,437,438,442,443,444,7031);
exec('add_attr','rubatr',10023,100232,'Wyrównanie pozostałych składników wynagrodzenia',1,
     'Wyrównanie pozostałych składników wynagrodzenia, uwzględnione przy przygotowaniu danych do RP-7.',,439);
exec('add_attr','rubatr',1002,10024,'Ekwiwalent za urlop',1,
     'Pozostałe składniki wynagrodzenia(ekwiwalent za urlop), uwzględnione przy przygotowaniu danych do RP-7.',,440);
exec('add_attr','rubatr',1002,10025,'Świadczenia w naturze',1,
     'Świadczenia w naturze, uwzględnione przy przygotowaniu danych do RP-7.');
exec('add_attr','rubatr',1002,10026,'Świadczenia wypłacone zamiast wynagrodzenia',1,
     'Świadczenia wypłacone zamiast wynagrodzenia, uwzględnione przy przygotowaniu danych do RP-7.',
     ,465,467,468,470,510,511,512,513,514,515,516,517,523,524,529,530,531,533,537);
exec('add_use','rubatr',10026,7017,7018);
exec('add_attr','rubatr',10026,100261,'Świadczenia wypłacone',1,
     'Świadczenia wypłacone, uwzględnione przy przygotowaniu danych do RP-7.',
     ,465,468,510,512,514,516,523,529,531,533);
exec('add_attr','rubatr',10026,100262,'Wyrównania wypłaconych świadczeń',1,
     'Wyrównania świadczeń, uwzględnione przy przygotowaniu danych do RP-7.',
     ,467,469,470,511,513,515,517,524,530,532,537,7017,7018);
1


\rubatr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Aktualizacja atrybutów rubryk płacowych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja atrybutów rubryk płacowych'


\rp7_os
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Zmiany w tabeli RP7_OS.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_stat:=obj_new('JO');
_stat.JO:=0;
params_set('stat',_stat);

P.cntx_psh();
RP7_OS.cntx_psh();
RP7_OS.prefix();
__UPG.msg('Liczba rekordów do przetworzenia: %1' [$RP7_OS.size()]);
RP7_OS.for_each(
   "  {? RP7_OS.P<>null() & RP7_OS.WYDZIAL<>RP7_OS.P().WYDZIAL
      || RP7_OS.WYDZIAL:=RP7_OS.P().WYDZIAL;
         params_get().stat.JO+=RP7_OS.put()
      ?}
   ",1);
RP7_OS.cntx_pop();
P.cntx_pop();
__UPG.msg('Liczba zaaktualizowanych jednostek organizacyjnych: %1' [$_stat.JO]);
1


\rp7_os_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Zmiany w tabeli RP7_OS - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiany w tabeli RP7_OS'


\rp7_kw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Zmiany w tabeli RP7_KW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_stat:=obj_new('T','W');
_stat.T:=_stat.W:=0;
params_set('stat',_stat);
R.cntx_psh();
R.prefix();
RP7_KW.cntx_psh();
RP7_KW.index('ZAPIS');
RP7_KW.prefix();
__UPG.msg('Liczba rekordów do przetworzenia: %1' [$RP7_KW.size()]);
RP7_KW.for_each("
   _stat:=params_get().stat;
   _put:=0;
   {? _poz:='SZNWU'*RP7_KW.T
   || RP7_KW.T:=$_poz;
      _stat.T+=1;
      _put+=1
   ?};
   {? RP7_KW.R<>null() & RP7_KW.W=''
   || RP7_KW.W:=RP7_KW.R().RT;
      _stat.W+=1;
      _put+=1
   ?};
   {? _put
   || RP7_KW.put()
   ?}
",1);
RP7_KW.cntx_pop();
R.cntx_pop();
{? _stat.T
|| __UPG.msg('Zmieniono wartości pola RP7_KW.T. Liczba zmian: %1' [$_stat.T])
|| __UPG.msg('Wartości pola RP7_KW.T nie wymagały zmian.')
?};
{? _stat.W
|| __UPG.msg('Zmieniono wartości pola RP7_KW.W. Liczba zmian: %1' [$_stat.T])
|| __UPG.msg('Wartości pola RP7_KW.W nie wymagały zmian.')
?};
1


\rp7_kw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Zmiany w tabeli RP7_KW - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiany funkcjonalne w tabeli wyjaśnień składników na zaświadczeniu ZUS Rp-7'


\vat7_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [17.14]
:: OPIS: ER/WRT/XP/12.41/1702/0066 : Powtórzony bład  ER/WRT/XP/12.41/1604/0039 w e-deklaracji VAT(17)
::       ER/WRT/XP/12.41/1703/0031 : błąd e-deklaracji VAT 7 w przypadku wystąpienia załącznika VAT-ZD
::----------------------------------------------------------------------------------------------------------------------
exec('fiks_06a','upgrade_1714','VAT7',17,'vzz:P_9',57);
exec('fiks_06a','upgrade_1714','VAT7',17,'vzt:P_10',58);
exec('fiks_22a','upgrade_1714',17,69);
1


\vat7_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [17.14]
:: OPIS: ER/WRT/XP/12.41/1702/0066 : Powtórzony bład  ER/WRT/XP/12.41/1604/0039 w e-deklaracji VAT(17) - OPIS
::       ER/WRT/XP/12.41/1703/0031 : błąd e-deklaracji VAT 7 w przypadku wystąpienia załącznika VAT-ZD
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
' Poprawki spisowe z 12.41 dotyczące zmian w schemacie deklaracji VAT-7'


\fiks_06a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula pomocnicza do transferu
::   WE: _a - typ deklaracji
::       _b - nr deklaracji
::       _c - pozycja deklaracji
::       _d - nr pola
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_a,_a,_b);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_a,VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key(_c,)
      || ISTDEFS.REGULY:='exec(\'poz_edek\',\'xml\','+$_d+')';
         ISTDEFS.put()
      ?}
   ?}
?}


\edeklaracje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Dodanie nowych e-deklaracji
::----------------------------------------------------------------------------------------------------------------------
_dod_ver:="
   VAT_VER.index('VER_OD'); VAT_VER.prefix(_a,_c,_c,_b,_d);
   _jest:=VAT_VER.first();
   {? ~_jest
   || _old:={? _a='FKS' || 'FIKS' |? _a='PPL' || 'KALI' || '' ?};
      VAT_VER.prefix(_old,_c,_c,_b,_d);
      _jest:=VAT_VER.first()
   ?};
   {? _jest
   || _add:=0
   || _add:=1;
      VAT_VER.blank();
      VAT_VER.SYSTEM:=_a;
      VAT_VER.OD:=_b;
      VAT_VER.NAZWA:=_c;
      VAT_VER.NR:=_d
   ?};
   {? _<6 || _f:='' ?};
   {? _<5 || _e:=0 ?};
   _put:=0;
   {? VAT_VER.NRF<>_e || VAT_VER.NRF:=_e; _put+=1 ?};
   {? VAT_VER.WIERSZ<>_f || VAT_VER.WIERSZ:=_f; _put+=1 ?};
   VAT_VER.prefix();
   {? _add || VAT_VER.add(1) |? _put || VAT_VER.put(1) ?}
";
_dod_ver('FKS',date(2017,01,11),'POD',4);
_dod_ver('FKS',date(2017,01,11),'PODK',4);
_dod_ver('FKS',date(2017,01,11),'VAT27',2);
1


\edeklaracje_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Dodanie nowych e-deklaracji - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie wersji e-deklaracji'


\fiks_22a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Formula pomocnicza do transferu
::   WE: _a - nr deklaracji
::       _b - pozycja deklaracji
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','VAT7','VAT7',_a);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','VAT7',VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('P_'+$_b,)
      || ISTDEFS.REGULY:='{? exec(\'vat_zd_size\',\'vat\',\'N\') || \'1\' || 0 ?}';
         ISTDEFS.put()
      ?}
   ?}
?};
1


\ZTP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli ZTP
::   WY: -1/0/1
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

ZTP.cntx_psh();
ZTP.index('TP');
ZTP.prefix();
_fml:="
   _put:=0;
   {? ZTP.PROJZAKR=''
   || ZTP.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty');
      ZTP.PROJWJO:='N';
      ZTP.PROJZKH:='N';
      _put:=1
   ?};
   {? _put>0
   || ZTP.put()
   ?};
   ~~
";
_result:=ZTP.for_each(_fml,1);
ZTP.cntx_pop();
_result


\ZTP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli ZTP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów zleceń o nowe pola związane z obsługą projektów.\n'
'Może się nie udać, tylko jeżeli pole tabeli ZTP zostało zablokowane przez innego użytkownika.'


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja czynność dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_dom:=spli_str('PKD.PPL.TTE.ZWS','.');
_len:=obj_len(_dom);
FUN.prg_start(_len,'Aktualizacja czynności.'@,,,1);
{! _i.._len
|! FUN.prg_next();
   _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
   {? _domain<>null()
   || exec('import_needed_dom','#b_design',_domain,0);
      __UPG.msg('Zaktualizowano czynności obszaru: %1.'@[_dom[_i]])
   ?}
!};
FUN.prg_stop();
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja czynność dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czynności dla modelera'


\paramdef_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i Parametryzacja
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',0)<>0
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.');
   1
?}


\paramdef_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i Parametryzacja - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: kod - PR/WRT/XP/12.41/1703/0043
::       Agregacja pozycji VAT na potrzeby JPK_VAT
::  OLD: \fiks_24/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','VAT',null,date(2017,1,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref,'R',);
   {? ISTDEFS.first()
   || {!
      |? {? ISTDEFS.REGULY='$VAT_PS.ref()+$VAT_PS.crc()'
         || ISTDEFS.REGULY:='VAT_PS.get();$VAT_PS.ref()+$VAT_PS.crc()';
            ISTDEFS.put()
         ?};
         ISTDEFS.next()
      !}
   ?}
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
1


\jpk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: kod - PR/WRT/XP/12.41/1703/0043
::       Agregacja pozycji VAT na potrzeby JPK_VAT - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacje schematów JPK'


\typy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.14]
:: OPIS: Aktualizacja typów dokumentów i zamówień
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
TYPYDOK.prefix();
TYPYDOK.for_each("
   {? TYPYDOK.PROJZAKR=''
   || TYPYDOK.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty');
      TYPYDOK.PROJZKH:='N';
      TYPYDOK.PROJZKH:='N';
      TYPYDOK.put()
   ?}
",1);
TYPYSP.prefix();
TYPYSP.for_each("
   {? TYPYSP.PROJZAKR=''
   || TYPYSP.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty');
      TYPYSP.PROJZKH:='N';
      TYPYSP.put()
   ?}
",1);
TYPYZAM.prefix();
TYPYZAM.for_each("
   {? TYPYZAM.PROJZAKR=''
   || TYPYZAM.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty');
      TYPYZAM.PROJZKH:='N';
      TYPYZAM.PROJZKH:='N';
      TYPYZAM.put()
   ?}
",1);
1


\typy_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.14]
:: OPIS: Aktualizacja typów dokumentów i zamówień - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów dokumentów i zamówień.'


\wz_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Aktualizacja wzorca projekty
::----------------------------------------------------------------------------------------------------------------------
RS.index('TAB_POLE');
RS.prefix();
{? RS.find_key('UD_SKL','UD_SKL','SYMBOL','SYMBOL','Projekty','Projekty')
|| RS.WZ:='Projekty_schemat';
   RS.put();
   SLU.prefix();
   SLU.for_each("
      {? SLU.WZ='Projekty'
      || SLU.WZ:='Projekty_schemat';
         SLU.put()
      ?}
   ",1)
?};
1


\wz_proj_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Aktualizacja wzorca projekty - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wzorca słowników: projekty'


\jpk_01
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1703/0062  błędny symbol dokumentu JPK_VAT
::  OLD: \fiks_26/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','VAT',null,date(2017,1,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref,'DowodSprzedazy');
   {? ISTDEFS.first()
   || ISTDEFS.REGULY:='VAT_PS.SYM_ZEW';
      ISTDEFS.put()
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'DowodZakupu');
   {? ISTDEFS.first()
   || ISTDEFS.REGULY:='VAT_PS.SYM_ZEW';
      ISTDEFS.put()
   ?}
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
1


\jpk_01_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1703/0062  błędny symbol dokumentu JPK_VAT
::----------------------------------------------------------------------------------------------------------------------
'Poprawka spisowa do schematu JPK VAT'


\vat_ue
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1703/0055
::       Deklaracja VATUE w wersji 4 dla osoby fizycznej wymagane dodatkowe pole NIP
::  OLD: \fiks_25/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
exec('vat_uea','upgrade_1714',4,'POD');
exec('vat_uea','upgrade_1714',4,'PODK')


\vat_ue_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1703/0055  Deklaracja VATUE w wersji 4 dla osoby fizycznej wymagane dodatkowe pole NIP
::----------------------------------------------------------------------------------------------------------------------
'Poprawka spisowa do deklaracji VAT UE'


\vat_uea
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.30]
:: OPIS: Formula pomocnicza do vat_ue
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_b,_b,_a);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_b,VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('etd:OsobaFizyczna')
      || _ref:=ISTDEFS.ref();
         ISTDEFS.index('DRZEWO');

         ISTDEFS.prefix(ISTDEFS.ISTDEF,#_ref);
         {? ISTDEFS.first()
         || {! |? {? ISTDEFS.OPIS='etd:PESEL'
                  || ISTDEFS.del
                  |? ISTDEFS.OPIS='etd:NIP'
                  || ISTDEFS.REGULY:='STR.gsub(XINFO.NIP,\'-\',\'\')';
                     ISTDEFS.WYM:='T';
                     ISTDEFS.put();
                     ISTDEFS.next()
                  || ISTDEFS.next()
                  ?}
            !}
         ?}
      ?}
   ?}
?};
1


\PX_WYK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PX_WYK
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
PX_WYK.cntx_psh();
PX_WYK.prefix();
_formula:="
   _put:=0;
   {? PX_WYK.ZAK=''
   || PX_WYK.ZAK:='T';
      _put:=1
   ?};
   {? PX_WYK.DATA0=date(0,0,0)
   || PX_WYK.DATA0:=PX_WYK.DATA;
      _put:=1
   ?};
   {? PX_WYK.CZAS0=time(0,0,0)
   || PX_WYK.CZAS0:=PX_WYK.CZAS;
      _put:=1
   ?};
   {? _put || PX_WYK.put(1) ?}
";
_result:=PX_WYK.for_each(_formula,1);
PX_WYK.cntx_pop();
_result


\PX_WYK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PX_WYK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli wykonań planu strategicznego o znacznik zakończenia oraz datę i godzinę rozpoczęcia rejestracji.'


\fiks_27
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1704/0010
::       Błąd w strukturze deklaracji CIT_8
::  OLD: \fiks_27/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAT_VER.index('VER_NR'); VAT_VER.prefix('FIKS','CIT8','CIT8',24);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FIKS','D','CIT8',VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('P_66',)
      || {? ISTDEFS.seek(ISTDEFS.TREE,) & ISTDEFS.OPIS='grupa'
         || ISTDEFS.WYM:='N';
            ISTDEFS.HIDDEN:='T';
            ISTDEFS.put()
         ?}
      ?}
   ?}
?};
_res


\fiks_27_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1704/0010 - opis
::       Błąd w strukturze deklaracji CIT_8
::----------------------------------------------------------------------------------------------------------------------
'Poprawka błędu w strukturze deklaracji CIT-8'


\sch_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.14]
:: OPIS: kod - ER/WRT/XP/17.14/1706/0007
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('imp_wn','sch_imp',exec('nazwa_rachunek','obiegi'))


\sch_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.14]
:: OPIS: kod - ER/WRT/XP/17.14/1706/0007 - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka błędu usuwania rachunku bankowego we wniosku w obiegu - \'Zmiana rachunku bankowego\''

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:03 56d420d463c25235f06d0cf159903eca12636a7689495b28c1fd1cfcb4f2d67e029076fb2b2ecb791fd5c763944e2c5238aac8c10438fa0bea39a861f1b8b37dc2996bcf2e314ba31d04e5b29434c8185b5025bd880bd1511c9228b4f87db40ca45bcccdd19897e20ac55012c3a1ddc39d74da2c3f9656812d59215c418e237c

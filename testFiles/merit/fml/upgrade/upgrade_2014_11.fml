:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2014_11.fml
:: Utworzony: 17.06.2020
:: Autor: [IS]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 19.42 na 20.14
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Główna formuła aktualizacji 20.14_11
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZWS',,,,,'T');
__UPG.add_task('stalesys_ini',,,'ZWS',,,,,'T');
__UPG.add_task('stalesys_wid',,,'ZWS',,,,,'T');
__UPG.add_task('wersje_raportow',,,'PPK',,,,,'T');
__UPG.add_task('dane_instytucji',,,'PPK',,,,,'T');
__UPG.add_task('rep_tran',,,'PPK',,,,,'T');

:: Zadania automatyczne, wykonywane dla każdej firmy osobno
__UPG.add_task('wnioski_komunikaty',,'N','PPK',,,,,'T');
__UPG.add_task('stalesys_PPK',,'N','PPK',,,,,'T');
__UPG.add_task('role_PPK',,'N','PPK',,,,,'T');

:: Zadania manualne, wykonywane raz dla całego systemu

:: Zadania manualne, wykonywane dla każdej firmy osobno

~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.OBE','.');
_dom:=spli_str('PPK','.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.'@,,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja definicji stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_kst','#stalesys',KST_PPK);

__UPG.msg('Zaktualizowano definicje stałych systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja definicji stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji stałych systemu'


\stalesys_wid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja widoków stałych systemu
::----------------------------------------------------------------------------------------------------------------------
:: Uzupełnienie widoków stałych systemu:
KST_LST.cntx_psh();
KST_LST.prefix();
KST_LST.f_set(
   'NUMER',
   'join KST_DOM using(KST_LST.KST_DOM,KST_DOM.REFERENCE) '
   'join KST_MAP using(KST_LST.KST_MAP,KST_MAP.REFERENCE) '
   'join KST_ZES using(KST_MAP.KST_ZES,KST_ZES.REFERENCE) '
   'join B_DOMAIN using(KST_DOM.B_DOMAIN,B_DOMAIN.REFERENCE) ',
   'B_DOMAIN.SYMBOL=\'PPK\' and KST_ZES.SYMBOL=\'KST_PPK\''
);
{? KST_LST.f_last()
|| _num:=KST_LST.NUMER;
   _dom:=KST_LST.KST_DOM;
   KST_MAP.cntx_psh();
   KST_MAP.index('KST_MAP');
   KST_MAP.prefix('KST_PPK','KST_PPK');
   {? KST_MAP.find_key('DWT_OKR','DWT_OKR')
::    Znajdź pole w zmiennej reprezentujące stałą
   || {? ~KST_LST.find_tab(,'KST_DOM',,'=',_dom,'KST_MAP',,'=',KST_MAP.ref())
::       Uzupełnij widok o znalezioną stałą
      || KST_LST.KST_MAP:=KST_MAP.ref();
         KST_LST.NUMER:=_num+1;
         KST_LST.add()
      ?}
   ?};
   KST_MAP.cntx_pop()
?};
KST_LST.f_clear();
KST_LST.cntx_pop();

__UPG.msg('Zaktualizowano widoki stałych systemu.');
1


\stalesys_wid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja widoków stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja widoków stałych systemu'


\wersje_raportow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja elementów i wersji raportów.
::----------------------------------------------------------------------------------------------------------------------
:: Elementy raportów
exec('dane_sys','ppk_xel');
__UPG.msg('Zaktualizowano elementy raportów.');

:: Wersje raportów
exec('dane_sys','ppk_xwe');
__UPG.msg('Zaktualizowano wersje raportów.');

1


\wersje_raportow_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja elementów i wersji raportów - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja elementów i wersji raportów.'


\dane_instytucji
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja nazwy wybranych instytucji finansowych
::----------------------------------------------------------------------------------------------------------------------
{? (_typ:=exec('slo_typ','ext_slo','PPK_IF'))=null
|| __UPG.msg('Brak typu słownika PPK_IF');
   return(-1)
?};

_err:=0;
_nip:='5271024937';
ADRES.cntx_psh();
ADRES.clear();
{? ADRES.find_tab(,'TYP','SYMBOL','=','PPK_IF','NIP',,'=',_nip)
|| _nazwa:='Generali Investment Towarzystwo Funduszy Inwestycyjnych SA';
   _email:='ppk@generali-investment.pl';
   _mod:=0;
   {? ADRES.NAZWA<>_nazwa
   || ADRES.NAZWA:=_nazwa;
      _mod+=1
   ?};
   {? ADRES.EMAIL<>_email
   || ADRES.EMAIL:=_email;
      _mod+=1
   ?};
   {? _mod<>0
   || {? ADRES.put()
      || __UPG.msg('Zaktualizowano dane instytucji finansowej o NIP %1.'[_nip])
      || __UPG.msg('Wystąpił błąd podczas aktualizacji danych instytucji finansowej o NIP %1.'[_nip]);
         _err:=1
      ?}
   || __UPG.msg('Dane instytucji finansowej o NIP %1 nie wymagały aktualizacji.'[_nip])
   ?}
|| __UPG.msg('Nie znaleziono instytucji finansowej o NIP %1.'[_nip]);
   _err:=1
?};
ADRES.cntx_pop();

{? _err<>0 || -1 || 1 ?}


\dane_instytucji_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja nazwy wybranych instytucji finansowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja nazwy wybranych instytucji finansowych.'


\rep_tran
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Kompilacja wydruków dziedziny PPK.
::----------------------------------------------------------------------------------------------------------------------
{? rep_tran('ppk*')<>0
|| __UPG.msg('Zaktualizowano wydruki dziedziny PPK.');
   1
|| __UPG.msg('Aktualizacja wydruków dziedziny PPK nie powiodła się.');
   -1
?}


\rep_tran_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Kompilacja wydruków dziedziny PPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Kompilacja wydruków dziedziny PPK.'


\wnioski_komunikaty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja rodzajów wniosków WODW, AWW i utworzenie DWP oraz aktualizacja definicji komunikatów.
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');
__RUB.fill();

_rub:="
   {? (_rub:=__RUB.sys_kod(_a))=0
   || __UPG.msg('Nie znaleziono rubryki o atrybucie %1.'[$_a])
   ?};
   _rub
";
{? (_r710:=_rub(710))=0 |
   (_r711:=_rub(711))=0 |
   (_r712:=_rub(712))=0 |
   (_r713:=_rub(713))=0
|| return(-1)
?};

:: Komunikaty błędów
_err:=obj_new('wrd_find','wrd_add','wst_add');
_err.wrd_find:="__UPG.msg('Nie znaleziono rodzaju wniosku %1.'[_a]); 0";
_err.wrd_add:="__UPG.msg('Dodanie rodzaju wniosku %1 (%2) nie powiodło się.'[_a,_b]); 0";
_err.wst_add:="__UPG.msg('Przypisanie rubryki %1 do rodzaju wniosku %2 nie powiodło się.'[$_a,_b]); 0";
:: Komunikaty powodzenia
_msg:=obj_new('wrd_add','wst_add');
_msg.wrd_add:="__UPG.msg('Dodano/znaleziono rodzaju wniosku %1 (%2).'[_a,_b]); 1";
_msg.wst_add:="__UPG.msg('Przypisano rubrykę %1 do rodzaju wniosku %2.'[$_a,_b]); 1";
:: Obsługa aktualizacji
_upg:=obj_new('chk','err','msg','wrd_find','wrd_add','wst_add');
_upg.chk:=0;
_upg.err:=_err;
_upg.msg:=_msg;
_upg.wrd_find:="
   {? (_ref:=exec('szukaj','ppk_wrd','U',_a))=null
   || .err.wrd_find(_a);
      .chk-=1
   ?};
   _ref
";
_upg.wrd_add:="{? _a=null || .chk-=1; .err || .msg ?}.wrd_add(_b,_c)";
_upg.wst_add:="{? _a=null || .chk-=1; .err || .msg ?}.wst_add(_b,_c)";

:: Dodaj definicję WB25B
_symbol:='WB25B';
_opis:='Blokada wpłat na podstawie art. 25';
_ref:=exec('dodaj','ppk_wrd','U',_symbol,_opis,,'N','N');
{? _upg.wrd_add(_ref,_symbol,_opis)<>null
|| _upg.wst_add(exec('dodaj','ppk_wst',_ref,_r710,'T'),_r710,_symbol);
   _upg.wst_add(exec('dodaj','ppk_wst',_ref,_r711,'T'),_r711,_symbol);
   _upg.wst_add(exec('dodaj','ppk_wst',_ref,_r712,'T'),_r712,_symbol);
   _upg.wst_add(exec('dodaj','ppk_wst',_ref,_r713,'T'),_r713,_symbol)
?};

:: Dodaj definicję WB25W
_symbol:='WB25W';
_opis:='Wznowienie wpłat na podstawie art. 25';
_ref:=exec('dodaj','ppk_wrd','U',_symbol,_opis,,'N','N');
_upg.wrd_add(_ref,_symbol,_opis);

:: Dodaj definicję DOWU25
_symbol:='DOWU25';
_opis:='Deklaracja opłacania wpłat uczestnika art. 25';
_ref:=exec('dodaj','ppk_wrd','U',_symbol,_opis,,'N','T');
{? _upg.wrd_add(_ref,_symbol,_opis)<>null
|| _upg.wst_add(exec('dodaj','ppk_wst',_ref,_r712,,'N'),_r712,_symbol);
   _upg.wst_add(exec('dodaj','ppk_wst',_ref,_r713,,'N'),_r713,_symbol)
?};

:: Dodaj składnik 7086 do definicji AWW
_symbol:='AWW';
{? (_ref:=_upg.wrd_find(_symbol))<>null
|| _upg.wst_add(exec('dodaj','ppk_wst',_ref,_r713),_r713,_symbol)
?};

:: Dodaj składnik 7086 do definicji WODW
_symbol:='WODW';
{? (_ref:=_upg.wrd_find(_symbol))<>null
|| _upg.wst_add(exec('dodaj','ppk_wst',_ref,_r713),_r713,_symbol)
?};

:: Dodaj definicję DWP
_symbol:='DWP';
_opis:='Deklaracja wypłaty trans.-zmiana zarządzającego';
_ref:=exec('dodaj','ppk_wrd','U',_symbol,_opis,'UCZ_WYPŁATA_TRANSFEROWA','N','N');
_upg.wrd_add(_ref,_symbol,_opis);

:: Rodzaje komunikatów
exec('dane_sys','ppk_krd');
:: Zmiana rodzaju WYPTRANS i ZWROT
_symbol:=spli_str('WYPTRANS,ZWROT',',');
PPK_KRD.cntx_psh();
PPK_KRD.index('SYMBOL');
PPK_KRD.prefix('U');
{! _ii:=1..obj_len(_symbol)
|! {? PPK_KRD.find_key(_symbol[_ii])
   || PPK_KRD.PORTAL:='T';
      PPK_KRD.EDYCJA:='T';
      PPK_KRD.FORMULA:='T';
      PPK_KRD.memo_set('exec(\'%1\',\'ppk_krd\')'[-(_symbol[_ii])],'TRESC');
      {? ~(PPK_KRD.put() & PPK_KRD.memo_put(,'TRESC'))
      || __UPG.msg('Wystąpił błąd podczas aktualizacji rodzaju komunikatu %1.'[_symbol[_ii]]);
         _upg-=1
      ?}
   ?}
!};
PPK_KRD.cntx_pop();
obj_del(_symbol);
__UPG.msg('Zaktualizowano definicje komunikatów.');

{? _upg.chk<0 || -1 || 1 ?}


\wnioski_komunikaty_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja rodzajów wniosków WODW, AWW i utworzenie DWP oraz aktualizacja definicji komunikatów - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rodzajów wniosków WODW, AWW i utworzenie DWP oraz aktualizacja definicji komunikatów.'


\stalesys_PPK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja stałych dziedziny PPK
::----------------------------------------------------------------------------------------------------------------------
_zd:=date(0,0,0);
_info:='Ustawiono wymagane wartości początkowe.';

exec('czytaj','#stalesys',_zd,KST_PPK,'DWT_OKR');

{? KST_PPK.DWT_OKR=0
|| KST_PPK.DWT_OKR:=7;
   exec('zapisz_zes','#stalesys',_zd,KST_PPK,'DWT_OKR')
|| _info:='Ustawienie wartości początkowych nie było konieczne.'
?};

__UPG.msg(_info);
1


\stalesys_PPK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Aktualizacja stałych dziedziny PPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych dziedziny PPK'


\role_PPK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami dziedziny PPK.
::----------------------------------------------------------------------------------------------------------------------
_status:=1;
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_add:="
   _rola:=_a;
   _lista:=_b;
   _ok:=1;
   _uid:=spli_str(_lista,'.');
   {! _lp:=1 .. obj_len(_uid)
   |! {? exec('add_actrol_one','#b_role',_rola,_uid[_lp])
      || __UPG.msg('Roli ''%1'' nadano uprawnienia do czynności ''%2''.'[_rola,_uid[_lp]])
      || __UPG.msg('Nadanie roli ''%1'' uprawnień do czynności ''%2'' nie powiodło się.'[_rola,_uid[_lp]]);
         _ok:=0
      ?}
   !};
   _ok
";

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_rola:='Specjalista ds. PPK';
_ref:=exec('ref','#b_role',_rola);
_act:=spli_str('PPK_GRP_AUPO,PPK_GRP_A25B,PPK_GRP_A25W,PPK_EWI_ADWT,PPK_EWI_AKRW,PPK_EWI_ALIU,PPK_EWI_ARNW',',');
{! _ii:=1..obj_len(_act)
|! {? _ref=null()
::    To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
   || __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.'[_rola,_act[_ii]]);
      _status:=-1
   || {? ~_add(_rola,_act[_ii])
      || _status:=-1
      ?}
   ?}
!};

_status


\role_PPK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami dziedziny PPK - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ról/uprawnień związana z czynnościami dziedziny PPK.'


:Sign Version 2.0 jowisz:1048 2020/10/16 15:22:59 1f6e21ca02bce6c496a7d32b96e00bb9e5db0b5cbe2351a9bdba1892b99774f5518c69418266bb98942eb603b9e56fcf5cec971030bd82e4e26427909ee895b5246dfed42f490ca50d5d106a63595ddb3a510f1663fa6738b0ac709d4bae205edec93427867fff30ddac1183b2281f69a87848319695383d6844dd39bf56c550

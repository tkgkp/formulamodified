:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2137_05.fml
:: Utworzony: 21.09.2021
:: Autor: MB
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych na 21.37_05
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14_09]
:: OPIS: Główna formuła aktualizacji 21.14_09
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',,,,,'T');
__UPG.add_task('stalesys_ini',,,'ZWS',,,,,'T');
__UPG.add_task('plugins',,,'ZWS',,,,,'T');
__UPG.add_task('add_viudo_sch',,,'FKS',,,,,'T');
__UPG.add_task('funpar',,,'ZWS',,,,,'T');
__UPG.add_task('ecb_crt',,,'HBN',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
~~


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.OBE','.');
::_dom:=spli_str('LSP.LZK.PKD.PPL.TRE.PBA.LTR.TTE.ZWS.PED.LMG.LUM.PRC.HBN.OBE','.');
_str:='FKS.ZWS';
_dom:=spli_str(_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.'@,,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::  OLD: \upgrade_act_desc/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\add_viudo_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Dodaje schemat deklaracji VIU-DO
::----------------------------------------------------------------------------------------------------------------------
_imp:=-1;
{? exec('add_ver','xml','FKS',date(2021,4,1),'VIUDO',1,1)
|| _imp:=exec('upg_imp','xml','FKS','D','VIUDO',1,date(2021,4,1),'viu_do_1.dfg')
?};

_txt:='e-Deklaracja VIU-DO (1) obowiązująca od 01.04.2021';

{? _imp=1
|| __UPG.msg(_txt+' została dodana.');
   1
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.');
   1
|| __UPG.msg(_txt+' nie została dodana.');
   0
?}


\add_viudo_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Dodaje schemat deklaracji VIU-DO - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie schematu deklaracji VIU-DO (1)'


\plugins
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Uzupełnia definicję wtyczek wdrożeniowych
::----------------------------------------------------------------------------------------------------------------------
exec('init','plugins');
__UPG.msg('Uzupełniono definicję wtyczek wdrożeniowych.');
1


\plugins_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Uzupełnia definicję wtyczek wdrożeniowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie definicji wtyczek wdrożeniowych:\n'
'FKS_VIUDO_TU'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14_09]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',1)
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.')
?}


\ecb_crt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.14_09]
:: OPIS: Uzupełnianie słownika banków i oddziałów o Europejski Bank Centralny
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh(); SLO.cntx_psh(); BANORG.cntx_psh(); B.cntx_psh();
SLU.index('NAZ'); SLU.prefix('BANKI');

{? SLU.first()
|| SLO.index('SL'); SLO.prefix(SLU.ref());

   KRAJE.cntx_psh();
   KRAJE.index('KRAJE');
   KRAJE.prefix('DE');
   {? KRAJE.first() || _kraj_ref:=KRAJE.ref() || _kraj_ref:=null ?};
   KRAJE.cntx_pop();

   BANORG.index('NDX');
   BANORG.prefix('ECB','Europejski Bank Centralny',);

   _org:=_bank:=null;

   {? BANORG.first()
   || _org:=BANORG.ref()
   || BANORG.blank();
      BANORG.KOD:='ECB';
      BANORG.KODZ:='';
      BANORG.AKTYWNY:='T';
      BANORG.NAZ:='Europejski Bank Centralny';
      BANORG.NRCB:='';
      {? BANORG.add() || _org:=BANORG.ref() ?}
   ?};
   {? _org
   || B.index('BANK');
      B.prefix('Europejski Bank Centralny - Centrala','00000001');
      {? B.first()
      || _bank:=B.ref()
      || B.blank();
         B.BANORG:=_org;
         B.KOD:='00000';
         B.KODISO:=_kraj_ref;
         B.NB:='Europejski Bank Centralny - Centrala';
         B.NUMER:='00000001';
         B.STATUS:='B';
         {? B.add() || _bank:=B.ref() ?}
      ?}
   ?};

   TKRS.cntx_psh();
   TKRS.use('tabkrseb');
   TKRS.prefix();
   {? TKRS.first()
   || {!
      |? {? TKRS.count()>0
         || KRS.cntx_psh();
            KRS.use('kursy_eb');
            KRS.index('KRS_KRAJ');
            KRS.prefix(TKRS.ref);
            {? KRS.first() || {! |? KRS.del() !} ?};
            KRS.cntx_pop()
         ?};
         TKRS.del()
      !}
   ?};
   TKRS.cntx_pop();

   SLO.cntx_psh();
   SLO.index('SL_TR');
   SLO.prefix(SLU.ref,'Europejski Bank Centralny - Centrala');
   {? SLO.first()
   || {!
      |? SLO.del()
      !}
   ?};
   SLO.cntx_pop();

   {? _kraj_ref & ~SLO.find_key('99999')
   || SLO.blank();
      SLO.KOD:='99999';
      SLO.TR:='Europejski Bank Centralny - Centrala';
      SLO.add()
   ?}
?};
SLU.cntx_pop(); SLO.cntx_pop(); BANORG.cntx_pop(); B.cntx_pop();
__UPG.msg('Zaktualizowano słownik banków i oddziałów.');
1


\ecb_crt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.14_09]
:: OPIS:  Uzupełnianie słownika banków i oddziałów o Europejski Bank Centralny - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnianie słownika banków i oddziałów o Europejski Bank Centralny'

:Sign Version 2.0 jowisz:1048 2023/06/23 14:16:36 0e16bd739b04c9ac401172ddfc130479ccb9571edc19ba48c600dfb3aa4dab25268c5c89e34581dfe18a77bcaecb5fabc0b0417dc9b5858f472a69188bac046917761d7d4c2ef17e7567333849e249a59b454612a4b92da921fac6497ced7276c83f9838d30a66026a489d376a9146514298290242225f3f0826d24d5a51de71

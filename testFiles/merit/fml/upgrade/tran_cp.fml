:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tran_cp.fml
:: Utworzony: 18.01.2017
:: Autor: jaws
:: Systemy: Xpertis
::======================================================================================================================
:: Zawartość: Formuły włatwiające przenoszenie danych.
::======================================================================================================================


\start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
::  MOD: ARTSLO [19.22]
:: OPIS: Udostępnia funkcje biblioteki.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

::Sprawdznie, czy transfer uruchamiany jest na runtimie meritowym
_run:=user(11);
{? ~((1+_run)='O' | #(2+_run)>=17 | (1+_run)='R')
|| FUN.error('Funkcja może być uruchamiana tylko na runtime Meritowym.');
   return(0)
?};

_cfg:=exec('get_cfg','tran_cp');
:: Sprawdzenie, na jakim systemie uruchomiona została funkcja (Xpertis/Merit). Potrzebne do odpowiedniej inicjalizacji
:: obiektu RUB.
_ver:=user(10);
{? _ver<'17.00'
|| _rub:="
      exec('rubobj','rubobj');
      {? var_pres('RUB')<>type_of(@.CLASS.RUB)
      || RUB:=obj_new(@.CLASS.RUB)
      ?};
   "
|| _rub:="exec('__RUB','object');"
?};

popup(1,'Transfer',
::   'Przygotuj dane do przeniesienia','Uruchamia formuły eksportujące.',$(_rub+"exec('export_jf','tran_exp')"),
   'Przygotuj dane do przeniesienia','Uruchamia formuły naprawcze.',$(_rub+"exec('transfer_prep','tran_exp')"),
   'Eksportuj dane','Uruchamia formuły eksportujące dane.',$(_rub+"exec('transfer_exp','tran_exp')"),
   'Utwórz skrypty kopiujące','Tworzy skrypty kopiujące zbiory tabel.',$(_rub+"exec('create','tran_cp')"),
   '--',,"",
   'Wyświetl skrypt '+_cfg.bat,,$(_rub+'exec(\'txt_view\',\'tran_cp\',\''+_cfg.bat+'\')'),
   'Wyświetl skrypt '+_cfg.sh,,$(_rub+'exec(\'txt_view\',\'tran_cp\',\''+_cfg.sh+'\')'),
   '--',,"",
   'Zlokalizuj zbiory tabel','Zapisuje informacje o zbiorach tabel.',$(_rub+"exec('t_names','tran_cp')"),
   'Określ katalogi tabel','Zapisuje informacje o katalogach tabel.',"exec('t_dirs','tran_cp')",
   '--',,"",
   'Porównaj lokalizację tabel','Porównuje informacje o lokalizacji tabel.',"exec('loc_cmp','tran_cp')"
)


\create
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy skrypty kopiujące zbiory tabel
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=exec('get_cfg','tran_cp');

_SRC:=exec('t_names','tran_cp');
_DST:=exec('get_buff','tran_cp');
{? ~exec('imp_buff','tran_cp',_DST,_cfg.dirs)
|| return()
?};

_cp_dir:=pth_dir('xp_dane.pti');

_RES:=sql(
   'select :_a.FILE as FILE, :_a.DIR as SRC, :_b.DIR as DST '+
   'from :_a join :_b using (:_a.TABLE,:_b.TABLE)',
   _SRC,_DST
);

_sep:={? 2+sys_name(1)='U_'
      || '/'
      || '\\'
      ?};

:_bat:=fopen(_cfg.bat,'w',,,1);
_bat:=fopen(_cp_dir+_sep+_cfg.bat,'a',,,1);
{? ~_bat.is_open()
|| _cfg.ferror(_cfg.bat);
   return()
?};

:_sh:=fopen(_cfg.sh,'w',,,1);
_sh:=fopen(_cp_dir+_sep+_cfg.sh,'a',,,1);
{? ~_sh.is_open()
|| _cfg.ferror(_cfg.sh);
   _bat.fclose();
   return()
?};

:_sym:=exec('firm_sym','tran_cp');
_sym:=exec('firma_symbol','system');
_loop:=_RES.first();
{!
|? _loop
|! {? ~REF.WFIRM | (REF.WFIRM & _sym='001')
   || _bat.fwrite('copy \"'+_RES.SRC+_cfg.sep_win+_RES.FILE+'\" '+_RES.DST+_cfg.sep_win+_RES.FILE);
      _sh.fwrite('cp '+_RES.SRC+_cfg.sep_ux+_RES.FILE+' '+_RES.DST+_cfg.sep_ux+_RES.FILE)
   || {? _RES.DST*'f001' & ~(_RES.FILE*'zsb_')
      || _bat.fwrite('copy \"'+_RES.SRC+_cfg.sep_win+_RES.FILE+'\" '+'f'+_sym+'.mdb'+_cfg.sep_win+_RES.FILE);
         _sh.fwrite('cp '+_RES.SRC+_cfg.sep_ux+_RES.FILE+' '+'f'+_sym+'.mdb'+_cfg.sep_ux+_RES.FILE)
      ?}
   ?};
   _loop:=_RES.next()
!};

_bat.fclose();
_sh.fclose();

FUN.info(
   'Utworzono skrypty kopiujące zbiory tabel:\n'+
::   _cfg.bat+' w katalogu '+pth_dir(_cfg.bat)+'\n'+
::   _cfg.sh+' w katalogu '+pth_dir(_cfg.sh
   _cfg.bat+' w katalogu '+_cp_dir+'\n'+
   _cfg.sh+' w katalogu '+_cp_dir
);

~~


\t_names
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Lokalizuje zbiory tabel.
::   WE:
::   WY: alias tabeli o strukturze określonej w get_buff zawierającej informacje o położeniu zbiorów tabel
::----------------------------------------------------------------------------------------------------------------------
_cfg:=exec('get_cfg','tran_cp');
_BUF:=exec('get_buff','tran_cp');

_num:=tab_num();

{! _in:=1.._num
|! {? tab_real(_in)<>0
   || _acr:=tab_acr(_in);
      progress(
         (100*_in/_num)$0,
         'Trwa przetwarzanie informacji...\nTabela '+_acr,
         'Lokalizacja zbiorów'
      );
      _NAMES:=($(_acr+'.names()'))();
      _loop:=_NAMES.first();
      {!
      |? _loop
      |! _name:=_NAMES.NAME+'.mdb';
         _BUF.blank();
         _BUF.TABLE:=_acr;
         _BUF.FILE:=_name;
         _BUF.DIR:=pth_dir(':'+_name);
         _BUF.add();
         _loop:=_NAMES.next()
      !};
      obj_del(_NAMES)
   ?}
!};
prgs_clr();

exec('exp_buff','tran_cp',_BUF,_cfg.tables);

_BUF


\t_dirs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Określa katalogi tabel.
::   WE:
::   WY: alias tabeli o strukturze określonej w get_buff zawierającej informacje o katalogach tabel
::----------------------------------------------------------------------------------------------------------------------
_cfg:=exec('get_cfg','tran_cp');
_BUF:=exec('get_buff','tran_cp');

{? ~FUN.ask(
      'Czy na pewno aktualizować zawartość pliku '+_cfg.dirs+'?\n'+
      'Funkcja powinna być wykonywana wyłącznie w systemie Merit.'
   )
|| return(_BUF)
?};

{! _in:=1..tab_num()
|! {? tab_real(_in)<>0
   || _acr:=tab_acr(_in);
      {? _cfg.ignore*(','+_acr+',')=0
      || _acrTab:=_acr;
         {? (_wsk:=_cfg.conv_acr*(','+_acr+'('))>0
         || _buf:=((_wsk+1)+(+_acr))-_cfg.conv_acr;
            {? (_wsk:=_buf*')')>0 || _acrTab:=(_wsk-1)+_buf ?}
         ?};
         _BUF.blank();
         _BUF.TABLE:=_acrTab;
         _BUF.DIR:=pth_dir(':'+($(_acr+'.name()'))()+'.mdb');
         _BUF.add()
      ?}
   ?}
!};

{? _BUF.first()
|| _key:=_BUF.index('?');
   _BUF.index(_BUF.ndx_tmp(,,'DIR',,));
   _dir:=_BUF.DIR;
   _size:=_BUF.size();
   {!
   |? _BUF.prefix(_dir);
      {? _BUF.size()<>_size
      || _dir:=_dir-1;
         +_dir
      ?}
   !};
   _BUF.index(_key);
   {? +_dir<>0
   || _len:=+_dir;
      _loop:=_BUF.first();
      {!
      |? _loop
      |! _BUF.DIR:=_len-_BUF.DIR;
         _BUF.put();
         _loop:=_BUF.next()
      !}
   ?}
?};

exec('exp_buff','tran_cp',_BUF,_cfg.dirs);

_BUF


\get_cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca tablicę nazwaną zawierającą stałe.
::   WE:
::   WY: wskazanie tablicy nazwanej
::----------------------------------------------------------------------------------------------------------------------
_cfg:=obj_new(
   'tables',
   'dirs',
   'bat',
   'sh',
   'sep_win',
   'sep_ux',
   'sep_col',
   'imex_opt',
   'ignore',
   'ferror',
   'conv_acr'
);

_cfg.tables:='tran_tab.tra';
_cfg.dirs:='tran_dir.tra';

_cfg.bat:='copy.bat';
_cfg.sh:='cp.sh';

_cfg.sep_win:='\\';
_cfg.sep_ux:='/';

_cfg.sep_col:=%1+';';
_cfg.imex_opt:='UTF-8,pth,header';

: akronimy pomijanych tabel np:
: _cfg.ignore:=',SLOWNIK,TABELA,X,';
_cfg.ignore:=',UST_KOL,UST_MWYK,UST_S,EDOKGR,USERSDEP,K_HARM_U,KON_WYDR,KON_SCH,SYSLOG,SAMN,PORTALU,ISTXML,'
             'ETSCHPDN,ETSCHPDR,ETSCHPOD,ETYPYDTP,ETYPYFIR,ETYPYPOD,PKOD,POMI,SEZ,SIED,SKAN_DOK,SKAN_USR,TSR,ZSKON,ETYPY_P,';



: akronimy do zmiany dla tabel
: akronim tabeli Merit (akronim tabeli Xpertis);
_cfg.conv_acr:=',UPMPOJ(UPPOJ),';

_cfg.ferror:="FUN.error('Błąd dostępu do pliku '+_a+'.')";

_cfg


\get_buff
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy bufor danych.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_BUF:=tab_tmp(1,
   'TABLE','STRING[8]','Tabela',
   'FILE','STRING[32]','Plik',
   'DIR','STRING[128]','Ścieżka'
);

_BUF.win_sel(_BUF.mk_sel('Tabele',,1));

_BUF


\exp_buff
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zapisuje zawartość bufora danych do pliku.
::   WE: _a [TABLE] - alias bufora
::       _b [STRING] - nazwa pliku
::   WY: Wynik działania metody export tabeli
::----------------------------------------------------------------------------------------------------------------------
_cfg:=exec('get_cfg','tran_cp');

_res:=_a.export(
   _b,0,_cfg.sep_col,_cfg.imex_opt,,
   'TABLE',,1,,
   'FILE',,2,,
   'DIR',,3,
);

{? _res
|| FUN.info('Zapisano plik '+_b+' w katalogu '+pth_dir(_b)+'.')
|| FUN.error('Wystąpił błąd podczas zapisu pliku '+_b+'.')
?};

_res


\imp_buff
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wczytuje z pliku zawartość bufora danych.
::   WE: _a [TABLE] - alias bufora
::       _b [STRING] - nazwa pliku
::   WY: Wynik działania metody import tabeli
::----------------------------------------------------------------------------------------------------------------------
{? ~fexists(_b,1)
|| FUN.error('Plik '+_b+' nie istnieje.');
   return(0)
?};

_cfg:=exec('get_cfg','tran_cp');

_res:=_a.import(_b,1,0,_cfg.sep_col,_cfg.imex_opt,,
   'TABLE',,1,,
   'FILE',,2,,
   'DIR',,3,
);

{? _res
|| FUN.info('Wczytano plik '+_b+' z katalogu '+pth_dir(_b)+'.')
|| FUN.error('Wystąpił błąd podczas odczytu pliku '+_b+'.')
?};

_res


\txt_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyświetla zawartość pliku.
::   WE: _a [STRING] - nazwa pliku
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? fexists(_a,1)
|| txt_view(_a)
|| FUN.error('Plik '+_a+' nie istnieje.')
?}


\loc_cmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Porównuje położenie plików tabel pod względem lokalizacji w katalogach aplikacji i wspólnych
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_BUF:=tab_tmp(1,
   'TABLE','STRING[8]','Tabela',
   'FILE_SRC','STRING[8]','Plik źródłowy',
   'DIR_SRC','STRING[20]','Katalog źródłowy',
   'FILE_DST','STRING[8]','Plik docelowy',
   'DIR_DST','STRING[20]','Katalog docelowy',
   'EQUAL','STRING[1]','Zgodne [T/N/X]'
);

_wnd:=_BUF.mk_sel('Porównanie położenia plików','P',0,'#tran_loc_cmp',,,,,'U');
_BUF.win_fld(_wnd,,'TABLE',,,-10);
_BUF.win_fld(_wnd,,'FILE_SRC',,,-12);
_BUF.win_fld(_wnd,,'DIR_SRC',,,-20);
_BUF.win_fld(_wnd,,'FILE_DST',,,-12);
_BUF.win_fld(_wnd,,'DIR_DST',,,-20);
_BUF.win_fld(_wnd,,'EQUAL',,,-10,,,,,,2,,"'T'","'N'","'X'");

_menu:='Wczytaj';
_BUF.win_act(_wnd,1,'Menu',_menu);
_BUF.win_act(_wnd,1,'Formuła','Plik &źródłowy',_menu,,"exec('loc_gsrc','tran_cp')",,,,,,'Z');
_BUF.win_act(_wnd,1,'Formuła','Plik &docelowy',_menu,,"exec('loc_gdst','tran_cp')",,,,,,'D');
_BUF.win_act(_wnd,0,'Menu',_menu);
_BUF.win_act(_wnd,0,'Formuła','Plik &źródłowy',_menu,,"exec('loc_gsrc','tran_cp')",,,,,,'Z');
_BUF.win_act(_wnd,0,'Formuła','Plik &docelowy',_menu,,"exec('loc_gdst','tran_cp')",,,,,,'D');
_BUF.win_act(_wnd,0,'Formuła','Usuń zawartość',,,"exec('loc_del','tran_cp')",,,,,,'U');
_BUF.win_act(_wnd,,'Szukaj');
_BUF.win_act(_wnd,,'Kolejność');

_BUF.win_btn(_wnd,'text=%1,panel=bottom,align=begin'['Wczytaj plik &źródłowy'],'menu:WZ');
_BUF.win_btn(_wnd,'text=%1,panel=bottom,align=begin'['Wczytaj plik &docelowy'],'menu:WD');
_BUF.win_btn(_wnd,'text=%1,panel=bottom,align=end'['Usuń zawartość'],'menu:U');

_BUF.win_sel(_wnd);
_BUF.select();
~~


\loc_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Usuwa informacje o położeniu tabel.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_BUF:=cur_tab(1,1);

_BUF.erase();
_BUF.actions_grayed(_BUF.win_sel('?'));
~~


\loc_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Wczytuje informacje o położeniu tabel.
::   WE:
::   WY: alias tabeli lub ~~ w przypadku błędu lub rezygnacji
:: ~OST: INFILECHOOSER
::----------------------------------------------------------------------------------------------------------------------
_filter:='Pliki transferu (*.tra)|*.tra|Wszystkie pliki (*.*)|*.*';
_name:='tran_dir.tra';

ctr_set('!application','filechooser','reset');
ctr_set('!application','filechooser','setDialogTitle','Otwórz'@);
ctr_set('!application','filechooser','addDefaultFilter',_filter,'tra');
ctr_set('!application', 'filechooser','setAcceptAllFileFilterUsed',1);
ctr_set('!application','filechooser','setMultiSelectionEnabled',0);
ctr_set('!application','filechooser','setFileSelectionMode','FILES_ONLY');
ctr_set('!application','filechooser','setSelectedFile',_name);
_name:={? ctr_call('!application','filechooser','showOpenDialog')
       || ctr_call('!application','filechooser','getSelectedFile')
       || ''
       ?};
{? _name<>''
|| _name:='@'+_name;
   {? fexists(_name,0)=0
   || FUN.emsg('Plik "%1" nie istnieje.'[_name])
   ?}
|| return()
?};

_BUF:=exec('get_buff','tran_cp');
_res:=_BUF.import(_name,,,%1+';','UTF-8,nopth,header',,
   'TABLE',,1,,
   'DIR',,3,
);
{? _res=0
|| FUN.emsg('Wystąpił błąd podczas importu danych z pliku "%1".'[_name]);
   return()
?};

_BUF


\loc_gdst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Wczytuje informacje o położeniu tabel po stronie "źródłowe".
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: wczytaj katalogi źródłowe
_IMP:=exec('loc_get','tran_cp');
{? type_of(_IMP)<>type_of(SYSLOG)
|| return()
?};
:: tabela widoku
_BUF:=cur_tab(1,1);

_IMP.cntx_psh();
_loop:=_IMP.first();
{!
|? _loop
|! {? _BUF.find_tab(,'TABLE',,'=',_IMP.TABLE)
::    jest już wiersz na podstawie katalogów źródłowych
   || _BUF.FILE_DST:={? 3+_IMP.DIR='f00' || 'lokalny' || 'globalny' ?};
      _BUF.DIR_DST:=_IMP.DIR;
      _BUF.EQUAL:={? _BUF.FILE_DST=_BUF.FILE_SRC || 'T' || 'N' ?};
      _BUF.put()

   || _BUF.blank();
      _BUF.TABLE:=_IMP.TABLE;
      _BUF.FILE_DST:={? 3+_IMP.DIR='f00' || 'lokalny' || 'globalny' ?};
      _BUF.DIR_DST:=_IMP.DIR;
      _BUF.EQUAL:='X';
      _BUF.add()
   ?};
   _loop:=_IMP.next()
!};
_IMP.cntx_pop();
_BUF.first();

:: po wczytaniu wyłącz akcję honorując dotychczasowe wyłączenie
_aid:='W(D)%1W(D)'[_BUF.actions_grayed(_BUF.win_sel('?'))];
_BUF.actions_grayed(_BUF.win_sel('?'),_aid);
~~


\loc_gsrc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Wczytuje informacje o położeniu tabel po stronie "docelowe".
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: wczytaj katalogi docelowe
_IMP:=exec('loc_get','tran_cp');
{? type_of(_IMP)<>type_of(SYSLOG)
|| return()
?};
:: tabela widoku
_BUF:=cur_tab(1,1);

_IMP.cntx_psh();
_loop:=_IMP.first();
{!
|? _loop
|! {? _BUF.find_tab(,'TABLE',,'=',_IMP.TABLE)
::    jest już wiersz na podstawie katalogów docelowych
   || _BUF.FILE_SRC:={? 3+_IMP.DIR='f00' || 'lokalny' || 'globalny' ?};
      _BUF.DIR_SRC:=_IMP.DIR;
      _BUF.EQUAL:={? _BUF.FILE_SRC=_BUF.FILE_DST || 'T' || 'N' ?};
      _BUF.put()

   || _BUF.blank();
      _BUF.TABLE:=_IMP.TABLE;
      _BUF.FILE_SRC:={? 3+_IMP.DIR='f00' || 'lokalny' || 'globalny' ?};
      _BUF.DIR_SRC:=_IMP.DIR;
      _BUF.EQUAL:='X';
      _BUF.add()
   ?};
   _loop:=_IMP.next()
!};
_IMP.cntx_pop();
_BUF.first();

:: po wczytaniu wyłącz akcję honorując dotychczasowe wyłączenie
_aid:='W(Z)%1W(Z)'[_BUF.actions_grayed(_BUF.win_sel('?'))];
_BUF.actions_grayed(_BUF.win_sel('?'),_aid);
~~


:Sign Version 2.0 jowisz:1048 2023/06/23 14:16:37 0b508af13c2412911e4e1e34242c134705141e86731a7685a821cf9a52aa8800bf6b3d8abfcfb88bb49166ad43496136165845ec9eff88c5744cf6713ed14c73db6212ccdaadddc72c82e204ea7015264b7296bd842964704fecde183aa24cfad0f29c2f7cbe58924190d375885c48872c8a08d7155708d7f8ece5a64ffe8ceb

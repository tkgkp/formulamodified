:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2137_11.fml
:: Utworzony: 26.04.2022
:: Autor: [DAROKR]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.14 na 21.37
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [DAROKR] [21.37]
:: OPIS: Główna formuła aktualizacji 21.37_11
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('stalesys_ini',,,'ZWS',,,,,'T');
__UPG.add_task('kst_lim_skzw',,,'PPL',,,,,'T');
__UPG.add_task('rubryki_placowe',,,'PPL',,,,,'T');
__UPG.add_task('edeklaracje_2137_11',,,'PPL',,,,,'T');
__UPG.add_task('rep_tran',,,'PPL',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('vat_poz_2137_11',,'N','PPL',,,,1,'T');
:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
~~


\edeklaracje_2137_11
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Dodanie nowych e-deklaracji obowiązujących w 2022 dotyczące przychodów od 01.01.2022
::----------------------------------------------------------------------------------------------------------------------
_dod_ver:="
   VAT_VER.index('VER_OD'); VAT_VER.prefix(_a,_c,_c,_b,_d);
   _jest:=VAT_VER.first();
   {? ~_jest
   || _old:={? _a='FKS' || 'FIKS' |? _a='PPL' || 'KALI' || '' ?};
      VAT_VER.prefix(_old,_c,_c,_b,_d);
      _jest:=VAT_VER.first()
   ?};
   {? _jest
   || _add:=0
   || _add:=1;
      VAT_VER.blank();
      VAT_VER.SYSTEM:=_a;
      VAT_VER.OD:=_b;
      VAT_VER.NAZWA:=_c;
      VAT_VER.NR:=_d
   ?};
   {? _<6 || _f:='' ?};
   {? _<5 || _e:=0 ?};
   _put:=0;
   {? VAT_VER.NRF<>_e || VAT_VER.NRF:=_e; _put+=1 ?};
   {? VAT_VER.WIERSZ<>_f || VAT_VER.WIERSZ:=_f; _put+=1 ?};
   VAT_VER.prefix();
   {? _add || VAT_VER.add(1) |? _put || VAT_VER.put(1) ?}
";
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
{? _dod_ver('PPL',date(2022,1,1),'PIT11',13,28,',1,4,5,6,9,10,12,')
|| __UPG.msg('Dodano 13 wersja e-deklaracji PIT-11 obowiązująca od dnia 2022.01.01 dla przychodów od 2022.01.01')
|| __UPG.msg('Brak aktualizacji, 13 wersja e-deklaracji PIT-11 już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-11 wersja 13
{? exec('edek_stru','edeklar','PIT11',13,'pit-11_28_v1.dfg')
|| __UPG.msg('Dodano schemat 13 wersji e-deklaracji PIT-11.')
|| __UPG.msg('Brak aktualizacji, schemat 13 wersji e-deklaracji PIT-11 już istnieje.')
?};
_wakt:=',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,';
{? _dod_ver('PPL',date(2022,1,1),'PIT8AR',13,12,_wakt)
|| __UPG.msg('Dodano 13 wersja e-deklaracji PIT-8AR obowiązująca od dnia 2022.1.1 dla przychodów od 2022.01.01')
|| __UPG.msg('Brak aktualizacji, 13 wersja e-deklaracji PIT-8AR już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-8AR wersja 13
{? exec('edek_stru','edeklar','PIT8AR',13,'pit-8ar13_v1.dfg')
|| __UPG.msg('Dodano schemat 13 wersji e-deklaracji PIT-8AR.')
|| __UPG.msg('Brak aktualizacji, schemat 13 wersji e-deklaracji PIT-8AR już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-4R wersja 12 ale 2 wersja schemy
{? exec('edek_stru','edeklar','PIT4R',12,'pit-4r12_v1-2.dfg',date(2022,4,15))
|| __UPG.msg('Dodano schemat 12 wersji 2 wersja schemy e-deklaracji PIT-4R.')
|| __UPG.msg('Brak aktualizacji, schemat 12 wersji 2 wersja schemy e-deklaracji PIT-4R już istnieje.')
?};
::import poprawnych struktur e-deklaracji IFT1 wersja 16 ale 2 wersja schemy
{? exec('edek_stru','edeklar','IFT1',16,'ift-1-16_v1-2.dfg',date(2022,1,1))
|| __UPG.msg('Dodano schemat 16 wersji 2 wersja schemy e-deklaracji IFT1.')
|| __UPG.msg('Brak aktualizacji, schemat 16 wersji 2 wersja schemy e-deklaracji IFT1 już istnieje.')
?};
::import poprawnych struktur e-deklaracji IFT1R wersja 16 ale 2 wersja schemy
{? exec('edek_stru','edeklar','IFT1R',16,'ift-1r-16_v1-2.dfg',date(2022,1,1))
|| __UPG.msg('Dodano schemat 16 wersji 2 wersja schemy e-deklaracji IFT1R.')
|| __UPG.msg('Brak aktualizacji, schemat 16 wersji 2 wersja schemy e-deklaracji IFT1R już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
1


\edeklaracje_2137_11_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Dodanie nowych e-deklaracji obowiązujących w 2022 dotyczące przychodów od 01.01.2022
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych e-deklaracji obowiązujących w 2022 dotyczące przychodów od 01.01.2022'


\rep_tran
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Kompilacja wydruków dziedziny PPL.
::----------------------------------------------------------------------------------------------------------------------
{? rep_tran('ppl*')<>0
|| __UPG.msg('Zaktualizowano wydruki dziedziny PPL.');
   1
|| __UPG.msg('Aktualizacja wydruków dziedziny PPL nie powiodła się.');
   -1
?}


\rep_tran_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Kompilacja wydruków dziedziny PPL - opis
::----------------------------------------------------------------------------------------------------------------------
'Kompilacja wydruków dziedziny PPL.'


\vat_poz_2137_11
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Uzupełnienie nowych pól w tabeli VAT_POZ - formuła naprawcza.
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_stat:=obj_new('poprawiono','niepoprawiono','niewymaga');
   {! _lp:=1..obj_len(_stat)
   |! _stat[_lp]:=0
   !};
params_set('stat',_stat);
VAT_POZ.trig_off('*','*');
VAT_POZ.clear();
VAT_POZ.for_each("_stat:=params_get();
  _put:=0;
  {? VAT_POZ.ZP152='' || VAT_POZ.ZP152:='N'; _put+=1 ?};
  {? VAT_POZ.ZP153='' || VAT_POZ.ZP153:='N'; _put+=1 ?};
  {? VAT_POZ.ZP154='' || VAT_POZ.ZP154:='N'; _put+=1 ?};
  {? _put
  || {? VAT_POZ.put()
     || _stat.stat.poprawiono+=1
     || _stat.stat.niepoprawiono+=1
     ?}
  || _stat.stat.niewymaga+=1
  ?}",1
);
VAT_POZ.trig_on('*','*');
__UPG.msg('Liczba wierszy wymagających poprawienia: %1.' [$(_stat.poprawiono+_stat.niepoprawiono)]);
__UPG.msg('Liczba wierszy poprawionych: %1.' [$_stat.poprawiono]);
__UPG.msg('Liczba wierszy, których nie udało się poprawić: %1.' [$_stat.niepoprawiono]);
__UPG.msg('Liczba wierszy niewymagających poprawienia: %1.' [$_stat.niewymaga]);
_ret:={? _stat.niepoprawiono>0 || -1 || 1 ?};
_ret


\vat_poz_2137_11_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Uzupełnienie nowych pól w tabeli VAT_POZ - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie nowych pól w tabeli VAT_POZ: VAT_POZ.ZP152, VAT_POZ.ZP153, VAT_POZ.ZP154 w aktualizacji Merit 2137_11.'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\kst_lim_skzw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Dodanie pola do KST związanego z Polskim Ładem (limit odliczenia od dochodu składek członkowskich)
::----------------------------------------------------------------------------------------------------------------------
:: Dodanie pola określającego limit odliczenia od dochodu składek członkowskich.
{? var_pres('LIM_SKZW',KST)>0
|| exec('add_acr','#stalesys','LIM_SKZW','Limit odliczenia od dochodu składek członkowskich','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.LIM_SKZW','0',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.LIM_SKZW','300.00',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o limicie odliczenia od dochodu składek członkowskich.')
   ?}
?};
1


\kst_lim_skzw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Opis dodania pola do KST związanego z Polskim Ładem (limit odliczenia od dochodu składek członkowskich)
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - limit odliczenia od dochodu składek członkowskich.'


\rubryki_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Dodanie atrybutów płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

:: Aktualizacja atrybutów rubryk.
::Zmiany PIT-2022
::Składki członkowskie na rzecz związków zawodowych pobrane przez płatnika, podlegające odliczeniu od dochodu,
::o których mowa w art. 26 ust. 1 pkt 2c ustawy
exec('add_attr','rubatr',100,1006,'Składki na związki zawodowe odliczane od dochodu',1,
  'Składki członkowskie na rzecz związków zawodowych pobrane przez płatnika, podlegające odliczeniu od dochodu, '+
  '\no których mowa w art. 26 ust. 1 pkt 2c ustawy wykazywane na PIT-11 od 2022 roku.');

__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\rubryki_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [21.37]
:: OPIS: Dodanie rubryk i atrybutów płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie atrybutów płacowych.'

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:25 74688e15208f42c4c6c2c4ecda49f5a64e304aef9e357c16458f005be920918d245429fbab149c3068b2f402da06d1a2d1ef3354143ce1a1afbc56feb5237d07e0823bf9afca4adaf0f8ff24ea28f5f93a2b77e981e1aa273a8cec74cd74cb5e24533180c6ce666293188defeb7270d9683711b3e6004e85e2bb56bb9a516683

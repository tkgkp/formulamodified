:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2042.fml
:: Utworzony: 09.04.2020
:: Autor: [TS]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 20.14 na 20.42
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [TS] [20.42]
:: OPIS: Główna formuła upgrade-ów 20.xx
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',3,,,,'T');
__UPG.add_task('color',,,'ZWS',3,,,,'T');
__UPG.add_task('funpar',,,'ZWS',1);
__UPG.add_task('stalesys_ini',,,'ZWS');
__UPG.add_task('id_sync',,,'ZWS',1);
__UPG.add_task('report',,,'ZWS',1,,,,'T');
__UPG.add_task('areatitle',,,'ZWS');
__UPG.add_task('USERS',,,'ZWS');
__UPG.add_task('B_ACTION',,,'ZPR');
__UPG.add_task('fiks_70',,,'FKS');
__UPG.add_task('fiks_71',,,'FKS');
__UPG.add_task('tat_w_portal',,,'ZWS');
__UPG.add_task('etypy_w_portal',,,'ZWS');
__UPG.add_task('todo_lang',,,'ZWS');
__UPG.add_task('slo_rodz',,,'PKD');
__UPG.add_task('POCZTA',,,'ZWS',2,,,,'T');
__UPG.add_task('napraw_walut',,,'FKS');
__UPG.add_task('PKALSYNC',,,'POR');
__UPG.add_task('rubryka_771_UBZ',,,'PPL');
__UPG.add_task('M_ATR',,,'ZWS');
__UPG.add_task('akt_attr',,,'PPL');
__UPG.add_task('nwu',,,'PKD');
__UPG.add_task('POCZTAM',,,'ZWS');
__UPG.add_task('pm_rodz',,,'PPL');
__UPG.add_task('pm_budz',,,'PPL');
__UPG.add_task('dokum_bl',,,'ZWS');
__UPG.add_task('USERSF',,,'ZWS');
__UPG.add_task('act_parform_val',,,'OBE');
__UPG.add_task('akt_atrybut',,,'PPL');
__UPG.add_task('users_osoba_unique',,,'ZWS',1);
__UPG.add_task('wnr_rh',,,'PPL');
__UPG.add_task('fst_del_par',,,'ZWS');
__UPG.add_task('obe_automatyczna',,,'ZWS');
__UPG.add_task('jpk_przyczyna_korekty',,,'FKS');
__UPG.add_task('plugins',,,'ZWS',4,,,,'T');
__UPG.add_task('dok_td_fix',,,'FKS',,,,,'T');
__UPG.add_task('add_ire',,,'FKS',,,,,'T');
__UPG.add_task('ob_czy_wycofanie',,,'ZWS');
__UPG.add_task('sch_imp',,,'OBE',,,,,'T');
__UPG.add_task('akt_2042_09',,,'FKS',,,,,'T');
__UPG.add_task('fiks_75',,,'FKS',,,,,'T');
__UPG.add_task('edeklaracje_2042_14',,,'PPL',,,,,'T');
__UPG.add_task('upd_poz_wal',,,'FKS',,,,,'T');
__UPG.add_task('akt_2042_11',,,'FKS',,,,,'T');
__UPG.add_task('term_plat',,,'LSP',,,,,'T');
__UPG.add_task('jpk_v7_stale',,,'FKS',,,,,'T');
__UPG.add_task('atrybuty',,,'PPL');
__UPG.add_task('akt_2042_20',,,'PKD',,,,,'T');
__UPG.add_task('akt_2042_19',,,'PPL',,,,,'T');
__UPG.add_task('fiks_73',,,'FKS',,,,,'T');
__UPG.add_task('m4prognoza',,,'CTR',,,,,'T');
__UPG.add_task('ppl_plistrokzak_akt',,,'PPL');
:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('FO_USR',,'N','ZWS',2);
__UPG.add_task('zpr_ins_dele',,'N','ZPR',,,,0);
__UPG.add_task('ZTP',,'N','TTE',,,,1);
__UPG.add_task('ZL',,'N','TTE',,,,1);
__UPG.add_task('REZ',,'N','LMG',,,,1);
__UPG.add_task('actKoszt',,'N','LZK',,,,1);
__UPG.add_task('KARO',,'N','WYP',,,,1);
__UPG.add_task('f_zatra_add',,'N','ZWS',,,,1);
__UPG.add_task('role_personel',,'N','ZWS',,,,1);
__UPG.add_task('actTRANSP',,'N','LTR',,,,2);
__UPG.add_task('porslo',,'N','POR',,,,1);
__UPG.add_task('dokump_rodz',,'N','ZWS',,,,0);
__UPG.add_task('margines_we_wy',,'N','PRC',,,,0);
__UPG.add_task('TMAT',,'N','TTE',,,,1);
__UPG.add_task('TCHMAT',,'N','TTE',,,,1);
__UPG.add_task('dokum_k_nad',,'N','ZWS',,,,0);
__UPG.add_task('kparam',,'N','ZWS',,,,0);
__UPG.add_task('DK_C',,'N','LMG',,,,0);
__UPG.add_task('status_pracownika',,'N','PPL',,,,1);
__UPG.add_task('dk_mar_nar',,'N','LMG',,,,0);
__UPG.add_task('TKTL',,'N','TTE',,,,1);
__UPG.add_task('typysp_bl',,'N','ZWS',,,,1);
__UPG.add_task('FAKS_KZF',,'N','LSP',,,,1);
__UPG.add_task('zws_par_bl',,'N','ZPR',,,,0);
__UPG.add_task('zui_cli_ager',,'N','ZPR',,,,0);
__UPG.add_task('fo_700014',,'N','ZPR',,,,1,'T');
__UPG.add_task('dok_jpk_upgr',,'N','FKS',,,,1,'T');
__UPG.add_task('KTRWP',,'N','CTR',,,,1,'T');
__UPG.add_task('ustal_sciezka',,'N','ZWS',1);
:: Zadania manualne
__UPG.add_task('formuly_placowe','N','N','PPL',,,,1);
__UPG.add_task('ZS_DEF','N','N','PKD');
__UPG.add_task('PARAMS','N','T','ZWS');
__UPG.add_task('ppk_edi','N',,'PPK',1,,,1);
__UPG.add_task('oddelegowanie_edi','N',,'PKD',1,,,1);
__UPG.add_task('szablony','N',,'ZWS',2,,,1,'T');
__UPG.add_task('fiks_73i74',,,'FKS');
__UPG.add_task('abs_upd','N',,'ZWS');
__UPG.add_task('obe_fix_stat','N',,'OBE',2);
__UPG.add_task('rozliczenie_godzinowe','N','N','PRC',,,,1,'T');
__UPG.add_task('inwentaryzacja_lt','N','N','FST',,,,1,'T');
__UPG.add_task('formuly_IZOLACJA','N','N','PPL',1,,,1,'T');
__UPG.add_task('formuly_784','N','N','PPL',1,,,1,'T');
~~


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',1)
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.')
?}


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [20.14]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.OBE','.');
::_dom:=spli_str('LSP.LZK.PKD.PPL.TRE.PBA.LTR.TTE.ZWS.PED.LMG.LUM.PRC.HBN.OBE','.');
_dom:=spli_str('ZWS.ZPR.ZUI.POR.TRE.LTR.PKD.PPK.PPL.OBE.OBG.TTE.KKH.ZBL','.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [20.14]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\id_sync
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów
::----------------------------------------------------------------------------------------------------------------------
_init:=exec('init','sync_id');
_result:=_init.RESULT;
_args:=_init.ARGS;
{? _result
|| __UPG.msg('Zaktualizowano definicje identyfikatorów rekordów.')
|| __UPG.msg('Wystąpił problem podczas aktualizacji definicji identyfikorów rekordów %1 - %2 do systemu %3.'
      [_args.KOD,_args.OPIS,_args.SYSTEM])
?};
_result


\id_sync_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie definicji identyfikatorów rekordów:\n'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\USERS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizacja użytkowników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
USERS.cntx_psh();
USERS.prefix();
USERS.for_each("
   __lrec+=1;
   {? USERS.PORTAL2=''
   || USERS.PORTAL2:='N';
      {? USERS.put(1) || __lmod+=1 ?}
   ?}",
   1
);
USERS.cntx_pop();
__UPG.msg('Zaktualizowano użytkowników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\USERS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizacja użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja użytkowników'


\ZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.42]
:: OPIS: Aktualizacja zleceń
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZL.cntx_psh();
ZL.prefix();
ZL.for_each("
   __lrec+=1;
   _put:=0;
   {? ZL.RP=''
   || {? exec('is_main_podzlec','zl_link')>0
      || ZL.RP:='T'
      || ZL.RP:='N'
      ?};
       _put:=1
   ?};
   {? ZL.RP_REZ=''
   || ZL.RP_REZ:='N';
      _put:=1
   ?};
   {? _put>0
   || ZL.put();
      __lmod+=1
   ?};
   ~~
   ",
   1
);
ZL.cntx_pop();
__UPG.msg('Zaktualizowano zlecenia produkcyjne.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\ZL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.42]
:: OPIS: Aktualizacja zleceń - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zleceń produkcyjnych'


\FO_USR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.42]
:: OPIS: Aktualizacja parametrów FO użytkowników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_res:=exec('upgrade_usr','#params');

__UPG.msg('Zaktualizowano parametry użytkowników');
VAR_DEL.delete('__lrec','__lmod');
_res


\FO_USR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.42]
:: OPIS: Aktualizacja parametrów FO użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametów użytkowników'


\B_ACTION
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizacja czynności
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
B_ACTION.cntx_psh();
B_ACTION.prefix();
B_ACTION.for_each("
   __lrec+=1;
   {? B_ACTION.PORTAL=''
   || {? B_ACTION.memo_txt(,1,'PORT_OPR')=''
      || B_ACTION.PORTAL:='N'
      || B_ACTION.PORTAL:='T'
      ?};
      {? B_ACTION.put(1) || __lmod+=1 ?}
   ?}",
   1
);
B_ACTION.cntx_pop();
__UPG.msg('Zaktualizowano czynności.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\B_ACTION_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizacja czynności - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktulizacja czynności'


\zpr_ins_dele
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Formuła dodaje czynność ZPR_INS_DELE do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZPR_INS_ARCH'";
_uid:='ZPR_INS_DELE';

_ROLE:=sql(
   'select distinct B_ROLE.NAME '
   'from B_ACTROL join B_ACTION using (B_ACTROL.B_ACTION,B_ACTION.REFERENCE) '
                 'join B_ROLE using (B_ACTROL.B_ROLE,B_ROLE.REFERENCE) '
   'where B_ACTROL.FIRMA=:_a and B_ACTION.UID in (:_b) '
   'order by 1',
   exec('ref_firma','ustawienia'),_cr
);

{? type_of(_ROLE)=type_of(~~)
|| __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
   return(0)
?};

{? _ROLE.first()
|| _ret:=1;
   {!
   |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_ROLE.NAME])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_ROLE.NAME]);
         _ret:=-1
      ?};
      _ROLE.next()
   !};
   _ret
|| __UPG.msg('W firmie %1 brak ról z uprawnieniami do czynności %2.'[REF.FIRMA().SYMBOL,_cr]);
   1
?}


\zpr_ins_dele_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Formuła dodaje czynność ZPR_INS_DELE do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZPR_INS_DELE - Usuwanie instancji procesu" do odpowiednich ról'


\fiks_70
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2003/0060
::       Błąd przy jpk-sf 1.2 dla jednostki małej w  tys
::  OLD: \fiks_70/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_fix:=-1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','SF','JednostkaMalaWTysiacach(1)_v1-2',);
{? ISTDEF.first()
|| _fix:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'jma:F_I_1',);
   {? ISTDEFS.first()
   || _start:=_next:=0;
      ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.TREE,);
      {? ISTDEFS.next()
      || _next:=ISTDEFS.LP
      ?};
      ISTDEFS.cntx_pop();
      ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),#ISTDEFS.ref());
      {? ISTDEFS.first()
      || {!
         |? {? ISTDEFS.OPIS='jma:PozycjaUszczegolawiajaca_1'
            || ISTDEFS.OPIS:='jma:PozycjaUszczegolawiajaca';
               ISTDEFS.put()
            |? ISTDEFS.OPIS='jma:G_I_A_1'
            || _start:=ISTDEFS.LP
            ?};
            _start=0 & ISTDEFS.next()
         !}
      ?};
      ISTDEFS.cntx_pop();
      {? _next & _start
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('LP'); ISTDEFS.prefix(ISTDEF.ref());
         {! _ii:=_next-1//-1.._start
         |! {? ISTDEFS.find_key(_ii)
            || ISTDEFS.del()
            ?}
         !};
         {? ISTDEFS.find_key(_next)
         || {!
            |? ISTDEFS.LP:=_start;
               ISTDEFS.put();
               _start+=1;
               ISTDEFS.next()
            !}
         ?};
         ISTDEFS.cntx_pop();
         _fix:=1
      ?}
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
{? _fix=1
|| __UPG.msg('Poprawiono błąd w JPK_SF 1.2 dla jednostki małej w tysiącach')
|? _fix=0
|| __UPG.msg('Poprawka była już zastosowana wcześniej.')
|| __UPG.msg('Brak JPK_SF 1.2.\nPoprawka nie została naniesiona.')
?};
1


\fiks_70_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Poprawka błędu: ER/WRT/XP/12.51/2003/0060 - opis
::       Błąd przy jpk-sf 1.2 dla jednostki małej w  tys
::----------------------------------------------------------------------------------------------------------------------
'Poprawka: ER/WRT/XP/12.51/2003/0060\n'
'Błąd w JPK_SF 1.2 dla jednostki małej w tysiącach'


\tat_w_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Uzupełnia pole W_PORTAL dla tabeli typów atrybutu. Nowe pole oznacza, czy typ może być użyty na portalu HR.
::----------------------------------------------------------------------------------------------------------------------
TAT.cntx_psh();
TAT.prefix();
TAT.for_each("{? TAT.W_PORTAL='' || TAT.W_PORTAL:='N'; TAT.put() ?}");
TAT.cntx_pop();
__UPG.msg('Uzupełniono pole W_PORTAL dla tabeli typów atrybuty.');
1


\tat_w_portal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Uzupełnia pole W_PORTAL dla tabeli typów atrybutu. Nowe pole oznacza, czy typ może być użyty na portalu HR.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pole W_PORTAL dla tabeli typów atrybutu. Nowe pole oznacza, czy typ może być użyty na portalu HR.'


\etypy_w_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Uzupełnia pole W_PORTAL dla tabeli typów dokumentów w obiegu.
::       Nowe pole oznacza, czy typ może być użyty na portalu HR.
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
ETYPY.index('UNIK');
ETYPY.prefix();
ETYPY.for_each("
   {? ETYPY.W_PORTAL='' || ETYPY.W_PORTAL:='N'; ETYPY.put() ?}
");
ETYPY.cntx_pop();
__UPG.msg('Uzupełniono pole W_PORTAL dla tabeli typów dokumentów w obiegu.');
1


\etypy_w_portal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Uzupełnia pole W_PORTAL dla tabeli typów dokumentów w obiegu.
::       Nowe pole oznacza, czy typ może być użyty na portalu HR.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pole W_PORTAL dla tabeli typów dokumentów w obiegu. '
'Nowe pole oznacza, czy typ może być użyty na portalu HR.'


\fiks_71
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2002/0048
::       Nieprawidłowo wyliczana kwota VAT split payment
::  OLD: \fiks_71/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Fix');
Fix:=0;
_start:=date(2019,1,1);
_ar:=SSTALE.AR;
_ao:=SSTALE.AO;
ROK_F.cntx_psh();
ROK_F.index('ROKPOCZ');
ROK_F.prefix();
{? ROK_F.first()
|| OKRO_F.cntx_psh();
   OKRO_F.index('ROK');
   {!
   |? {? ROK_F.ZAM<>'T' & ROK_F.POCZ_ROK>=_start
      || SSTALE.AR:=ROK_F.ref();
         OKRO_F.prefix(SSTALE.AR);
         {? OKRO_F.first()
         || {!
            |? SSTALE.AO:=OKRO_F.ref();
               exec('open_tabele','open_tab');
               echo('Naprawa dokumentów - Firma: '+ROK_F.FIRMA().SYMBOL+', rok: '+ROK_F.NAZ+', okres: '+OKRO_F.NAZ);
               DOK.prefix();
               _sql:=sql(
                  'select DOK.REFERENCE as ref, count(*) as size '+
                  'from DOK join VPOZ where DOK.A=\'T\' '+
                  'group by DOK.REFERENCE, DOK.NK '+
                  'HAVING count(*)>1 '+
                  'order by 1'
               );
               {? _sql.first()
               || {!
                  |? {? DOK.seek(BIT.sqlint(_sql.REF),)
                     || exec('upd_dok','fks_sp',"Fix+=1; exec('update_ks1','fks_sp')")
                     ?};
                     _sql.next()
                  !}
               ?};
               &_sql;
               OKRO_F.next()
            !}
         ?}
      ?};
      ROK_F.next()
   !};
   OKRO_F.cntx_pop()
?};
ROK_F.cntx_pop();
{? SSTALE.AO<>_ao | SSTALE.AR<>_ar
|| SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   exec('open_tabele','open_tab')
?};
echo();
{? Fix=0
|| __UPG.msg('Nie znaleziono danych do naprawienia.')
|| __UPG.msg('Liczba naprawionych pozycji dokumentów księgowych: '+$Fix)
?};
VAR_DEL.delete('Fix');
1


\fiks_71_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2002/0048 - opis
::       Nieprawidłowo wyliczana kwota VAT split payment
::----------------------------------------------------------------------------------------------------------------------
'Poprawka: ER/WRT/XP/12.51/2002/0048\n'
'Nieprawidłowo wyliczanej kwoty VAT split payment'


\actKoszt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja kosztów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__lre1','__lmo1','__lre2','__lmo2');
__lrec:=__lmod:=__lre1:=__lmo1:=__lre2:=__lmo2:=0;

_msk:=FAKS_K.names();
_msk.clear();
{? _msk.first()
|| exec('fak_psh','open_tab');
   ND.cntx_psh();
   DK.cntx_psh();
   {!
   |? _oddz:=1+(_msk.NAME+3);
      _rok:=(_msk.NAME+3)+2;
      exec('fak_open','open_tab',_oddz,_rok);
      FAKS_K.prefix();
      FAKS_K.for_each("_put:=0;
                       __lrec+=1;
                       {? FAKS_K.SC=''
                       || FAKS_K.SC:='N';
                          _put:=1
                       ?};
                       {? FAKS_K.D=date(0,0,0)
                       || FAKS_K.D:=FAKS_K.FAKS().DW;
                          _put:=1
                       ?};
                       {? _put || {? FAKS_K.put(1) || __lmod+=1 ?} ?}",1);
      FAP_K.prefix();
      FAP_K.for_each("_put:=0;
                      __lre1+=1;
                      {? FAP_K.KK=null() & FAP_K.FAKS_K().KK<>null()
                      || FAP_K.KK:=FAP_K.FAKS_K().KK;
                         _put:=1
                      ?};
                      {? FAP_K.D=date(0,0,0)
                      || FAP_K.D:=FAP_K.FAKS_K().D;
                         _put:=1
                      ?};
                      {? FAP_K.DKSQL<>'' & FAP_K.IDND=''
                      || _mask:=form(8+FAP_K.DKSQL)+3;
                         ND.use('nagdo'+_mask);
                         DK.use('dokma'+_mask);
                         FAP_K.IDND:=exec('FindAndGet','#table',DK,FAP_K.DKSQL,,\"N().IDADD\",'');
                         _put:=1
                      ?};
                      {? _put || {? FAP_K.put(1) || __lmo1+=1 ?} ?}",1);
      _msk.next()
   !};
   exec('fak_pop','open_tab');
   ND.cntx_pop();
   DK.cntx_pop()
?};
obj_del(_msk);
__UPG.msg('Zaktualizowano koszty dla zakupów.');
__UPG.msg('Nagłówki - przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
__UPG.msg('Pozycje - przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lre1,$__lmo1]);

_msk:=N_K.names();
_msk.clear();
{? _msk.first()
|| exec('mag_psh','open_tab');
   exec('fak_psh','open_tab');
   do();
   {!
   |? _oddz:=1+(_msk.NAME+3);
      _rok:=(_msk.NAME+3)+2;
      exec('mag_open','open_tab',_oddz,_rok);
      exec('fak_open','open_tab',_oddz,_rok);

      N_K.prefix();
      {? N_K.first()
      || {!
         |? __lre2+=1;
            FAKS_K.index('OLD');
            {? ~FAKS_K.find_key(N_K.IDADD)
            || FAKS_K.blank();
               FAKS_K.FAKS:=null();
               FAKS_K.OPIS:=N_K.OP;
               FAKS_K.SV:=null();
               FAKS_K.WN:=0;
               FAKS_K.WV:=0;
               FAKS_K.WB:=N_K.WB;
               FAKS_K.WAL:=INFO.NAROD;
               FAKS_K.WW:=0;
               FAKS_K.KRS:=0;
               FAKS_K.FAP:='';
               FAKS_K.KK:=N_K.KK;
               FAKS_K.KON:='';
               FAKS_K.ROZL:=N_K.ROZL;
               FAKS_K.ROZLICZ:={? N_K.SC='T' || 'S' || 'N' ?};
               FAKS_K.IDND:=N_K.N().IDADD;
               FAKS_K.IDTR:='';
               FAKS_K.INMG:=1;
               FAKS_K.OLD:=N_K.IDADD;
               FAKS_K.SC:=N_K.SC;
               {? FAKS_K.add(1)
               || __lmo2+=1;
                  _ref:=FAKS_K.ref();
                  DK_K.index('N_K');
                  DK_K.prefix(N_K.ref());
                  {? DK_K.first()
                  || {!
                     |? FAP_K.blank();
                        FAP_K.FAP:=null();
                        FAP_K.FAKS_K:=_ref;
                        FAP_K.WB:=DK_K.WP;
                        FAP_K.DKSQL:=$DK_K.DK;
                        FAP_K.WDK:=0;
                        FAP_K.IDND:=FAP_K.FAKS_K().IDND;
                        FAP_K.CK:=0;
                        FAP_K.KK:=DK_K.KK;
                        FAP_K.D:=DK_K.D;
                        FAP_K.WYC:=0;
                        FAP_K.IL_R:=DK_K.IL_R;
                        FAP_K.W_R:=DK_K.W_R;
                        FAP_K.DK_KSQL:=DK_K.DK_KSQL;
                        {? ~FAP_K.add(1) || _res:=0; undo() ?};
                        DK_K.next()
                     !}
                  ?}
               || _res:=0; undo()
               ?}
            ?};
            _res & N_K.next()
         !}
      ?};
      _res & _msk.next()
   !};
   end();
   exec('mag_pop','open_tab');
   exec('fak_pop','open_tab')
?};
obj_del(_msk);
__UPG.msg('Zaktualizowano koszty dla dokumentów magazynowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lre2,$__lmo2]);
VAR_DEL.delete('__lrec','__lmod','__lre1','__lmo1','__lre2','__lmo2');
_res


\actKoszt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [20.42]
:: OPIS: Aktualizacja kosztów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktulizacja rozliczenia kosztów dla zakupów i magazynu'


\KARO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizacja dokumentów obrotu wyposażeniem i dostaw pierwotnych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
KARO.trig_off('*','*');
DK.trig_off('*','*');
KARO.cntx_psh();
KARO.prefix();
KARO.for_each("
      __lrec+=1;
      _formula:=\"
         exec('FindAndGet','#table',DK,DK.PRDK,,\"\"{? DK.WYP<>1 || DK.WYP:=1; DK.put() ?}\"\",~~);
         DK.PRDK
      \";
      _prdk:=exec('FindAndGet','#table',DK,KARO.SRDK,,_formula,'');
      {? KARO.PRDK='' & KARO.SRDK<>''
      || KARO.PRDK:=_prdk;
         {? KARO.put(1) || __lmod+=1 ?}
      ?}
   ",
   1
);
KARO.cntx_pop();
KARO.trig_on('*','*');
DK.trig_on('*','*');
__UPG.msg('Zaktualizowano dokumenty obrotu wyposażeniem.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\KARO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizacja dokumentów obrotu wyposażeniem i dostaw pierwotnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dokumentów obrotu wyposażeniem'


\todo_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Aktualizacja tłumaczeń list todo
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
{? ~BI_PREL.lock(2)
||
   __UPG.msg('Tabela BI_PREL niedostępna.');
   return(0)
?};
B_PREL.cntx_psh();
B_ACTION.cntx_psh();
:: korekta złych odwołań
B_ACTION.prefix();
B_ACTION.for_each("{? (7+B_ACTION.FMTODO)='LMG_MOB'
                   || B_ACTION.FMTODO:='LMO_MOB'+(7-B_ACTION.FMTODO);
                      B_ACTION.put(1)
                   ?}",0);
BI_PREL.trig_off('*','*');
BI_PREL.cntx_psh();
BI_PREL.use('bi_e____');
BI_PREL.prefix();
__res:=1;
BI_PREL.for_each("
   __lrec+=1;
   {? BI_PREL.TM_DUPD=0 & BI_PREL.B_PREL().CLASS='B_ACTION'
   ||
      BI_PREL.DESCTODO:='T';
      {? BI_PREL.put(1) || __lmod+=1 ?};
      exec('desc','#bi_todo',BI_PREL.ref())
   ?}",
   1
);
_res:=__res;
BI_PREL.cntx_pop();
BI_PREL.trig_on('*','*');
B_PREL.cntx_pop();
B_ACTION.cntx_pop();
BI_PREL.unlock();
__UPG.msg('Zaktualizowano tłumaczenia list TODO.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__res');
_res


\todo_lang_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Aktualizacja tłumaczeń list todo - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tłumaczeń list TODO'


\slo_rodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Aktualizacja wartości nowych pól w tabeli SLO_RODZ: LP, SYSTEM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_NAG:=sql('select distinct SLO_RODZ.SLO_KOD from SLO_RODZ order by 1');
{? type_of(_NAG)=type_of(~~)
|| __UPG.msg('Błąd wewnętrzny.')
|? _NAG.first()
|| _kod:=exec('kod','ext_slo','P_DODINF','KONTAKT');
   SLO_RODZ.cntx_psh();
   SLO_RODZ.trig_off('*','*');
   SLO_RODZ.prefix();
   _lzmian:=0;
   _ok:=1;
   {!
   |? SLO_RODZ.f_set('LP,KOD',,'"SLO_RODZ"."SLO_KOD"=\':_a\'',_NAG.SLO_KOD);
      do();
      {? SLO_RODZ.f_first()
      || SLO_RODZ.SLO_KOD();
         _lp:=1;
         {!
         |? _put:=0;
            {? SLO_RODZ.LP<>_lp
            || SLO_RODZ.LP:=_lp;
               _put+=1
            ?};
            {? SLO_RODZ.SYSTEM=''
            || SLO_RODZ.SYSTEM:={? SLO_RODZ.SLO_KOD=_kod & ',WEW,KOM,E_MAIL,'*',%1,' [SLO_RODZ.KOD] || 'T' || 'N' ?};
               _put+=1
            ?};
            {? _put
            || _lzmian+=SLO_RODZ.put()
            ?};
            _lp+=1;
            SLO_RODZ.f_next()
         !}
      ?};
      {? ~end()
      || _ok:=0
      ?};
      _NAG.next()
   !};
   SLO_RODZ.f_clear();
   SLO_RODZ.trig_on('*','*');
   SLO_RODZ.cntx_pop();

   {? ~_ok
   || __UPG.msg('Aktualizacja wartości pól SLO_RODZ.LP i SLO_RODZ.SYSTEM nie powiodła się.');
      _ret:=-1
   |? _lzmian
   || __UPG.msg('Aktualizaca wartości pól SLO_RODZ.LP i SLO_RODZ.SYSTEM zakończyła się skucesem.');
      _ret:=1
   || __UPG.msg('Aktualizaca wartości pól SLO_RODZ.LP i SLO_RODZ.SYSTEM była przeprowadzona wcześniej.');
      _ret:=1
   ?}
|| __UPG.msg('Brak danych w tabeli SLO_RODZ - aktualizacja nie jest wymagana.');
   _ret:=1
?};
_ret


\slo_rodz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Aktualizacja wartości nowych pól w tabeli SLO_RODZ: LP, SYSTEM - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wartości nowych pól w tabeli SLO_RODZ: LP, SYSTEM.'


\ZTP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja typów zleceń
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZTP.cntx_psh();
ZTP.prefix();
ZTP.for_each("
   __lrec+=1;
   _put:=0;
   {? ZTP.RP_REZ=''
   || ZTP.RP_REZ:='N';
       _put:=1
   ?};
   {? _put>0
   || ZTP.put();
      __lmod+=1
   ?};
   ~~
   ",
   1
);
ZTP.cntx_pop();
__UPG.msg('Zaktualizowano typy zleceń produkcyjnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\ZTP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja typów zleceń - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów zleceń produkcyjnych'


\REZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja rezerwacji
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
REZ.cntx_psh();
_msk:=REZ.names();
_msk.prefix();
{? _msk.first()
|| {!
   |? REZ.use(_msk.NAME);
      REZ.prefix();
      REZ.for_each("__lrec+=1; {? REZ.RP_REZ='' || REZ.RP_REZ:='N'; {? REZ.put(1) || __lmod+=1 ?} ?}",0);
      _msk.next()
   !}
?};
REZ.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano rezerwacje w magazynie.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\REZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja rezerwacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rezerwacji w magazynie'


\f_zatra_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Lista czynności, dla których przypisane zostaną formy współpracy. Założenia:
:: 1. Czynności są oddzielone przecinkami.
:: 2. W ramach czynności przekazywana jest również informacja o kodach form współpracy (oddzielonych znakiem dwukropka).
::    Zakładamy, że duże litery dotyczą form docelowych.
_lista:=spli_str(
   'PKD_EZK_OPDI:Z:R,'
   'PKD_EZK_ORDI:Z:R,'
   'POR_BNF_RPRC:P:Z:K,'
   'POR_BNF_PPRC:P:Z:K,'
   'PKD_EZK_OPSP:P,'
   'PKD_EZK_OPLN:P:Z:K,'
   'PKD_EZK_ORLN:P:Z:K,'
   'PKD_EZK_DEKU:P:Z:K',
   ','
);

_ret:=1;
B_ACTION.cntx_psh();
B_ACTION.f_clear();
B_ACTION.index('UNIK');
B_ACTION.prefix();
_lenl:=obj_len(_lista);
{! _lp:=1 .. _lenl
|! _acz:=spli_str(_lista[_lp],':');
   _uid:=_acz[1];
   {? B_ACTION.find_key(_uid,)
   || _lena:=obj_len(_acz);
      {! _lf:=2 .. _lena
      |! _fz:=_acz[_lf];
         __UPG.msg('Forma współpracy: %1 | Czynność: %2 - %3' [_fz,_uid,B_ACTION.NAME]);
         _add:=exec('f_zatra_add','f_zatr',{? -_fz=_fz || 'N' || 'T' ?},_fz,_uid);
         {? _add=''
         || __UPG.msg(' - uprawnienia nadane')
         || __UPG.msg(_add);
            _ret:=0
         ?}
      !}
   || __UPG.msg('"%1" - nieprawidłowy kod czynności.' [_uid]);
      _ret:=0
   ?};
   obj_del(_acz)
!};
B_ACTION.cntx_pop();
_ret


\f_zatra_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Nadanie czynnościom uprawnień do form współpracy'


\role_personel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi.
::----------------------------------------------------------------------------------------------------------------------
{? exec('roleActions','upgrade',
      'Dyrektor HR,Przeglądający Kadry,Przeglądający Płace',
      'POR_BNF_PPRC,PKD_EZK_OPSP'
   ) &
   exec('roleActions','upgrade',
      'Dyrektor HR',
      'PPL_GRP_PMBD'
   ) &
   exec('roleActions','upgrade',
      'Dyrektor HR,Przeglądający Kadry',
      'PKD_EZK_OPLN'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr,Specjalista ds. płac,Kierownik ds płac',
      'POR_BNF_RPRC,PKD_EZK_OPSP,ZWS_PAR_PBKN'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr',
      'PKD_EZK_OPLN,PKD_EZK_ORLN'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr,Specjalista ds. płac',
      'PKD_EZK_DEKU'
   )
|| 1
|| -1
?}


\role_personel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ról/uprawnień związana z czynnościami personelowymi.'


\actTRANSP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja danych związanych z transportem
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__tabid');
__tabid:=tab_tmp(1,'IDADD','STRING[31]','','UID','STRING[48]','','TAB','STRING[8]','');

__lrec:=__lmod:=0;
ZK_N.cntx_psh();
_msk:=ZK_N.names();
_msk.prefix();
{? _msk.first()
|| {!
   |? ZK_N.use(_msk.NAME);
      ZK_N.prefix();
      ZK_N.for_each("{? ZK_N.TRN='T' & ZK_N.KH<>null()
                     || __tabid.prefix();
                        __tabid.blank();
                        __tabid.IDADD:=ZK_N.IDADD;
                        __tabid.UID:=ZK_N.uidref();
                        __tabid.TAB:='ZK_N';
                        __tabid.add(1)
                     ?}",0);
      _msk.next()
   !}
?};
ZK_N.cntx_pop();
obj_del(_msk);

TR_NZL.cntx_psh();
_msk:=TR_NZL.names();
_msk.prefix();
{? _msk.first()
|| {!
   |? TR_NZL.use(_msk.NAME);
      TR_NZL.prefix();
      TR_NZL.for_each("__lrec+=1;
                       {? TR_NZL.ORDER=''
                       || TR_NZL.ORDER:=exec('code_order','transport_paczki',TR_NZL.IDADD,TR_NZL.ref(),TR_NZL.ODDZ);
                          {? TR_NZL.put(1) || __lmod+=1 ?}
                       ?};
                       {? TR_NZL.RODZ='R' & TR_NZL.KH<>null()
                       || __tabid.prefix();
                          __tabid.blank();
                          __tabid.IDADD:=TR_NZL.IDADD;
                          __tabid.UID:=TR_NZL.uidref();
                          __tabid.TAB:='TR_NZL';
                          __tabid.add(1)
                       ?}",0);
      _msk.next()
   !}
?};
TR_NZL.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano dyspozycje transportu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

__lrec:=__lmod:=0;
TR_ZEWN.cntx_psh();
TR_ZEWN.prefix();
TR_ZEWN.for_each("__lrec+=1;
                  {? TR_ZEWN.API=''
                  || TR_ZEWN.API:='N';
                     {? TR_ZEWN.put(1) || __lmod+=1 ?}
                  ?}",0);
TR_ZEWN.cntx_pop();
__UPG.msg('Zaktualizowano rodzaje dostawy.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

__lrec:=__lmod:=0;
__tabid.clear();
{? __tabid.first()
|| {!
   |? __lrec+=1;
      {? __tabid.TAB='ZK_N'
      || {? exec('FindAndGet','#table',ZK_N,__tabid.UID,,"exec('addTR_HKH','transport_wspolne',ZK_N,0)",1)
         || __lmod+=1
         ?}
      |? __tabid.TAB='TR_NZL'
      || {? exec('FindAndGet','#table',TR_NZL,__tabid.UID,,"exec('addTR_HKH','transport_wspolne',TR_NZL,0)",1)
         || __lmod+=1
         ?}
      ?};
      __tabid.next()
   !}
?};
__UPG.msg('Zaktualizowano podpowiedzi transportu dla kontrahentów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

VAR_DEL.delete('__lrec','__lmod','__tabid');
_res


\actTRANSP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja danych związanych z transportem - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych związanych z transportem'


\POCZTA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Uzupełnia pola w tabeli POCZTA
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');

__template_done:=0;
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   _put:=0;
   {? +POCZTA.SOURCE=16
   || POCZTA.SOURCE:=exec('FindAndGet','#table',ref_tab(POCZTA.SOURCE),POCZTA.SOURCE,,\"uidref()\",POCZTA.SOURCE);
      _put:=1
   ?};
   {? __template_done=0 & POCZTA.TEMPLATE=''
   || POCZTA.TEMPLATE:='template_old.htm';
      _put:=1
   ?};
   {? _put || {? POCZTA.put(1) || __lmod+=1 ?} ?}
";

POCZTA.cntx_psh();
POCZTA.prefix();
{? POCZTA.first()
|| {!
   |? {? POCZTA.TEMPLATE='template_old.htm' || __template_done:=1 ?};
      __template_done=0 & POCZTA.next()
   !}
?};
_result:=POCZTA.for_each(_formula,1);
POCZTA.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano pola w kartotece wiadomości e-mail.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji kartoteki wiadomości e-mail.')
?};

VAR_DEL.delete('__lrec','__lmod','__template_done');
_result


\POCZTA_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Uzupełnia pola w tabeli POCZTA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki wiadomości e-mail.'


\POCZTAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.42]
:: OPIS: Uzupełnia pola w tabeli POCZTAM
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   _put:=0;
   {? POCZTAM.RODZ=''
   || POCZTAM.RODZ:='P';
      _put:=1
   ?};
   {? _put || {? POCZTAM.put(1) || __lmod+=1 ?} ?}
";
_result:=POCZTAM.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano pola w kartotece serwerów poczty poczty.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji kartoteki serwerów poczty.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\POCZTAM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.42]
:: OPIS: Uzupełnia pola w tabeli POCZTAM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki serwerów poczty.'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\porslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Dodanie słowników wykorzystywanych na portalu
::----------------------------------------------------------------------------------------------------------------------
::exec('init','portal_slowniki');
::__UPG.msg('Dodano słowniki portalowe.');
1


\parslo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Dodanie słowników wykorzystywanych na portalu - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie słowników wykorzystywanych na portalu.'


\PKALSYNC
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Dodanie zapisów do synchronizacji kalendarzy pracowników z HR Portal
::----------------------------------------------------------------------------------------------------------------------
::P.cntx_psh();
::P.use('pracowni');
::P.index('PRACONAZ');
::P.prefix();
::P.for_each("exec('update4P','pkalsync')",1);
::P.cntx_pop();
::__UPG.msg('Dodano zapisy do synchronizacji kalendarzy pracowników z HR Portal.');
1


\PKALSYNC_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Dodanie słowników wykorzystywanych na portalu - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie zapisów do synchronizacji kalendarzy pracowników z HR Portal.'


\napraw_walut
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MP] [20.42]
:: OPIS: Naprawa wartości wn i ma dla tabeli POZ dla rekordów walutowych
::----------------------------------------------------------------------------------------------------------------------
_poz:=POZ.names();
_r1:=0;
{? _poz.first()
|| POZ.cntx_psh();
   DOK.cntx_psh();
   {!
   |? POZ.use(_poz.NAME); POZ.prefix();
      DOK.use('doku'+(_poz.NAME+4)); DOK.prefix();
      POZ.for_each("{? POZ.SUMW$2<>0 & POZ.MA_WAL$2=0 & POZ.WN_WAL$2=0 || _r1:=1; POZ.put() ?} ");
      _poz.next()
   !};
   DOK.cntx_pop();
   POZ.cntx_pop()
?};
{? _r1=1
|| __UPG.msg('Zaktualizowano tabelę POZ')
|| __UPG.msg('Nie udało się zaktualizować tabeli POZ')
?};
1


\napraw_walut_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MP] [20.42]
:: OPIS: Naprawa wartości wn i ma dla tabeli POZ dla rekordów walutowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Naprawa wartości wn i ma dla tabeli POZ dla rekordów walutowych'


\dokump_rodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Wypełnienie pola RODZ w tabeli DOKUMP - powiadomienia o kontaktach
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;
DOKUMP.index('DISP');
{? DOKUMP.first()
|| {!
   |? __lrec+=1;
      {? DOKUMP.RODZ=''
      || _nxt:=null();
         DOKUMP.cntx_psh();
         {? DOKUMP.next()
         || _nxt:=DOKUMP.ref()
         ?};
         DOKUMP.cntx_pop();
         DOKUMP.RODZ:='M';
         {? DOKUMP.put(1)
         || __lmod+=1
         ?};
         {? _nxt=null()
         || _dalej:=0
         || _dalej:=DOKUMP.seek(_nxt)
         ?}
      || _dalej:=DOKUMP.next()
      ?};
      _dalej
   !}
?};
__UPG.msg('Zaktualizowano dane powiadomień o kontaktach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\dokump_rodz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Wypełnienie pola RODZ w tabeli DOKUMP - powiadomienia o kontaktach
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych powiadomień o kontaktach'


\margines_we_wy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Dodanie pola do KST - określenie rodzaju marginesów We/Wy.
::----------------------------------------------------------------------------------------------------------------------
exec('add_acr','#stalesys','R_WEWY_R','Sposób interpretacji marginesów We/Wy','T','N','T','KST','PRC');
_dodano:=exec('kst_war_add','#stalesys','KST.R_WEWY_R','\'S\'',date(0,0,0));

{? _dodano
|| __UPG.msg('Dodano informacje o sposobie interpretacji marginesów We/Wy.')
?};

exec('add_acr','#stalesys','R_PWE','Margines wejścia po godzinie zaplanowanej','T','N','T','KST','PRC');
_dodano:=exec('kst_war_add','#stalesys','KST.R_PWE','\'time(0,0,0)\'',date(0,0,0));
{? _dodano
|| __UPG.msg('Dodano informacje do stałych systemu odnośnie marginesu wejścia po godzinie zaplanowanej.')
?};

exec('add_acr','#stalesys','R_PWY','Margines wyjścia przed godziną zaplanowaną','T','N','T','KST','PRC');
_dodano:=exec('kst_war_add','#stalesys','KST.R_PWY','\'time(0,0,0)\'',date(0,0,0));
{? _dodano
|| __UPG.msg('Dodano informacje do stałych systemu odnośnie marginesu wyjścia przed godziną zaplanowaną.')
?};

:: Uzupełnienie pola RODZAJ w tabeli R_MARG dla każdego współpracownika.
R_MARG.use('r_margi');
R_MARG.prefix();
{? R_MARG.for_each("{? R_MARG.RODZAJ='' || R_MARG.RODZAJ:='S'; R_MARG.put() ?}")
|| __UPG.msg('Uzupełniono indywidualne informacje o sposobie interpretacji marginesów We/Wy.')
?};
1


\margines_we_wy_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Opis funkcji aktualizacji stałych systemu zwiazanych z określeniem rodzaju marginesów We/Wy.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - sposób interpretacji marginesów We/Wy oraz'+
' dodatkowe marginesy po wejściu i przed wyjściem'


\TMAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Aktualizacja surowców technologicznych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
TMAT.trig_off('*','*');
TKTL.cntx_psh();
TMAT.cntx_psh();
_msk:=TMAT.names();
_msk.prefix();
{? _msk.first()
|| {!
   |? TMAT.use(_msk.NAME);
      TMAT.prefix();
      TMAT.for_each("
         __lrec+=1;
         _put:=0;

         {? TMAT.PT<>null & TMAT.XJMP=0 & TMAT.WARN>0
            & TMAT.PT().J().KOD=exec('FindAndGet','#table',TKTL,TMAT.NRK,,\"TKTL.JM().KOD\",'')
         || _xjm:=exec('FindAndGet','#table',TKTL,TMAT.NRK,,\"TKTL.XJM\",1);
            TMAT.XJMP:=((TMAT.WARN/_xjm)*100)$2;
            _put:=1
         |? TMAT.PT<>null & TMAT.DJM<>null() & (TMAT.XJMP=0 | TMAT.DXJMP=0) & TMAT.DWARN>0
            &  TMAT.DJM().KOD=exec('FindAndGet','#table',TKTL,TMAT.NRK,,\"TKTL.JM().KOD\",'')
         || {? TMAT.DXJMP<>0
            || TMAT.XJMP:=TMAT.DXJMP;
               _put:=1
            || _xjm:=exec('FindAndGet','#table',TKTL,TMAT.NRK,,\"TKTL.XJM\",1);
               TMAT.DXJMP:=(((TMAT.DWARN)/_xjm)*100)$2;
               TMAT.XJMP:=TMAT.DXJMP;
               _put:=1
            ?}
         ?};

         {? _put>0
         || TMAT.put();
            __lmod+=1
         ?};
         ~~
         ",
         1
      );
      _msk.next()
   !}
?};
TMAT.cntx_pop();
TKTL.cntx_pop();
TMAT.trig_on('*','*');
__UPG.msg('Zaktualizowano surowce technologiczne.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\TMAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Aktualizacja surowców technologicznych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja surowców technologicznych'


\TCHMAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Aktualizacja zamienników surowca technologicznego
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
TCHMAT.trig_off('*','*');
TKTL.cntx_psh();
TCHMAT.cntx_psh();
_msk:=TCHMAT.names();
_msk.prefix();
{? _msk.first()
|| {!
   |? TCHMAT.use(_msk.NAME);
      TCHMAT.prefix();
      TCHMAT.for_each("
         __lrec+=1;
         _put:=0;

         {? TCHMAT.PT<>null & TCHMAT.XJMP=0 & TCHMAT.WARN>0
            & TCHMAT.PT().J().KOD=exec('FindAndGet','#table',TKTL,TCHMAT.NRK,,\"TKTL.JM().KOD\",'')
         || _xjm:=exec('FindAndGet','#table',TKTL,TCHMAT.NRK,,\"TKTL.XJM\",1);
            TCHMAT.XJMP:=((TCHMAT.WARN/_xjm)*100)$2;
            _put:=1
         |? TCHMAT.PT<>null & TCHMAT.DJM<>null() & (TCHMAT.XJMP=0 | TCHMAT.DXJMP=0) & TCHMAT.DWARN>0
            &  TCHMAT.DJM().KOD=exec('FindAndGet','#table',TKTL,TCHMAT.NRK,,\"TKTL.JM().KOD\",'')
         || {? TCHMAT.DXJMP<>0
            || TCHMAT.XJMP:=TCHMAT.DXJMP;
               _put:=1
            || _xjm:=exec('FindAndGet','#table',TKTL,TCHMAT.NRK,,\"TKTL.XJM\",1);
               TCHMAT.DXJMP:=(((TCHMAT.DWARN)/_xjm)*100)$2;
               TCHMAT.XJMP:=TCHMAT.DXJMP;
               _put:=1
            ?}
         ?};

         {? _put>0
         || TCHMAT.put();
            __lmod+=1
         ?};
         ~~
         ",
         1
      );
      _msk.next()
   !}
?};
TCHMAT.cntx_pop();
TKTL.cntx_pop();
TCHMAT.trig_on('*','*');
__UPG.msg('Zaktualizowano zamienniki surowca technologicznego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\TCHMAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Aktualizacja zamienników surowca technologicznego - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zamienników surowca technologicznego'


\dokum_k_nad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Uzupełnia pole K_NAD dla tabeli DOKUM. Nowe pole oznacza, czy dokument znajduje się w książce nadawczej.
::----------------------------------------------------------------------------------------------------------------------
DOKUM.cntx_psh();
DOKUM.index('KH3F');
DOKUM.prefix();
DOKUM.for_each("{? DOKUM.K_NAD='' || DOKUM.K_NAD:='N'; DOKUM.put() ?}");
DOKUM.cntx_pop();
__UPG.msg('Uzupełniono pole K_NAD dla tabeli DOKUM.');
1


\dokum_k_nad_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Uzupełnia pole K_NAD dla tabeli DOKUM. Nowe pole oznacza, czy dokument znajduje się w książce nadawczej.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pole K_NAD dla tabeli DOKUM. Nowe pole oznacza, czy dokument znajduje się w książce nadawczej.'


\rubryka_771_UBZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.42]
:: OPIS: ER/WRT/XP/12.51/2007/0036 Składnik 771 Podstawowa skł. PPE błędnie wliczany do składki zdrowotnej.
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? R.find_key(771)
|| {? R.UBZ='T'
   || R.UBZ:='N';
      R.put();
      __UPG.msg('Składnik 771 "Podstawowa skł. PPE" wyłączony z wliczania do składki zdrowotnej.')
   || __UPG.msg('Składnik 771 "Podstawowa skł. PPE" poprawnie nie jest wliczany do składki zdrowotnej.')
   ?}
?};
R.cntx_pop();
1


\rubryka_771_UBZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.42]
:: OPIS: ER/WRT/XP/12.51/2007/0036 Składnik 771 Podstawowa skł. PPE błędnie wliczany do składki zdrowotnej opis.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka wyłączenia z podstawy składki zdrowotnej składnika 771 "Podstawowa skł. PPE".'


\kparam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Aktualizacja stałych systemu: KPARAM, z historią zmian i dodane do widoku stałych systemu
::----------------------------------------------------------------------------------------------------------------------
_fld:=spli_str('OKD,CZY_ZMKU,LSP_WSZ',',');

{! _ii:=1..obj_len(_fld)
|! _acr:=_fld[_ii];
   exec('add_acr','#stalesys',_acr,'','T','N','T','KPARAM','LSP')
!};

_kstzes_ref:=null();
KST_ZES.cntx_psh();
KST_ZES.index('SYMBOL');
KST_ZES.prefix('KPARAM',);
{? KST_ZES.first()
|| _kstzes_ref:=KST_ZES.ref()
?};
KST_ZES.cntx_pop();

{? _kstzes_ref<>null()
|| KST_DOM.cntx_psh();
   KST_DOM.index('DOM_SYM');
   KST_DOM.prefix(_kstzes_ref);
   _kstdom_nowy:=1;
   {? KST_DOM.find_key('LSP',)
   || _kstdom_nowy:=0
   || KST_DOM.blank();
      KST_DOM.KST_ZES:=_kstzes_ref;
      KST_DOM.B_DOMAIN:=exec('domain_ref','#b_domain','LSP');
      KST_DOM.add()
   ?};
   KST_DOM.cntx_pop();
   {? _kstdom_nowy
   || {! _ii:=1..obj_len(_fld)
      |! _acr:=_fld[_ii];
         exec('add_acr','#stalesys',_acr,'','T','N','T','KPARAM','LSP')
      !}
   ?}
?};

obj_del(_fld);

exec('init_wer','#stalesys','KPARAM');

__UPG.msg('Zaktualizowano stałe systemu - parametry kasy.');

1


\kparam_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Aktualizacja stałych systemu: KPARAM, z historią zmian i dodane do widoku stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - parametry kasy.'


\M_ATR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja wzorców strybutów dostawy
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
M_ATR.cntx_psh();
M_ATR.prefix();
M_ATR.for_each("__lrec+=1; {? M_ATR.MOD='' || M_ATR.MOD:='N'; {? M_ATR.put(1) || __lmod+=1 ?} ?}",0);
M_ATR.cntx_pop();
__UPG.msg('Zaktualizowano wzorce atrybutów dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\M_ATR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja wzorców strybutów dostawy - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wzorców atrybutów dostaw'


\DK_C
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja cech dostawy
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
DK_C.cntx_psh();
_msk:=DK_C.names();
_msk.prefix();
{? _msk.first()
|| {!
   |? DK_C.use(_msk.NAME);
      DK_C.prefix();
      DK_C.for_each("__lrec+=1; {? DK_C.UMOD='' || DK_C.UMOD:=DK_C.UZUP; {? DK_C.put(1) || __lmod+=1 ?} ?}",0);
      _msk.next()
   !}
?};
DK_C.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano cechy dostawy.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\DK_C_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Aktualizacja cech dostawy - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja cech dostaw'


\akt_attr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Usunięcie przypisania atrybutu 9021 dla rubryki 650. Wprowadzenie atrybutu 9021 dla rubryk z atrybutem 551.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_txt:='Aktualizacja atrybutów dla składników płacowych zakończyła się niepowodzeniem';
_result:=-1;
:: Definicje atrybutów.
exec('__RUB','object');
__RUB.fill();
exec('add_attr','rubatr',15,153,'Bez dopełnienia potrącenia dla całego miesiąca',1,
   'Jeżeli nie wprowadzono nieobecności kalendarzowych we wszystkie dni miesiąca,\n'+
   'wynagrodzenie zostanie naliczone nawet wtedy, gdy nieobecność wypełnia wszystkie dni robocze w miesiącu.');
__UPG.msg('Wprowadzono atrybut 153 (Bez dopełnienia potrącenia dla całego miesiąca).');

RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
{? RA_DEF.find_key(9021)
|| RA_USE.index('RA_USE');
   RA_USE.prefix(RA_DEF.ref(),__RUB.ref(650));
   {? RA_USE.first()
   || {!
      |? RA_USE.del()
      !};
      _txt:='Operacja usunięcia przypisanego atrybutu 9021 dla rubryki 650 zakończyła się powodzeniem.';
      __UPG.msg(_txt)
   ?};
   _ZAS:=__RUB.sys_rub(551);
   {? _ZAS.first()
   || {!
      |? exec('add_use','rubatr',9021,_ZAS.RN);
         _ZAS.next()
      !};
      _txt:='Wprowadzono atrybut 9021 dla rubryk z atrybutem 551.';
      _result:=1
   ?}
?};
__UPG.msg(_txt);
_result


\akt_attr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dodanie atrybutu 153. Usunięcie przypisania atrybutu 9021 dla rubryki 650. '+
'Wprowadzenie atrybutu 9021 dla rubryk z atrybutem 551.'


\formuly_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Aktualizacja formuł płacowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='784';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,D,%1'[%255],',');
_err:=exec('formuly_import','upgrade_2042',_tp,_rn,'formuly_txt');
{? _err>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err]);
   -1
|| 1
?}


\formuly_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Funkcja do aktualizacji formuł płacowych.
::   WE: _a [ARRAY] - Tablica aktualizowanych typów formuł płacowych.
::       _b [ARRAY] - Tablica kodów aktualizowanych składników płacowych.
::       _c [STRING] - Nazwa formuły do importu formuł płacowych.
::   WY: _err (0/1) - Liczba błędów, które wystąpiły podczas importu.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;
_formula:=_c;
_err:=0;

FM.cntx_psh();
{! _ni:=1..obj_len(_tp)
|! FM.index('FORMNAZ');
   FM.prefix(exec('ref_firma','ustawienia'),_tp[_ni]);
   {? FM.size()>0
   || {! _mi:=1..obj_len(_rn)
      |! _fm:=form(exec(_formula,'upgrade_2042',_tp[_ni],#_rn[_mi]));
         {? _fm<>''
         || _ok:=1;
            _org:='';
            _found:=FM.find_tab(,'R','RN','=',#_rn[_mi]);
            {? _found
            || _org:=FM.memo_txt(,1,'F')
            || FM.blank();
               FM.TP:=_tp[_ni];
               FM.R:=__RUB.ref(#_rn[_mi]);
               FM.W:={? 'U,F,D'*_tp[_ni] || 'T' || 'N' ?};
               {? ~FM.add()
               || __UPG.msg('Utworzenie formuły typu "%1" dla składnika %2 nie powiodło się.'[_tp[_ni],_rn[_mi]]);
                  _err+=0;
                  _ok:=0
               ?}
            ?};
            {? _found & _fm=_org
            || __UPG.msg('Formuła typu "%1" dla składnika %2 nie wymaga aktualizacji.'[_tp[_ni],_rn[_mi]])
            |? _ok
            || FM.memo_set(_fm,'F');
               {? FM.memo_put(,'F')
               || {? _found
                  || __UPG.msg('Zaktualizowano formułę typu "%1" dla składnika %2.'[_tp[_ni],_rn[_mi]])
                  || __UPG.msg('Utworzono formułę typu "%1" dla składnika %2.'[_tp[_ni],_rn[_mi]])
                  ?}
               |? _found
               || __UPG.msg('Aktualizacja formuły typu "%1" dla składnika %2 nie powiodła się.'[_tp[_ni],_rn[_mi]]);
                  _err+=1
               ?}
            ?}
         ?}
      !}
   ?}
!};
FM.cntx_pop();
_err


\formuly_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Zwraca treść formuły płacowej.
::   WE: _a [STRING] - typ formuły
::       _b [INTEGER] - numer rubryki
::   WY: treść formuły
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_tp:=_a;
_rn:=_b;

{? _rn=784
:: ==================
:: Koszty uzyskania
|| "
_skl_prac:=FUNKCJE.L(765,767);
_korekty:={? _skl_prac>0 || fabs(FUNKCJE.L(470)+FUNKCJE.L(7017)+FUNKCJE.L(532)) ?};
_l:=FUNKCJE.L(780)-FUNKCJE.L(7002);
{? _l>0
|| _l+=_korekty-FUNKCJE.L_SYS(551)-FUNKCJE.L_SYS(32)+{? ~FUNKCJE.L_SYS(9022) || _skl_prac ?}
|| _l:=0
?};
{? _l>0 & P_IPOD.KU<>'B'
|| _ku:={? P_IPOD.KU='Z'
             || KST.KUZ
             || KST.KU
             ?};
   _n_zysk:=FUNKCJE.L_SYS(73);
   {? _n_zysk>0
   || _brutto:=FUNKCJE.L(500);
      {? FUNKCJE.L_SYS(710) & _brutto>0
      || _l-=_n_zysk/_brutto*FUNKCJE.L_SYS(724)$2
      ?}
   ?};
   {? _l>0
   || _ku-=FUNKCJE.LP(784,,,,1);
      {? _ku<0 || _ku:=0 ?};
      {? _l<_ku
      || _l
      || _ku
      ?}
   ?}
?}"
|| ""
?}


\formuly_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Aktualizacja formuł płacowych.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja formuł płacowych'


\status_pracownika
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Dodanie statusu pracownika. Uzupełnienie danych o statusach historycznych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_add:="
   SLO_KOD.cntx_psh;
   SLO_KOD.index('KOD');
   SLO_KOD.prefix(_a);
   {? ~SLO_KOD.find_key(_b,_b)
   || SLO_KOD.blank(1);
      SLO_KOD.SLO_TYP:=_a;
      SLO_KOD.KOD:=_b;
      SLO_KOD.NAZWA:=_c;
      SLO_KOD.SYSTEM:='T';
      SLO_KOD.add
   ?};
   SLO_KOD.cntx_pop";

_ok:=0;
: Statusy listy płac
{? (_typ:=exec('slo_typ','ext_slo','P_STAT'))<>null()
|| _add(_typ,'LIST_MOD','Modyfikacja danych na liście płac.');
   __UPG.msg('Dodano nowy typ: P_STAT do słownika uniwersalnego.');
   _ok:=1
?};

{? _ok
|| _param:=exec('get_par','#parametr',266);
   {? type_of(_param)<>type_of('') || return(0) ?};
   {? +_param=10 & #(4+_param)>0 & #(2+(5-_param))>0
   || _param:=date(#(4+_param),#(2+(5-_param)),1)
   || _param:=date(0,0,0)
   ?};
   {? _param=date(0,0,0) || return(1) ?};

   _rok:=_param~1;
   _mc:=_param~2;

   O.cntx_psh();
   P.cntx_psh();
   LS.cntx_psh();
   _firma:=exec('ref_firma','ustawienia');
   _ndx:=O.ndx_tmp(,1,'FIRMA',,,'F_ZATR','KOD',,'R',,,'M',,,'D',,);
   O.index(_ndx);
   O.prefix(_firma,'P');
   _elim:="
      _prac:='';
      {? _a.first()
      || {!
         |? {? _a.P=_prac
            || _a.STATUS:=1;
               _a.put()
            || _prac:=_a.P
            ?};
            _a.next()
         !}
      ?}";
:: wykasowanie pozycji tabeli wg wskazanego statusu
   _del:="
      {? _a.first()
      || {! |? {? _a.STATUS=_b || _a.del(,1)=2 || _a.next() ?} !}
      ?}";
   _sql:="sql('
      select distinct LS.P, LS.MOD_DATA D, LS.MOD_CZAS T, LS.MOD_KTO KTO, 0 STATUS
      from LS join O
      where O.REFERENCE = :_a
      order by 1,2,3
      ',_a)";
   P.prefix();
   {? O.find_le(_rok,_mc)
   || {!
      |? LS.use(O.LT);
         LS.prefix();
         {? LS.first
         || {? var_pres('_tab')>100 || obj_del(_tab) ?};
            _tab:=_sql(O.ref);
:: eliminacja powtarzających się rekordów dla pracownika na liście pomimo distinct
            _elim(_tab);
            _del(_tab,1);
            {? _tab.first
            || {!
               |? {? P.seek(_tab.P,)
                  || exec('del_P_STAT','pracownik',P.ref,'LIST_MOD',O.ref());
                     exec('put_P_STAT','pracownik',P.ref,'LIST_MOD',_tab.D,_tab.T,O.ref(),
                        'Naliczono składniki listy płac.')
                  ?};
                  _tab.next
               !};
               _ok+=1
            ?}
         ?};
         O.next
      !};
      {? _ok>1
      || __UPG.msg('Uzupełniono informacje historyczne w statusach dla pracownika.')
      ?}
   ?};
   O.ndx_drop(_ndx);
   LS.cntx_pop();
   O.cntx_pop();
   P.cntx_pop()
?};
_ok>0


\status_pracownika_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Dodanie statusu pracownika. Uzupełnienie danych o statusach historycznych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wprowadzenie statusu pracownika. Uzupełnienie danych o statusach historycznych.'


\dk_mar_nar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Uzupełnia pola MAR i NAR dla tabeli z pozycjami dokumentów magazynowych.
::       Nowe pola dotyczą wyliczania Marży i Narzutu.
::----------------------------------------------------------------------------------------------------------------------
DK.trig_off('*','*');
DK.cntx_psh();
DK.index('ALL');
DK.prefix();
_maski:=DK.names;
{? _maski.first ||
   {!|?
      DK.use(_maski.NAME);
      DK.prefix();
      DK.for_each("DK.MAR:=((DK.CN-DK.C)*DK.IL)$2; DK.NAR:={? DK.C<>0 || ((DK.CN-DK.C)/DK.C*100)$2 || 0 ?}; DK.put(1)");
      _maski.next()
   !}
?};
DK.cntx_pop();
DK.trig_on('*','*');
__UPG.msg('Uzupełniono pola MAR i NAR dla tabeli DK.');
1


\dk_mar_nar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Uzupełnia pola MAR i NAR dla tabeli z pozycjami dokumentów magazynowych.
::       Nowe pola dotyczą wyliczania Marży i Narzutu.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola MAR i NAR dla tabeli z pozycjami dokumentów magazynowych. Nowe pola dotyczą wyliczania Marży i Narzutu.'


\nwu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła uzupełnia nowe pola w tabeli NWU.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_stat:=obj_new('poprawiono','niepoprawiono','niewymaga');
{! _lp:=1 .. obj_len(_stat)
|! _stat[_lp]:=0
!};
params_set('stat',_stat);
NWU.cntx_psh();
NWU.index('DATA');
NWU.prefix();
NWU.trig_off('*','*');
NWU.for_each("
   _stat:=params_get().stat;
   {? NWU.CREATOR=null()
   || NWU.CREATOR:=NWU.P;
      {? NWU.put()
      || _stat.poprawiono+=1
      || _stat.niepoprawiono+=1
      ?}
   || _stat.niewymaga+=1
   ?}
");
NWU.trig_on('*','*');
NWU.cntx_pop();
__UPG.msg('Liczba wierszy wymagających poprawienia: %1.' [$(_stat.poprawiono+_stat.niepoprawiono)]);
__UPG.msg('Liczba wierszy poprawionych: %1.' [$_stat.poprawiono]);
__UPG.msg('Liczba wierszy, których nie udało się poprawić: %1.' [$_stat.niepoprawiono]);
__UPG.msg('Liczba wierszy niewymagających poprawienia: %1.' [$_stat.niewymaga]);

{? _stat.niepoprawiono>0
|| -1
|| 1
?}


\nwu_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła uzupełnia nowe pola w tabeli NWU - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia nowe pola w tabeli NWU.'


\pm_rodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła uzupełnia nowe pola w tabeli PM_RODZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
PM_RODZ.cntx_psh();
PM_RODZ.prefix();
{? PM_RODZ.first()
|| PM_RODZ.trig_off('*','*');
   PM_DYSP.cntx_psh();
   PM_DYSP.prefix();
   _stat:=obj_new('poprawiono','niepoprawiono','niewymaga');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};
   {!
   |? {? PM_RODZ.AKT=''
      || PM_RODZ.AKT:=PM_RODZ.PM_DYSP().AKT;
         {? PM_RODZ.put()
         || _stat.poprawiono+=1
         || _stat.niepoprawiono+=1
         ?}
      || _stat.niewymaga+=1
      ?};
      PM_RODZ.next()
   !};
   PM_DYSP.cntx_pop();
   PM_RODZ.trig_on('*','*');

   __UPG.msg('Liczba wierszy wymagających poprawienia: %1.' [$(_stat.poprawiono+_stat.niepoprawiono)]);
   __UPG.msg('Liczba wierszy poprawionych: %1.' [$_stat.poprawiono]);
   __UPG.msg('Liczba wierszy, których nie udało się poprawić: %1.' [$_stat.niepoprawiono]);
   __UPG.msg('Liczba wierszy niewymagających poprawienia: %1.' [$_stat.niewymaga]);

   _ret:={? _stat.niepoprawiono>0 || -1 || 1 ?}

|| __UPG.msg('Tabela nie zawiera rekordów. Aktualizacja nie jest wymagana.');
   _ret:=1
?};
PM_RODZ.cntx_pop();
_ret


\pm_rodz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła uzupełnia nowe pola w tabeli PM_RODZ - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia nowe pola w tabeli PM_RODZ.'


\pm_budz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Inicjacja tabeli PM_BUDZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;

PM_NAG.cntx_psh();
PM_NAG.index('UNIQUE');
PM_NAG.prefix();
{? PM_NAG.first()
|| _stat:=obj_new('poprawiono','niepoprawiono','niewymaga');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};

   PM_RODZ.cntx_psh();
   PM_RODZ.index('UNIQUE');
   PM_RODZ.prefix();
   PM_BUDZ.cntx_psh();
   PM_BUDZ.index('OKRES');
   PM_BUDZ.prefix();
   R.cntx_psh();
   R.prefix();
   _ok:=1;
   {!
   |? {? PM_NAG.PM_BUDZ=null()
      || do();
         PM_BUDZ.prefix(PM_NAG.PM_DYSP,PM_NAG.R);
         _od:=date(PM_NAG.ROK,PM_NAG.MSC,1);
         _budz:=null();
::       Odnalezienie lub utworzenie budżetu.
         {? PM_BUDZ.find_le(_od)
         || _budz:=PM_BUDZ.ref()
         || PM_BUDZ.blank();
            PM_BUDZ.PM_DYSP:=PM_NAG.PM_DYSP;
            PM_BUDZ.R:=PM_NAG.R;
            PM_BUDZ.SYMBOL:='NoLimit (%1)' [$PM_NAG.R().RN];
            PM_BUDZ.OPIS:='Brak limitu';
            PM_BUDZ.OD:=_od;
            {? PM_BUDZ.add()
            || _budz:=PM_BUDZ.ref()
            ?}
         ?};
         {? _budz=null()
         || _stat.niepoprawiono+=1
         || PM_NAG.PM_BUDZ:=_budz;
            {? ~PM_RODZ.find_key(PM_NAG.PM_DYSP,PM_NAG.R)
::             Rodzaj nie został znaleziony - dysponent nie ma już prawa przydzielania danego rodzaju premii.
            || PM_BUDZ.DO:=date(PM_NAG.ROK,PM_NAG.MSC,0)
            ?};
::          Jeżeli nawet nie zaaktualizowaliśmy PM_BUDZ.DO, to PM_BUDZ.put() ma za zadanie zaktualizować sumy.
            {? PM_NAG.put() & PM_BUDZ.put()
            || _stat.poprawiono+=1
            || _stat.niepoprawiono+=1
            ?}
         ?};
         _ok:=end()
      || _stat.niewymaga+=1
      ?};
      _ok & PM_NAG.next()
   !};
   R.cntx_pop();
   PM_BUDZ.cntx_pop();
   PM_RODZ.cntx_pop();

   {? _ok
   || __UPG.msg('Liczba wierszy wymagających poprawienia: %1.' [$(_stat.poprawiono+_stat.niepoprawiono)]);
      __UPG.msg('Liczba wierszy poprawionych: %1.' [$_stat.poprawiono]);
      __UPG.msg('Liczba wierszy, których nie udało się poprawić: %1.' [$_stat.niepoprawiono]);
      __UPG.msg('Liczba wierszy niewymagających poprawienia: %1.' [$_stat.niewymaga]);
      _ret:={? _stat.niepoprawiono>0 || -1 || 1 ?}
   || __UPG.msg('Aktualizacja danych nie powiodła się. Operację należy powtórzyć.');
      _ret:=-1
   ?}

|| __UPG.msg('Moduł premii uznaniowych nie jest używany. Inicjowanie tabeli budżetów nie jest wymagane.');
   _ret:=1
?};
PM_NAG.cntx_pop();

_ret


\pm_budz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Inicjacja tabeli PM_BUDZ - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zainicjowanie tabeli budżetów premii uznaniowych.'


\TKTL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Aktualizacja pól kart technologicznych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
TKTL.trig_off('*','*');
TKTL.cntx_psh();
_msk:=TKTL.names();
_msk.prefix();
{? _msk.first()
|| {!
   |? TKTL.use(_msk.NAME);
      TKTL.prefix();
      TKTL.for_each("
         __lrec+=1;
         _put:=0;

         {? TKTL.IMP_ERR=''
         || TKTL.IMP_ERR:='N';
            _put:=1
         ?};

         {? _put>0
         || TKTL.put();
            __lmod+=1
         ?};
         ~~
         ",
         1
      );
      _msk.next()
   !}
?};
TKTL.cntx_pop();
TKTL.trig_on('*','*');
__UPG.msg('Zaktualizowano karty technologiczne.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\TKTL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Aktualizacja pól kart technologicznych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kart technologicznych'


\dokum_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Wypełnienie pola BL, BL_VISIB w tabeli DOKUM - kontakty
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;
DOKUM.index('DOKF');
{? DOKUM.first()
|| {!
   |? _put:=0;
      __lrec+=1;
      {? DOKUM.BL='' || _put:=1; DOKUM.BL:='N' ?};
      {? DOKUM.BL_VISIB='' || _put:=1; DOKUM.BL_VISIB:='N' ?};
      {? _put
      || {? DOKUM.put(1) || __lmod+=1 ?}
      ?};
      DOKUM.next()
   !}
?};
__UPG.msg('Zaktualizowano dane kontaktów w zakresie dokumentów Businesslink.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\dokum_BL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Wypełnienie pola BL w tabeli DOKUM - kontakty
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych kontaktów w zakresie dokumentów Businesslink'


\typysp_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Wypełnienie pola BL w tabeli TYPYSP - typy dokumentów
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;
TYPYSP.index('TYPYKOD');
{? TYPYSP.first()
|| {!
   |? __lrec+=1;
      {? TYPYSP.BL=''
      || TYPYSP.BL:='N';
         {? TYPYSP.put(1)
         || __lmod+=1
         ?}
      ?};
      TYPYSP.next()
   !}
?};
__UPG.msg('Zaktualizowano dane typów dokumentów sprzedaży i zakupu w zakresie dokumentów Businesslink.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\typysp_bl_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Wypełnienie pola BL w tabeli TYPYSP - typy dokumentów
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dane typów dokumentów sprzedaży i zakupu w zakresie dokumentów Businesslink'


\ZS_DEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Uzupełnienie zależności służbowych.
::----------------------------------------------------------------------------------------------------------------------
exec('transfer','zs_lib')


\ZS_DEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Uzupełnienie zależności służbowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie zależności służbowych.'


\USERSF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizacja użytkowników w firmach
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
USERSF.cntx_psh();
USERSF.prefix();
USERSF.for_each("
   __lrec+=1;
   {? USERSF.PORTAL2=''
   || USERSF.PORTAL2:='N';
      {? USERSF.put(1) || __lmod+=1 ?}
   ?}",
   1
);
USERSF.cntx_pop();
__UPG.msg('Zaktualizowano użytkowników w firmach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\USERSF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizacja użytkowników w firmach - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja użytkowników w firmach'


\act_parform_val
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [20.42]
:: OPIS: Dla czynności rejestracji przekazania i akceptacji ustawia wartość parametrów formułowych jako SCH_FORM.TRESC
::  OLD: \act_parform_val/upgrade_20xx.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=0;

_actList:=spli_str('OBE_FAW_DARP,OBE_FAW_CPRZ,OBE_FAW_EAKP,OBG_FZO_DARK,OBG_FZO_CPRZ,OBG_FZO_EAKC',',');
_lenActL:=obj_len(_actList);

B_ELE.cntx_psh();
B_ELE.index('SYMBOL');
{! _lp:=1 .. _lenActL
|! _uidAct:=_actList[_lp];
   B_ELE.prefix(_uidAct);
   {? B_ELE.first()
   || B_PREL.cntx_psh();
      B_PREL.index('ELEROL');
      {!
      |? B_PREL.prefix(B_ELE.ref());
         {? B_PREL.first()
         || B_VALPRT.cntx_psh();
            B_VALPRT.index('PREL');
            {!
            |? B_VALPRT.prefix(B_PREL.ref());
               {? B_VALPRT.first()
               || {!
                  |? {? B_PORT.seek(B_VALPRT.B_PORT)
                     || {? B_PORT.KIND = 'IN' & ',FORM_WAL,FORM_PRZ,FORM_ODR,FORMWODR,B_ROLEF,USR_FORM,'*B_PORT.SYMBOL
                        || SCH_FORM.cntx_psh();
                           SCH_FORM.index('UNIK');
                           SCH_FORM.prefix(REF.FIRMA,'E',B_VALPRT.VALUE,);
                           {? SCH_FORM.first()
                           || _fml:=exec('val2fml','#convert',SCH_FORM.TRESC,type_of(''));
                              B_VALPRT.VALUE:=SCH_FORM.TRESC;
                              B_VALPRT.FORMULA:=_fml;
                              B_VALPRT.put;
                              __UPG.msg(B_VALPRT.B_PREL().B_PROC().NAME+' '+B_VALPRT.B_PREL().B_PROC().VER+' Proces zaktualizowany');
                              _res:=1
                           || __UPG.msg(B_PREL.B_PROC().NAME+' '+B_PREL.B_PROC().VER+' '+B_PORT.KIND+' '+B_PORT.SYMBOL+' Brak formuły dla parametru do aktualizacji');
                              _res:=1
                           ?};
                           SCH_FORM.cntx_pop()
                        || __UPG.msg(B_PREL.B_PROC().NAME+' '+B_PREL.B_PROC().VER+' '+B_PORT.KIND+' '+B_PORT.SYMBOL+' Parametr nie jest typu wyjściowego formułowego');
                           _res:=1
                        ?}
                     || __UPG.msg(B_PREL.B_PROC().NAME+' '+B_PREL.B_PROC().VER+' Brak parametru do aktualizacji');
                        _res:=1
                     ?};
                     B_VALPRT.next()
                  !}
               || __UPG.msg(B_PREL.B_PROC().NAME+' '+B_PREL.B_PROC().VER+' Brak wartości parametru do aktualizacji');
                  _res:=1
               ?};
               B_PREL.next()
            !};
            B_VALPRT.cntx_pop()
         || __UPG.msg(B_PREL.B_PROC().NAME+' '+B_PREL.B_PROC().VER+' Brak elementu procesu do aktualizacji');
            _res:=1
         ?};
         B_ELE.next()
      !};
      B_PREL.cntx_pop()
   || __UPG.msg('Brak ogólnego elemrntu procesu do aktualizacji z czynnością '+_uidAct);
      _res:=1
   ?}
!};
B_ELE.cntx_pop();

{? _res=0
|| __UPG.msg('Błąd działania formuły potransferowej')
?};

_res


\act_parform_val_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [20.42]
:: OPIS: Dla czynności rejestracji przekazania i akceptacji ustawia wartość parametrów formułowych jako SCH_FORM.TRESC
::  OLD: \act_parform_val_desc/upgrade_20xx.fml
::----------------------------------------------------------------------------------------------------------------------
'Dla czynności rejestracji, przekazania i akceptacji faktur i wniosków ustawia wartość parametrów formułowych jako SCH_FORM.TRESC'


\FAKS_KZF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Aktualizuje FAKS_KZF.FAKS_UID
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
FAKS_KZF.cntx_psh();
FAKS_KZF.index('FAK');
FAKS_KZF.prefix();
FAKS_KZF.for_each("
   __lrec+=1;
   _put:=0;
   {? FAKS_KZF.FAKS_UID=''
   ||
      FAKS_KZF.FAKS_UID:=exec('FindAndGet','#table',FAKS,FAKS_KZF.FAKS,,\"FAKS.uidref()\",'');
      _put:=1
   ?};
   {? _put & FAKS_KZF.put() || __lmod+=1 ?}
");
FAKS_KZF.cntx_pop();

__UPG.msg('Zaktualizacowano pola na liście faktur do korekty w korekcie zbiorczej..');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

VAR_DEL.delete('__lrec','__lmod');
_res


\FAKS_KZF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Aktualizuje FAKS_KZF.FAKS_UID - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól na liście faktur do korekty w korekcie zbiorczej.'


\akt_atrybut
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Aktualizacja atrybutów dla rubryk płacowych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

:: Definicje atrybutów dotyczących rozliczenia godzin przepracowanych.
exec('__RUB','object');
__RUB.fill();

exec('add_attr','rubatr',,8,'Rozliczenie czasu przepracowanego',1,,,
   48,54,55,56,57,58,63,64,65,66,67,68,69,7011,7015,7016,7026,7027,7028,7029);
exec('add_use','rubatr',8,7035,7036,7037,7038,7056,7057);
exec('add_attr','rubatr',8,81,'Godziny przepracowane',1,,,48,54,55,56,58,63,64,65,67,68,69);
   exec('add_attr','rubatr',81,811,'Praca zaplanowana',1,,,54);
   exec('add_attr','rubatr',81,812,'Przekroczenia dobowe',1,,,48,55,56,63);
      exec('add_attr','rubatr',812,8121,'Nadgodziny',1,,,55,56);
      exec('add_attr','rubatr',812,8122,'Odbiory',1,,,63);
      exec('add_attr','rubatr',812,8123,'Odpracowania',1,,,48);
   exec('add_attr','rubatr',81,813,'Potencjalne przekroczenia średniotygodniowe',1,,,58,65);
   exec('add_attr','rubatr',81,814,'Ponadwymiarowe',1,,,64);
   exec('add_attr','rubatr',81,815,'Dopełnienia',1,,,67,68,69);
exec('add_attr','rubatr',8,82,'Rozliczenie wynagrodzenia i dodatków',1,,,
   48,54,55,56,57,58,63,64,65,66,67,68,69,7011,7015,7016,7026,7027,7028,7029);
exec('add_use','rubatr',82,7035,7036,7037,7038,7056,7057);
   exec('add_attr','rubatr',82,821,'Wynagrodzenie miesięczne',1,,,48,54,55,56,57,63,64,65,67,68,69,7015,7016);
      exec('add_attr','rubatr',821,8211,'Praca wg planu',1,,,54);
      exec('add_attr','rubatr',821,8212,'Przekroczenia dobowe',1,,,48,55,56,63,7015,7016);
      exec('add_attr','rubatr',8212,82121,'Nadgodziny',1,,,55,56,7015,7016);
         exec('add_attr','rubatr',82121,821211,'Nadgodziny 50%',1,,,55,7015);
         exec('add_attr','rubatr',82121,821212,'Nadgodziny 100%',1,,,56,7016);
      exec('add_attr','rubatr',8212,82122,'Odbiory',1,,,63);
      exec('add_attr','rubatr',8212,82123,'Odpracowania',1,,,48);
      exec('add_attr','rubatr',821,8213,'Potencjalne przekroczenia średniotygodniowe',1,,,65);
      exec('add_attr','rubatr',821,8214,'Ponadwymiarowe',1,,,64);
      exec('add_attr','rubatr',821,8215,'Dopełnienia',1,,,67,68,69);
         exec('add_attr','rubatr',8215,82151,'Wynagrodzenie',1,,,67);
         exec('add_attr','rubatr',8215,82152,'Dodatek 50%',1,,,68);
         exec('add_attr','rubatr',8215,82153,'Dodatek 100%',1,,,69);
      exec('add_attr','rubatr',821,8216,'Nocne',1,,,57);

   exec('add_attr','rubatr',82,822,'Dodatki okresowe',1,,,58,66,7011,7026,7027,7028,7029,7035,7036,7037,7038,7056,7057);
      exec('add_attr','rubatr',822,8221,'Przekroczenia średniotygodniowe',1,,,58,66,7011);
      exec('add_attr','rubatr',822,8222,'Dzień wolny',1,,,7037,7038);
      exec('add_attr','rubatr',822,8223,'Dzień świąteczny',1,,,7028,7029);
      exec('add_attr','rubatr',822,8224,'Godziny ponadwymiarowe',1,,,7026,7027,7035,7036,7056,7057);

__UPG.msg('Dodano atrybuty dotyczące rozliczenia godzin przepracowanych.');

   exec('add_attr','rubatr',193,1934,'Nieobecności wymagające wprowadzenia komentarza',1,
      'Nieobecności, dla których złożenie wniosku (na portalu) bez wprowadzenia komentarza (przyczyny)'
      ' nie jest możliwe',,12);
::Poprawka brak rubryki w atrybucie 19421 - Warunkowo pomijane w zapytaniach sql
exec('add_use','rubatr',19421,3,15);
__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');
_result


\akt_atrybut_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Aktualizacja atrybutów dla rubryk płacowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja atrybutów dla rubryk płacowych.'


\zws_par_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Formuła dodaje czynności ZWS_PAR_BLDS i ZWS_PAR_BLDZ do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

{! _ii:=1..2

|! {? _ii=2
   || _cr:=''+"'ZWS_PAR_LTDZ'";
      _uid:='ZWS_PAR_BLDZ'
   || _cr:=''+"'ZWS_PAR_LTDS'";
      _uid:='ZWS_PAR_BLDS'
   ?};

   _ROLE:=sql(
      'select distinct B_ROLE.NAME '
      'from B_ACTROL join B_ACTION using (B_ACTROL.B_ACTION,B_ACTION.REFERENCE) '
                    'join B_ROLE using (B_ACTROL.B_ROLE,B_ROLE.REFERENCE) '
      'where B_ACTROL.FIRMA=:_a and B_ACTION.UID in (:_b) '
      'order by 1',
      exec('ref_firma','ustawienia'),_cr
   );

   {? type_of(_ROLE)=type_of(~~)
   || __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
      return(0)
   ?};

   {? _ROLE.first()
   || {!
      |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
         || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_ROLE.NAME])
         || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_ROLE.NAME]);
            _ret:=-1
         ?};
         _ROLE.next()
      !}
   || __UPG.msg('W firmie %1 brak ról z uprawnieniami do czynności %2.'[REF.FIRMA().SYMBOL,_cr])
   ?};

   obj_del(_ROLE)

!};

_ret


\zws_par_bl_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Formuła dodaje czynności ZWS_PAR_BLDS i ZWS_PAR_BLDZ do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_BLDS - Typy dokumentów sprzedaży dla Businesslink" i ' +
'"ZWS_PAR_BLDS - Typy dokumentów zakupu dla Businesslink" do odpowiednich ról'


\zui_cli_ager
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Formuła dodaje czynność ZUI_CLI_AGER do odpowiednich ról - Administrator
::----------------------------------------------------------------------------------------------------------------------
_uid:='ZUI_CLI_AGER';
_lista:=''+"'Administrator'";

_ROLE:=sql(
   'select distinct B_ROLE.NAME '
   'from B_ROLE '
   'where B_ROLE.NAME in (:_a) '
   'order by 1',
   _lista
);

{? type_of(_ROLE)=type_of(~~)
|| __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
   return(0)
?};

{? _ROLE.first()
|| _ret:=1;
   {!
   |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_ROLE.NAME])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_ROLE.NAME]);
         _ret:=-1
      ?};
      _ROLE.next()
   !};
   _ret
|| __UPG.msg('Brak ról do przypisania dla czynności %1.'[_uid]);
   1
?}


\zui_cli_ager_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Formuła dodaje czynność ZUI_CLI_AGER do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZUI_CLI_AGER - Obsługa synchronizacji z API" do odpowiednich ról.'


\dok_jpk_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła naprawcza po użyciu niepoprawnej wersji formuły dok_jpk_upgr - kod - ER/WRT/XP/20.42/2011/0022
::----------------------------------------------------------------------------------------------------------------------
DOK_JPK.cntx_psh();
_maski:=DOK_JPK.names();
{? _maski.first()
|| {!
   |? DOK_JPK.use(_maski.NAME);
      DOK_JPK.erase();
      _maski.next()
   !}
?};
DOK_JPK.cntx_pop();
1


\dok_td_fix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MP] [20.42]
:: OPIS: Aktualizacja typu dokumentu dla poprawki kod - ER/WRT/XP/20.42/2011/0022
::----------------------------------------------------------------------------------------------------------------------
FIRMA.cntx_psh(); ROK_F.cntx_psh(); OKRO_F.cntx_psh(); DOK.cntx_psh();
FIRMA.index('SYMBOL2'); FIRMA.prefix('S');
__lrec:=0;
__lret:=0;
_where:='where DOK.JPK_V_T not like \'\'';
_sql:='select JPK_V_T from @DOK ';
_sql+=_where;
_tab:=sql(_sql);
{? ~_tab.first() & FIRMA.first()
|| __lret:=1;
   {!
   |? {? FIRMA.Z='N'
      || ROK_F.index('ROKPOCZ'); ROK_F.prefix(FIRMA.ref());
         {? ROK_F.find_ge(date(2018,1,1))
         || {!
            |? OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
               {? OKRO_F.first()
               || {!
                  |? _maska:=ROK_F.KOD+form(OKRO_F.NR,-2);
                     DOK.cntx_psh();
                     DOK.use('doku'+_maska);
                     echo('Uaktualnienie dokumentów księgowych: '+DOK.name());
                     DOK.prefix();
                     DOK.for_each("
                        {? DOK.JPK_V_T='' & DOK.DOK_REJ().JPK_V_T<>''
                        || DOK.JPK_V_T:=DOK.DOK_REJ().JPK_V_T;
                           __lrec+=1;
                           DOK.put()
                        ?};
                        ~~
                     ",1);
                     DOK.cntx_pop();
                     OKRO_F.next()
                  !}
               ?};
               ROK_F.next()
            !}
         ?}
      ?};
      FIRMA.next()
   !};
   echo()
?};
{? __lret=1
|| __UPG.msg('Zaktualizowano typ dokumentu dla następującej liczby zapisów: %1'[$__lrec])
|? __lret=0
|| __UPG.msg('Aktualizacja wartości pola DOK.JPK_V_T była przeprowadzona wcześniej.')
?};
FIRMA.cntx_pop(); ROK_F.cntx_pop(); OKRO_F.cntx_pop(); DOK.cntx_pop();
1


\dok_td_fix_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Aktualizacja typu dokumentu - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typu dokumentu'


\dok_jpk_upgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Wypełnia tabelę DOK_JPK
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
__lrec:=0;
{? ROK_F.find_ge(date(2018,1,1))
|| {!
   |? _maska:=ROK_F.KOD;
      DOK.cntx_psh(); DVAT.cntx_psh(); DOK_JPK.cntx_psh();
      DVAT.use('dvat__'+_maska);
      echo('Uaktualnienie dokumentów księgowych: '+DVAT.name());
      DVAT.prefix();
      DVAT.for_each("
      {? DVAT.DOK<>null() & DVAT.OBR().POCZ>date(2020,09,30)
      || DOK.use(ref_name(DVAT.DOK));
         _mdok:=ref_name(DVAT.DOK);
         DOK_JPK.use('dokj'+(_mdok+4));
         DOK.prefix();
         {? DOK.seek(DVAT.DOK)
         || DOK_JPK.index('UNIK');
            DOK_JPK.prefix(DOK.ref());
            {? ~DOK_JPK.first()
            || DOK_JPK.blank();
               DOK_JPK.JPK_GTU:=DOK.JPK_GTU;
               DOK_JPK.JPK_PROC:=DOK.JPK_PROC;
               DOK_JPK.DATE:=DOK.DR;
               DOK_JPK.TIME:=time(0,0,0);
               DOK_JPK.OPERATOR:=DOK.ZAR;
               DOK_JPK.DOK:=DOK.ref();
               __lrec+=1;
               DOK_JPK.add()
            ?};
            VATZMDEK.index('ROK');
            VATZMDEK.prefix(ROK_F.ref());
            {? VATZMDEK.first() & VATZMDEK.find_le(DVAT.OBR().NR)
            || SIK.ROZL_VAT:=VATZMDEK.ROZL_VAT
            || SIK.ROZL_VAT:=ROK_F.ROZL_VAT
            ?};
            _typ:='V7'+SIK.ROZL_VAT;
            PVAT.cntx_psh(); VAT_PS.cntx_psh(); VAT_DEK.cntx_psh();
            PVAT.use('pvat__'+(6-DVAT.name()));
            VAT_PS.use('vat_ps'+ROK_F.KOD);
            PVAT.index('NR');
            PVAT.prefix(DVAT.ref());
            {? PVAT.first()
            || {!
               |? {? _typ='V7M' | _typ='V7K' & DVAT.OBR().POCZ~2%*3<>0
                  || _okres:=$(DVAT.OBR().POCZ~1)+'/'+form(DVAT.OBR().POCZ~2,-2)
                  |? _typ='V7K' & DVAT.OBR().POCZ~2%*3=0
                  || _v:=#((7+$DVAT.OBR().KON)+2);
                     _okres:=(4+$DVAT.OBR().KON)+'/'
                             +{? _v<=3 || '1' |? _v<=6 || '2' |? _v<=9 || '3' || '4' ?}
                 ?};
                 VAT_DEK.index('UNIK');
                 VAT_DEK.prefix(ROK_F.ref(),_typ,_okres);
                 {? VAT_DEK.first()
                 || {!
                    |? VAT_PS.index('PVAT_REF');
                       VAT_PS.prefix(VAT_DEK.ref(),$PVAT.ref());
                       {? VAT_PS.first()
                       || {!
                          |? {? VAT_PS.CRC=PVAT.crc()
                             || DOK_JPK.VAT_DEK:=$VAT_PS.VAT_DEK;
                                _dok:=DOK_JPK.DOK;
                                _vdek:=DOK_JPK.VAT_DEK;
                                DOK_JPK.cntx_psh();
                                DOK_JPK.index('UNIK');
                                DOK_JPK.prefix(_dok,_vdek);
                                {? DOK_JPK.first() || _zap:=0 || _zap:=1 ?};
                                DOK_JPK.cntx_pop();
                                {? _zap=1
                                || DOK_JPK.cntx_psh();
                                   DOK_JPK.index('UNIK');
                                   DOK_JPK.prefix(_dok,'',);
                                   {? DOK_JPK.first()
                                   || DOK_JPK.VAT_DEK:=_vdek;
                                      DOK_JPK.prefix();
                                      DOK_JPK.put();
                                      _zap:=0
                                   || _zap:=1
                                   ?};
                                   DOK_JPK.cntx_pop()
                                ?};
                                {? _zap=1 || __lrec+=1; DOK_JPK.add() ?}
                             ?};
                             VAT_PS.next()
                          !}
                       ?};
                       VAT_DEK.next()
                    !}
                 ?};
                  PVAT.next()
               !}
            ?};
            PVAT.cntx_pop(); VAT_PS.cntx_pop(); VAT_DEK.cntx_pop()
         ?}
      ?}
      ",1);
      DOK.cntx_pop(); DVAT.cntx_pop(); DOK_JPK.cntx_pop();
      ROK_F.next()
   !};
   echo()
?};
ROK_F.cntx_pop();
DOK.cntx_psh();  DOK_JPK.cntx_psh(); ROZLVAT.cntx_psh();
ROZLVAT.index('DOK');
ROZLVAT.prefix();
{? ROZLVAT.first()
|| {!
   |? DOK.use(ref_name(ROZLVAT.DOK));
      _mdok:=ref_name(ROZLVAT.DOK);
      DOK_JPK.use('dokj'+(_mdok+4));
      DOK_JPK.cntx_psh();
      DOK_JPK.index('UNIK');
      DOK_JPK.prefix(ROZLVAT.DOK);
      {? ~DOK_JPK.first()
      || DOK_JPK.blank();
         DOK_JPK.JPK_GTU:=ROZLVAT.DOK().JPK_GTU;
         DOK_JPK.JPK_PROC:=ROZLVAT.DOK().JPK_PROC;
         DOK_JPK.DATE:=ROZLVAT.DOK().DR;
         DOK_JPK.TIME:=time(0,0,0);
         DOK_JPK.OPERATOR:=ROZLVAT.DOK().ZAR;
         DOK_JPK.DOK:=ROZLVAT.DOK;
         __lrec+=1;
         DOK_JPK.add()
      ?};
      DOK_JPK.cntx_pop();
      ROZLVAT.next()
   !}
?};
DOK.cntx_pop(); DOK_JPK.cntx_pop(); ROZLVAT.cntx_pop();
{? __lrec=0
|| __UPG.msg('Nie znaleziono dokumentów wymagających aktualizacji danych.')
|| __UPG.msg('Dodano %1 powiązań dokumentów i deklaracji JPK_V7.'[$__lrec])
?};
VAR_DEL.delete('__lrec');
1


\dok_jpk_upgr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Wypełnia tabelę DOK_JPK - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli DOK_JPK dla dokumentów nierozliczonych, tabeli DVAT oraz deklaracji JPK_V7'


\PARAMS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Usunięcie parametrów 232, 241 i 244.
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask(
      'Od wartości parametrów zależy działanie zadania odtwarzającego strukturę zależności służbowych.\n'
      'Zaleca się usunięcie parametrów po odtworzeniu i zweryfikowaniu zależności we wszystkich firmach.\n\n'
      'Czy na pewno usunąć parametry 232, 241 i 244?'
   )
|| __UPG.msg('Zrezygnowano z wykonania zadania.');
   return(-1)
?};

_num:=spli_str('232,241,244',',');
_len:=obj_len(_num);

_err:=0;
PARAMS.cntx_psh();
PARAMS.index('FIRMA');
PARAMS.prefix(null);
{! _ii:=1.._len
|! _val:=#_num[_ii];
   {? PARAMS.find_key(_val)
   || {? PARAMS.del(,1)
      || __UPG.msg('Usunięto parametr %1.'[$_val])
      || __UPG.msg('Błąd usunięcia parametru %1.'[$_val]);
         _err+=1
      ?}
   || __UPG.msg('Brak parametru %1.'[$_val])
   ?}
!};
PARAMS.cntx_pop();

{? _err<>0 || -1 || 1 ?}


\PARAMS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Usunięcie parametrów 232, 241 i 244 - opis.
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie parametrów 232, 241 i 244.'


\users_osoba_unique
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Weryfikacja unikalności powiązania użytkownika i osoby.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Używam "join" a nie "left join", bo interesują mnie tylko uzytkownicy z uzupełnionym polem OSOBA.
_TAB:=sql(
   'select USERS.OSOBA, OSOBA.NAZWISKO, OSOBA.PIERWSZE '
   'from USERS join OSOBA using (USERS.OSOBA,OSOBA.REFERENCE) '
   'group by USERS.OSOBA, OSOBA.NAZWISKO,OSOBA.PIERWSZE '
   'having count(*)>1 '
   'order by OSOBA.NAZWISKO, OSOBA.PIERWSZE'
);
{? _TAB.first()
|| __UPG.msg('Podczas weryfikacji znaleziono osoby powiązane z więcej niż jednym użytkownikiem.');
   __UPG.msg('Dane WYMAGAJĄ naprawy. Należy doprowadzić do unikalności powiązania użytkownika i osoby.');
   __UPG.msg('Lista osób niespełniających założeń unikalności:');
   {!
   |? __UPG.msg(' * %1 %2' [_TAB.NAZWISKO,_TAB.PIERWSZE]);
      _TAB.next()
   !};
   -1
|| __UPG.msg('Wszystkie powiązania są unikalne.');
   1
?}


\users_osoba_unique_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Weryfikacja unikalności powiązania użytkownika i osoby - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Weryfikacja unikalności powiązania użytkownika i osoby.'


\wnr_rh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [20.42]
:: OPIS: Dodanie pól do KST zwiazanych z numerowanie rachunków do umów cywilnoprawnych
::----------------------------------------------------------------------------------------------------------------------
exec('add_acr','#stalesys','NR_RH_AU','Numeracja rachunków umów zleceń','T','N','T','KST','PPL');
{? exec('kst_war_add','#stalesys','KST.NR_RH_AU','\'B\'',date(0,0,0))
|| __UPG.msg('Dodano informacje o automatycznej numeracji rachunków do umów zleceń.')
?};
exec('add_acr','#stalesys','NR_RH_FM','Format numeru rachunku określony przez formułę','T','N','T','KST','PPL');
{? exec('kst_war_add','#stalesys','KST.NR_RH_FM','\'\'',date(0,0,0))
|| __UPG.msg('Dodano informacje w polu: Format numeru rachunku określony przez formułę.')
?};
1


\wnr_rh_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [20.42]
:: OPIS: Opis funkcji aktualizacji stałych systemu zwiazanych z numerowaniem rachunków uwów zleceń
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - numeracja rachunków do umów cywilnoprawnych.'


\fst_del_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.42]
:: OPIS: Usunięcie parametru ZWS_PAR_U_DOM
::----------------------------------------------------------------------------------------------------------------------
:Identyfikator parametru do usunięcia (pole ID z tabeli FP_DEF)
_id:='ZWS_PAR_U_DOM';
FP_STR.cntx_psh();
FP_DEF.cntx_psh();
FP_WYK.cntx_psh();
FP_FIR.cntx_psh();
FP_AKT.cntx_psh();
FP_INS.cntx_psh();
::Usuwanie danych z tabeli FP_DEF
FP_DEF.index('ID');
FP_DEF.prefix('T',_id,);

{? FP_DEF.first() ||
   _def:=FP_DEF.ref;
   FP_DEF.del(1);
:: Usuwanie danych z tabeli FP_STR
   FP_STR.index(FP_STR.ndx_tmp(,1,'FP_DEF',,,'ID',,));
   FP_STR.prefix(_def,);
   {? FP_STR.first() || FP_STR.del(1) ?};
:: Usuwanie powiązanych danych z tabeli FP_WYK
   FP_WYK.index(FP_WYK.ndx_tmp(,1,'FP_DEF',,,'FP_AKT',,));
   FP_WYK.prefix(_def,);
   {? FP_WYK.first() ||
      {! |?
         FP_WYK.del(1);
         FP_WYK.next()
      !}
   ?};
:: Usuwanie powiązanych danych z tabeli FP_FIR
   FP_FIR.index('UNIQUE');
   FP_FIR.prefix(_def,);
   {? FP_FIR.first() ||
      {! |?

::       Usuwanie powiązanych danych z tabeli FP_INS
         FP_INS.index('UNIQUE');
         FP_INS.prefix(FP_FIR.ref,);
         {? FP_INS.first() ||
            {! |?
               FP_INS.del(1);
               FP_INS.next()
            !}
         ?};

         FP_FIR.del(1);
         FP_FIR.next()
      !}
   ?};
:: Usuwanie powiązanych danych z tabeli FP_AKT
   FP_AKT.index(FP_AKT.ndx_tmp(,1,'FP_DEF',,,'FP_WER',,));
   FP_AKT.prefix(_def,);
   {? FP_AKT.first() ||
      {! |?
         FP_AKT.del(1);
         FP_AKT.next()
      !}
   ?};
   __UPG.msg('Parametr o identyfikatorze ''%1'' został usunięty z systemu.' [_id]);
   _status:=1
||
   __UPG.msg('Usunięcie parametru o identyfikatorze ''%1'' nie powiodło się.' [_id]);
   _status:=-1
?};
FP_STR.cntx_pop();
FP_DEF.cntx_pop();
FP_WYK.cntx_pop();
FP_FIR.cntx_pop();
FP_AKT.cntx_pop();
FP_INS.cntx_pop();
_status


\fst_del_par_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.42]
:: OPIS: Usunięcie parametru ZWS_PAR_U_DOM
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie parametru ZWS_PAR_U_DOM.'


\obe_automatyczna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Aktualizacja wybranych czynności obiegowych, aby można było uruchamić automatycznie
::----------------------------------------------------------------------------------------------------------------------
_czyn:=spli_str('OBE_FAW_CPRZ,OBE_FAW_EAKP',',');
B_ACTION.cntx_psh();
B_ACTION.index('UNIK');
_cnt:=obj_len(_czyn);
{! _ind:=1.._cnt
|! B_ACTION.prefix(_czyn[_ind],);
   {? B_ACTION.first()
   || B_PREL.cntx_psh(); B_PREL.index('ACCESS'); B_PREL.prefix(B_ACTION.B_ELE);
      {? B_PREL.first()
      || {!
         |? {? B_PREL.AUTOMAT<>'M'
            || B_PREL.AUTOMAT:='M'; B_PREL.put()
            ?};
            B_PREL.next()
         !}
      ?};
      B_PREL.cntx_pop()
   ?}
!};
B_ACTION.cntx_pop();
__UPG.msg('Zaktualizowano czynności akceptacji i przekazania wniosków w obiegu.')


\obe_automatyczna_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Aktualizacja wybranych czynności obiegowych, aby można było uruchamić automatycznie - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wybranych czynności obiegowych, aby można było uruchamić automatycznie'


\ppk_edi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import komunikatów EDI dla Pracowniczych Planów Kapitałowych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',obj_ntab_names(obj_new('ZWS_IMPORTY_PPK')));
__UPG.msg('Zaktualizowano definicje komunikatów EDI w mechaniźmie importów PPK.');
1


\ppk_edi_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import komunikatów EDI dla Pracowniczych Planów Kapitałowych - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import komunikatów EDI dla Pracowniczych Planów Kapitałowych.'


\jpk_przyczyna_korekty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.42]
:: OPIS: Wydłużenie pola przyczyna korekty do 100 znaków
::----------------------------------------------------------------------------------------------------------------------
_r1:=_ret:=0;
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','FA',null,date(2019,11,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'PrzyczynaKorekty');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[100]';
      _r1:=ISTDEFS.put()
   ?}
?};
{? _r1 || _ret:=1 ?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
{? _ret
|| __UPG.msg('Poprawiono długość przyczyny korekty na JPK_FA(3).')
|| __UPG.msg('Brak potrzeby wykonania działania naprawczego na JPK_FA(3).')
?};
1


\jpk_przyczyna_korekty_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.42]
:: OPIS: Wydłużenie pola przyczyna korekty do 100 znaków - opis
::----------------------------------------------------------------------------------------------------------------------
'Wydłużenie pola przyczyna korekty do 100 znaków'


\oddelegowanie_edi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import komunikatów EDI dla Kadr - uzupełnienie o Oddelegowanie.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',obj_ntab_names(obj_new('ZWS_IMPORTY_PKD')));
__UPG.msg('Zaktualizowano definicje komunikatów EDI w mechaniźmie importów dla dziedziny Kadry.');
1


\oddelegowanie_edi_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import komunikatów EDI dla Kadr - uzupełnienie o Oddelegowanie - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import komunikatów EDI dla Kadr.'


\fo_700014
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Ustawienie uniwersalnego parametru aplikacyjnego 700014
::----------------------------------------------------------------------------------------------------------------------
_nr:=700014;
exec('get','#params',_nr);
_interface:=exec('interface','#params','A',,_nr);
_value:=_interface.get();
{? _value=''
|| _interface.save('template_old.htm');
   __UPG.msg('Ustawiono uniwersalny parametr aplikacyjny "700014 Domyślny wzorzec wiadomości".')
|| __UPG.msg('Nie zmieniono wartości uniwersalnego parametru aplikacyjnego "700014 Domyślny wzorzec wiadomości".')
?};
1


\fo_700014_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Ustawienie uniwersalnego parametru aplikacyjnego 700014 - opis
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Ustawienie uniwersalnego parametru aplikacyjnego "700014 Domyślny wzorzec wiadomości".'


\plugins
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Uzupełnia definicję wtyczek wdrożeniowych
::----------------------------------------------------------------------------------------------------------------------
exec('init','plugins');
__UPG.msg('Uzupełniono definicję wtyczek wdrożeniowych.');
1


\plugins_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Uzupełnia definicję wtyczek wdrożeniowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie definicji wtyczek wdrożeniowych:\n'
'PKD_P_WIN_EDIT,\n'
'PXX_TAB_001'


\szablony
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import zestawów danych i definicji szablonów.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_ASDZ,ZWS_PAR_ASDD',','));
__UPG.msg('Zaktualizowano zestawy danych i definicje szablonów.');
1


\szablony_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import zestawów danych i definicji szablonów - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import zestawów danych i definicji szablonów.'


\fiks_73i74
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: kod: ER/WRT/XP/12.51/2012/0004 - JPK_V7 Kodkraju nadania
::       kod: ER/WRT/XP/12.51/2009/0002 oraz ER/WRT/XP/12.51/2011/0040 - JPK_V7: Brak możliwości oznaczenia pozycji w
::       wierszu KorektaPodstawyOpodt dla sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_licz:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','V7');
{? ISTDEF.first()
|| {!
   |? ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'KodKrajuNadaniaTIN',);
      {? ISTDEFS.first()
      || {! |? ISTDEFS.cntx_psh();
               ISTDEFS.TYPFLD:=$"STRING[2]";
               ISTDEFS.NAZTAB:=$"KH_KRAJ";
               _licz+=ISTDEFS.put();
               ISTDEFS.cntx_pop();
               ISTDEFS.next()
          !}
       ?};
       ISTDEFS.prefix(ISTDEF.ref(),'KorektaPodstawyOpodt',);
       {? ISTDEFS.first() &
          (
             ISTDEFS.REGULY=$"{? VAT_PS.TYP='S' || '1' || '' ?}" |
             ISTDEFS.REGULY=$"{? VAT_PS.TYP='N' || '1' || '' ?}"
          )
       || ISTDEFS.REGULY:=$"{? -VAT_PS.TYP='n' || '1' || '' ?}";
          _licz+=ISTDEFS.put()
       ?};
       ISTDEFS.cntx_pop();
       ISTDEF.next()
   !}
?};
ISTDEF.cntx_pop();
{? _licz
|| __UPG.msg('Zmieniono schematy JPK_V7')
|| __UPG.msg('Nie zmieniono schematów JPK_V7')
?};
1


\fiks_73i74_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: kod: ER/WRT/XP/12.51/2012/0004
::       kod: ER/WRT/XP/12.51/2009/0002 oraz ER/WRT/XP/12.51/2011/0040
::----------------------------------------------------------------------------------------------------------------------
'JPK_V7 KodKrajuNadaniaTIN, KorektaPodstawyOpodt - zmiany w schemacie'


\abs_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Aktualizacja znaczników AbStore
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Czy dla tabel: Słownik materiałów, Grupy materiałów, Kontrahenci,
   \nGrupy kontrahentów, Odbiorcy kontrahenta, Osoby kontaktowe,
   \nwyłączyć znaczniki integracji z AbStore?'@,,'&Tak','&Nie');

{? _choice=1
||
   VAR_DEL.delete('__lrec','__lmod');
   __lrec:=__lmod:=0;
:: Słownik materiałów
   M.cntx_psh();
   M.for_each("
   __lrec+=1;
   {? M.ABSTORE<>'' | M.ABSTOREC<>''
   ||
      M.ABSTORE:='';
      M.ABSTOREC:='';
      M.prefix();
      {? M.put() || __lmod+=1 ?}
   ?}
   ",1);
   M.cntx_pop();
:: Grupy materiałów
   MGR.cntx_psh();
   MGR.for_each("
   __lrec+=1;
   {? MGR.ABSTORE<>'' | MGR.ABSTOREC<>''
   ||
      MGR.ABSTORE:='';
      MGR.ABSTOREC:='';
      MGR.prefix();
      {? MGR.put() || __lmod+=1 ?}
   ?}
   ",1);
   MGR.cntx_pop();
:: Kontrahenci
   KH.cntx_psh();
   KH.for_each("
   __lrec+=1;
   {? KH.ABSTORE<>''
   ||
      KH.ABSTORE:='';
      KH.prefix();
      {? KH.put() || __lmod+=1 ?}
   ?}
   ",1);
   KH.cntx_pop();
:: Grupy kontrahentów
   GRKH.cntx_psh();
   GRKH.for_each("
   __lrec+=1;
   {? GRKH.ABSTORE<>''
   ||
      GRKH.ABSTORE:='';
      GRKH.prefix();
      {? GRKH.put() || __lmod+=1 ?}
   ?}
   ",1);
   GRKH.cntx_pop();
:: Odbiorcy kontrahenta
   KH_ODB.cntx_psh();
   KH_ODB.for_each("
   __lrec+=1;
   {? KH_ODB.ABSTORE<>''
   ||
      KH_ODB.ABSTORE:='';
      KH_ODB.prefix();
      {? KH_ODB.put() || __lmod+=1 ?}
   ?}
   ",1);
   KH_ODB.cntx_pop();
:: Osoby kontaktowe
   KH_OSOB.cntx_psh();
   KH_OSOB.for_each("
   __lrec+=1;
   {? KH_OSOB.ABSTORE<>''
   ||
      KH_OSOB.ABSTORE:='';
      KH_OSOB.prefix();
      {? KH_OSOB.put() || __lmod+=1 ?}
   ?}
   ",1);
   KH_OSOB.cntx_pop();

   __UPG.msg('Zaktualizowano znaczniki AbStore.');
   __UPG.msg('Przetworzono: %1 rekordów, z czego zmodyfikowano: %2.'[$__lrec,$__lmod]);
   VAR_DEL.delete('__lrec','__lmod')
|? _choice=2
||
   __UPG.msg('Nie zaktualizowano znaczników AbStore.')
|? _choice=0
||
   return(-1)
?}


\abs_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Aktualizacja znaczników AbStore
::----------------------------------------------------------------------------------------------------------------------
'Zadanie dla klientów nie korzystających z AbStore, które wyłącza znaczniki integracji dla tabel:
Słownik materiałów, Grupy materiałów, Kontrahenci, Grupy kontrahentów, Odbiorcy kontrahenta, Osoby kontaktowe.'


\obe_fix_stat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Formuła naprawcza dla nieprawidłowych statusów dokumentów w obiegu po odswieżeniu formuł desc
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask(
      'Formuła odświeża opisy zadań na liście TODO dla czynności związanych z dokumentami w obiegu oraz poprawia statusy na tabeli EDOKUM. '
      'W przypadku zaobserwowania nieprawidłowych bieżących statusów czynności na dokumentach w obiegu zaleca się kontynuację zadania.\n'
      'Czy chcesz wykonać zadanie?'
   )
|| __UPG.msg('Zrezygnowano z wykonania zadania.');
   return(-1)
?};
:: Przejście po liście todo w celu aktualizacji stanu edokum i edokos
_symbol:='OBE_FAW_CPRZ,OBE_FAW_EAKP,FKS_DKS_DFDM,FKS_DKS_DFWO,FKS_DKS_DFZO,OBE_FAW_DARP,OBE_FDL_CPRZ,'
         'OBE_FDL_DBRP,OBE_FDL_EBKP,OBG_FZO_CPRZ,OBG_FZO_DARK,OBG_FZO_EAKC';
{? ~BI_PREL.lock(2)
|| return(0)
?};
B_PREL.cntx_psh(); B_ELE.cntx_psh();
BI_PREL.trig_off('*','*');
BI_PREL.cntx_psh();
BI_PREL.use('bi_e____');
BI_PREL.prefix();
{? BI_PREL.first()
|| {!
   |? {? BI_PREL.B_PREL().CLASS='B_ACTION' & _symbol*BI_PREL.B_PREL().B_ELE().SYMBOL>0
      || BI_PREL.DESCTODO:='T';
         BI_PREL.put(1);
         exec('desc','#bi_todo',BI_PREL.ref())
      ?};
      BI_PREL.next()
   !}
?};
BI_PREL.cntx_pop();
BI_PREL.trig_on('*','*');
B_PREL.cntx_pop(); B_ELE.cntx_pop();
BI_PREL.unlock();

:: Poprawa statusów na EDOKUM
EDOKUM.trig_off('*','*'); EDOKOS.trig_off('*','*');
EDOKUM.cntx_psh(); EDOKOS.cntx_psh(); USERS.cntx_psh();
_mask:=EDOKUM.names();
{? _mask.first()
|| {!
   |? EDOKUM.use(_mask.NAME); EDOKUM.prefix();
      EDOKOS.use('skid_y'+(_mask.NAME+2));
      {? EDOKUM.first()
      || {!
:: Usuwanie statusów utworzonych przez użytkownika service
         |? EDOKOS.index('SZUK3'); EDOKOS.prefix(EDOKUM.ref(),'W');
            {? EDOKOS.size()>0 & EDOKOS.first()
            || {!
               |? {? EDOKOS.USERS().KOD='service' || EDOKOS.del(); 0 || EDOKOS.next() ?}
               !}
            ?};
            {? EDOKUM.ZAM<>'T'
:: Zmiana statusów procesów, które juz się zakonczyły, czyszczenie B_PREL i B_PRELS
            || {? EDOKUM.STATUS='T' & EDOKUM.B_PRELS<>''
               || _res:=exec('isActiveProcOrProblems','#b__box',EDOKUM.uidref());
                  {? ~_res.isActive
                  || EDOKUM.B_PREL:=EDOKUM.B_PRELS:=''; EDOKUM.put()
                  ?};
                  obj_del(_res)
               |? EDOKUM.B_PRELS<>''
:: Naprawa dokumentów zadekretowanych
               || EDOKOS.index('SZUK9'); EDOKOS.prefix(EDOKUM.ref);
                  {? EDOKOS.last() & EDOKOS.OPERACJA='D' & EDOKOS.STATUS='T' & EDOKOS.B_PRELS<>EDOKUM.B_PRELS
                  || EDOKUM.B_PREL:=EDOKOS.B_PREL; EDOKUM.B_PRELS:=EDOKOS.B_PRELS; EDOKUM.put
                  ?}
               ?}
            ?};
            EDOKUM.next()
         !}
      ?};
      _mask.next()
   !}
?};
obj_del(_mask);
EDOKUM.cntx_pop(); EDOKOS.cntx_pop(); USERS.cntx_pop();
EDOKUM.trig_on('*','*'); EDOKOS.trig_on('*','*');
__UPG.msg('Naprawiono statusy dokumentów w obiegu.');
1


\obe_fix_stat_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Formuła naprawcza dla nieprawidłowych statusów dokumentów w obiegu po odswieżeniu formuł desc - opis
::----------------------------------------------------------------------------------------------------------------------
'Formuła naprawcza dla statusów bieżącej czynności dokumentów w obiegu.\n'
'Formuła odświeża opisy zadań na liście TODO dla czynności związanych z dokumentami w obiegu oraz poprawia statusy na tabeli EDOKUM.\n'
'W przypadku zaobserwowania nieprawidłowych bieżących statusów czynności na dokumentach w obiegu zaleca się wykonanie zadania.'


\rozliczenie_godzinowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: ER/WRT/XP/12.51/2010/0035 - modyfikuje wartość pola KAL_OPIS.RODZAJ, KAL_DEF.RODZAJ i KAL_BUFF.RODZAJ na 'G'
::       Obecnie jedynie wartość 'G' jest poprawnie kwalifikowana w systemie.
::----------------------------------------------------------------------------------------------------------------------
_res:=exec('rozliczenie_godzinowe','napraw_p');
{? _res.ok
|| {? _res.data>#0
   || __UPG.msg('Naniesiono zmianę rodzaju dnia na "Godzinowy" dla kalendarzy i buforów planowania od daty: %1.'
                [$_res.data])
   || __UPG.msg('Naniesiono zmianę rodzaju dnia na "Godzinowy" dla kalendarzy i buforów planowania.')
   ?}
|| __UPG.msg('Podczas nanoszenia zmian rodzaju dnia na "Godzinowy" '
             'dla kalendarzy i buforów planowania wystąpiły błędy.')
?};
_res.ok


\rozliczenie_godzinowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Zmiana sposobu rozliczania czasu pracy na godzinowy - opis
::----------------------------------------------------------------------------------------------------------------------
'Zmiana rodzaju dnia na "Godzinowy" dla kalendarzy i buforów planowania.'


\add_ire
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: PR/WRT/XP/20.42/2101/0019 - Zmiany związane z BREXIT - Irlandia Północna
::----------------------------------------------------------------------------------------------------------------------
KRAJE.cntx_psh(); KRAJE.index('KRAJE'); KRAJE.prefix();
SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
SLUAPPL.cntx_psh(); SLUAPPL.index('NAZ'); SLUAPPL.prefix('F');
SLO.cntx_psh(); SLO.index('SL'); SLO.prefix();
_ret:=0;
{? ~KRAJE.find_key('XI')
|| {? SLU.find_key('WALUTY')
   || {? SLO.find_key(SLU.ref(),'GBP')
      || _wal:=SLO.ref
      || _wal:=0
      ?}
   ?};
   {? _wal
   || KRAJE.blank();
      KRAJE.SYM:=KRAJE.KODZUS:=KRAJE.KODISO:='XI'; KRAJE.NAZ:='Irlandia Północna'; KRAJE.WAL:=_wal; KRAJE.IBAN:='T';
     _ret:=KRAJE.add()
   ?}


?};
{? _ret
|| {? SLU.find_key('~KRAJE UE')
   || {? ~SLO.find_key(SLU.ref(),KRAJE.SYM)
      || SLO.blank();SLO.KOD:=KRAJE.SYM; SLO.TR:=KRAJE.NAZ;
         _ret:=SLO.add(1)
      ?}
   ?}
?};
_uchwyt:=fopen('stawki_ue.dfg','ur',1);
_licz:=0; _vat:=0;
{? _uchwyt
|| {! |?
      _str:=fread(_uchwyt);
      {? _str<>'\n' & +|_str & _str*'~STAWKI VAT XI'
      || {? _vat=0
         || {? ~SLU.find_key(14+_str) | SLU.NAZ<>14+_str
            || SLU.blank(); SLU.SYSTEM:='T'; SLU.NAZ:=14+_str; SLU.WZ:='prosty'; SLU.DL:=8;
               SLU.OP:='Stawki VAT w kraju '+{? KRAJE.find_key(SLU.NAZ+2) || KRAJE.NAZ || '' ?};
               _vat:=SLU.add()
            || _vat:=1
            ?}
         ?};
         {? _vat
         || _slu:=SLU.ref();
            {? ~SLUAPPL.find_key(14+_str) | SLUAPPL.SLU().NAZ<>14+_str
            || SLUAPPL.blank(1); SLUAPPL.GDZIE:='F'; SLUAPPL.SLU:=_slu; SLUAPPL.add()
            ?};
            SLUAPPL.SLU();
            _str:=15-_str;
            {? ~SLO.find_key(SLU.ref(),1-form(('Q'+(6+_str))))
            || SLO.blank(); SLO.KOD:=1-form(('Q'+(6+_str))); SLO.TR:=7-_str;
               _licz+=SLO.add(1)
            ?}
         ?}
      ?};
      _str<>'\n'
   !};
fclose(_uchwyt)
?};
KRAJE.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop(); SLO.cntx_pop();
exec('kodZUSKraju','dane_startowe');

{? _ret
|| __UPG.msg('Dodano Irlandię Północną do słownika KRAJE UE')
|| __UPG.msg('Nie dodano Irlandii Północnej do słownika KRAJE UE')
?};
{? _licz
|| __UPG.msg('Dodano słownik stawek VAT dla Irlandii Północnej ~STAWKI VAT XI')
|| __UPG.msg('Nie dodano słownika stawek VAT dla Irlandii Północnej ~STAWKI VAT XI')
?};
1


\add_ire_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: PR/WRT/XP/20.42/2101/0019 - Zmiany związane z BREXIT - Irlandia Północna - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie Irlandii Północnej do słownika krajów UE i stawek VAT do sławnika ~STAWKI VAT XI'


\ob_czy_wycofanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Poprawa w procesach warunku na bramie sprawdzającej, czy dokument w obiegu został wycofany
::----------------------------------------------------------------------------------------------------------------------
B_CHOICE.cntx_psh();
B_CHOICE.prefix();
{? B_CHOICE.first()
|| {!
   |? {? B_CHOICE.FORMULA='exec(\'FindAndGet\',\'#table\',EDOKUM,_a.EDOKUM,,\"{? @.EDOKUM.WYCOFAJ<>\'\' || @.EDOKUM.WYCOFAJ:=\'\'; @.EDOKUM.put()  || 0 ?}\",0)'
      || B_CHOICE.FORMULA:='_a.EDOKUM<>~~ & exec(\'FindAndGet\',\'#table\',EDOKUM,_a.EDOKUM,,\"{? @.EDOKUM.WYCOFAJ<>\'\' || @.EDOKUM.WYCOFAJ:=\'\'; @.EDOKUM.put()  || 0 ?}\",0)';
         B_CHOICE.put()
      ?};
      B_CHOICE.next()
   !}
?};
B_CHOICE.cntx_pop();
__UPG.msg('Poprawiono warunek sprawdzający, czy dokument w obiegu jest wycofany.');
1


\ob_czy_wycofanie_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Poprawa w procesach warunku na bramie sprawdzającej, czy dokument w obiegu został wycofany - OPIS
::----------------------------------------------------------------------------------------------------------------------
'Poprawa w procesach warunku na bramie sprawdzającej, czy dokument w obiegu został wycofany.'


\sch_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: ER/WRT/XP/20.42/2101/0045
::       Importuje "personelowe" wnioski w obiegu dot. grafików - poprawki związane z obsługą wniosków w jTerm
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:="
{? _a
|| __UPG.msg('Zakończono import wniosku: \"%1\".'@[_b])
|| __UPG.msg('Nie udało się zaimportować wniosku: \"%1\".'@[_b])
?}
";
_result:=0;
_typobieg:=exec('add','typ_ob','Obieg wniosków');
_nazwa:=exec('nazwa_nadg_w','obiegi');
_result+=_msg(exec('imp_wn','sch_imp',_nazwa,1,0),_nazwa);
_nazwa:=exec('nazwa_nadg_p','obiegi');
_result+=_msg(exec('imp_wn','sch_imp',_nazwa,1,0),_nazwa);
_result>0


\sch_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: ER/WRT/XP/20.42/2101/0045
::       Importuje "personelowe" wnioski w obiegu dot. grafików  - poprawki związane z obsługą wniosków w jTerm - opis
::----------------------------------------------------------------------------------------------------------------------
'Import wniosków w obiegu dot. grafików'


\akt_2042_09
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Aktualizacja 20.42_09 - kompilacja wydruku
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('fks_optermp*');
__UPG.msg('Zaktualizowano wydruk fks_optermpl.');
_result


\akt_2042_09_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: :: OPIS: Aktualizacja 20.42_09 - kompilacja wydruku - opis
::----------------------------------------------------------------------------------------------------------------------
'Kompilacja wydruku sprawozdania o stosowanych terminach zapłaty w transakcjach handlowych'


\fiks_75
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: kod: PR/WRT/XP/20.42/2101/0051 - JPK FA(3) - brak obsługi węzła [Zamówienia] i [ZamówieniaCtrl]
::----------------------------------------------------------------------------------------------------------------------
_fml:="
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),_a,);
   {? ISTDEFS.first()
   || ISTDEFS.REGULY:=_b;
      ISTDEFS.FORMULA:=_c;
      ISTDEFS.FORM_XML:=_d;
      ISTDEFS.NAZTAB:=_e;
      ISTDEFS.TYPFLD:=_f;
      {? ISTDEFS.put()
      || __UPG.msg('%1 - poprawiono'[_a])
      ?}
   ?};
   ISTDEFS.cntx_pop()
";
_add:="
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),_a,);
   {? ISTDEFS.first()
   || _lp:=ISTDEFS.LP+1;
      _nad:=ISTDEFS.TREE;
      ISTDEFS.cntx_psh();
      ISTDEFS.index('LP'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.next() & ISTDEFS.OPIS<>'R'
      || {? ISTDEFS.last()
         || {!
            |? ISTDEFS.LP+=1;
               ISTDEFS.put();
               ISTDEFS.LP-1>_lp & ISTDEFS.prev()
            !}
         ?};
         ISTDEFS.blank(1);
         ISTDEFS.ISTDEF:=ISTDEF.ref();
         ISTDEFS.TREE:=_nad;
         ISTDEFS.LP:=_lp;
         ISTDEFS.OPIS:='R';
         ISTDEFS.REGULY:=_b;
         ISTDEFS.NAZTAB:='R';
         ISTDEFS.TYPFLD:='STRING[27]';
         ISTDEFS.COMMENT:='T';
         ISTDEFS.WYM:='N';
         ISTDEFS.HIDDEN:='N';
         ISTDEFS.prefix();
         {? ISTDEFS.add()
         || __UPG.msg('R - dodano za %1'[_a])
         ?}
      ?};
      ISTDEFS.cntx_pop()
   ?};
   ISTDEFS.cntx_pop()
";
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','FA','JPK_FA(3)_v1-0');
{? ISTDEF.first()
|| _fml('Zamowienie','',$"exec('fa_nag_f','jpk',_a,1)",$"exec('fa_nag_n','jpk')",'FA_ZAL','');
   _fml('P_2AZ',$"exec('fa_naz','jpk')",$"",$"",'ZAL_SYM','STRING[50]');
   _fml('WartoscZamowienia',$"form(sFB,,2,'9.')",$"",$"",'WZ','REAL');
   _fml('ZamowienieWiersz',$"",$"exec('fa_zal_p','jpk',_a)",$"exec('fa_zal_p','jpk',2)",'ZAL_POZ','REFERENCE');
   _fml('P_7Z',$"FaPoz[1]",$"",$"",'SYM','STRING[100]');
   _fml('P_8AZ',$"FaPoz[2]",$"",$"",'MIARA','STRING[8]');
   _fml('P_8BZ',$"FaPoz[3]",$"",$"",'ILOSC','REAL');
   _fml('P_9AZ',$"FaPoz[4]",$"",$"",'CN','REAL');
   _fml('P_11NettoZ',$"FaPoz[7]",$"",$"",'NETTO','REAL');
   _fml('P_11VatZ',$"FaPoz[11]",$"",$"",'VAT','REAL');
   _fml('P_12Z',$"FaPoz[9]",$"",$"",'SVAT','STRING[2]');
   _fml('ZamowienieCtrl',$"NrJPK",$"",$"",'FA_ZAL_C','');
   _fml('LiczbaZamowien',$"$NrJPK",$"",$"",'LF','INTEGER');
   _fml('WartoscZamowien',$"form(SumaB,,2,'9.')",$"",$"",'WF','REAL');
   _add('WartoscZamowienia',$"$DOK.ref()+$DOK.crc()");
   _add('P_12Z',$"FaPoz[10]")
|| __UPG.msg('Nie znaleziono schematu JPK_FA (3).')
?};
ISTDEF.cntx_pop();
1


\fiks_75_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: kod: PR/WRT/XP/20.42/2101/0051 - JPK FA(3) - brak obsługi węzła [Zamówienia] i [ZamówieniaCtrl]
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu JPK FA(3) -  dodanie obsługi węzła [Zamówienia] i [ZamówieniaCtrl]'


\edeklaracje_2042_14
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.42]
:: OPIS: Dodanie nowych e-deklaracji IFT-1R(16) obowiązujących od 1.1.2021 dotyczące przychodów od 01.01.2020
::       rozporządzenie z 28.12.2020
::----------------------------------------------------------------------------------------------------------------------
_dod_ver:="
   VAT_VER.index('VER_OD'); VAT_VER.prefix(_a,_c,_c,_b,_d);
   _jest:=VAT_VER.first();
   {? ~_jest
   || _old:={? _a='FKS' || 'FIKS' |? _a='PPL' || 'KALI' || '' ?};
      VAT_VER.prefix(_old,_c,_c,_b,_d);
      _jest:=VAT_VER.first()
   ?};
   {? _jest
   || _add:=0
   || _add:=1;
      VAT_VER.blank();
      VAT_VER.SYSTEM:=_a;
      VAT_VER.OD:=_b;
      VAT_VER.NAZWA:=_c;
      VAT_VER.NR:=_d
   ?};
   {? _<6 || _f:='' ?};
   {? _<5 || _e:=0 ?};
   _put:=0;
   {? VAT_VER.NRF<>_e || VAT_VER.NRF:=_e; _put+=1 ?};
   {? VAT_VER.WIERSZ<>_f || VAT_VER.WIERSZ:=_f; _put+=1 ?};
   VAT_VER.prefix();
   {? _add || VAT_VER.add(1) |? _put || VAT_VER.put(1) ?}
";
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
{? _dod_ver('PPL',date(2021,1,1),'IFT1',16,16,',1,2,3,4,5,6,7,8,9,10,11,12,')
|| __UPG.msg('Dodano 16 wersja e-deklaracji IFT-1 obowiązująca od dnia 2021.01.01 dla przychodów od 2020.01.01')
|| __UPG.msg('Brak aktualizacji, 16 wersja e-deklaracji IFT-1 już istnieje.')
?};
{? _dod_ver('PPL',date(2021,1,1),'IFT1R',16,16,',1,2,3,4,5,6,7,8,9,10,11,12,')
|| __UPG.msg('Dodano 16 wersja e-deklaracji IFT-1R obowiązująca od dnia 2021.01.01 dla przychodów od 2020.01.01')
|| __UPG.msg('Brak aktualizacji, 16 wersja e-deklaracji IFT-1R już istnieje.')
?};
::import poprawnych struktur e-deklaracji IFT-1R wersja 16
{? exec('edek_stru','edeklar','IFT1',16,'ift-1-16_v1.dfg')
|| __UPG.msg('Dodano schemat 16 wersji e-deklaracji IFT-1.')
|| __UPG.msg('Brak aktualizacji, schemat 16 wersji e-deklaracji IFT-1 już istnieje.')
?};
{? exec('edek_stru','edeklar','IFT1R',16,'ift-1r-16_v1.dfg')
|| __UPG.msg('Dodano schemat 16 wersji e-deklaracji IFT-1R.')
|| __UPG.msg('Brak aktualizacji, schemat 16 wersji e-deklaracji IFT-1R już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
1


\edeklaracje_2042_14_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [20.42]
:: OPIS: Dodanie nowych e-deklaracji IFT-1R(16) obowiązujących od 2021.01.01 dotyczące przychodów od 2020.01.01
::       rozporządzenie z 28.12.2020
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych e-deklaracji IFT-1R(16) obowiązujących od 2021.01.01 dotyczące przychodów od 2020.01.01'


\inwentaryzacja_lt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.14]
:: OPIS: ER/WRT/XP/20.14/2101/0005 - Usunięcie zlikwidowanych środków z istniejących arkuszy inwentaryzacji
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
SRXD.cntx_psh();
exec('maski','srodobj');
SRXD.index('DOK_L');
SRXD.prefix();
_tmp:=tab_tmp(,
               'NRINW','INTEGER', 'Nr inwentaryzacji',
               'DATA','DATE','Data wprowadzenia',
               'NRA','INTEGER','Numer arkusza',
               'JORG','STRING[16]','J. organizacyjna',
               'ODD','STRING[8]','J. księowa',
               'NRI','STRING[20]','Numer',
               'NAZWA','STRING[100]','Nazwa',
               'GR','STRING[1]','Grupa',
               'WARP','REAL','Wart. pod.',
               'WARF','REAL','Wart. bil.',
               'REF','INTEGER','Ref');
{? SRXD.first()
|| {!
   |? _ok:=1;
      {? SRXD.SRXI().STATUS='T' | SRXD.SRXA().STATUS='T'
      || _ok:=0
      ?};
      SRSR.cntx_psh();
      _lt:=0;
      SRSR.index('NRI_O');
      SRSR.prefix(SRXD.ODD,SRXD.KIND,SRXD.NRI);
      {? SRSR.first()
      || SRDO.cntx_psh();
         SRDO.index('RODZOKR');
         SRDO.prefix(SRSR.ref(),SRXD.SRXI().OKRO_F);
         {? SRDO.first()
         || {!
            |? {? SRDO.TYP=FINFO.SRXI_LT
               || _lt:=1
               ?};
               SRDO.next()
            !}
         ?};
         SRDO.cntx_pop()
      ?};
      SRSR.cntx_pop();
      {? _lt=1 & _ok=1
      || _tmp.NRINW:=SRXD.SRXI().NR;
         _tmp.DATA:=SRXD.SRXI().DW;
         _tmp.NRA:=SRXD.SRXA().NR;
         _tmp.JORG:=SRXD.JORG().SYMBOL;
         _tmp.ODD:=SRXD.ODD().OD;
         _tmp.NRI:=SRXD.NRI;
         _tmp.NAZWA:=SRXD.NST;
         _tmp.GR:=SRXD.GR;
         _tmp.WARP:=SRXD.WARP;
         _tmp.WARF:=SRXD.WARF;
         _tmp.REF:=SRXD.ref();
         _tmp.add()
      ?};
      SRXD.next()
   !}
?};
_okienko:=_tmp.mk_sel('Pozycje arkuszy inwentaryzacji - środki zlikwidowane',,0);
_tmp.win_fld(_okienko,,'NRINW',,,5);
_tmp.win_fld(_okienko,,'DATA');
_tmp.win_fld(_okienko,,'NRA');
_tmp.win_fld(_okienko,,'JORG');
_tmp.win_fld(_okienko,,'ODD');
_tmp.win_fld(_okienko,,'NRI');
_tmp.win_fld(_okienko,,'NAZWA',,,40);
_tmp.win_fld(_okienko,,'GR');
_tmp.win_fld(_okienko,,'WARP');
_tmp.win_fld(_okienko,,'WARF');
_tmp.win_sel(_okienko);
{? _tmp.size()>0
|| _tmp.select();
   _pyt:=FUN.choice('Czy usunąć pozycje arkuszy inwentaryzacji?',,'Tak');
   SRXD.index('SRXINRI');
   {? _pyt<>1
   || SRXD.cntx_pop();
      return(-1)
   ?}
|| _pyt2:=FUN.choice('W otwartych arkuszach nie ma nieprawidłowości. \nChchesz odznaczyć zadanie jako wkonane?',,'Tak',,,,'Nie');
   {? _pyt2<>1
   || SRXD.cntx_pop();
      return(-1)
   ?}
?};
{? _tmp.first() & _pyt=1
|| {!
   |? SRXD.prefix();
      {? SRXD.seek(_tmp.REF)
      || SRXD.del()
      ?};
      _tmp.next()
   !}
?};
SRXD.cntx_pop();
1


\inwentaryzacja_lt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.14]
:: OPIS: Usunięcie zlikwidowanych środków z istniejących arkuszy inwentaryzacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie zlikwidowanych środków z istniejących arkuszy inwentaryzacji.'


\upd_poz_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: kod: ER/WRT/XP/20.42/2101/0042 - Błędne uzupełnienie wartości w walucie na pozycjach dokumentu
::----------------------------------------------------------------------------------------------------------------------
_poz:=POZ.names();
licz:=0;
{? _poz.first()
|| POZ.cntx_psh();
   DOK.cntx_psh();
   {!
   |? POZ.use(_poz.NAME); POZ.prefix();
      DOK.use('doku'+(_poz.NAME+4)); DOK.prefix();
      POZ.for_each("{? POZ.WAL<>FINFO.NAROD & POZ.SUMW<>0 & (POZ.STR='Ma' & POZ.MA_WAL=0 | POZ.STR='Wn' & POZ.WN_WAL=0)
                    || licz+=POZ.put()
                    ?}");
      _poz.next()
   !};
   DOK.cntx_pop();
   POZ.cntx_pop()
?};
{? licz
|| __UPG.msg('Poprawiono %1 pozycji walutowych.'@[$licz])
|| __UPG.msg('Nie poprawiono pozycji walutowych')
?};
licz;
1


\upd_poz_wal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: kod: ER/WRT/XP/20.42/2101/0042 - Błędne uzupełnienie wartości w walucie na pozycjach dokumentu
::----------------------------------------------------------------------------------------------------------------------
'Poprawienie stron pozycji walutowych'


\akt_2042_11
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Aktualizacja 20.42_11 - nowe wersje e-deklaracji VAT-UE i VAT-UEK
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{! _i:=1..2
|! _typ:={? _i=1 || 'POD' || 'PODK' ?};
   _fn:={? _i=1 || '' || 'k' ?};
   _imp:=-1;
   {? exec('add_ver','xml','FKS',date(2020,6,1),_typ,5)
   || _imp:=exec('upg_imp','xml','FKS','D',_typ,5,date(2021,1,12),'vat-ue'+_fn+'_5_2.dfg')
   ?};

   _txt:='e-Deklaracja VAT-UE'+(~_fn)+' (5) obowiązująca od 12.01.2021';

   {? _imp=1
   || __UPG.msg(_txt+' została dodana.')
   |? _imp=0
   || __UPG.msg(_txt+' już istnieje.')
   || __UPG.msg(_txt+' nie została dodana.');
      _ok:=0
   ?}
!};
_ok


\akt_2042_11_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Aktualizacja 20.42_11 - nowe wersje e-deklaracji VAT-UE i VAT-UEK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja 20.42_11 - na schematy e-deklaracji VAT-UE/K (5) obowiązujące od stycznia 2021 roku'


\term_plat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Dodanie parametru dla form płatności: Czy drukować termin płatności?
::----------------------------------------------------------------------------------------------------------------------
RS.cntx_psh();
RS.index('RS');
RS.prefix();
{? RS.find_key('~Płatności')
|| exec('new_par','slo_slu',RS.ref(),7,'Czy drukować termin płatności?','TAK_NIE')
?};
RS.cntx_pop()


\term_plat_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Dodanie parametru dla form płatności: Czy drukować termin płatności?
::----------------------------------------------------------------------------------------------------------------------
'Dodanie parametru dla form płatności: Czy drukować termin płatności?'


\jpk_v7_stale
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Mapowanie stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('map_stale','upgrade_2014_01','XINFO','FIZ','KST.PODFIZ');
exec('map_stale','upgrade_2014_01','XINFO','IMIE','KST.PODIME');
exec('map_stale','upgrade_2014_01','XINFO','NAZWISKO','KST.PODNAZ');
exec('map_stale','upgrade_2014_01','XINFO','DATA_UR','KST.PODURDAT');
1


\jpk_v7_stale_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Mapowanie stałych systemu
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie mapy stałych systemu na potrzeby aktualizacji 2014_01\n'
'Dane licencjobiorcy - osoba fizyczna'


\atrybuty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [21.14]
:: OPIS: Dodanie atrybutów składników płacowych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

exec('__RUB','object');
__RUB.fill();

exec('add_attr','rubatr',8,83,'Kwalifikacja godzin',1);
   exec('add_attr','rubatr',83,831,'Obsługa godzin firmowych',1);
      exec('add_attr','rubatr',831,8311,'Wprowadzenie godzin do kwalifikacji',1,
         'Wprowadzenie firmowych godzin do kwalifikacji.');
      exec('add_attr','rubatr',831,8312,'Rozliczane poprzez zamknięcie miesiąca',1);
      exec('add_attr','rubatr',831,8313,'Rozliczane poprzez zamknięcie okresu',1);
   exec('add_attr','rubatr',83,832,'Przenoszenie godzin do rozliczenia',1,,,
      1,48,54,55,56,57,63,64,65,66,67,68,69);
   exec('add_use','rubatr',832,7011,7015,7016,7026,7027,7028,7029,7035,7036,7037,7038,7056,7057);
      exec('add_attr','rubatr',832,8321,'Podczas zamykania rozliczenia miesiąca',1,,,
         48,54,55,56,57,63,64,65,67,68,69,7015,7016);
         exec('add_attr','rubatr',8321,83211,'Weryfikacja danych w kartotece godzin',1,
            'Usuwanie danych w kartotece godzin, które są jeszcze nierozliczone na liście płac w celu uniknięcia '
            'duplikacji zapisów.',,48,54,55,56,57,63,64,65,67,68,69,7015,7016);
      exec('add_attr','rubatr',832,8322,'Podczas zamykania rozliczenia okresu',1,,,
         66,7011,7026,7027,7028,7029,7035,7036,7037,7038,7056,7057);
         exec('add_attr','rubatr',8322,83221,'Weryfikacja danych w kartotece godzin',1,
            'Usuwanie danych w kartotece godzin, które są jeszcze nierozliczone na liście płac w celu uniknięcia '
            'duplikacji zapisów.',,66,7011,7026,7027,7028,7029,7035,7036,7037,7038,7056,7057);

__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\atrybuty_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK[20.42]
:: OPIS: Dodanie atrybutów składników płacowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja atrybutów składników płacowych.'


\akt_2042_20
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [20.42]
:: OPIS: Aktualizacja 20.42_20 - aktualizacja sprawozdania GUS Z-02
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
R.clear();
exec('__RUB','object');
__RUB.fill();
exec('add_attr','rubatr',633,6338,'Składki na PFRON',1,
   'Wpłaty na Państwowy Fundusz Rehabilitacji Osób Niepełnosprawnych');

__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\akt_2042_20_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [20.42]
:: OPIS: Aktualizacja 20.42_20 - aktualizacja sprawozdania GUS Z-02
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja 20.42_20 - aktualizacja sprawozdania GUS Z-02 za rok 2020'


\akt_2042_19
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN, JASZAFRA [20.42]
:: OPIS: Aktualizacja 20.42_19: Okres izolacji - pandemia koronowirusa
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';

:: Aktualizacja opisów składników płacowych.
{? +exec('import','rubryki','g','7125,7126',1,0)
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);
{? _result=-1 || return(_result) ?};
:: Definicje atrybutów.
exec('__RUB','object');
__RUB.fill();

:: aktualizacja atrybutów rubryk
exec('add_attr','rubatr',1211,12114,'Izolacja',1,,,7126);
exec('add_uses','rubatr',7126,14,15,16,19,100,126,129,151,161,191,192);
exec('add_uses','rubatr',7126,195,196,1001,1293,1921,10011,14331,100112);

exec('add_attr','rubatr',12211,122114,'Izolacja',1,,,7125);
exec('add_uses','rubatr',7125,14,15,16,19,123,126,129,151,162,191,192);
exec('add_uses','rubatr',7125,195,196,1223,1293,1921,12231,14313);

_wyn:=_zas:=null();
R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? R.find_key(7125)
|| _zas:=R.ref()
?};
{? R.find_key(7126)
|| _wyn:=R.ref()
?};
R.cntx_pop();

:: Aktualizacja tabeli kodów nieobecności chorobowych
KST_CHOR.cntx_psh();
KST_CHOR.index('KST_CHOR');
KST_CHOR.prefix();
{? _wyn & _zas & ~KST_CHOR.find_key(7126)
|| KST_CHOR.WYN:=_wyn;
   KST_CHOR.ZAS:=_zas;
   KST_CHOR.add()
?};
KST_CHOR.cntx_pop();
__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\akt_2042_19_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN, JASZAFRA [20.42]
:: OPIS: Aktualizacja 20.42_19: Okres izolacji - pandemia koronowirusa
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja 20.42_19: Okres izolacji'


\formuly_IZOLACJA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN, JASZAFRA [20.42]
:: OPIS: Aktualizacja 20.42_19 formuł płacowych o naliczanie składników izolacji.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='7125,7126';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,R,%1'[%255],',');
_err:=exec('formuly_import_iz','upgrade_2042',_tp,_rn,'formuly_txt_iz');

{? _err>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err]);
   -1
|| 1
?}


\formuly_import_iz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN, JASZAFRA [20.42]
:: OPIS: Funkcja do aktualizacji formuł płacowych.
::   WE: _a [ARRAY] - Tablica aktualizowanych typów formuł płacowych.
::       _b [ARRAY] - Tablica kodów aktualizowanych składników płacowych.
::       _c [STRING] - Nazwa formuły do importu formuł płacowych.
::   WY: _err (0/1) - Liczba błędów, które wystąpiły podczas importu.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;
_formula:=_c;
_err:=0;

FM.cntx_psh();
{! _ni:=1..obj_len(_tp)
|! FM.index('FORMNAZ');
   FM.prefix(exec('ref_firma','ustawienia'),_tp[_ni]);
   {? FM.size()>0
   || {! _mi:=1..obj_len(_rn)
      |! _fm:=form(exec(_formula,'upgrade_2042',_tp[_ni],#_rn[_mi]));
         {? _fm<>''
         || _ok:=1;
            _org:='';
            _found:=FM.find_tab(,'R','RN','=',#_rn[_mi]);
            {? _found
            || _org:=FM.memo_txt(,1,'F')
            || FM.blank();
               FM.TP:=_tp[_ni];
               FM.R:=__RUB.ref(#_rn[_mi]);
               FM.W:={? 'U,F,D'*_tp[_ni] || 'T' || 'N' ?};
               {? ~FM.add()
               || __UPG.msg('Utworzenie formuły typu "%1" dla składnika %2 nie powiodło się.'[_tp[_ni],_rn[_mi]]);
                  _err+=0;
                  _ok:=0
               ?}
            ?};
            {? _found & _fm=_org
            || __UPG.msg('Formuła typu "%1" dla składnika %2 nie wymaga aktualizacji.'[_tp[_ni],_rn[_mi]])
            |? _ok
            || FM.memo_set(_fm,'F');
               {? FM.memo_put(,'F')
               || {? _found
                  || __UPG.msg('Zaktualizowano formułę typu "%1" dla składnika %2.'[_tp[_ni],_rn[_mi]])
                  || __UPG.msg('Utworzono formułę typu "%1" dla składnika %2.'[_tp[_ni],_rn[_mi]])
                  ?}
               |? _found
               || __UPG.msg('Aktualizacja formuły typu "%1" dla składnika %2 nie powiodła się.'[_tp[_ni],_rn[_mi]]);
                  _err+=1
               ?}
            ?}
         ?}
      !}
   ?}
!};
FM.cntx_pop();
_err


\formuly_txt_iz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN, JASZAFRA [20.42]
:: OPIS: Zwraca treść formuły płacowej.
::   WE: _a [STRING] - typ formuły
::       _b [INTEGER] - numer rubryki
::   WY: treść formuły
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_tp:=_a;
_rn:=_b;

{? _rn=7125 & _tp<>'R'
:: ==================
:: Izolacja zas.
|| "
FUNKCJE.NK(7125)"

|? _rn=7126 & _tp<>'R'
:: ==================
:: Izolacja wynagr.
|| "
FUNKCJE.NK(7126)"


|? _rn=7125 & _tp='R'
:: =================
:: Izolacja zas.
|| "
'wyliczenie znajduje sie w formule: zasilki, wywołanie w 200';~~"

|| ""
?}


\formuly_IZOLACJA_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN, JASZAFRA [20.42]
:: OPIS: Aktualizacja formuł płacowych o naliczanie składników izolacji  - opis.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie formuł płacowych o naliczanie składnika Izolacja'


\KTRWP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Uaktualnienie wartości podziałów controllingowych dla planów amortyzacji
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('oSr');
oSr:=obj_new('TODO','OK');
oSr.TODO:=0;
oSr.OK:=0;
KTRWP.cntx_psh();
SRSR.cntx_psh();
_names:=KTRWP.names();
{? _names.first()
|| VAR_DEL.delete('iSr','nSr');
   iSr:=SRSR.ndx_tmp('',1,'IDADD',,);
   nSr:=SRSR.names();
   {!
   |? echo('Uaktualnienie podziałów dla planów amortyzacji: %1.'[_names.NAME]);
      KTRWP.use(_names.NAME);
      KTRWP.prefix();
      KTRWP.for_each("
         {? +KTRWP.SROD_ID=31
         || oSr.TODO+=1;
            {? nSr.first()
            || {!
               |? SRSR.use(nSr.NAME);
                  SRSR.index(iSr);
                  SRSR.prefix();
                  {? SRSR.find_key(KTRWP.SROD_ID)
                  || KTRWP.SROD_ID:=SRSR.uidref();
                     KTRWP.put();
                     oSr.OK+=1;
                     0
                  || nSr.next()
                  ?}
               !}
            ?}
         ?}
      ");
      _names.next()
   !};
   SRSR.ndx_drop(iSr);
   VAR_DEL.delete('iSr','nSr');
   echo()
?};
KTRWP.cntx_pop();
SRSR.cntx_pop();
__UPG.msg('Liczba podziałów do uaktualnienia: %1.'[$oSr.TODO]);
__UPG.msg('Liczba podziałów uaktualnionych: %1.'[$oSr.OK]);
VAR_DEL.delete('oSr');
1


\KTRWP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Uaktualnienie podziałów controllingowych dla planów amortyzacji
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie podziałów controllingowych dla planów amortyzacji.\n'
'Poprawka błędu: ER/WRT/XP/20.42/2104/0012'


\fiks_73
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: kod: ER/WRT/XP/20.42/2104/0020
::       JPK_V7 - ujęcie korekty podatku (VAT na plus) z wykorzystaniem ulgi na złe długi u wierzyciela
::----------------------------------------------------------------------------------------------------------------------
_fix:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','V7');
{? ISTDEF.first()
|| {!
   |? ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'KorektaPodstawyOpodt',);
      {? ISTDEFS.first() &
         (
            ISTDEFS.REGULY=$"{? VAT_PS.TYP='S' || '1' || '' ?}" |
            ISTDEFS.REGULY=$"{? VAT_PS.TYP='N' || '1' || '' ?}"
         )
      || ISTDEFS.REGULY:=$"{? -VAT_PS.TYP='n' || '1' || '' ?}";
         ISTDEFS.put();
         __UPG.msg('Poprawiono schemat: %1.'[ISTDEF.VER]);
         _fix:=1
      ?};
      ISTDEFS.cntx_pop();
      ISTDEF.next()
   !}
?};
ISTDEF.cntx_pop();
{? _fix=0
|| __UPG.msg('Schematy JPK_V7 są poprawne.')
?};
1


\fiks_73_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: kod: ER/WRT/XP/20.42/2104/0020
::       JPK_V7 - ujęcie korekty podatku (VAT na plus) z wykorzystaniem ulgi na złe długi u wierzyciela
::----------------------------------------------------------------------------------------------------------------------
'Naprawa schematów JPK_V7\n'
'ER/WRT/XP/20.42/2104/0020 - ujęcie korekty podatku (VAT na plus) z wykorzystaniem ulgi na złe długi u wierzyciela'


\m4prognoza
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Uaktualnienie znacznika K_WERSJE.CZY_SYS wersji controllingu o nazwie Prognoza
::       ER/WRT/XP/20.42/2104/0027 - Błąd agregacji danych - dane agregowane dla wersji Prognoza.
::----------------------------------------------------------------------------------------------------------------------
K_WERSJE.cntx_psh();
K_WERSJE.index('CZY_SYS'); K_WERSJE.prefix('T','N','Prognoza',);
{? K_WERSJE.first()
|| K_WERSJE.prefix();
   K_WERSJE.CZY_SYS:='N';
   K_WERSJE.put();
   __UPG.msg('Poprawiono wersję controllingu o nazwie: Prognoza.')
|| __UPG.msg(
      'Nie znaleziono wersji controllingu o nazwie: Prognoza.\n'
      'Naprawa jest zbędna.'
   )
?};
K_WERSJE.cntx_pop();
1


\m4prognoza_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Uaktualnienie znacznika K_WERSJE.CZY_SYS wersji controllingu o nazwie Prognoza
::       ER/WRT/XP/20.42/2104/0027 - Błąd agregacji danych - dane agregowane dla wersji Prognoza.
::----------------------------------------------------------------------------------------------------------------------
'Naprawa znacznika K_WERSJE.CZY_SYS wersji controllingu o nazwie: Prognoza\n'
'ER/WRT/XP/20.42/2104/0027 - Błąd agregacji danych - dane agregowane dla wersji Prognoza.'


\ustal_sciezka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: ER/WRT/XP/20.42/2105/0029
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('ustal_sciezka_typ','zs_def')
|| __UPG.msg('Wystąpił błąd podczas ponownego ustalania ścieżek zależności służbowych.');
   -1
|| __UPG.msg('Zaktualizowano ścieżki w zależnościach służbowych.');
   1
?}


\ustal_sciezka_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: ER/WRT/XP/20.42/2105/0029
::----------------------------------------------------------------------------------------------------------------------
'Weryfikacja ścieżek w zależnościach służbowych wszystkich typów.'


\ppl_plistrokzak_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [20.42]
:: OPIS: Kompilacja wydruku ppl_plistrokzak.rpm
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('[p]pl_plistrokzak');
__UPG.msg('Zaktualizowano wydruk ppl_plistrokzak.');
_result


\ppl_plistrokzak_akt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [20.42]
:: OPIS: Kompilacja wydruku ppl_plistrokzak.rpm - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizuje po poprawkach wydruk ppl_plistrokzak.rpm'


\formuly_784
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Aktualizacja formuł płacowych dla składnika 784.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='784';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,W,D,%1'[%255],',');
_err:=exec('formuly_import','upgrade_2042',_tp,_rn,'formuly_784_txt');
{? _err>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err]);
   -1
|| 1
?}


\formuly_784_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Zwraca treść formuły płacowej.
::   WE: _a [STRING] - typ formuły
::       _b [INTEGER] - numer rubryki
::   WY: treść formuły
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_tp:=_a;
_rn:=_b;

{? _rn=784
:: ==================
:: Koszty uzyskania
|| "
exec('KU','lista_licz')"
|| ""
?}


\formuly_784_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.42]
:: OPIS: Aktualizacja formuł płacowych.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja formuł płacowych dla KU'


\fst_set_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Kod: ER/WRT/XP/20.42/2105/0009 - Niespójność w danych. Brak przypisania środka do pracownika na podstawie osoby
::            Dodatkowa formułana wypade błędu podczas wykonywania zadań potransferowych
::----------------------------------------------------------------------------------------------------------------------
exec('fst_set_p','upgrade_2114')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:16:37 706ca8b779147663dafc0bb92076482750691b9e139fe3a99197cb424c0df208be46f7d3c5198800883818934c7bc05badc271a05692cebc2b77e3029af8556a84cbb814114d7f0e1425ba14c9df7fc9d2d6a30b7e959f9398bb90f8f477ace3a1041d289cbc74d61c617ab9f24577a2af135370f3891e64df05bd96b6a19db5

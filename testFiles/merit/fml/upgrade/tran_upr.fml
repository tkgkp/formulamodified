:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tran_upr.fml
:: Utworzony: 05.07.2018
:: Autor: [rr]
:: Systemy: Xpertis
::======================================================================================================================
:: Zawartość: Formuły wspomagające nadawanie uprawnień przy przejściu pomiędzy systemami Xpertis > Merit
::======================================================================================================================


\cfg_create
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy obiekt przechowujący konfigurację.
::   WE:
::   WY: tablica nazwana o strukturze
::       PSW - alias tabeli przechowująca zawartość pliku psw
::       GROUP - alias tabeli grup ochron
::       USERS - alias tabeli użytkowników
::       USRGRP - alias tabeli użytkowników grupy
::       ROLE - alias tabeli ról
::       GROLE - alias tabeli ról grupy
::       USROLE - alias tabeli ról użytkownika
::----------------------------------------------------------------------------------------------------------------------
: wartość zwracana
_cfg:=obj_new(
:  zawartość psw
   'PSW',
:  tabele konfiguracji
   'GROUP','USERS','USRGRP',
   'ROLE','GROLE','USROLE',
:  "zmienne" pomocnicze
   'OBJECT',
:  "metody" pomocnicze
   'usr_role',
   'grp_role',
   'add_role',
   'usr_prot',
   'is_mem'
);

: informacje z pliku psw
_cfg.PSW:=tab_tmp(2,
   'REF','TREE_REF','Nadrzędny',
   'NUM','REAL','Numer',
   'TXT','STRING[255]','Tekst'
);

: grupy ochron
_cfg.GROUP:=tab_tmp(1,
   'GROUP','STRING[20]','Symbol grupy',
   'DESC','STRING[100]','Opis grupy'
);

: użytkownicy
_cfg.USERS:=tab_tmp(1,
   'USERS','STRING[100]','Symbol użytkownika',
   'DESC','STRING[30]','Opis użytkownika',
   'PROT','STRING[1]','Ochrona modyfikacji',
   'WWW','STRING[1]','Użytkownik internetowy',
   'EMAIL','STRING[60]','Adres poczty elektronicznej',
   'ACTIVE','STRING[1]','Aktywny',
   'SUPER','STRING[1]','Użytkownik specjalny?',
   'PASSWORD','STRING[40]','Hasło',
   'WEBPASS','STRING[40]','Hasło WWW',
   'REF','STRING[16]','Wskazanie użytkownika'
);
: proste wartości początkowe pól
_cfg.USERS.fld_fml('PROT','BLANK',"'N'");
_cfg.USERS.fld_fml('WWW','BLANK',"'N'");
_cfg.USERS.fld_fml('ACTIVE','BLANK',"'N'");

: użytkownicy grupy
_cfg.USRGRP:=tab_tmp(2,
   'GROUP','STRING[20]','Symbol grupy',
   'USERS','STRING[100]','Symbol użytkownika',
   'DESC','STRING[100]','Opis użytkownika',
   'PROT','STRING[1]','Ochrona modyfikacji',
   'WWW','STRING[1]','Użytkownik internetowy'
);
: proste wartości początkowe pól
_cfg.USRGRP.fld_fml('PROT','BLANK',"'N'");
_cfg.USRGRP.fld_fml('WWW','BLANK',"'N'");

: role
_cfg.ROLE:=tab_tmp(1,
   'ROLE','STRING[100]','Nazwa roli'
);

: role grupy
_cfg.GROLE:=tab_tmp(2,
   'GROUP','STRING[20]','Symbol grupy',
   'ROLE','STRING[100]','Nazwa roli',
   'ON','STRING[1]','Aktywna'
);
: proste wartości początkowe pól
_cfg.GROLE.fld_fml('ON','BLANK',"'T'");

: role użytkownika
_cfg.USROLE:=tab_tmp(2,
   'USERS','STRING[100]','Symbol użytkownika',
   'ROLE','STRING[100]','Nazwa roli',
   'ON','STRING[1]','Aktywna'
);
: proste wartości początkowe pól
_cfg.USROLE.fld_fml('ON','BLANK',"'T'");

: przypisanie ról użykownikowi
_cfg.usr_role:="
   _cnt:=0;
   {! _ni:=2.._
   |! .USROLE.clear();
      {? ~.USROLE.find_tab(1,
            'USERS',,'=',_a,
            'ROLE',,'=',_[_ni]
         )
      || .USROLE.blank();
         .USROLE.USERS:=_a;
         .USROLE.ROLE:=_[_ni];
         _cnt+=.USROLE.add()
      ?}
   !};
   {? _cnt<>0
   || .usr_prot(_a,'T')
   ?}
";

: przypisanie ról grupie
_cfg.grp_role:="
   _cnt:=0;
   {! _ni:=2.._
   |! .GROLE.clear();
      {? ~.GROLE.find_tab(1,
            'GROUP',,'=',_a,
            'ROLE',,'=',_[_ni]
         )
      || .GROLE.blank();
         .GROLE.GROUP:=_a;
         .GROLE.ROLE:=_[_ni];
         _cnt+=.GROLE.add()
      ?}
   !}
";

: przypisanie ról
_cfg.add_role:=_cfg.usr_role;

: zmiana statusu ochrony użytkownika
_cfg.usr_prot:="
   .USERS.cntx_psh();
   .USERS.clear();
   {? .USERS.find_key(_a,)
   || .USERS.PROT:=_b;
      .USERS.put()
   ?};
   .USERS.cntx_pop();
   .USRGRP.cntx_psh();
   .USRGRP.clear();
   .USRGRP.f_set(,,'USERS=\\\':_a\\\' and PROT<>\\\':_b\\\'',_a,_b);
   _loop:=.USRGRP.f_first();
   {!
   |? _loop
   |! .USRGRP.PROT:=_b;
      .USRGRP.put();
      _loop:=.USRGRP.f_next()
   !};
   .USRGRP.f_clear();
   .USRGRP.cntx_pop()
";

: czy użytkownik należy do grupy (0/1)
_cfg.is_mem:="
   _ret:=0;
   .USRGRP.cntx_psh();
   .USRGRP.clear();
   _ret:=.USRGRP.find_tab(,'USERS',,'=',_a);
   .USRGRP.cntx_pop();
   _ret
";

_cfg


\cfg_read
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Wczytuje konfigurację bazową i zmiany wprowadzone przez użytkownika.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::       _b [STRING] - nazwa pliku psw
::       _c [INTEGER] - czy odczyt zgodnie z pth
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_name:=_b;
_path:={? var_pres('_c')=type_of(0) || _c<>0 || 1 ?};
_PSW:=_cfg.PSW;
_GRP:=_cfg.GROUP;
_USR:=_cfg.USERS;
_UGR:=_cfg.USRGRP;

: wczytaj zawartość pliku psw
exec('psw_read','tran_upr',_PSW,_name,_path);

:_name:='tran_upr.dfg';
_name:={? REF.WFIRM || 'tran_upr_'+FIRMA.SYMBOL+'.dfg' || 'tran_upr.dfg' ?};
: wczytaj plik stanu konfiguracji
{? (_file:=fopen(_name,'r',1))>0
|| {!
   |? (_line:=fread(_file))<>'\n'
   |! ($_line)(_cfg)
   !};
   fclose(_file);
   return()
?};

: grupy ochron
_tag:='name: ';
{? _PSW.find_tab(,'TXT',,'=','[protection keys]')
|| _PSW.f_set(,,'REF=:_a and TXT like(\':_b%\')',#_PSW.ref(),_tag);
   _loop:=_PSW.f_first();
   {!
   |? _loop
   |! _data:=spli_str(_PSW.TXT,',');
      _GRP.GROUP:=form((+_tag)-_data[1]);
      _GRP.DESC:={? obj_len(_data)>3 || form(_data[4]) || '' ?};
      _GRP.add();
      obj_del(_data);
      _loop:=_PSW.f_next()
   !}
?};

: użytkownicy
{? _PSW.find_tab(,'TXT',,'=','[users]')
|| _PSW.f_set(,,'REF=:_a',#_PSW.ref());
   _loop:=_PSW.f_first();
   {!
   |? _loop
   |! _data:=spli_str(_PSW.TXT,',');
      _USR.USERS:=form(_data[1]);
      _USR.DESC:=form(_data[2]);
      _USR.PASSWORD:=form(_data[6]);
      _USR.add();
      obj_del(_data);
      _loop:=_PSW.f_next()
   !}
?};

: uzupełnienie listy użytkowników
: użytkownicy bez przypisanej grupy
USERS.cntx_psh();
USERS.index('USR_KKOD');
USERS.prefix();
_loop:=USERS.first();
{!
|? _loop
|! {? _USR.find_key(USERS.KOD,)
   || _USR.ACTIVE:='T';
      _USR.REF:=$USERS.ref();
      _USR.put(1)
   || _USR.blank();
      _USR.USERS:=USERS.KOD;
      _USR.DESC:=USERS.DANE;
      _USR.ACTIVE:='T';
      _USR.REF:=$USERS.ref();
      _USR.add(1)
   ?};
   _loop:=USERS.next()
!};
USERS.cntx_pop();
: użytkownicy internetowi
I_USERS.cntx_psh();
I_USERS.index('LOGIN');
I_USERS.prefix();
_loop:=I_USERS.first();
{!
|? _loop
|! {? I_USERS.USERS<>null
   || {? _USR.find_tab(,'REF',,'=',$I_USERS.USERS)
      || _USR.ACTIVE:=I_USERS.AKT_PORT;
         _USR.EMAIL:=I_USERS.EMAIL;
         _USR.WEBPASS:=I_USERS.HASLO;
         _USR.WWW:='T';
         _USR.put()
      ?}
   |? ~_USR.find_key(I_USERS.LOGIN,)
   || _USR.blank();
      _USR.USERS:=I_USERS.LOGIN;
      _USR.DESC:=
         {? I_USERS.OSOBA<>null || I_USERS.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE
         |? I_USERS.KH<>null || I_USERS.KH().NAZ_P
         || I_USERS.EMAIL
         ?};
      _USR.ACTIVE:=I_USERS.AKT_PORT;
      _USR.EMAIL:=I_USERS.EMAIL;
      _USR.WEBPASS:=I_USERS.HASLO;
      _USR.WWW:='T';
      _USR.add()
   ?};
   _loop:=I_USERS.next()
!};
I_USERS.cntx_pop();

: użytkownicy grupy
_tag:='group: ';
{? _PSW.find_tab(,'TXT',,'=','[group members]')
|| _PSW.f_set(,,'REF=:_a and TXT like(\':_b%\')',#_PSW.ref(),_tag);
   _loop:=_PSW.f_first();
   {!
   |? _loop
   |! _group:=form((+_tag)-_PSW.TXT);
      _PSW.cntx_psh();
      _PSW.prefix(#_PSW.ref());
      _loop:=_PSW.first();
      {!
      |? _loop
      |! _UGR.blank();
         _UGR.GROUP:=_group;
         _UGR.USERS:=_PSW.TXT;
         {? _USR.find_tab(,'USERS',,'=',_UGR.USERS)
         || _UGR.DESC:=_USR.DESC
         ?};
         _UGR.add();
         _loop:=_PSW.next()
      !};
      _PSW.cntx_pop();
      _loop:=_PSW.f_next()
   !}
?};

: uzupełnij listę ról
exec('fill_role','tran_upr',_cfg);

: uzupełnij proponowane role grup
exec('fill_grole','tran_upr',_cfg);

: przypisz użytkownikom role na podstawie grup
exec('update_roles','tran_upr',_cfg);

: uzupełnij proponowane role użytkowników
exec('fill_usrole','tran_upr',_cfg);
exec('fill_webrole','tran_upr',_cfg);

: porządki
_PSW.f_clear();

_cfg


\fill_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Uzupełnia listę ról.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;

_add:="
   _a.ROLE.ROLE:=_b;
   _a.ROLE.add()
";
_add(_cfg,'Administrator');
_add(_cfg,'Administrator mobilnego magazynu');
_add(_cfg,'Akceptujący budżet');
_add(_cfg,'Akceptujący faktury I poziom');
_add(_cfg,'Akceptujący faktury II poziom');
_add(_cfg,'Akceptujący umowy handlowe');
_add(_cfg,'Akceptujący wnioski pracownicze');
_add(_cfg,'Dokonujący oceny pracowniczej');
_add(_cfg,'Dyrektor HR');
_add(_cfg,'Dyrektor finansowy');
_add(_cfg,'Dyrektor produkcji');
_add(_cfg,'Dyrektor sprzedaży');
_add(_cfg,'Dyrektor zakupów');
_add(_cfg,'E-brygadzista');
_add(_cfg,'E-pracownik');
_add(_cfg,'Generujący zadania');
_add(_cfg,'Kasjer');
_add(_cfg,'Kierownik ds płac');
_add(_cfg,'Kontroler');
_add(_cfg,'Koordynator obiegu faktur');
_add(_cfg,'Koordynator obiegu wniosków');
_add(_cfg,'Księgowy');
_add(_cfg,'Logistyk');
_add(_cfg,'Magazynier');
_add(_cfg,'MeritHUB - użytkownik');
_add(_cfg,'Obsługa Businesslink');
_add(_cfg,'Opiekun środków trwałych');
_add(_cfg,'Opiekun wyposażenia');
_add(_cfg,'Planista produkcji');
_add(_cfg,'Pracownik');
_add(_cfg,'Pracownik - redakcja własnego kalendarza');
_add(_cfg,'Pracownik - redakcja własnego wykonania');
_add(_cfg,'Pracownik - wypełnianie ankiet');
_add(_cfg,'Pracownik - zatwierdzanie własnego kalendarza');
_add(_cfg,'Pracownik - zatwierdzanie własnego wykonania');
_add(_cfg,'Produkcja');
_add(_cfg,'Przegladający Przelewy elektroniczne');
_add(_cfg,'Przeglądający Finanse');
_add(_cfg,'Przeglądający Kadry');
_add(_cfg,'Przeglądający Kasa');
_add(_cfg,'Przeglądający Magazyny');
_add(_cfg,'Przeglądający Płace');
_add(_cfg,'Przeglądający PPK');
_add(_cfg,'Przeglądający Produkcja');
_add(_cfg,'Przeglądający Sprzedaż');
_add(_cfg,'Przeglądający Środki trwałe');
_add(_cfg,'Przeglądający Zakupy');
_add(_cfg,'Przeglądający Zleceniobiorców');
_add(_cfg,'Przełożony');
_add(_cfg,'Przełożony - planowanie czasu pracy');
_add(_cfg,'Przełożony - rozliczenie czasu pracy');
_add(_cfg,'Rejestrujący karty drogowe');
_add(_cfg,'Rejestrujący wykonania produkcji');
_add(_cfg,'Sekretariat');
_add(_cfg,'Specjalista HR');
_add(_cfg,'Specjalista ds. bezpieczeństwa danych');
_add(_cfg,'Specjalista ds. kadr');
_add(_cfg,'Specjalista ds. ocen pracowniczych');
_add(_cfg,'Specjalista ds. odpadów');
_add(_cfg,'Specjalista ds. planowania czasu pracy');
_add(_cfg,'Specjalista ds. płac');
_add(_cfg,'Specjalista ds. płatności');
_add(_cfg,'Specjalista ds. PPK');
_add(_cfg,'Specjalista ds. projektów');
_add(_cfg,'Specjalista ds. reklamacji');
_add(_cfg,'Specjalista ds. rozliczania czasu pracy');
_add(_cfg,'Specjalista ds. szkoleń');
_add(_cfg,'Specjalista ds. środków trwałych');
_add(_cfg,'Specjalista ds. transportu');
_add(_cfg,'Specjalista ds. windykacji');
_add(_cfg,'Specjalista ds. wyposażenia');
_add(_cfg,'Specjallista ds. zleceniobiorców');
_add(_cfg,'Specjalista ds. umów cyklicznych');
_add(_cfg,'Specjalista ds. zasobów');
_add(_cfg,'Sprzedawca');
_add(_cfg,'Technolog');
_add(_cfg,'Uzupełniający budżet');
_add(_cfg,'Wysyłający na delegacje');
_add(_cfg,'Zakupy');
_add(_cfg,'Portal HR - Pracownik');
_add(_cfg,'Portal HR - Przełożony (bez wynagrodzeń)');
_add(_cfg,'Portal HR - Przełożony (z wynagrodzeniami)');
_add(_cfg,'Portal HR - Przełożony akceptujący (wnioski bez wynagrodzeń)');
_add(_cfg,'Portal HR - Przełożony akceptujący (wnioski z wynagrodzeniami)');
_add(_cfg,'Portal HR - Akceptujący wnioski o dofinansowanie');
_add(_cfg,'Portal SEOD - Akceptujący wnioski');
_add(_cfg,'Portal SEOD - Użytkownik');
~~


\fill_grole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Przypisuje role do grup ochron.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_GRP:=_cfg.GROUP;

: dla wszystkich grup pobierz informacje o rolach
_cfg.add_role:=_cfg.grp_role;
_GRP.cntx_psh();
_GRP.prefix();
_loop:=_GRP.first();
{!
|? _loop
|! {? var_pres('OBJECT',_cfg)>=100
   || obj_del(_cfg.OBJECT)
   ?};
   exec('role4grp','tran_upr',_cfg,_GRP.GROUP);
   _loop:=_GRP.next()
!};
_GRP.cntx_pop();
_cfg.add_role:=_cfg.usr_role;
{? var_pres('OBJECT',_cfg)>=100
|| obj_del(_cfg.OBJECT)
?};
~~


\fill_usrole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Uzupełnia proponowane role dla użytkowników.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_PSW:=_cfg.PSW;
_USR:=_cfg.USERS;

: otwórz bibliotekę
_name:='tran_upr';
_ext:='.fml';
{? (_file:=fopen(_name+_ext,'r',1))<=0
|| FUN.emsg('Błąd dostępu do pliku "'+_name+_ext+'".');
   return()
?};
: utwórz listę formuł odpowiedzialnych za proponowanie ról użytkowników
_FML:=tab_tmp(1,'NAME','STRING[15]','Nazwa formuły');
{!
|? (_line:=fread(_file))<>'\n'
|! {? _line*'\\sub_'=1
   || _FML.NAME:=1-_line;
      _FML.add()
   ?}
!};
fclose(_file);

: wywoływanie formuł proponujących role użytkownika
{? _PSW.find_tab(,'TXT',,'=','[sub-users]')
|| _tag:='subsystem: ';
   _ref:=#_PSW.ref();
   _PSW.cntx_psh();
   _PSW.prefix(_ref);
   _loop:=_PSW.first();
   {!
   |? _loop
   |! {? _PSW.TXT*_tag=1
      || {? var_pres('OBJECT',_cfg)>=100
         || obj_del(_cfg.OBJECT)
         ?};
         _fml:='sub_'+form((+_tag)-_PSW.TXT);
         {? _FML.find_key(_fml,)
         || _ref:=#_PSW.ref();
            _PSW.cntx_psh();
            _PSW.prefix(_ref);
            _loop:=_PSW.first();
            {!
            |? _loop
            |! _usr:=_PSW.TXT;
               {? _USR.find_key(_usr,)
               || exec(_fml,_name,_cfg,_usr)
               ?};
               _loop:=_PSW.next()
            !};
            _PSW.cntx_pop()
         ?}
      ?};
      _loop:=_PSW.next()
   !};
   _PSW.cntx_pop();
   {? var_pres('OBJECT',_cfg)>=100
   || obj_del(_cfg.OBJECT)
   ?}
?};
~~


\fill_webrole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Uzupełnia proponowane role dla użytkowników internetowych.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_USR:=_cfg.USERS;

: uzyskaj role Merit dla wszystkich ról użytkowników internetowych
I_USROLE.cntx_psh();
I_USERS.cntx_psh();
I_USERS.index('LOGIN');
I_USERS.prefix();
_loop:=I_USERS.first();
{!
|? _loop
|! {? var_pres('OBJECT',_cfg)>=100
   || obj_del(_cfg.OBJECT)
   ?};
   I_USROLE.index('I_DISP');
   I_USROLE.prefix(REF.FIRMA,I_USERS.ref());
   _loop:=I_USROLE.first();
   {!
   |? _loop
   |! {? _USR.find_tab(,'USERS',,'=',I_USERS.LOGIN)
:        uzyskaj rolę Merit dla roli użytkownika internetowego
      || exec('sub_portal','tran_upr',_cfg,I_USERS.LOGIN,I_USROLE.I_ROLE().KOD)
      ?};
      _loop:=I_USROLE.next()
   !};
   _loop:=I_USERS.next()
!};
I_USERS.cntx_pop();
I_USROLE.cntx_pop();
{? var_pres('OBJECT',_cfg)>=100
|| obj_del(_cfg.OBJECT)
?};
~~


\cfg_write_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Zapisuje zmiany wprowadzone przez użytkownika.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_GRO:=_cfg.GROLE;
_URO:=_cfg.USROLE;

:_name:='tran_upr.dfg';
_name:={? REF.WFIRM || 'tran_upr_'+FIRMA.SYMBOL+'.dfg' || 'tran_upr.dfg' ?};
: otwórz plik stanu konfiguracji
{? (_file:=fopen(_name,'w',1))<=0
|| FUN.emsg('Błąd dostępu do pliku "'+_name+'".');
   return()
?};

: zapisz zawartość wszystkich tabel konfiguracji
exec('cfg_write_tab','tran_upr',_a,'ROLE',_file);
exec('cfg_write_tab','tran_upr',_a,'GROUP',_file);
exec('cfg_write_tab','tran_upr',_a,'GROLE',_file);
exec('cfg_write_tab','tran_upr',_a,'USRGRP',_file);
exec('cfg_write_tab','tran_upr',_a,'USERS',_file);
exec('cfg_write_tab','tran_upr',_a,'USROLE',_file);

: porządki
fclose(_file);
~~


\cfg_write_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Zapisuje zawartość tabeli.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::       _b [STRING] - nazwa komórki zawierającej alias tabeli
::       _c [INTEGER] - uchwyt pliku
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_cfg:=_a;
_tab:=_b;
_fhd:=_c;

_TAB:=($('_a.'+_tab))(_cfg);

_line:='\'_TAB:=_a.'+_tab+';';
{! _ni:=1.._TAB.fld_num()
|! _acr:=_TAB.fld_acr(_ni);
   _line+='_TAB.'+_acr+':=\\\'\'+_a.'+_tab+'.'+$_acr+'+\'\\\';'
!};
_line+='_TAB.add()\'';

_TAB.cntx_psh();
_TAB.prefix();
_loop:=_TAB.first();
{!
|? _loop
|! fwrite(_fhd,($_line)(_a));
   _loop:=_TAB.next()
!};
_TAB.cntx_pop();
~~


\cfg_reload
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Wczytuje zawartość wskazanego pliku PSW.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
:: ~OST: INFILECHOOSER
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;

{? ~FUN.ask(
      'Wczytanie pliku spowoduje usunięcie danych.\n'+
      'Czy na pewno kontynuować działanie?'
   )
|| return()
?};

_name:=app_info('def_name')+'.psw';
_filter:='Pliki PSW (*.psw)|*.psw';
ctr_set('!application','filechooser','reset');
ctr_set('!application','filechooser','setDialogTitle','Otwórz'@);
ctr_set('!application','filechooser','addDefaultFilter',_filter,'psw');
ctr_set('!application', 'filechooser','setAcceptAllFileFilterUsed',1);
ctr_set('!application','filechooser','setMultiSelectionEnabled',0);
ctr_set('!application','filechooser','setFileSelectionMode','FILES_ONLY');
ctr_set('!application','filechooser','setCurrentDirectory',pth_dir(_name));
ctr_set('!application','filechooser','setSelectedFile',_name);
_name:={? ctr_call('!application','filechooser','showOpenDialog')
       || ctr_call('!application','filechooser','getSelectedFile')
       || ''
       ?};

{? _name=''
|| return()
?};
_name:='@'+_name;
{? ~fexists(_name,0)
|| return()
?};

: usuń plik zapisu sesji
:_save:='tran_upr.dfg';
_save:={? REF.WFIRM || 'tran_upr_'+FIRMA.SYMBOL+'.dfg' || 'tran_upr.dfg' ?};
{? fexists(_save,1)
|| ferase(_save,1)
?};

_ttype:=type_of(SYSLOG);
{! _ii:=1..obj_len(_cfg)
|! {? type_of(_cfg[_ii])=_ttype
   || _cfg[_ii].prefix();
      _cfg[_ii].erase()
   ?}
!};

exec('cfg_read','tran_upr',_cfg,_name,0);
_cfg.GROUP.first();

FUN.info('Zakończono przygotowanie konfiguracji.');
~~


\export_dat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy i wyświetla okienko zawierające dane do przeniesienia do arkuszy Excel.
::       Zgodnie z wymaganiami dane nie są eksportowane do pliku CSV ale prezentowane w okienku.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;

: użytkownicy
_USER:=tab_tmp(2,
   'CODE','STRING[10]','Symbol użytkownika',
   'SUPER','STRING[1]','Użytkownik specjalny?',
   'PASSWORD','STRING[40]','Hasło',
   'WEBPASS','STRING[40]','Hasło WWW',
   'INET','STRING[100]','Login dostępu przez WWW',
   'DESC','STRING[100]','Nazwa',
   'EMAIL','STRING[60]','Adres poczty elektronicznej',
   'TEL','STRING[30]','Telefon',
   'ACTIVE','STRING[1]','Aktywny',
   'TRUEDESC','STRING[1]','Opis użytkownika'
);

: role użytkowników
_UROL:=tab_tmp(2,
   'CODE','STRING[10]','Symbol użytkownika',
   'INET','STRING[100]','Login dostępu przez WWW',
   'ROLE','STRING[100]','Nazwa roli',
   'DATA_OD','DATE','Obowiązuje od (data)',
   'CZAS_OD','TIME','Obowiązuje od (czas)'
);

_cfg.USROLE.cntx_psh();
_cfg.USROLE.prefix();
_loop:=_cfg.USERS.first();
_data_od:=date();
_czas_od:=time();
{!
|? _loop
|!
:  przenosimy tylko aktywnych użytkowników
:  realizacja zgłoszenia 1538643386000000
   {? _cfg.USERS.ACTIVE='T'
   || _USER.blank();
      {? _cfg.USERS.WWW<>'T'
      || _USER.CODE:=-_cfg.USERS.USERS
      || _USER.INET:=_cfg.USERS.USERS
      ?};
      {? _cfg.USERS.REF<>''
      || _USER.CODE:=-_cfg.USERS.USERS
      ?};
      _USER.DESC:=_cfg.USERS.DESC;
      _USER.SUPER:='N';
      _USER.PASSWORD:=_cfg.USERS.PASSWORD;
      _USER.WEBPASS:=_cfg.USERS.WEBPASS;
      _USER.EMAIL:=_cfg.USERS.EMAIL;
      _USER.ACTIVE:=_cfg.USERS.ACTIVE;
      {? _USER.add()
      || _UROL.CODE:=_USER.CODE;
         _UROL.INET:=_USER.INET;
         _UROL.DATA_OD:=_data_od;
         _UROL.CZAS_OD:=_czas_od;
         _cfg.USROLE.f_set(,,'USERS=\':_a\'',_cfg.USERS.USERS);
         _loop:=_cfg.USROLE.f_first();
         {!
         |? _loop
         |! _UROL.ROLE:=_cfg.USROLE.ROLE;
            _UROL.add();
            _loop:=_cfg.USROLE.f_next()
         !}
      ?}
   ?};
   _loop:=_cfg.USERS.next()
!};
_cfg.USROLE.f_clear();
_cfg.USROLE.cntx_pop();

: okienko z listą użytkowników
_wnd:=_USER.mk_sel(,'T',,'#exp_upr_user',,,,,'U');
_USER.win_fld(_wnd,,'ACTIVE',,,-3,,,,,,2,,"'T'","'N'");
_USER.win_fld(_wnd,,'CODE',,,-10);
_USER.win_fld(_wnd,,'INET',,,-20);
_USER.win_fld(_wnd,,'DESC',,,-30);
_USER.win_fld(_wnd,,'EMAIL',,,-30);
_USER.win_fld(_wnd,,'TEL',,,-5);
_USER.win_fld(_wnd,,'SUPER',,,-3);
_USER.win_fld(_wnd,,'PASSWORD',,,-40);
_USER.win_fld(_wnd,,'WEBPASS',,,-40);
_USER.win_fld(_wnd,,'TRUEDESC',,,-20);
_USER.win_act(_wnd,,'Szukaj');
_USER.win_act(_wnd,,'Kolejność');
_USER.win_sel(_wnd);

: okienko z listą ról użytkowników
_wnd:=_UROL.mk_sel(,'T',,'#exp_upr_urol',,,,,'U');
_UROL.win_fld(_wnd,,'CODE',,,-10);
_UROL.win_fld(_wnd,,'INET',,,-20);
_UROL.win_fld(_wnd,,'ROLE',,,-63);
_UROL.win_fld(_wnd,,'DATA_OD',,,-20);
_UROL.win_fld(_wnd,,'CZAS_OD',,,-20);
_UROL.win_act(_wnd,,'Szukaj');
_UROL.win_act(_wnd,,'Kolejność');
_UROL.win_sel(_wnd);

: główne okienko do kopiowania danych do Excel
_wnd:=_USER.grp_make('Uprawnienia',,'#exp_upr_main');
_USER.grp_sel(_wnd,_USER,_USER.win_sel('?'),'Użytkownicy',,,,,,,,,'maximized');
_USER.grp_sel(_wnd,_UROL,_UROL.win_sel('?'),'Role użytkowników',,,,,,,,,'maximized');
_USER.win_sel(_wnd);

: pokaż dane
_USER.first();
_UROL.first();
_USER.select();
~~


\export_csv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Zapisuje w pliku csv aktywne role użytkowników.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY:
:: ~OST: INSYSEXEC
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;

: aktywne role użytkowników
_OUT:=sql(
   'select :_b.USERS as CODE, :_b.DESC as DESCR, :_a.ROLE '+
   'from :_a join :_b using(:_a.USERS,:_b.USERS) '+
   'where :_a.ON=\'T\' '+
   'order by CODE, DESCR, ROLE',
   _cfg.USROLE,_cfg.USERS
);

_name:='usr_role.csv';
: zapisz role do pliku csv
_OUT.export(_name,,,'UTF-8',,
   'CODE',,1,,
   'DESCR',,2,,
   'ROLE',,3,
);

: pokaż zawartość
sys_exec(_name);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Wczytuje z pliku csv aktywne role użytkowników.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_name:='usr_role.csv';

{? ~fexists(_name,1)
|| FUN.emsg('Nie znaleziono pliku "%1".'@[_name]);
   return()
?};

: bufor danych
_BUF:=tab_tmp(2,
   'USER',  'STRING[20]', 'Symbol użytkownika'@,
   'ROLE',  'STRING[100]','Nazwa roli'@,
   'DESC',  'STRING[100]','Opis użytkownika'@,
   'TEST',  'INTEGER',    'Liczba komunikatów'@,
   'INFO',  'SYS_MEMO',   'Komentarz'@,
   'USER_E','INTEGER',    'Użytkownik istnieje [0/1/2]'@,
   'USER_R','STRING[16]', 'Wskazanie użytkownika'@,
   'ROLE_E','INTEGER',    'Rola istnieje [0/1]'@,
   'ROLE_R','STRING[16]', 'Wskazanie roli'@,
   'DATA_E','INTEGER',    'Dane istnieją [0/1]'@,
   'DATA_R','STRING[16]', 'Wskazanie danych'@
);

: wczytaj dane z pliku wymiany
{? ~_BUF.import(_name,,,,'UTF-8',,
      'USER',,1,,
      'DESC',,2,,
      'ROLE',,3,
   )
|| return()
?};

B_USRROL.cntx_psh();
B_USRROL.index('WER');
B_USRROL.prefix(REF.FIRMA);
B_ROLE.cntx_psh();
B_ROLE.index('UNIK');
B_ROLE.prefix(REF.FIRMA);
B_ROLE.prefix();
USERS.cntx_psh();
USERS.index('USR_KKOD');
USERS.prefix();

_BUF.cntx_psh();
_loop:=_BUF.first();
{!
|? _loop
|! _info:='';
   {? USERS.find_tab(,'KOD',,'=',_BUF.USER)
   || _BUF.USER_R:=$USERS.ref();
      _BUF.USER_E:=1
   |? USERS.find_tab(,'WEBLOGIN',,'=',_BUF.USER)
   || _BUF.USER_R:=$USERS.ref();
      _BUF.USER_E:=2;
      _BUF.TEST+=1;
      _info+=
         'Znaleziono użytkownika na podstawie identyfikatora '
         'dla dostępu przez przeglądarkę internetową.\n'
   |? USERS.find_tab(,'DANE',,'=',_BUF.DESC)
   || _BUF.USER_R:=$USERS.ref();
      _BUF.USER_E:=2;
      _BUF.TEST+=1;
      _info+='Znaleziono użytkownika na podstawie opisu.\n'
   || _BUF.TEST+=1;
      _info+='Nie znaleziono użytkownika.\n'
   ?};
   {? B_ROLE.find_tab(,'NAME',,'=',_BUF.ROLE)
   || _BUF.ROLE_R:=$B_ROLE.ref();
      _BUF.ROLE_E:=1
   || _BUF.TEST+=1;
      _info+='Nie znaleziono roli.\n'
   ?};
   {? B_USRROL.find_tab(,
         'USERS','KOD','=',_BUF.USER,
         'B_ROLE','NAME','=',_BUF.ROLE
      )
   || _BUF.DATA_R:=$B_USRROL.ref();
      _BUF.DATA_E:=1
   ?};
   _BUF.put();
   {? _info<>''
   || _BUF.memo_set(_info,'INFO');
      _BUF.memo_put()
   ?};
   _loop:=_BUF.next()
!};
_BUF.cntx_pop();

: dodaj interfejs użytkownika
_wnd:=_BUF.mk_sel('Role użytkowników'@,,,'imp_tran_upr',,,,,'U');
_BUF.win_fld(_wnd,,'USER_E',,,-3,,,,,,2,,"1","0","2");
_BUF.win_fld(_wnd,,'ROLE_E',,,-3,,,,,,2,,"1","0");
_BUF.win_fld(_wnd,,'DATA_E',,,-3,,,,,,2,,"1","0");
_BUF.win_fld(_wnd,,'USER',,,20);
_BUF.win_fld(_wnd,,'DESC',,,40);
_BUF.win_fld(_wnd,,'ROLE',,,50);
_BUF.win_fld(_wnd,,'TEST',,,-3);
_BUF.win_fld(_wnd,,'INFO',,,50);

_BUF.win_act(_wnd,,'Formuła','Wprowadź'@,,,
   "  _BUF:=cur_tab(1,1);
      _BUF.DATA_E=0 & USERS.seek(_BUF.USER_R) & B_ROLE.seek(_BUF.ROLE_R)
   ",
   "  _BUF:=cur_tab(1,1);
      B_USRROL.clear();
      B_USRROL.blank();
      B_USRROL.B_ROLE:=B_ROLE.ref();
      B_USRROL.USERS:=USERS.ref();
      B_USRROL.FIRMA:=REF.FIRMA;
      B_USRROL.U1_KOD:=exec('username','#users');
      B_USRROL.U1_DANE:=userdata();
      {? B_USRROL.add()
      || _BUF.DATA_R:=$B_USRROL.ref();
         _BUF.DATA_E:=1;
         _BUF.put()
      ?}
   ",
   1,1
);
_BUF.win_act(_wnd,,'Rekord',,,,
   "  _BUF:=cur_tab(1,1);
      _wnd:=cur_win(1,1);
      {? _BUF.sel_size()>0
      || _BUF.actions_grayed(_wnd);
         return()
      ?};
      {? _a=0
      || return()
      ?};
      _BUF.actions_grayed(_wnd,
         {? _BUF.DATA_E<>0 | ~USERS.seek(_BUF.USER_R) | ~B_ROLE.seek(_BUF.ROLE_R)
         || 'W'
         || ''
         ?}
      );
      ~~
   "
);
_BUF.win_act(_wnd,,'Szukaj');
_BUF.win_act(_wnd,,'Kolejność');
_BUF.win_act(_wnd,,'Wyświetl',,,,"cur_tab(1,1).memo_view(,'INFO')");

_BUF.win_btn(_wnd,'text=%1,panel=right'['Wprowadź'@],'menu:W');

_BUF.win_sel(_wnd);
_BUF.select();

: porządki
obj_del(_BUF);
USERS.cntx_pop();
B_ROLE.cntx_pop();
B_USRROL.cntx_pop();
~~


\start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy i wyświetla panel z proponowanymi rolami użytkowników.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
:: funkcja niedostępna w przeglądarce
|| FUN.emsg(exec('interm_nacc_msg','#system'));
   return()
?};

: utwórz i wczytaj konfigurację
_name:=app_info('def_name')+'.psw';
_cfg:=exec('cfg_create','tran_upr');
exec('cfg_read','tran_upr',_cfg,_name);

VAR_DEL.delete('__EXP_CFG');
__EXP_CFG:=_cfg;

_PSW:=_cfg.PSW;
: Zawartość pliku psw (okienko hierarchiczne bez możliwości modyfikacji danych)
_wnd:=_PSW.mk_sel('Zawartość pliku "'+_name+'"','P',0,'#tran_upr_psw',,,,1);
_PSW.win_fld(_wnd,,'TXT',,,100);
_PSW.win_sel(_wnd);

_GRP:=_cfg.GROUP;
: Grupy ochron (okienko wertowania bez możliwości modyfikacji danych)
_wnd:=_GRP.mk_sel('Grupy ochron','P',0,'#tran_upr_grp',,,,,'U','T');
_GRP.win_fld(_wnd,,'GROUP',,,20);
_GRP.win_fld(_wnd,,'DESC',,,60);
_GRP.win_act(_wnd,0,'Formuła','Zawartość PSW',,,"__EXP_CFG.PSW.select()");
_GRP.win_act(_wnd,0,'Formuła','Wczytaj PSW',,,"exec('cfg_reload','tran_upr',__EXP_CFG)");
_GRP.win_act(_wnd,0,'Formuła','Dane dla Excel',,,"exec('export_dat','tran_upr',__EXP_CFG)");
_GRP.win_act(_wnd,0,'Formuła','Eksportuj role',,,"exec('export_csv','tran_upr',__EXP_CFG)");
_GRP.win_act(_wnd,0,'Szukaj');
_GRP.win_act(_wnd,0,'Kolejność');
_GRP.win_sel(_wnd);

_USR:=_cfg.USERS;
: Użytkownicy systemu (okienko wertowania bez możliwości modyfikacji danych)
_wnd:=_USR.mk_sel('Użytkownicy systemu','P',0,'#tran_upr_usr',,,,,'U','T');
_USR.win_fld(_wnd,,'USERS',,,20);
_USR.win_fld(_wnd,,'DESC',,,30);
_USR.win_fld(_wnd,,'ACTIVE',,,-3,,,,,,2,,"'T'","'N'");
_USR.win_fld(_wnd,,'PROT',,,-3,,,,,,2,,"'T'","'N'");
_USR.win_fld(_wnd,,'WWW',,,-3,,,,,,2,,"'T'","'N'");
_USR.win_act(_wnd,0,'Formuła','Zmień aktywność',,,"
   _TAB:=cur_tab(1,1);
   _TAB.ACTIVE:={? _TAB.ACTIVE='T' || 'N' || 'T' ?};
   _TAB.put()
");
_USR.win_act(_wnd,0,'Formuła','Chroń',,,"exec('xp_users_zb','tran_upr',__EXP_CFG)");
_USR.win_act(_wnd,0,'Formuła','Nie ochraniaj',,,"exec('xp_users_zb','tran_upr',__EXP_CFG)");
_USR.win_act(_wnd,0,'Formuła','Dane dla Excel',,,"exec('export_dat','tran_upr',__EXP_CFG)");
_USR.win_act(_wnd,0,'Formuła','Eksportuj role',,,"exec('export_csv','tran_upr',__EXP_CFG)");
_USR.win_act(_wnd,0,'Szukaj');
_USR.win_act(_wnd,0,'Kolejność');
_USR.win_sel(_wnd);

_UGR:=_cfg.USRGRP;
: Użytkownicy grupy (okienko wertowania bez możliwości modyfikacji danych)
_wnd:=_UGR.mk_sel('Użytkownicy grupy','P',0,'#tran_upr_ugr',,,,,'U','T');
_UGR.win_fld(_wnd,,'USERS',,,20);
_UGR.win_fld(_wnd,,'DESC',,,53);
_UGR.win_fld(_wnd,,'PROT',,,-3,,,,,,2,,"'T'","'N'");
_UGR.win_fld(_wnd,,'WWW',,,-3,,,,,,2,,"'T'","'N'");
_UGR.win_act(_wnd,0,'Formuła','Chroń',,,"exec('xp_users_zb','tran_upr',__EXP_CFG)");
_UGR.win_act(_wnd,0,'Formuła','Nie ochraniaj',,,"exec('xp_users_zb','tran_upr',__EXP_CFG)");
_UGR.win_act(_wnd,0,'Szukaj');
_UGR.win_act(_wnd,0,'Kolejność');
_UGR.win_sel(_wnd);

_ROL:=_cfg.ROLE;
: Role (okienko wertowania bez możliwości modyfikacji danych)
_wnd:=_ROL.mk_sel('Role','P',0,'#tran_upr_rol',,,,,'U','T');
_ROL.win_fld(_wnd,,'ROLE',,,50);
_ROL.win_act(_wnd,0,'Formuła','Ten',,,,"sel_exit()",1);
_ROL.win_act(_wnd,0,'Szukaj');
_ROL.win_act(_wnd,0,'Kolejność');
_ROL.win_sel(_wnd);

_GRO:=_cfg.GROLE;
: Proponowane role grupy (okienko wertowania umożliwiające modyfikację danych)
_wnd:=_GRO.mk_sel('Proponowane role grupy','P',0,'#tran_upr_gro',,,,,'U','T');
_GRO.win_fld(_wnd,,'ON',,,-3,,,,,,2,,"'T'","'N'");
_GRO.win_fld(_wnd,,'ROLE',,,50);
: formuła wykonywana po każdej akcji modyfikującej dane
_aa:="grp_disp(__EXP_CFG.USRGRP,__EXP_CFG.USRGRP.win_sel('?'),1,1)";
_GRO.win_act(_wnd,1,'Formuła','Dołącz',,,"exec('xp_grole_db','tran_upr',__EXP_CFG)",_aa,1);
_GRO.win_act(_wnd,0,'Formuła','Dołącz',,,"exec('xp_grole_db','tran_upr',__EXP_CFG)",_aa,1);
_GRO.win_act(_wnd,0,'Formuła','Popraw',,,"exec('xp_grole_pb','tran_upr',__EXP_CFG)",_aa);
_GRO.win_act(_wnd,0,'Formuła','Usuń',,,"exec('xp_grole_ub','tran_upr',__EXP_CFG)",_aa);
_GRO.win_act(_wnd,0,'Formuła','Aktywuj',,,"exec('xp_grole_zb','tran_upr',__EXP_CFG)",_aa);
_GRO.win_act(_wnd,0,'Formuła','Wyłącz',,,"exec('xp_grole_zb','tran_upr',__EXP_CFG)",_aa);
_GRO.win_act(_wnd,0,'Szukaj');
_GRO.win_act(_wnd,0,'Kolejność');
_GRO.win_sel(_wnd);

_URO:=_cfg.USROLE;
: Proponowane role użytkownika (okienko wertowania umożliwiające modyfikację danych)
_wnd:=_URO.mk_sel('Proponowane role użytkownika','P',0,'#tran_upr_uro',,,,,'U','T');
_URO.win_fld(_wnd,,'ON',,,-3,,,,,,2,,"'T'","'N'");
_URO.win_fld(_wnd,,'ROLE',,,50);
_URO.win_act(_wnd,1,'Formuła','Dołącz',,,"exec('xp_usrole_db','tran_upr',__EXP_CFG)",,1);
_URO.win_act(_wnd,0,'Formuła','Dołącz',,,"exec('xp_usrole_db','tran_upr',__EXP_CFG)",,1);
_URO.win_act(_wnd,0,'Formuła','Popraw',,,"exec('xp_usrole_pb','tran_upr',__EXP_CFG)");
_URO.win_act(_wnd,0,'Formuła','Usuń',,,"exec('xp_usrole_ub','tran_upr',__EXP_CFG)");
_URO.win_act(_wnd,0,'Formuła','Aktywuj',,,"exec('xp_usrole_zb','tran_upr',__EXP_CFG)");
_URO.win_act(_wnd,0,'Formuła','Wyłącz',,,"exec('xp_usrole_zb','tran_upr',__EXP_CFG)");
_URO.win_act(_wnd,0,'Szukaj');
_URO.win_act(_wnd,0,'Kolejność');
_URO.win_sel(_wnd);

: tryb wyświetlania dla wszystkich okien
_mode:='maximized_with_title';

: Panel główny ról proponowanych dla użytkowników
_wnd:=_GRP.grp_make('Uprawnienia użytkowników',
   "  _cfg:=__EXP_CFG;
      _cfg.GROUP.first();
      _cfg.USERS.first();
      1
   ",
   '#tran_upr'
);

: Grupy ochron
_GRP.grp_sel(_wnd,_GRP,_GRP.win_sel('?'),'wg Grup',
:  po odświeżeniu
   "  _cfg:=__EXP_CFG;
      grp_disp(_cfg.GROLE,_cfg.GROLE.win_sel('?'),1,1);
      grp_disp(_cfg.USRGRP,_cfg.USRGRP.win_sel('?'),1,1)
   ",
:  położenie, wysokość
   ,,15,
:  przed i po obsłudze
   "","",
:  utrwalenie, aktywacja, tryb
   ,,_mode
);

: Użytkownicy grupy
_GRP.tab_splt(_wnd,'tab0','horizontal','users');
_GRP.grp_sel(_wnd,_UGR,_UGR.win_sel('?'),,
:  po odświeżeniu
   "  _cfg:=__EXP_CFG;
      _TAB:=cur_tab(1,1);
      grp_disp(_cfg.USROLE,_cfg.USROLE.win_sel('?'),1,1);
      _TAB.actions(_TAB.win_sel('?'),{? _TAB.PROT='T' || 'C' || 'N' ?},,1)
   ",
:  położenie, wysokość
   ,,15,
:  przed i po obsłudze
   "  {? _a || return() ?};
      __EXP_CFG.USRGRP.prefix(__EXP_CFG.GROUP.GROUP,)
   ","",
:  utrwalenie, aktywacja, tryb
   ,,_mode
);

: Proponowane role użytkownika
_GRP.tab_splt(_wnd,'users','vertical','usrole');
_GRP.grp_sel(_wnd,_URO,_URO.win_sel('?'),,
:  po odświeżeniu
   "  _UGR:=__EXP_CFG.USRGRP;
      _TAB:=cur_tab(1,1);
      _hid:=
         {? _UGR.size()=0  || ':D'
         |? _UGR.PROT<>'T' || 'DPUAW:D'
         || {? _TAB.ON='T' || 'A' || 'W' ?}
         ?};
      _TAB.actions(_TAB.win_sel('?'),_hid,,1)
   ",
:  położenie, wysokość
   ,,15,
:  przed i po obsłudze
   "  {? _a
      || __EXP_CFG.USROLE.fld_fml('USERS','BLANK',$('__EXP_CFG.USRGRP.USERS'));
         return()
      ?};
      __EXP_CFG.USROLE.prefix(__EXP_CFG.USRGRP.USERS,)
   ",
   "  {? _a
      || __EXP_CFG.USROLE.fld_fml('USERS','BLANK')
      ?}
   ",
:  utrwalenie, aktywacja, tryb
   ,,_mode
);

: Proponowane role grupy
_GRP.tab_splt(_wnd,'tab0','vertical','grole');
_GRP.grp_sel(_wnd,_GRO,_GRO.win_sel('?'),,
:  po odświeżeniu
   "  _TAB:=cur_tab(1,1);
      _hid:={? _TAB.ON='T' || 'A' || 'W' ?};
      _TAB.actions(_TAB.win_sel('?'),_hid,,1)
   ",
:  położenie, wysokość
   ,,15,
:  przed i po obsłudze
   "  {? _a || return() ?};
      __EXP_CFG.GROLE.prefix(__EXP_CFG.GROUP.GROUP,)
   ","",
:  utrwalenie, aktywacja, tryb
   ,,_mode
);

: Użytkownicy
_GRP.grp_sel(_wnd,_USR,_USR.win_sel('?'),'wg Użytkowników',
:  po odświeżeniu
   "  _cfg:=__EXP_CFG;
      _TAB:=cur_tab(1,1);
      grp_disp(_cfg.USROLE,_cfg.USROLE.win_sel('?'),1,1);
      _TAB.actions(_TAB.win_sel('?'),{? _TAB.PROT='T' || 'C' || 'N' ?},,1)
   ",
:  położenie, wysokość
   ,,,
:  przed i po obsłudze
   "","",
:  utrwalenie, aktywacja, tryb
   ,,_mode
);

: Proponowane role użytkownika
_GRP.tab_splt(_wnd,'tab0','vertical','usrole');
_GRP.grp_sel(_wnd,_URO,_URO.win_sel('?'),,
:  po odświeżeniu
   "  _USR:=__EXP_CFG.USERS;
      _TAB:=cur_tab(1,1);
      _hid:=
         {? _USR.size()=0  || ':D'
         |? _USR.PROT<>'T' || 'DPUAW:D'
         || {? _TAB.ON='T' || 'A' || 'W' ?}
         ?};
      _TAB.actions(_TAB.win_sel('?'),_hid,,1)
   ",
:  położenie, wysokość
   ,,15,
:  przed i po obsłudze
   "  {? _a
      || __EXP_CFG.USROLE.fld_fml('USERS','BLANK',$('__EXP_CFG.USERS.USERS'));
         return()
      ?};
      __EXP_CFG.USROLE.prefix(__EXP_CFG.USERS.USERS,)
   ",
   "  {? _a
      || __EXP_CFG.USROLE.fld_fml('USERS','BLANK')
      ?}
   ",
:  utrwalenie, aktywacja, tryb
   ,,_mode
);

: wyświetl konfigurację
_GRP.win_sel(_wnd);
_GRP.select();

: zapisz stan konfiguracji
exec('cfg_write_obj','tran_upr',_cfg);

: porządki
VAR_DEL.delete('__EXP_CFG');
~~


\xp_users_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Chroń/Nie ochraniaj" okienka tabeli użytkowników grupy.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_TAB:=cur_tab(1,1);

_zmien:="
   _a.cntx_psh();
   _a.clear();
   _a.f_set(,,'USERS=\\\':_a\\\' and PROT<>\\\':_b\\\'',_b,_c);
   _loop:=_a.f_first();
   {!
   |? _loop
   |! _a.PROT:=_c;
      _a.put();
      _loop:=_a.f_next()
   !};
   _a.f_clear();
   _a.cntx_pop()
";

{? _TAB.PROT='T'
|| {? FUN.ask(
         'Czy na pewno wyłączyć ochronę modyfikacji listy ról użytkownika?\n'+
         'Role użytkownika zostaną zaktualizowane zgodnie z rolami grup.'
      )
:     świadome działanie...
   || _cfg.usr_prot(_TAB.USERS,'N');
      exec('update_roles','tran_upr',_a)
   ?}
|| _cfg.usr_prot(_TAB.USERS,'T')
?};
~~


\xp_grole_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Dołącz" okienka tabeli ról grupy.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_ROLE:=_cfg.ROLE;
_GROUP:=_cfg.GROUP;
_GROLE:=_cfg.GROLE;

: wynik
_done:=0;

: role poza już użytymi
_ROLE.clear();
_ROLE.f_set(,,
   'ROLE not in (select :_a.ROLE from :_a where :_a.GROUP=\':_b\')',
   _GROLE,_GROUP.GROUP
);

: udostępnij
{? _ROLE.select()
|| _GROLE.blank();
   _GROLE.GROUP:=_GROUP.GROUP;
   _GROLE.ROLE:=_ROLE.ROLE;
   {? _GROLE.add()
:     propaguj modyfikację
   || exec('update_roles','tran_upr',_a);
      _done:=1
   ?}
?};

_cfg.ROLE.f_clear();
_done


\xp_grole_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Popraw" okienka tabeli ról grupy.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_ROLE:=_cfg.ROLE;
_GROUP:=_cfg.GROUP;
_GROLE:=_cfg.GROLE;

: wynik
_done:=0;

: role poza użytymi i bieżącą
_ROLE.clear();
_ROLE.f_set(,,
   'ROLE=\':_c\' or '+
   'ROLE not in (select :_a.ROLE from :_a where :_a.GROUP=\':_b\')',
   _GROLE,_GROUP.GROUP,_GROLE.ROLE
);

: ustal bieżącą i udostępnij do wyboru
_ROLE.f_find(_GROLE.ROLE,);
{? _ROLE.select(,1)
|| _GROLE.ROLE:=_ROLE.ROLE;
   {? _GROLE.put()
:     propaguj modyfikację
   || exec('update_roles','tran_upr',_a);
      _done:=1
   ?}
?};

_ROLE.f_clear();
_done


\xp_grole_ub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Usuń" okienka tabeli ról grupy.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć bieżący zapis?') & _a.GROLE.del(,1)
|| exec('update_roles','tran_upr',_a);
   1
?}


\xp_grole_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Aktywuj/Wyłącz" okienka tabeli ról grupy.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_GROLE:=_cfg.GROLE;

: ustaw znacznik włączenia na wartość o przeciwnym znaczeniu
_GROLE.ON:={? _GROLE.ON<>'T' || 'T' || 'N' ?};
{? _GROLE.put()
|| exec('update_roles','tran_upr',_a);
   1
?}


\xp_usrole_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Dołącz" okienka tabeli ról użytkownika.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_ROLE:=_cfg.ROLE;
_USERS:=_cfg.USERS;
_USROLE:=_cfg.USROLE;

: role poza już użytymi
_ROLE.clear();
_ROLE.f_set(,,
   'ROLE not in (select :_a.ROLE from :_a where :_a.USERS=\':_b\')',
   _USROLE,_USERS.USERS
);

: udostępnij
{? _ROLE.select()
|| _USROLE.blank();
   _USROLE.ROLE:=_ROLE.ROLE;
   _USROLE.add()
?};

_cfg.ROLE.f_clear();
~~


\xp_usrole_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Popraw" okienka tabeli ról użytkownika.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_ROLE:=_cfg.ROLE;
_USERS:=_cfg.USERS;
_USROLE:=_cfg.USROLE;

: role poza użytymi i bieżącą
_ROLE.clear();
_ROLE.f_set(,,
   'ROLE=\':_c\' or '+
   'ROLE not in (select :_a.ROLE from :_a where :_a.USERS=\':_b\')',
   _USROLE,_USERS.USERS,_USROLE.ROLE
);

: ustal bieżącą i udostępnij do wyboru
_ROLE.f_find(_USROLE.ROLE,);
{? _ROLE.select(,1)
|| _USROLE.ROLE:=_ROLE.ROLE;
   _USROLE.put()
?};

_ROLE.f_clear();
~~


\xp_usrole_ub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Usuń" okienka tabeli ról użytkownika.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
FUN.ask('Czy usunąć bieżący zapis?') & _a.USROLE.del(,1);
~~


\xp_usrole_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Obsługa akcji "Aktywuj/Wyłącz" okienka tabeli ról użytkownika.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_USROLE:=_cfg.USROLE;

: ustaw znacznik o przeciwnym znaczeniu
_USROLE.ON:={? _USROLE.ON<>'T' || 'T' || 'N' ?};
_USROLE.put();

~~


\update_roles
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Przypisuje użytkownikom role wynikające z konfiguracji.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej elementy konfiguracji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_cfg:=_a;
_ROLE:=_cfg.ROLE;
_GROUP:=_cfg.GROUP;
_GROLE:=_cfg.GROLE;
_USRGRP:=_cfg.USRGRP;
_USROLE:=_cfg.USROLE;

: zachowaj kontekst
{! _ni:=1..obj_len(_a)
|! {? type_of(_a[_ni])=type_of(SYSLOG)
   || _a[_ni].cntx_psh();
      _a[_ni].clear()
   ?}
!};

: utwórz listę ról wszystkich użytkowników
: pomiń użytkowników chronionych przed zmianami
_TMP:=sql(
   'select :_a.USERS, :_b.GROUP, :_b.ROLE, :_b.ON '+
   'from :_a, :_b '+
   'where :_a.PROT<>\'T\' and :_a.GROUP=:_b.GROUP ',
   _USRGRP,_GROLE
);

: usuń z listy role "wyłączone",
: gdy te same role są "włączone"
_loop:=_GROLE.first();
{!
|? _loop
|! {? _GROLE.ON='T'
   || {!
      |? _TMP.find_tab(,
            'GROUP',,'=',_GROLE.GROUP,
            'ROLE',,'=',_GROLE.ROLE,
            'ON',,'<>','T'
         )
      |! _TMP.del()
      !}
   ?};
   _loop:=_GROLE.next()
!};

: usuń nadmiarowe role
_loop:=_USRGRP.first();
{!
|? _loop
|! {? _USRGRP.PROT<>'T'
   || _USROLE.prefix(_USRGRP.USERS,);
      {? _USROLE.first()
      || {!
         |? {? _GROLE.find_tab(,
                  'GROUP',,'=',_USRGRP.GROUP,
                  'ROLE',,'=',_USROLE.ROLE
               )
            || _USROLE.next()
            || _USROLE.del()
            ?}
         !}
      ?}
   ?};
   _loop:=_USRGRP.next()
!};
_USROLE.prefix();

: aktualizuj ustwienia ról
_loop:=_TMP.first();
{!
|? _loop
|! {? ~_USROLE.find_tab(,
         'USERS',,'=',_TMP.USERS,
         'ROLE',,'=',_TMP.ROLE
      )
:     dodaj brakującą
   || _USROLE.blank();
      _USROLE.USERS:=_TMP.USERS;
      _USROLE.ROLE:=_TMP.ROLE;
      _USROLE.ON:=_TMP.ON;
      _USROLE.add()

   |? _USROLE.ON<>_TMP.ON
:     aktualizuj ustawienie
   || _USROLE.ON:=_TMP.ON;
      _USROLE.put()
   ?};
   _loop:=_TMP.next()
!};

: przywróć kontekst
{! _ni:=1..obj_len(_a)
|! {? type_of(_a[_ni])=type_of(SYSLOG)
   || _a[_ni].cntx_pop()
   ?}
!};
~~


\psw_read
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Wczytuje zawartość pliku psw.
::   WE: _a [TABLE] - tabela do zapisu
::       _b [STRING] - nazwa pliku
::       _c [INTEGER] - czy odczyt zgodnie z pth
::   WY: alias tabeli tymczasowej z zawartością pliku
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_TAB:=_a;
_name:=_b;
_path:={? var_pres('_c')=type_of(0) || _c<>0 || 1 ?};

: otwórz plik do odczytu
{? (_file:=fopen(_name,'r',_path))<=0
:  to się nie powinno stać, zwróć pustą tabelę
|| FUN.error('Błąd dostępu do pliku "'+_name+'".');
   return(_TAB)
?};

: czytaj cały plik
{!
|? (_line:=fread(_file))<>'\n'
|! {? (_line:=form(_line))<>''
:     zapisz niepuste wiersze
   || _TAB.NUM:=_TAB.tm_stamp();
      _TAB.TXT:=_line;
      _TAB.add()
   ?}
!};

: porządki
fclose(_file);

: klucze dla optymalnego przetwarzania
_num_key:=_TAB.ndx_tmp(,,'NUM',,);
_def_key:=_TAB.index('?');

: organizacja sekcji
_TAB.cntx_psh();
_TAB.clear();
_TAB.index(_num_key);
_loop:=_TAB.first();
_ref:=0;
{!
|? _loop
|! {? (1+_TAB.TXT)='[' & (_TAB.TXT+1)=']'
   || _ref:=#_TAB.ref()
   || _TAB.REF:=_ref;
      _TAB.put()
   ?};
   _loop:=_TAB.next()
!};
_TAB.cntx_pop();

: organizacja elementów we wskazanych głównych sekcjach
exec('psw_node','tran_upr',1,_TAB,'[group members]','group: ');
exec('psw_node','tran_upr',1,_TAB,'[group hierarchy]','group: ');
exec('psw_node','tran_upr',0,_TAB,'[group notes]','group: ');
exec('psw_node','tran_upr',1,_TAB,'[sub-users]','subsystem: ');
exec('psw_node','tran_upr',0,_TAB,'[security elements]','name: ');
exec('psw_node','tran_upr',0,_TAB,'[protection keys]','name: ');

_TAB


\psw_node
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Zmiania organizację zapisów w tabeli z zawartością psw.
::   WE: _a [INTEGER] - tryb organizacji (0 - przenieś jako liść, 1 - utwórz powiązaną listę)
::       _b [TABLE] - alias tabeli z zawartością psw
::       _c [STRING] - identyfikator elementu głównego
::       _d [STRING] - prefiks reorganizowanych elementów
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_mod:=_a;
_TAB:=_b;
_grp:=_c;
_tag:=_d;

_TAB.cntx_psh();
_TAB.clear();
: ustal rekord sekcji głównej
{? ~_TAB.find_tab(,'TXT',,'=',_grp)
|| _TAB.cntx_pop();
   return()
?};

: przetwarzaj całą zawartość sekcji
_TAB.f_set(,,'REF=:_a',#_TAB.ref());
_loop:=_TAB.f_first();
{!
|? _loop
|! {? _TAB.TXT*_tag=1
:     element wyróżniony
   || _ref:=#_TAB.ref();
:     przetwarzaj kolejne elementy do końca dziedziny
:     lub do napotkania następnego elementu wyróżnionego
      _loop:=_TAB.f_next();
      {!
      |? _loop & _TAB.TXT*_tag=0
      |!
:        modyfikuj strukturę
         {? _mod=1
         ||
:           lista elementów odzielonych przecinkami
            _item:=spli_str(_TAB.TXT,',');
:           usuń oryginalny zapis
            _TAB.del();
:           dołącz elementy jako listki
            {! _ii:=1..obj_len(_item)
            |! _TAB.blank();
               _TAB.REF:=_ref;
               _TAB.NUM:=_TAB.tm_stamp();
               _TAB.TXT:=form(_item[_ii]);
               _TAB.add()
            !};
            obj_del(_item)
         ||
:           przenieś na niższy poziom
            _TAB.REF:=_ref;
            _TAB.put()
         ?};
         _loop:=_TAB.f_next()
      !}
   || _loop:=_TAB.f_next()
   ?}
!};

: porządki
_TAB.f_clear();
_TAB.cntx_pop();

~~


\role4grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Ustala role dla grupy ochron.
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _b='!ADMIN' | _b='ADMIN'
|| ''

|? _b='!CONTROL' | _b='CONTROL'
|| ''

|? _b='!GIODO' | _b='GIODO'
|| _a.add_role(_b,
      'Specjalista ds. bezpieczeństwa danych'
   )

|? _b='!KADRY' | _b='KADRY'
|| _a.add_role(_b,
      'Przeglądający Kadry',
      'Specjalista ds. kadr'
   )

|? _b='!PŁACE' | _b='PŁACE'
|| _a.add_role(_b,
      'Przeglądający Płace',
      'Specjalista ds. płac'
   )

|? _b='wdrożenia'
|| ''
?};
~~


\sub_alert
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
~~


\sub_control
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('i_users','tran_upr',_a,_b,'CNkierownik',"
   exec('cnkier','tran_upr',_a,_b)
");
~~


\sub_emag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_usr:=exec('zwr_ref','podstawy','USERS','USR_KKOD',_b,_b);

:: są uprawnienia do conajmniej jednego magazynu
{? exec('zwr_ref','podstawy','USERS_UP','MG','MG',_usr)
|| _a.add_role(_b,
      'Przeglądający Magazyny',
      'Magazynier'
   )
?};

{? 0
||
   _a.add_role(_b,
      'Dyrektor sprzedaży',
      'Dyrektor zakupów',
      'Logistyk'
   )
?};

:: są uprawnienia do co najmniej jednego stanowiska
{? exec('zwr_ref','podstawy','USERS_UP','STS','STS',_usr)
:: są uprawnione typy dokumentów
   |  exec('fo_wart','funkcje',2002,2,OPERATOR.USER)<>''
:: jest domyślny typ dokumentu
   |  exec('fo_wart','funkcje',2003,2,OPERATOR.USER)<>''
:: nadano uprawnienia do menu użytkownika
   |  exec('menuk_usr','tran_upr',_usr,'EMAG'
         ,'FAKTURY'
         ,'OFERTY'
         ,'POZYCJA ZAMÓWIENIA'
         ,'ZAMÓWIENIA'
      )
|| _a.add_role(_b,
      'Przeglądający Sprzedaż',
      'Sprzedawca'
   )
?};

:: są uprawnienia do co najmniej jednego stanowiska
{? exec('zwr_ref','podstawy','USERS_UP','STZ','STZ',_usr)
:: są uprawnione typy dokumentów
   |  exec('fo_wart','funkcje',6002,2,OPERATOR.USER)<>''
:: jest domyślny typ dokumentu
   |  exec('fo_wart','funkcje',6003,2,OPERATOR.USER)<>''
:: nadano uprawnienia do menu użytkownika
   |  exec('menuk_usr','tran_upr',_usr,'EMAG'
         ,'ZAMÓWIENIA DOSTAW'
      )
|| _a.add_role(_b,
      'Przeglądający Zakupy',
      'Zakupy'
   )
?};
~~


\sub_estra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('USERS_ES')<>type_of(SYSLOG)
|| return()
?};
USERS.cntx_psh();
USERS.index('USR_KKOD');
USERS.prefix(_b,);
{? USERS.first()
|| USERS_ES.index('USERS_ES');
   USERS_ES.prefix(USERS.ref());
   {? USERS_ES.first()
   || {? USERS_ES.CZY_SR='N'
      || _a.add_role(_b,'Specjalista ds. środków trwałych')
      || _a.add_role(_b,'Opiekun środków trwałych')
      ?}
   || _a.add_role(_b,'Przeglądający Środki trwałe')
   ?}
?};
USERS.cntx_pop();
~~


\sub_faktury
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,'Sprzedawca');
{? 0
||
   _a.add_role(_b,'Dyrektor sprzedaży')
?};
~~


\sub_fiks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('OBJECT',_a)<=0
|| _tab:=sql(
      'select distinct DOK.ZAA as U from @DOK order by 1 union all '+
      'select distinct DOK.KSIEGOWY from @DOK union all '+
      'select distinct DOK.ZAR from @DOK'
   );
   _usr:=tab_tmp(1,'USER','STRING[10]',);
   _a.OBJECT:=_usr;
   {? _tab.first()
   || {!
      |? {? _tab.U<>''
         || _poz:=_tab.U*':';
            {? _poz
            || _user:=|(_poz-_tab.U);
               {? ~_usr.find_key(_user,)
               || _usr.USER:=_user;
                  _usr.add()
               ?}
            ?}
         ?};
         _tab.next()
      !}
   ?}
?};
{? _a.OBJECT.find_key(_b,)
|| _a.add_role(_b,'Księgowy')
?};
_a.add_role(_b,'Przeglądający Finanse');
~~


\sub_fiks_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
~~


\sub_homebank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('OBJECT',_a)<=0
|| _tab:=sql(
      'select distinct PB.USER_GEN as U from @PB order by 1 '+
      'union all select distinct PB.USER_ZTW from @PB'
   );
   _usr:=tab_tmp(1,'USER','STRING[10]',);
   _a.OBJECT:=_usr;
   {? _tab.first()
   || {!
      |? {? _tab.U<>''
         || _poz:=_tab.U*':';
            {? _poz
            || _user:=|(_poz-_tab.U);
               {? ~_usr.find_key(_user,)
               || _usr.USER:=_user;
                  _usr.add()
               ?}
            ?}
         ?};
         _tab.next()
      !}
   ?}
?};
{? _a.OBJECT.find_key(_b,)
|| _a.add_role(_b,'Specjalista ds. płatności')
?};
~~


\sub_if
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,'Księgowy');
1


\sub_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,'Księgowy');
~~


\sub_kali
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: role, których nie można nadać automatycznie
{? 0
|| _a.add_role(_b,
      'Dyrektor HR',
      'Kierownik ds płac'
   )
?};
: sprawdź przynależność do predefiniowanej grupy
{? _a.is_mem(_b,'!KADRY') | _a.is_mem(_b,'KADRY')
|| _a.add_role(_b,
      'Przeglądający Kadry',
      'Specjalista ds. kadr',
      'Specjalista ds. szkoleń'
   )
?};
: sprawdź przynależność do predefiniowanej grupy
{? _a.is_mem(_b,'!PŁACE') | _a.is_mem(_b,'PŁACE')
|| _a.add_role(_b,
      'Przeglądający Płace',
      'Specjalista ds. płac'
   )
?};
: sprawdź dostępność form współpracy
{? var_pres('USERS_FZ')<>type_of(SYSLOG)
|| _a.add_role(_b,
      'Przeglądający Zleceniobiorców',
      'Specjallista ds. zleceniobiorców'
   )
|| USERS_FZ.cntx_psh();
   USERS_FZ.prefix();
   {? ~USERS_FZ.find_tab(,
         'USERS','KOD','=',_b,
         'F_ZATR','KOD','=','Z'
      )
   || _a.add_role(_b,
         'Przeglądający Zleceniobiorców',
         'Specjallista ds. zleceniobiorców'
      )
   ?};
   USERS_FZ.cntx_pop()
?};
~~


\sub_kasa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Przeglądający Kasa',
   'Kasjer'
);
~~


\sub_obiegi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('i_users','tran_upr',_a,_b,'FKdob',"
   exec('fkdob','tran_upr',_a,_b)
");
~~


\sub_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika internetowego (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::       _c [STRING] - kod roli internetowej
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _c='CNkierownik'
|| exec('cnkier','tran_upr',_a,_b);
   ~~
|? _c='EKkierownik'
|| ''

|? _c='EMklient'
|| ''

|? _c='EMmobilny-handlowiec'
|| ''

|? _c='EMumowy'
|| ''

|? _c='EMzamowienia'
|| ''

|? _c='FKdob'
|| exec('fkdob','tran_upr',_a,_b);
   ~~
|? _c='FKhandlowiec'
|| ''

|? _c='FKrozlicz'
|| ''

|? _c='FKzalicz'
|| ''

|? _c='FKzapot'
|| ''

|? _c='IFManager'
|| ''

|? _c='IFkierownik'
|| ''

|? _c='KAkierownik-DG'
|| _a.add_role(_b,
      'Przełożony'
   )

|? _c='KAkierownik-DO'
|| _a.add_role(_b,
      'Przełożony'
   )

|? _c='KAkierownik-DO-OZ'
|| _a.add_role(_b,
      'Przełożony'
   )

|? _c='KAkierownik-DP'
|| _a.add_role(_b,
      'Przełożony'
   )

|? _c='KAkierownik-G-EKP'
|| _a.add_role(_b,
      'Przełożony - rozliczenie czasu pracy'
   )

|? _c='KAkierownik-G-EKW'
|| _a.add_role(_b,
      'Pracownik - redakcja własnego wykonania',
      'Pracownik - zatwierdzanie własnego wykonania'
   )

|? _c='KAkierownik-G-PKP'
|| _a.add_role(_b,
      'Przełożony'
   )

|? _c='KAkierownik-G-PKW'
|| _a.add_role(_b,
      'Pracownik'
   )

|? _c='KAkierownik-PCP-EKP'
|| _a.add_role(_b,
      'Przełożony - planowanie czasu pracy'
   )

|? _c='KAkierownik-PCP-EKW'
|| _a.add_role(_b,
      'Pracownik - redakcja własnego kalendarza',
      'Pracownik - zatwierdzanie własnego kalendarza'
   )

|? _c='KAkierownik-PCP-PKP'
|| _a.add_role(_b,
      'Przełożony'
   )

|? _c='KAkierownik-PCP-PKW'
|| _a.add_role(_b,
      'Pracownik'
   )

|? _c='KAkierownik-U'
|| _a.add_role(_b,
      'Przełożony'
   )

|? _c='KAkierownik-UP'
|| _a.add_role(_b,
      'Przełożony'
   )

|? _c='KApracownik'
|| _a.add_role(_b,
      'Dokonujący oceny pracowniczej',
      'Pracownik'
   )

|? _c='PJkierownik'
|| ''

|? _c='REkierownik'
|| _a.add_role(_b,'Specjalista ds. zasobów')

|? _c='REopiekun'
|| _a.add_role(_b,'Specjalista ds. zasobów')

|? _c='REpracownik'
|| _a.add_role(_b,'Specjalista ds. zasobów')

|? _c='portalAdmins'
|| ''

|? _c='portalEditors'
|| ''

|? _c='portalManagers'
|| ''

|? _c='POPracownik'
|| _a.add_role(_b,'Portal HR - Pracownik')

|? _c='POKierownik'
|| _a.add_role(_b,'Portal HR - Przełożony (z wynagrodzeniami)')

|? _c='POKierowniBW'
|| _a.add_role(_b,'Portal HR - Przełożony (bez wynagrodzeń)')

?};
~~


\sub_prod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_usr:=exec('zwr_ref','podstawy','USERS','USR_KKOD',_b,_b);

:: Planista, gdy ma dostęp do planu zasobów i planu wielowymiarowego
{? exec('menuk_usr','tran_upr',_usr,'PRODUKCJA'
      ,'PLANY ZASOBÓW'
      ,'WWPP'
      ,'PX_GRP'
      ,'PX_KONT'
      ,'PX_OBJ'
      ,'PX_POZ'
   )
|| _a.add_role(_b,'Planista produkcji')
?};

:: Technolog, gdy ma dostęp do menu kart i wzorców
{? exec('tvip_usr','tran_upr',_usr)
      |
   exec('menuk_usr','tran_upr',_usr,'PRODUKCJA'
      ,'TECHNOLOGIA'
      ,'WZORZEC'
      ,'OPERACJE TECHNOLOGII'
   )
|| _a.add_role(_b,'Technolog')
?};

:: Dyrektor, gdy ma funkcje analityczne i podgląd dostępności zasobów
{? exec('menuk_usr','tran_upr',_usr,'PRODUKCJA'
      ,'ANALIZA WYKONANIA'
      ,'KALKULACJE'
      ,'MODYFIKACJE KALK.TKW'
      ,'MODYFIKACJE KALK.ZL.'
      ,'BRYGADA'
      ,'GNIAZDO'
      ,'HARMONOGRAM ST.'
      ,'KALKULACJE'
      ,'LINIA'
      ,'MODYFIKACJE KALK.TKW'
      ,'MODYFIKACJE KALK.ZL.'
      ,'STANOWISKO'
      ,'ZASOBY'
   )
|| _a.add_role(_b,'Dyrektor produkcji')
?};

:: Produkcja, gdy ma dostęp do ogólnej obsługi produkcji, głównie zleceń i wykonań
{? exec('zztu_usr','tran_upr',_usr)
      |
   exec('menuk_usr','tran_upr',_usr,'PRODUKCJA'
      ,'FUNKCJE DODATKOWE'
      ,'PODGLĄD ZLECEŃ'
      ,'POZYCJA PRZEWODNIKA'
      ,'PRZEWODNIK'
      ,'RACHUBA'
      ,'ZLECENIA'
   )
|| _a.add_role(_b,'Produkcja')
?};

:: Standardowo brak e-kiosków w wersji 12.41/12.51
{? 0 || _a.add_role(_b,'E-brygadzista','E-pracownik') ?};

:: Bezwarunkowo, gdy dostęp do podsystemu Produkcja
_a.add_role(_b,'Przeglądający Produkcja');
~~


\sub_projekty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Specjalista ds. projektów'
);
~~


\sub_pulpit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
~~


\sub_rcp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Specjalista ds. planowania czasu pracy',
   'Specjalista ds. rozliczania czasu pracy'
);
~~


\sub_remonty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,'Specjalista ds. zasobów');
~~


\sub_umowy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,'Specjalista ds. umów cyklicznych');
~~


\sub_wyposaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Specjalista ds. wyposażenia',
   'Opiekun wyposażenia'
);
~~


\sub_zkl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Specjalista ds. ocen pracowniczych',
   'Specjalista ds. szkoleń',
   'Specjalista HR'
);
~~


\sub_zdo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,'Specjalista ds. bezpieczeństwa danych');
~~


\i_users
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Uruchamia formułę, gdy użytkownik aktywny w roli internetowej
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::       _c [STRING] - kod roli
::       _d [RULE] - formuła do uruchomienia
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh();
USERS.index('USR_KKOD');
USERS.prefix(_b,);
{? USERS.first()
|| I_USERS.cntx_psh();
   I_USERS.index('USERS');
   I_USERS.prefix(USERS.ref());
   {? I_USERS.first()
   || I_ROLE.cntx_psh();
      I_ROLE.index('KOD');
      I_ROLE.prefix(_c,);
      {? I_ROLE.first()
      || I_USROLE.cntx_psh();
         I_USROLE.index('I_USERS');
         I_USROLE.prefix(REF.FIRMA,I_USERS.ref(),I_ROLE.ref());
         {? I_USROLE.first() & I_USROLE.AKT='T'
         || _d(_a,_b,_c)
         ?};
         I_USROLE.cntx_pop()
      ?};
      I_ROLE.cntx_pop()
   ?};
   I_USERS.cntx_pop()
?};
USERS.cntx_pop();
1


\menuk_usr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Zwraca informację, czy użytkownik ma uprawnienia do wskazanych menu użytkownika w podsystemie Produkcja
::   WE: _a - USERS.ref()
::       _b - system
::       _c.._[_] - kod menu użytkownika (MENUK.KOD)
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_system:=_b;
MENUK_U.index('UM');
_it:=3;
{!
|? MENUK_U.prefix(_a,_b,_[_it],);
   {? MENUK_U.first()
   || _result:=1
   ?};
   _it+=1;
   _result=0 & _it<=_
!};
_result


\tvip_usr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Zwraca informację, czy użytkownik ma jakiekolwiek uprawnienia do zatwierdzania kart technologicznych
::   WE: _a - USERS.ref()
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
TVIP.index('VT');
TVIP.prefix(_a);
_result:=TVIP.first();
_result


\zztu_usr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Zwraca informację, czy użytkownik ma jakiekolwiek uprawnienia do zatwierdzania zlecel
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
ZZTU.index('UTT');
ZZTU.prefix(_a);
_result:=ZZTU.first();
_result


\cnkier
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Dodanie ról do Merit dla roli CNKIEROWNIK Xpertis-a
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::----------------------------------------------------------------------------------------------------------------------
K_USERS.index('I_USERS');
K_USERS.prefix('CONTROL',I_USERS.ref());
{? K_USERS.first()
|| {? K_USERS.TYP='S'
   || _a.add_role(_b,'Kontroler');
      _a.add_role(_b,'Generujący zadania')
   |? K_USERS.TYP='Z'
   || {? K_USERS.ZN_UZU='T'
      || _a.add_role(_b,'Uzupełniający budżet')
      ?};
      {? K_USERS.ZN_AKC='T'
      || _a.add_role(_b,'Akceptujący budżet')
      ?}
   ?}
?}


\fkdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Dodanie ról do Merit dla roli FKDOB Xpertis-a
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::----------------------------------------------------------------------------------------------------------------------
EOSOBY.index('UNIK');
EOSOBY.prefix(REF.FIRMA,I_USERS.ref());
{? ~EOSOBY.first()
|| return()
?};
{? EOSOBY.WPROWADZ='T' | EOSOBY.WPROWAW='T'
|| _a.add_role(_b,'Sekretariat')
?};
{? EOSOBY.WPROWAD='T'
|| _a.add_role(_b,'Pracownik')
?};
{? EOSOBY.AKC_MERW='T'
|| _a.add_role(_b,'Akceptujący wnioski pracownicze')
?};
{? EOSOBY.WPROWAW='W' | EOSOBY.AKC_MERW='W' | EOSOBY.AKC_MERD='W'
|| _a.add_role(_b,'Koordynator obiegu wniosków')
?};
{? EOSOBY.AKC_MER<>'N'
|| _a.add_role(_b,'Akceptujący faktury I poziom');
   _a.add_role(_b,'Akceptujący faktury II poziom')
?};
{? EOSOBY.WPROWADZ='W'
|| _a.add_role(_b,'Koordynator obiegu faktur')
?};
{? EOSOBY.WPROWAD='W'
|| _a.add_role(_b,'Wysyłający na delegacje')
?};
{? EOSOBY.DEK_DEL | EOSOBY.DEK_DOB | EOSOBY.DEK_WNI
|| _a.add_role(_b,'Księgowy')
?}


\perm_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
::   WE: _a - nazwa uprawnienia: np.: FJKS, domyślnie pusta = wszystkie uprawniena
:: OPIS: Uaktualnienie uprawnień do danych użytkowników dla bieżącej firmy
::----------------------------------------------------------------------------------------------------------------------
{? _=0 | type_of('_a')<>type_of('')
|| _perm:=''
|| _perm:=_a
?};
USERS.index('USR_KKOD');
USERS.prefix();
{? USERS.first()
|| B_PERM.index('NAME');
   {? _perm<>''
   || B_PERM.prefix(_perm)
   || B_PERM.prefix()
   ?};
   B_PERM_U.index('USER');
   {!
   |? {? B_PERM.first()
      || {!
         |? B_PERM_U.prefix(REF.FIRMA,USERS.ref(),B_PERM.ref());
            {? B_PERM_U.first()
            || _val:=exec('perm_usr_upd','tran_upr',USERS.KOD,B_PERM.NAME);
               {? _val<>~~
               || {? _val=2
                  || B_PERM_U.FULL:='T'
                  || B_PERM_U.FULL:='N'
                  ?};
                  B_PERM_U.ACCESS:={? _val || 'T' || 'N' ?};
                  B_PERM_U.put()
               ?}
            ?};
            B_PERM.next()
         !}
      ?};
      USERS.next()
   !};
   {? _perm<>''
   || B_PERM.prefix()
   ?}
?}


\perm_usr_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Zwraca zakres uprawnień użytkownika
::       W buforze użytkownik (USERS) i uprawnienie (B_PERM)
::   WE: _a - kod użytkownika
::       _b - nazwa uprawnienia: np.: FJKS
::   WY: zakres uprawnień: 0-brak 1-nadanie 2-nadane do wszystkiego ~~-bez zmian
::----------------------------------------------------------------------------------------------------------------------
_perm:=_b;
_ret:=~~;
{? _perm='FJKS'
|| USERSF.index('UNIK'); USERSF.prefix(REF.FIRMA,USERS.ref());
   {? USERSF.first() & USERSF.LOCK='N'
   || _ret:=2
   || USERSDEP.index('USERSDEP'); USERSDEP.prefix(REF.FIRMA,USERS.ref());
      _ret:=USERSDEP.first()
   ?}
|? _perm='HRB'
|| USERSF.index('UNIK'); USERSF.prefix(REF.FIRMA,USERS.ref());
   {? USERSF.first() & USERSF.ALL_RB='N'
   || _ret:=2
   || UPR_RB.index('RB'); UPR_RB.prefix(USERS.ref());
      _ret:=UPR_RB.first()
   ?}
|? _perm='HRP'
|| UPR.index('REFKOD'); UPR.prefix(USERS.ref());
   _ret:={? UPR.find_key('WSZYSTKIE',) || 2 || UPR.first() ?}
|? _perm='ODDZ'
|| _ret:=exec('FindInSet','#table','USERS_UP','ODDZ','ODDZ',USERS.ref(),,,,null())<>null()
|? _perm='LMG'
|| _ret:=exec('FindInSet','#table','USERS_UP','MAG','MG',USERS.ref(),,,,null())<>null()
|? _perm='LSP'
|| _ret:=1
|? _perm='LZK'
|| _ret:=1
|? _perm='ZSMG'
|| _ret:=1
|? _perm='ZWMG'
|| _ret:=1
|? _perm='TR_RODZ'
|| _ret:=1
|? _perm='F_ZATR'
|| USERS_FZ.cntx_psh();
   USERS_FZ.index('FK_USR');
   USERS_FZ.prefix(USERS.ref());
   _all:=USERS_FZ.size();
   USERS_FZ.index('DOSTEP');
   USERS_FZ.prefix('T',REF.FIRMA,USERS.ref());
   _cur:=USERS_FZ.size();
   USERS_FZ.cntx_pop();

   {? _cur=0 || _ret:=0
   |? _cur=_all || _ret:=2
   |? _cur<>_all || _ret:=1
   ?}
|? _perm='UD_SKL'
|| UDB_UPR.cntx_psh();
   UDB_UPR.f_clear();
   UDB_UPR.prefix();
   UDB_UPR.f_set(,,'UDB_UPR.USERS like :_a and UDB_UPR.DOMYSLNY=\'T\'',USERS.ref());
   _all:=UDB_UPR.f_size();
   UDB_UPR.f_set(,,'UDB_UPR.USERS like :_a and UDB_UPR.DOMYSLNY like \'T\' and UDB_UPR.DOSTEP like \'T\'',USERS.ref());
   _cur:=UDB_UPR.f_size();
   UDB_UPR.f_clear();
   UDB_UPR.cntx_pop();
   {? _cur=0 || _ret:=0
   |? _cur=_all || _ret:=2
   |? _cur<>_all || _ret:=1
   ?}
|? _perm='KAS'
|| _ret:=exec('FindInSet','#table','KUSERUPR','KU_ST',USERS.ref(),,,,,null())<>null()
|? _perm='KAS_MUL'
|| _ret:=1
|? _perm='TARS'
|| _ret:=1
|? _perm='TARD'
|| _ret:=1
?};

_ret


\sub_ppk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Przeglądający PPK',
   'Specjalista ds. PPK'
);
~~


\sub_edp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Specjalista ds. kadr'
);
~~


\sub_por
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [21.14]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Przeglądający Kadry',
   'Dyrektor HR',
   'Specjalista ds. kadr',
   'Specjalista HR'
);
~~


\sub_pep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Propozycje ról wg uprawnień danego użytkownika dla podsystemu (nazwa formuły)
::   WE: _a [OBJECT] - wskazanie tablicy konfiguracji
::       _b [STRING] - kod użytkownika
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.add_role(_b,
   'Specjalista ds. kadr'
);
~~

:Sign Version 2.0 jowisz:1045 2023/12/21 13:22:36 43cbc7987f95cb183133c9879c48ca915d18b146b9d709333005ad9fe28c752850b4513ca04a6a34e73c9353858360a4bb29a2316383143765f9fca5d901e351c92089606b0742b0c1e3d30b08340b7e33ab7eca1524521c56728c25a5ba46d41b8f985f79d12f0ee5db939cd1c8d8ada4eb28fe7b823194f0e480777be50fde

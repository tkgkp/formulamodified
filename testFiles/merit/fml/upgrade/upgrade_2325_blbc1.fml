:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2325_BLBC1.fml
:: Utworzony: 12.09.2023
:: Autor: [AWI]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 23.25 na 23.25_BLBC1
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::
::            Programowe wyłączenie zadania transferowego.
::             W plikach upgrade_rrxx.fml korzystamy z metody
::               __UPG.off_task(<task>,[<operator>],[<value>])
::                 - dodaje do listy zadanie,
::                   które ma zostać wyłączone wg warunku porównania wg operatora porównania i wartości, gdzie:
::                <task> - symbol zadania do wyłączenia;
::                [<operator>] - operator porównania - domyślnie operator '<'; dostępne opcje:
::                                   '<' - wartość mniejsza;
::                                   '<=' - wartość mniejsza lub równa;
::                                   '=' - wartość równa;
::                [<value>] - wersja systemu lub aktualizacji do porównania z wersją zadania
::                            - domyślnie aktualna wersja aktualizacji (wg pliku).
::            W pliku upgrade.fml NALEŻY umieścić wywołanie poniższej metody na końcu formuły \init
::               (po wywołaniach __UPG.add_head()):
::              __UPG.act_off() - wyłączenie wszystkich zadań wskazanych do wyłączenia.
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [23.25_BLBC1]
:: OPIS: Główna formuła upgrade-ów RR.xx
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
::__UPG.id_ver(date(2023,9,30),time(0,0,0));

:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('DOKUMZ',,,'ZWS');
__UPG.add_task('funpar',,,'ZWS');
__UPG.add_task('color',,,'ZWS');

:: Zadania wykonywane dla każdej firmy osobno

:: Zadania manualne wykonywane raz dla całego systemu

:: Zadania manualne wykonywane dla każdej firmy osobno

~~


\DOKUMZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Uzupełnia pola tabeli DOKUMZ
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

DOKUM.cntx_psh();
DOKUMZ.cntx_psh();
DOKUMZA.cntx_psh();

_formula:="
   __lrec+=1;
   _put:=0;
   {? DOKUMZ.DOK<>null()
   || DOKUM.use(ref_name(DOKUMZ.DOK));
      {? DOKUMZ.FIRMA=null()
      || DOKUMZ.FIRMA:=DOKUMZ.DOK().FIRMA;
         _put:=1
      ?};
      {? DOKUMZ.REFSQL='' & DOKUMZ.DOK().REFSQL<>''
      || DOKUMZ.REFSQL:=DOKUMZ.DOK().REFSQL;
         _put:=1
      ?}
   ?};
   {? DOKUMZ.BC<>'N' & DOKUMZ.BC<>'T'
   || DOKUMZ.BC:='N';
      _put:=1
   ?};
   {? DOKUMZ.ARCH<>'N' & DOKUMZ.ARCH<>'T'
   || DOKUMZ.ARCH:='N';
      _put:=1
   ?};
   {? _put
   || {? DOKUMZ.put(1)
      || __lmod+=1;
         1
      ?}
   || 1
   ?}
";
DOKUM.use('dokum');
DOKUMZ.prefix();
_result:=DOKUMZ.for_each(_formula,1);

{? _result
|| _formula:="
      __lrec+=1;
      _put:=0;
      {? DOKUMZA.DOK<>null()
      || DOKUM.use(ref_name(DOKUMZA.DOK));
         {? DOKUMZ.FIRMA=null()
         || DOKUMZA.FIRMA:=DOKUMZA.DOK().FIRMA;
            _put:=1
         ?};
         {? DOKUMZA.REFSQL='' & DOKUMZA.DOK().REFSQL<>''
         || DOKUMZA.REFSQL:=DOKUMZA.DOK().REFSQL;
            _put:=1
         ?}
      ?};
      {? DOKUMZA.BC<>'N' & DOKUMZA.BC<>'T'
      || DOKUMZA.BC:='N';
         _put:=1
      ?};
      {? DOKUMZA.ARCH<>'N' & DOKUMZA.ARCH<>'T'
      || DOKUMZA.ARCH:='N';
         _put:=1
      ?};
      {? _put
      || {? DOKUMZA.put(1)
         || __lmod+=1;
            1
         ?}
      || 1
      ?}
   ";
   _names:=DOKUMZA.names();
   {? _names.first()
   || {!
      |? DOKUMZA.use(_names.NAME);
         DOKUMZA.prefix();
         _result:=DOKUMZA.for_each(_formula,1);
         _result & _names.next()
      !}
   ?}
?};

DOKUMZA.cntx_pop();
DOKUMZ.cntx_pop();
DOKUM.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano załączniki.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji załączników.')
?};
VAR_DEL.delete('__lrec','__lmod');

_result


\DOKUMZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Uzupełnia pola w tabeli DOKUMZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja załączników.'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? exec('aktualizuj','funpar',1)
|| FP_DEF.cntx_psh();
   FP_DEF.index('ID');
   FP_DEF.prefix('T','ZWS_PAR_PPNW',);
   _del:={? FP_DEF.first() || FP_DEF.del(,1) || 1 ?};
   FP_DEF.prefix('T','ZWS_PAR_PORCAS',);
   _del*={? FP_DEF.first() || FP_DEF.del(,1) || 1 ?};
   FP_DEF.cntx_pop();
   {? _del>0
   || __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
   || __UPG.msg('Usunięcie czynności parametryzacyjnej ZWS_PAR_PPNW lub ZWS_PAR_PORCAS nie powiodło się.');
      _ret:=-1
   ?}
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.');
   _ret:=-1
?};
_ret


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'

:Sign Version 2.0 jowisz:1045 2023/09/27 10:38:13 368516b9918933884112627735926b3ac9d306a5c7036cf7039b25c2d48d7bdaf53b84b884cbab8ee16c7d97848cb765ded18c10e0868c22d9d7f2c2c8f29095288f8acb4158b6973783d6f841aa3354df3690f984d8df16724dd0037a98aa5d7f1df6eaf07f504e74cad2be1b6dfe06c4e29fa6a15176bcb5357384dd11bf85

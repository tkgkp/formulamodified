:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_1802.fml
:: Utworzony: 17.10.2017
:: Autor: [TS]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 17.42 na 18.02
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - realizacja zakwalifikowana jako wykonana
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Główna formuła upgrade-ów 18.02
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR');
__UPG.add_task('funpar',,,'ZWS');
__UPG.add_task('stalesys',,,'ZWS');
__UPG.add_task('color',,,'ZWS',2);
__UPG.add_task('report',,,'ZWS');
__UPG.add_task('k_harm',,,'CTR');
__UPG.add_task('k_role',,,'ZWS');
__UPG.add_task('jpk',,,'FKS');
__UPG.add_task('MA_NJP',,,'FST');
__UPG.add_task('SRSR_AMOR100',,,'FST');
__UPG.add_task('areatitle',,,'ZWS');
__UPG.add_task('ku_l50',,,'PPL');
__UPG.add_task('MGRP',,,'ZWS');
__UPG.add_task('zz_kryt_exp',,,'POC');
__UPG.add_task('move_doks',,,'POC');
__UPG.add_task('zz_kryt_imp',,,'POC');
__UPG.add_task('create_doks',,,'POC');
__UPG.add_task('firma_p',,,'POC');
__UPG.add_task('skid_par',,,'POC');
__UPG.add_task('szk_tren',,,'PSZ');
__UPG.add_task('zo_test',,,'POC');
__UPG.add_task('zo_cel',,,'POC');
__UPG.add_task('zo_celk',,,'POC');
__UPG.add_task('szk_prac',,,'POC');
__UPG.add_task('slo_kod_wer_wyn',,,'PSZ');
__UPG.add_task('zo_osoba_del',,,'POC');
__UPG.add_task('stn_grp',,,'PSZ');
__UPG.add_task('rmk',,,'FKS');
__UPG.add_task('add_rub_graf',,,'PPL');
__UPG.add_task('wiki_stale_read',,,'ZWS');
__UPG.add_task('PRC_stale',,,'ZWS');
__UPG.add_task('wiki_stale_save',,,'ZWS');
__UPG.add_task('con_rodf_all',,,'CTR');
:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('GROP',,'N','TTE',,,,1);
__UPG.add_task('PROD_REJ',,'N','TTE',,,,1);
__UPG.add_task('updateFAKSO',,'N','ZWS',,,,1);
__UPG.add_task('ZLGD',,'N','TTE',,,,1);
__UPG.add_task('updateTYPYSP',,'N','ZWS',,,,1);
__UPG.add_task('updateKAL_BUFF',,'N','PRC',,,,1);
__UPG.add_task('R_ZMIANY',,'N','ZWS',,,,1);
__UPG.add_task('KHODB2position',,'N','LSP',,,,1);
__UPG.add_task('PLIKIDOC',,'N','TTE',,,,1);
__UPG.add_task('dokumTOPER2ZGP',,'N','TTE',,,,1);
__UPG.add_task('ZGP',,'N','TTE',,,,1);
__UPG.add_task('ROK_F',,'N','ZWS',,,,1);
__UPG.add_task('ZL',,'N','TTE',,,,1);
__UPG.add_task('updateMDOST',,'N','ZWS',,,,1);
__UPG.add_task('zus_one',,'N','HBN',,,,1);
__UPG.add_task('plat_zkn',,'N','LSP',,,,1);
__UPG.add_task('zf_rpi',,'N','POC',,,,1);
__UPG.add_task('zf_wid_clean',,'N','POC',,,,1);
__UPG.add_task('BRAKI_GD',,'N','TTE',,,,1);
__UPG.add_task('R_CZYT',,'N','ZWS',,,,1);
__UPG.add_task('R_ODN',,'N','PRC',,,,1);
__UPG.add_task('faks_odb',,'N','LSP',,,,1);
__UPG.add_task('f_zatra',,'N','PRC',,,,1);
__UPG.add_task('r_opczyt',,'N','PRC',,,,1);
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.PKD.PKW.PPL.TTE.WYP.ZWS.FST.FKS.KON.OBE.OBG','.');
_dom:=spli_str('LSP.POC.PBA.PRC.PSZ.ZWS.WYP.TTE.HBN','.');
_len:=obj_len(_dom);
FUN.prg_start(_len+2,'Aktualizacja czynności.'@,,,1);

:: Uzupełnienie domen (zawsze)
FUN.prg_next();
exec('add_domain','#b_action');
__UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
FUN.prg_next();
exec('fill_tab','#b_type',0);
__UPG.msg('Zaktualizowano typy danych.');

{! _i.._len
|! FUN.prg_next();
   _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
   {? _domain<>null()
   || exec('import_needed_dom','#b_design',_domain,0);
      __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
   ?}
!};
FUN.prg_stop();
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',0)<>0
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.');
   1
?}


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\GROP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w tabeli GROP
::----------------------------------------------------------------------------------------------------------------------
_result:=GROP.for_each("{? GROP.PROBLEM='' || GROP.PROBLEM:='N'; GROP.put() ?}",1);
__UPG.msg('Zaktualizowano pole GROP.PROBLEM.');
_result


\GROP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w tabeli GROP
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli GROP (grupy operacji).'


\PROD_REJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Aktualizuje zapisy w tabeli do rejestracji wykonań PROD_REJ
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('load_4rej','zl_grop',0);
{? _result.RESULT
|| __UPG.msg('Zaktualizowano %1 zapisów do rejestracji wykonań produkcji.'[$_result.IL])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji zapisów do rejestracji wykonań produkcji.')
?};
_result.RESULT


\PROD_REJ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Aktualizuje zapisy w tabeli do rejestracji wykonań produkcji PROD_REJ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów w tabeli do rejestracji wykonań produkcji (PROD_REJ).'


\updateFAKSO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje znacznik MODIFY dla informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_msk:=FAKSO.names();
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
FAKSO.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? FAKSO.use(_msk.NAME);
      FAKSO.prefix();
      FAKSO.for_each("
         _put:=0;
         __lrec+=1;
         {? FAKSO.MODIFY='' || _put:=1; FAKSO.MODIFY:='N' ?};
         {? _put || {? FAKSO.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
FAKSO.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano zapisy i definicje dla informacji dodatkowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\updateFAKSO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje znacznik MODIFY dla informacji dodatkowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji informacji dodatkowych.'


\ZLGD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.02]
:: OPIS: Uzupełnia brakujące pola w tabeli ZLGD
::----------------------------------------------------------------------------------------------------------------------
_result:=0;

:: Wyłączam triggery
TRIG_OFF.ZLGD:='T';
{? exec('get','#params',500611,2)='T'
|| exec('trig_ZLGD','prod_rej',0)
?};

_formula:="
   _result:=0;
   _can_continue:=1;

   ZLGD.cntx_psh();
   ZLGD.index('ZPARN');
   ZLGD.prefix();
   {? ZLGD.first()
   || {!
      |? _put:=0;
         {? ZLGD.POTW=''
         || ZLGD.POTW:='N';
            _put:=1
         ?};
         {? _put>0
         || _can_continue:=ZLGD.put()
         ?};
         ZLGD.next() & _can_continue>0
      !}
   ?};
   ZLGD.cntx_pop();
   {? _can_continue>0
   || _result:=1
   ?};
   _result
";

_result:=exec('for_each_mask','#table',ZLGD,_formula,,,,1,1);

:: Włączam triggery
TRIG_OFF.ZLGD:='';
{? exec('get','#params',500611,2)='T'
|| exec('trig_ZLGD','prod_rej',1)
?};

{? _result
|| __UPG.msg('Zaktualizowano zapisy w tabeli rejestracji wykonań do zleceń.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji zapisów w tabeli rejestracji wykonań do zleceń.')
?};
_result


\ZLGD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia brakujące pola w tabeli ZLGD
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów w tabeli rejestracji wykonań do zleceń (ZLGD).'


\k_harm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Aktualizuje zapisy w tabeli K_HARM
::----------------------------------------------------------------------------------------------------------------------
K_HARM.prefix();
K_HARM.for_each("
   {? K_HARM.ARKUSZ=''
   || K_HARM.ARKUSZ:='E'; K_HARM.put()
   ?}
");
K_HARM_W.prefix();
K_HARM_W.for_each("
   {? K_HARM_W.K_HARM=null
   || K_HARM_W.K_HARM:=K_HARM_W.K_HARM_P().K_HARM;
      K_HARM_W.put()
   ?}
");
__UPG.msg('Zaktualizowano harmonogramy budżetowania.');
1


\k_harm_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Aktualizuje zapisy w tabeli K_HARM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja harmonogramów budżetowych'


\jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Aktualizuje schematy JPK
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'HTTPPATH');
SKID.ISTDEF:='J';
BPMN.SYM_DOM:='FKS';
_wyn:=exec('edek_upd1','xml',0);
{? _wyn=-1
|| __UPG.msg('Nie udało się zaktualizować schematu JPK_VAT.');
   -1
|| __UPG.msg('Liczba wykonanych aktualizacji schematów JPK_VAT: %1.'[$_wyn]);
   1
?}


\jpk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Aktualizuje schematy JPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie schematu JPK_VAT (3)'


\updateTYPYSP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje znacznik NDVAT dla typów dokumentów sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
TYPYSP.cntx_psh();
TYPYSP.prefix();
TYPYSP.for_each("
   _put:=0;
   __lrec+=1;
   {? TYPYSP.NDVAT='' || _put:=1; TYPYSP.NDVAT:='N' ?};
   {? _put || {? TYPYSP.put() || __lmod+=1 ?} ?}
");
TYPYSP.cntx_pop();
__UPG.msg('Zaktualizowano definicje dla typów dokumentów sprzedaży.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\updateTYPYSP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje znacznik NDVAT dla typów dokumentów sprzedaży - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji typów dokumentów sprzedaży.'


\updateKAL_BUFF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.02]
:: OPIS: Aktualizuje pola KAL_BUFF.NB i KAL_BUFF.NH ze względu na zmianę obsługi nieobecności w H (z kodem 'BOW')
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lmod:=0;
_lrec:=0;
P.cntx_psh();
P.index('OSOBA');
P.prefix();
KAL_BUFF.cntx_psh();
KAL_BUFF.index('NIEOBECN');

:: uzupełnia pola WYL i WYM
H.cntx_psh();
KAL_BUFF.prefix();
KAL_BUFF.for_each("
   {? KAL_BUFF.H & (KAL_BUFF.WYL=0 | KAL_BUFF.WYM=0)
   || KAL_BUFF.WYL:=KAL_BUFF.H().WYL;
      KAL_BUFF.WYM:=H.WYM;
      KAL_BUFF.put()
   ?}
");
H.cntx_pop();

:: aktualizuje dawne nieobecności 'BOW'
KAL_BUFF.prefix();
N.cntx_psh();
_ndx:=N.ndx_tmp('',1,'P',,0, 'NB','RN',0, 'OD',,0);
N.index(_ndx);
_tab:=sql(''+"select REFERENCE as REF, P, NB, NH, DATA from KAL_BUFF where KAL_BUFF.NH like '%hist%' order by P, DATA");
{? _tab.first()
|| _result:=0;
   {!
   |? _lrec+=1;
      {? P.seek(_tab.P)
      || _n_nr:=#_tab.NB;
         N.prefix(P.ref(),_n_nr);
         {? N.find_tab(1,'OD',,'<=',_tab.DATA,'DO',,'>=',_tab.DATA) & KAL_BUFF.seek(_tab.REF)
         || KAL_BUFF.NH:=$N.ref();
            _lmod+=KAL_BUFF.put()
         |? _n_nr=2
         || N.prefix(P.ref(),25);
::          jeżeli dla urlopu bezpłatnego nie została znaleziona nieobecnośc z kodem 2 (urlop bezpłatny), to zobacz
::          czy nie ma przypadkiem tej nieobecności na kodzie 25 (urlop bezpłatny udzielony na wniosek pracodawcy)
            {? N.find_tab(1,'OD',,'<=',_tab.DATA,'DO',,'>=',_tab.DATA) & KAL_BUFF.seek(_tab.REF)
            || KAL_BUFF.NH:=$N.ref();
               _lmod+=KAL_BUFF.put()
            ?}
         ?}
      ?};
      _tab.next()
   !};
   _result:={? _lmod>0 || {? _lmod=_lrec || 1 || -1 ?} || 0 ?}
?};
N.cntx_pop();
N.ndx_drop(_ndx);
KAL_BUFF.cntx_pop();
P.cntx_pop();

{? _result
|| __UPG.msg('Zakończono aktualizowanie zapisów w tabeli buforów planowania czasu pracy (KAL_BUFF).');
   {? _lrec
   || __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
      {? _result=-1
      || __UPG.msg('W trakcie aktualizacji nie udało się poprawić wszystkich wymaganych zapisów.');
         __UPG.msg('W polu KAL_BUFF.NH nie powinno być zapisów wskazujących na tabelę H _hist*.');
         __UPG.msg('Należy zweryfikować poprawność rekordów w tabeli KAL_BUFF.')
      ?}
   || __UPG.msg('W trakcie aktualizacji nie znaleziono zapisów z wprowadzoną '+
                'nieobecnością, powiązanych z tabelą H i kodem \'BOW\'.')
   ?}
|| __UPG.msg('Wystąpił błąd podczas aktualizacji zapisów w tabeli buforów planowania czasu pracy.')
?};
_result


\updateKAL_BUFF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.02]
:: OPIS: Aktualizuje pola KAL_BUFF.NB i KAL_BUFF.NH ze względu na zmianę obsługi nieobecności w H (z kodem 'BOW')
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów w tabeli buforów planowania czasu pracy (KAL_BUFF)'


\R_ZMIANY
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [areKc] [18.02]
:: OPIS: Wczytanie definicji pracy zmianowej. Import danych z pliku tekstowego do tabeli.
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
R_ZMIANY.clear();
{? R_ZMIANY.size()
|| __UPG.msg('Nie uzupełniono tabeli definicji pracy zmianowej.');
   __UPG.msg('Tabela nie jest pusta.')
|| {? exec('import','prc_zmiany')
   || __UPG.msg('Tabela definicji pracy zmianowej została uzupełniona.');
      {? R_ZMIANY.size()=1
      || __UPG.msg('Do tabeli dodano: %1 rekord.'@[$R_ZMIANY.size()])
      || __UPG.msg('Do tabeli dodano: %1 rekordy.'@[$R_ZMIANY.size()])
      ?};
      _result:=1
   || __UPG.msg('Nie uzupełniono tabeli definicji pracy zmianowej.');
      __UPG.msg('Brak pliku "zmiany.txt" zawierającego definicje pracy zmianowej.')
   ?}
?};
_result


\R_ZMIANY_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [areKc] [18.02]
:: OPIS: Wczytanie definicji pracy zmianowej.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słownika definicji pracy zmianowej.'


\KHODB2position
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Uzupełnia zapisy KH i ODB dla pozycji FAKS, ND, ZK_N, ZD_NAG
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ND.names();

ND.cntx_psh();
DK.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ND.use(_msk.NAME);
      DK.use('dokma'+(_msk.NAME+3));
      DK.prefix();
      DK.for_each("
         _put:=0;
         __lrec+=1;
         {? DK.KH=null() & DK.N().KH<>null() || _put:=1; DK.KH:=DK.N().KH ?};
         {? DK.KH_ODB=null() & DK.N().KH_ODB<>null() || _put:=1; DK.KH_ODB:=DK.N().KH_ODB ?};
         {? _put || {? DK.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
ND.cntx_pop();
DK.cntx_pop();

__UPG.msg('Zaktualizowano zapisy dla pozycji dokumentów magazynowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
_msk:=FAKS.names();
FAKS.cntx_psh();
FAP.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? FAKS.use(_msk.NAME);
      FAP.use('fakpo'+(_msk.NAME+3));
      FAP.prefix();
      FAP.for_each("
         _put:=0;
         __lrec+=1;
         {? FAP.KH=null() & FAP.FAKS().KH<>null() || _put:=1; FAP.KH:=FAP.FAKS().KH ?};
         {? FAP.KH_ODB=null() & FAP.FAKS().KH_ODB<>null() || _put:=1; FAP.KH_ODB:=FAP.FAKS().KH_ODB ?};
         {? _put || {? FAP.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();
FAP.cntx_pop();

__UPG.msg('Zaktualizowano zapisy dla pozycji dokumentów sprzedaży i zakupów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
_msk:=ZK_N.names();
ZK_N.cntx_psh();
ZK_P.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ZK_N.use(_msk.NAME);
      ZK_P.use('zkpoz'+(_msk.NAME+3));
      ZK_P.prefix();
      ZK_P.for_each("
         _put:=0;
         __lrec+=1;
         {? ZK_P.KH=null() & ZK_P.N().KH<>null() || _put:=1; ZK_P.KH:=ZK_P.N().KH ?};
         {? ZK_P.KH_ODB=null() & ZK_P.N().ODB<>null() || _put:=1; ZK_P.KH_ODB:=ZK_P.N().ODB ?};
         {? _put || {? ZK_P.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
ZK_N.cntx_pop();
ZK_P.cntx_pop();

__UPG.msg('Zaktualizowano zapisy dla pozycji zamówień sprzedaży.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
_msk:=ZD_NAG.names();
ZD_NAG.cntx_psh();
ZD_POZ.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ZD_NAG.use(_msk.NAME);
      ZD_POZ.use('zdpoz'+(_msk.NAME+3));
      ZD_POZ.prefix();
      ZD_POZ.for_each("
         _put:=0;
         __lrec+=1;
         {? ZD_POZ.KH=null() & ZD_POZ.ZD_NAG().KH<>null() || _put:=1; ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH ?};
         {? ZD_POZ.KH_MSC=null() & ZD_POZ.ZD_NAG().KH_MSC<>null() || _put:=1; ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC ?};
         {? _put || {? ZD_POZ.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
ZD_NAG.cntx_pop();
ZD_POZ.cntx_pop();

__UPG.msg('Zaktualizowano zapisy dla pozycji zamówień dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

VAR_DEL.delete('__lrec','__lmod');
_res


\KHODB2position_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Uzupełnia zapisy KH i ODB dla pozycji FAKS, ND, ZK_N, ZD_NAG - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów dla pozycji:\n'
'   dokumentów magazynowych,\n'
'   dokumentów sprzedaży,\n'
'   dokumentów zakupu,\n'
'   zamówień sprzedaży,\n'
'   zamówień dostaw.'


\dokumTOPER2ZGP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia dokumentację do pozycji przewodników na podstawie źródłowych operacji
::----------------------------------------------------------------------------------------------------------------------
exec('declare','tech_doc');
KOMM.init();
_result:=1;
ZGP.cntx_psh();
ZGP.prefix();
{? ZGP.first()
|| {!
   |? {? ZGP.RTOPER<>''
      || DocLib.copy('TOPER',exec('FindAndGet','#table',TOPER,ZGP.RTOPER,,"ref()",null()),'ZGP',ZGP.ref())
      ?};
      ZGP.next()
   !}
?};
ZGP.cntx_pop();
__UPG.msg('Uzupełniono dokumentację do pozycji przewodników.');
_tab:=KOMM.tab();
{? _tab.first()
|| __UPG.msg('Poniższe komunikaty mogą wynikać z powtórnego uruchomienia funkcji.');
   {!
   |? __UPG.msg(_tab.MESSAGE);
      _tab.next()
   !}
?};
_result


\dokumTOPER2ZGP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia dokumentację do pozycji przewodników na podstawie źródłowych operacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie dokumentacji do pozycji przewodników na podstawie źródłowych operacji.'


\PLIKIDOC
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Przepisuje dokumenty z biblioteki PLIB_DOC bezpośrednio do załączników PLIKIDOC,
::       przepisuje załączniki z PLIKIIMG do PLIKIDOC
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
PLIKIDOC.cntx_psh(); PLIKIIMG.cntx_psh();
PLIKIDOC.prefix(); PLIKIIMG.prefix();
__lrec:=__lmod:=0;
{? _result=1
|| _formula:="
      __lrec+=1;
      {? PLIKIDOC.DOC=null()
      || PLIKIDOC.DOC:=PLIKIDOC.FILE().DOC;
         __lmod+=1;
         PLIKIDOC.put()
      ?}
   ";
   _result:=PLIKIDOC.for_each(_formula,1);
   __UPG.msg('Zaktualizowano zapisy dla załączników (dokumenty).');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};
{? _result=1
|| __lrec:=__lmod:=0;
   _formula:="
      __lrec+=1;
      PLIKIDOC.index('TRS');
      PLIKIDOC.prefix(PLIKIIMG.TAB,PLIKIIMG.MSK,PLIKIIMG.REF,'',null(),PLIKIIMG.FILE().IMAGE);
      {? ~PLIKIDOC.first()
      || PLIKIDOC.blank(1);
         PLIKIDOC.TAB:=PLIKIIMG.TAB;
         PLIKIDOC.REF:=PLIKIIMG.REF;
         PLIKIDOC.MSK:=PLIKIIMG.MSK;
         PLIKIDOC.DOC:=PLIKIIMG.FILE().IMAGE;
         __lmod+=1;
         PLIKIDOC.add()
      ?}
   ";
   _result:=PLIKIIMG.for_each(_formula,1);
   __UPG.msg('Zaktualizowano zapisy dla załączników (obrazy).');
   __UPG.msg('Przetworzono: %1 zapisów, z czego przeniesiono: %2 zapisów.'[$__lrec,$__lmod])
?};
PLIKIDOC.cntx_pop(); PLIKIIMG.cntx_pop();
VAR_DEL.delete('__lrec','__lmod');
_result


\PLIKIDOC_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Przepisuje dokumenty z biblioteki bezpośrednio do załączników PLIKIDOC,
::       przepisuje załączniki z PLIKIIMG do PLIKIDOC - opis
::----------------------------------------------------------------------------------------------------------------------
'Reorganizacja załączników przypisanych do technologii i zleceń.'


\ZGP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w pozycjach przewodników
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
ZGP.cntx_psh(); ZGP.prefix();
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   {? ZGP.DOC_REJ=''
   || ZGP.DOC_REJ:='P';
      __lmod+=1;
      ZGP.put()
   ?}
";
_result:=ZGP.for_each(_formula,1);
__UPG.msg('Zaktualizowano pozycje przewodników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
ZGP.cntx_pop();
_result


\ZGP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w pozycjach przewodników - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w pozycjach przewodników.'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\ZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w zleceniach
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
ZL.cntx_psh(); ZL.prefix();
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   {? ZL.IL0=0 | ZL.ILPDOK=0 | ZL.ILPWYK=0 | ZL.ILDOK<>0 | ZL.ILWYK<>0
   || __lmod+=1;
      {? ZL.IL0=0 || ZL.IL0:=ZL.IL ?};
::    Pola ZL.ILPWYK oraz ZL.ILPDOK ustawia trigger przed put
      ZL.put()
   ?}
";
_result:=ZL.for_each(_formula,1);
__UPG.msg('Zaktualizowano zlecenia.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
ZL.cntx_pop();
_result


\ZL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w zleceniach - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w zleceniach.'


\ROK_F
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnia zawartość pola ZK w tabeli ROK_F
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
ROK_F.prefix();
__prec:=0;
_result:=ROK_F.for_each("{? ROK_F.ZK='' || ROK_F.ZK:='N'; ROK_F.put(); __prec+=1 ?}",1);
__UPG.msg('Zaktualizowano lata obrachunkowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$ROK_F.size(),$__prec]);
VAR_DEL.delete('__prec');
ROK_F.cntx_pop();
_result


\ROK_F_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Opis formuły uzupełniającej zawartość pola ZK w tabeli ROK_F
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w latach obrachunkowych.'


\SRSR_AMOR100
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnia zawartość pola SRSR.AMOR100
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
SRSR.cntx_psh();
_names:=SRSR.names();
{? _names.first()
|| {! |?
      SRSR.use(_names.NAME);
      SRSR.prefix();
      SRSR.for_each("{? SRSR.AMOR100='' || SRSR.AMOR100:='N'; SRSR.put() ?}");
      _names.next()
   !}
?};
__UPG.msg('Zaktualizowano dane środków trwałych.');
SRSR.cntx_pop();
_result


\SRSR_AMOR100_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Opis formuły uzupełniającej zawartość pola AMOR100 w tabeli SRSR
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w tabelach środków.'


\MA_NJP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnienie tabeli częstotliwości rozliczania amortyzacji jednorazowej
::----------------------------------------------------------------------------------------------------------------------
MAF.index('KOD');
MAF.prefix('P');
{? ~MAF.first()
|| MAF.blank();
   MAF.KOD:='P';
   MAF.OPIS:='W miesiącu wprowadzenia';
   MAF.add()
?};
MAF.prefix('M');
{? ~MAF.first()
|| MAF.blank();
   MAF.KOD:='M';
   MAF.OPIS:='Miesięcznie';
   MAF.add()
?};
MAF.prefix('K');
{? ~MAF.first()
|| MAF.blank();
   MAF.KOD:='K';
   MAF.OPIS:='Kwartalnie';
   MAF.add()
?};
MAF.prefix('R');
{? ~MAF.first()
|| MAF.blank();
   MAF.KOD:='R';
   MAF.OPIS:='Na koniec roku';
   MAF.add()
?};
MAF.prefix();
_result:=MAF.size()=4;
__UPG.msg('Uzupełniono słownik częstotliwości rozlicznia amortyzacji jednorazowej.');
_result


\MA_NJP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Opis formuły uzupełniającej słownik częstotliwości rozliczania amortyzacji jednorazowej
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słownika częstotliwości rozliczania amortyzacji jednorazowej'


\k_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Aktualizacja pola K_ROLE.TYPE
::----------------------------------------------------------------------------------------------------------------------
K_ROLE.prefix();
K_ROLE.for_each("
   {? K_ROLE.AREA=''
   || K_ROLE.AREA:='CTR';
      K_ROLE.put()
   ?}
");
__UPG.msg('Zaktualizowano pole w zakresach danych dla raportów controllingowych.');
1


\k_role_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Aktualizacja pola K_ROLE.TYPE - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola w zakresach danych dla raportów controllingowych'


\stalesys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');

_ctx:=obj_new('zes_ndx','def_ndx');
_ctx.zes_ndx:='SYMBOL';
_ctx.def_ndx:='KST_ZES';
params_set('ctx',_ctx);

: weryfikacja zgodności z definicją
exec('fun_wer_sys','#stalesys');


__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\updateMDOST
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje pole MKODK dla tabeli MDOST
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
MKODK.cntx_psh();
MKODK.prefix();
MKODK.for_each("
   _put:=0;
   __lrec+=1;
   {? MKODK.M<>null() & MKODK.KH<>null() & exec('act_mdostMkodk','kody_kresk',MKODK.KH,MKODK.M,MKODK.ref())=2
   || __lmod+=1
   ?}
");
MKODK.cntx_pop();
__UPG.msg('Zaktualizowano definicje dla dostawców materiałów o oznaczenia kodów zewnętrznych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\updateMDOST_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje znacznik NDVAT dla typów dokumentów sprzedaży - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji dostawców dla materiałów o oznaczenia kodów zewnętrznych.'


\zus_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnianie słownika ~Płatności ZUS i typów przelewów
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,FINFO);
SKID_RBK.cntx_psh(); SLU.cntx_psh(); SLO.cntx_psh(); BANORG.cntx_psh(); B.cntx_psh();
SKID_RBK.index('TAB'); SKID_RBK.prefix();
SLU.index('WZORZEC'); SLU.prefix('prosty','~Płatności ZUS');
{? SLU.first()
|| SLO.index('SL'); SLO.prefix(SLU.ref());

   KRAJE.cntx_psh();
   KRAJE.index('KRAJE');
   KRAJE.prefix('PL');
   {? KRAJE.first() || _kraj_ref:=KRAJE.ref() || _kraj_ref:=null ?};
   KRAJE.cntx_pop();

   BANORG.index('NDX');
   BANORG.prefix('60000','NBP ZUS',);

   _org:=_bank:=null;

   {? BANORG.first()
   || _org:=BANORG.ref()
   || BANORG.blank();
      BANORG.KOD:='60000';
      BANORG.KODZ:='';
      BANORG.AKTYWNY:='T';
      BANORG.NAZ:='NBP ZUS';
      BANORG.NRCB:='';
      {? BANORG.add() || _org:=BANORG.ref() ?}
   ?};
   {? _org
   || B.index('BANK');
      B.prefix('NBP ZUS SKŁADKI','60000002');
      {? B.first()
      || _bank:=B.ref()
      || B.blank();
         B.BANORG:=_org;
         B.KOD:='';
         B.KODISO:=_kraj_ref;
         B.NB:='NBP ZUS SKŁADKI';
         B.NUMER:='60000002';
         B.STATUS:='B';
         {? B.add() || _bank:=B.ref() ?}
      ?}
   ?};

   {? _kraj_ref & ~SLO.find_key('ZUS')
   || SLO.blank();
      SLO.KOD:='ZUS';
      SLO.TR:='ZUS e-składka';
      {? SLO.add()
      || SKID_RBK.prefix(REF.FIRMA,'ZUS','ZUS',0,SLO.ref());
         {? ~SKID_RBK.first()
         || SKID_RBK.prefix();
            SKID_RBK.blank(1);
            SKID_RBK.TAB:='ZUS';
            SKID_RBK.INNWAL:='N';
            SKID_RBK.RS:='N';
            SKID_RBK.RD:='N';
            SKID_RBK.FIRMA:=REF.FIRMA;
            SKID_RBK.SLU:=SLU.ref();
            SKID_RBK.SLO:=SLO.ref();
            SKID_RBK.N:='';
            SKID_RBK.BANK:=_bank;
            SKID_RBK.KRAJ:=_kraj_ref;
            SKID_RBK.WAL:=FINFO.NAROD;
            SKID_RBK.add()
         ?}
      ?}
   |? _kraj_ref & SLO.find_key('ZUS')
   || SKID_RBK.prefix(REF.FIRMA,'ZUS','ZUS',0,SLO.ref());
      {? ~SKID_RBK.first()
      || SKID_RBK.prefix();
         SKID_RBK.blank(1);
         SKID_RBK.TAB:='ZUS';
         SKID_RBK.INNWAL:='N';
         SKID_RBK.RS:='N';
         SKID_RBK.RD:='N';
         SKID_RBK.FIRMA:=REF.FIRMA;
         SKID_RBK.SLU:=SLU.ref();
         SKID_RBK.SLO:=SLO.ref();
         SKID_RBK.N:='';
         SKID_RBK.BANK:=_bank;
         SKID_RBK.KRAJ:=_kraj_ref;
         SKID_RBK.WAL:=FINFO.NAROD;
         SKID_RBK.add()
      ?}
   ?}
?};
SKID_RBK.cntx_pop(); SLU.cntx_pop(); SLO.cntx_pop(); BANORG.cntx_pop(); B.cntx_pop();
__UPG.msg('Zaktualizowano listę rachunków bankowych do płatności ZUS.');
TZL.cntx_psh();
TZL.index('KOD'); TZL.prefix();
TZL.KOD:='PPL: ZUS'; TZL.OPIS:='Przelewy z płac dla ZUS';
{? ~TZL.find_key(TZL.KOD,) || TZL.add(1) ?};
TZL.cntx_pop();
__UPG.msg('Zaktualizowano słownik typów przelewów bankowych.');
1


\zus_one_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS:  Uzupełnianie słownika ~Płatności ZUS i typów przelewów - opis
::----------------------------------------------------------------------------------------------------------------------
' Uzupełnianie słownika ~Płatności ZUS i typów przelewów'


\ku_l50
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Dodanie pol do KST kwota ograniczenia 50% kosztów uzyskania
::----------------------------------------------------------------------------------------------------------------------
exec('add_acr','#stalesys','KU_L50','Kwota ograniczenia 50% kosztów uzyskania','T','T','T','KST','PPL');
_dodano:=exec('kst_war_add','#stalesys','KST.KU_L50','0',date(0,0,0));
_dodano+=exec('kst_war_add','#stalesys','KST.KU_L50','42764',date(2013,01,1));
_dodano+=exec('kst_war_add','#stalesys','KST.KU_L50','85528',date(2018,01,1));
{? _dodano
|| __UPG.msg('Dodano informacje o kwocie ograniczenia 50% kosztów uzyskania.')
?};
1


\ku_l50_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Opis funkcji aktualizacji stałych systemu zwiazanych z kwota ograniczenia 50% kosztów uzyskania
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - kwota ograniczenia 50% kosztów uzyskania.'


\plat_zkn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Aktualizuje płatności na zamówieniach dostaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('czytaj','#stalesys',,INFO,'NAROD');
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ZK_N.names();
ZK_N.cntx_psh();
ZK_P.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? ZK_N.use(_msk.NAME);
      ZK_N.use((5+ZK_N.name())+(_msk.NAME+3));
      ZK_N.prefix();
      ZK_N.for_each("
         _put:=0;
         __lrec+=1;

         {? ZK_N.PL
         ||
            exec('plat_sel','faktury_plat',ZK_N);
            {? FZ.FORMAPLA=''
            ||
               _put:=1;
               FZ.FORMAPLA:=ZK_N.PL().KOD;
               FZ.PL:=ZK_N.PL;
               FZ.TERMPLAT:=ZK_N.DP+ZK_N.LD;
               ZK_N.TZ:=FZ.TERMPLAT;
               exec('plat_one','faktury_plat',ZK_N.ref());
               {? ZK_N.put() || __lmod+=1 ?};
               exec('plat_przel','faktury_plat',ZK_N.ref())

            ?}
         ?}
      ");
      _msk.next()
   !}
?};
ZK_N.cntx_pop();
ZK_P.cntx_pop();

__UPG.msg('Zaktualizowano płatności dla zamówień sprzedaży.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\plat_zkn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Aktualizuje płatności na zamówieniach dostaw - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja płatności na zamówieniach sprzedaży'


\MGRP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.02]
:: OPIS: Uzupełnia zawartość pól WYPOS, ZWROT, CTRLIL w tabeli MGRP
::----------------------------------------------------------------------------------------------------------------------
MGRP.cntx_psh();
MGRP.prefix();
__prec:=0;
_result:=MGRP.for_each("_put:=0;
                        {? MGRP.WYPOS=''
                        || MGRP.WYPOS:='N';
                           _put:=1
                        ?};
                        {? MGRP.ZWROT=''
                        || MGRP.ZWROT:='T';
                           _put:=1
                        ?};
                        {? MGRP.CTRLIL=''
                        || MGRP.CTRLIL:='O';
                           _put:=1
                        ?};
                        {? _put
                        || MGRP.put();
                           __prec+=1
                        ?}",1);
__UPG.msg('Zaktualizowano podgrupy materiałowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$MGRP.size(),$__prec]);
VAR_DEL.delete('__prec');
MGRP.cntx_pop();
_result


\MGRP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.02]
:: OPIS: Opis formuły uzupełniającej zawartość zawartość pól WYPOS, ZWROT, CTRLIL w tabeli MGRP
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w podgrupach materiałowych.'


\skid_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Aktualizacja listy parametrów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');
_zkl:='Zarządzanie kapitałem ludzkim: ';
::PAR_SKID.set(399,1,0,'','N',_zkl+'aktywne','''T'',''N''');
PAR_SKID.set(400,1,0,'','T',_zkl+'jednorodne kompetencje profili ocen','''T'',''N''');
PAR_SKID.set(401,1,0,'','T',_zkl+'globalne typy ocen','''T'',''N''');
PAR_SKID.set(402,1,0,'','N',_zkl+'lokalne typy ocen dla kompetencji','''T'',''N''');
PAR_SKID.set(403,1,0,'','N',_zkl+'lokalne typy ocen dla profili','''T'',''N''');
PAR_SKID.set(404,1,0,'','T',_zkl+'globalne metody oceny','''T'',''N''');
PAR_SKID.set(405,1,0,'','N',_zkl+'lokalne metody oceny dla kompetencji','''T'',''N''');
PAR_SKID.set(406,1,0,'','N',_zkl+'lokalne metody oceny dla profili','''T'',''N''');
PAR_SKID.set(407,1,0,'','T',_zkl+'globalne wartości ocen','''T'',''N''');
PAR_SKID.set(408,1,0,'','N',_zkl+'lokalne wartości ocen dla kompetencji','''T'',''N''');
PAR_SKID.set(409,1,0,'','N',_zkl+'zmiana wartości dla kompetencji','''T'',''N''');
PAR_SKID.set(410,1,0,'','N',_zkl+'zmiana opisów dla kompetencji','''T'',''N''');
PAR_SKID.set(411,1,0,'','N',_zkl+'lokalne wartości ocen dla stanowisk','''T'',''N''');
PAR_SKID.set(412,1,0,'','N',_zkl+'zmiana wartości dla stanowisk','''T'',''N''');
PAR_SKID.set(413,1,0,'','N',_zkl+'zmiana opisów dla stanowisk','''T'',''N''');
PAR_SKID.set(414,1,0,'','N',_zkl+'lokalne wartości ocen dla profili','''T'',''N''');
PAR_SKID.set(415,1,0,'','N',_zkl+'zmiana wartości dla profili','''T'',''N''');
PAR_SKID.set(416,1,0,'','N',_zkl+'zmiana opisów dla profili','''T'',''N''');
PAR_SKID.set(417,1,0,'','SP',_zkl+'domyślna metoda oceny');
PAR_SKID.set(418,1,0,'','N',_zkl+'domyślnie z oceną opisową','''T'',''N''');
PAR_SKID.set(419,1,0,'','N',_zkl+'zmiana domyślnego rodzaju profilu','''T'',''N''');
::PAR_SKID.set(420,2,0,'','7',_zkl+'powiadom z wyprzedzeniem o zakończeniu sesji ocen');
PAR_SKID.set(421,1,0,'','N',_zkl+'przeglądanie profili według rodzaju');
PAR_SKID.set(422,1,0,'','T',_zkl+'kopiowanie kompetencji tylko z profili bazowych');
::PAR_SKID.set(423,2,0,'','0',_zkl+'il. dni przed datą ważności szkolenia dla której pracownik może zostać zapisany');
::PAR_SKID.set(424,2,0,'','7',_zkl+'na ile dni przed datą rozpoczęcia szkolenia pracownik ma potwierdzić uczestnictwo');
::PAR_SKID.set(425,2,0,'','7',_zkl+'na ile dni przed datą potwierdzenia szkolenia wysłać monit');
::PAR_SKID.set(426,2,0,'','7',_zkl+'powiadom z wyprzedzeniem o zakończeniu planowania zatrudnienia');
PAR_SKID.set(427,1,0,'','T',_zkl+'wskazanie na wartość oczekiwaną na formularzu ocen');
::PAR_SKID.set(428,1,0,'','T',_zkl+'filtrowanie kompetencji celów');
::PAR_SKID.set(429,1,0,'','T',_zkl+'filtrowanie kompetencji obszarów rozwojowych');
::PAR_SKID.set(430,1,0,'','T',_zkl+'automatyczne przesuwanie pracownika z listy rezerwowej na podstawową');

__UPG.msg('Utworzenie listy parametrów wykorzystywanych w dziedzinach: POC, PBA, PSZ.');

1


\skid_par_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Aktualizacja listy parametrów - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy parametrów'


\zz_kryt_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Zwraca nazwę pliku zawierającego informacje o strukturze drzewa tabeli ZZ_KRYT.
::   WE:
::   WY: nazwa pliku
::----------------------------------------------------------------------------------------------------------------------
'zz_kryt_str.txt'


\zz_kryt_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Spłaszcza" strukturę kryteriów umożliwiając aktualizację danych tabeli ZZ_KRYT w indeksie UNIQUE.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_name:=exec('zz_kryt_str','upgrade_1802');
: zawartość pliku jest tworzona tylko raz
{? fexists(_name,1)=0
|| _handle:=fopen(_name,'w',1,0);
   {? _handle<=0
   || __UPG.msg('Błąd dostępu do pliku %1'[_name]);
      return(0)
   ?};
   ZZ_KRYT.cntx_psh();
   ZZ_KRYT.index('ZZ_KRYT');
   ZZ_KRYT.prefix();
   _loop:=ZZ_KRYT.first();
   {!
   |? _loop
   |! {? ZZ_KRYT.ZZ_KRYT<>0
      || fwrite(_handle,'%1|%2|%3'[$ZZ_KRYT.ref(),$ZZ_KRYT.ZZ_KRYT,$ZZ_KRYT.NUMER])
      ?};
      _loop:=ZZ_KRYT.next()
   !};
   ZZ_KRYT.cntx_pop();
   fclose(_handle)
?};

: wyłącz kontrolę podczas zapisu danych
_files:=exec('chk_off_files','phr_dane');
_arg:=exec('off_arg','phr_dane','_chk','1','_putb','1','_puta','~~');
_off:=exec('fml_off','phr_dane',_files,_arg);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_mk_flat:="
   _err:=0;
   ZZ_KRYT.cntx_psh();
   ZZ_KRYT.index('ZZ_KRYT');
   ZZ_KRYT.prefix(_a);
   {!
   |? _err=0 & ZZ_KRYT.first()
   |! _err+=_c(#ZZ_KRYT.ref(),_b,_c)
   !};
   ZZ_KRYT.cntx_pop();
   {? ZZ_KRYT.ZZ_KRYT<>0
   || ZZ_KRYT.ZZ_KRYT:=0;
      ZZ_KRYT.NUMER:=_b+#ZZ_KRYT.ref();
      ZZ_KRYT.cntx_psh();
      ZZ_KRYT.clear();
      {? ~ZZ_KRYT.put(1)
      || _err+=1
      ?};
      ZZ_KRYT.cntx_pop()
   ?};
   _err
";

: "spłaszczenie" struktury
: (wszystko albo nic)
do();
_err:=0;
ZZ_KRYT.cntx_psh();
ZZ_KRYT.index('NAZWA');
ZZ_KRYT.prefix();
_base:=ZZ_KRYT.size();
_loop:=ZZ_KRYT.first();
{!
|? _loop & do_state()=1
|! _err+=_mk_flat(#ZZ_KRYT.ref(),_base,_mk_flat);
   {? _err<>0
   || undo()
   ?};
   _loop:=ZZ_KRYT.next()
!};
ZZ_KRYT.cntx_pop();
end();

: jeśli "spłaszczenie" się nie powiodło,
: to plik śladowy powinien być usunięty
{? _err<>0
|| ferase(_name,1)
?};

: przywróć kontrolę zapisu danych
exec('chk_on','phr_dane',_off);
_err=0


\zz_kryt_exp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Spłaszcza" strukturę kryteriów umożliwiając aktualizację danych tabeli ZZ_KRYT w indeksie UNIQUE - opis.
::   WE:
::   WY: Opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Przygotowanie danych tabeli ZZ_KRYT do aktualizacji'


\move_doks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przenosi dokumenty kadrowe (tabela ZZ_DOK) ze zbioru zz_dok do zbioru docelowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_arg:=exec('off_arg','phr_dane','_chk','1','_putb','1','_puta','~~');
_off:=exec('fml_off','phr_dane',_files,_arg);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_dst:='zz_do'+($exec('ref_firma','ustawienia')+1);
exec('move_doks','phr_dane','zz_dok',_dst,1);
__UPG.msg('Zamieniono odwołania do zbioru zz_dok na %1'[_dst]);

exec('chk_on','phr_dane',_off);
1


\move_doks_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przenosi dokumenty kadrowe (tabela ZZ_DOK) ze zbioru zz_dok do zbioru docelowego - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Przeniesienie dokumentów kadrowych (tabela ZZ_DOK) ze zbioru zz_dok do zbioru docelowego'


\zz_kryt_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Odtwarza" strukturę kryteriów po aktualizacji danych tabeli ZZ_KRYT w indeksie UNIQUE.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_name:=exec('zz_kryt_str','upgrade_1802');
: wymagane istnienie pliku
{? fexists(_name,1)=0
|| __UPG.msg('Nie znaleziono pliku %1'[_name]);
   return(0)
?};
_handle:=fopen(_name,'r',1,0);
: wymagany dostęp do pliku
{? _handle<=0
|| __UPG.msg('Błąd dostępu do pliku %1'[_name]);
   return(0)
?};

: wyłącz kontrolę podczas zapisu danych
_files:=exec('chk_off_files','phr_dane');
_arg:=exec('off_arg','phr_dane','_chk','1','_putb','1','_puta','~~');
_off:=exec('fml_off','phr_dane',_files,_arg);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

: odtwórz strukturę drzewa
: (wszystko albo nic)
do();
_err:=0;
ZZ_KRYT.cntx_psh();
ZZ_KRYT.index('NAZWA');
{!
|? _err=0 & (_line:=fread(_handle))<>'\n'
|! _buf:=spli_str(_line,'|');
   ZZ_KRYT.prefix();
   {? ZZ_KRYT.seek(_buf[1])
   || ZZ_KRYT.ZZ_KRYT:=#_buf[2];
      ZZ_KRYT.NUMER:=#_buf[3];
      {? ~ZZ_KRYT.put(1)
      || _err+=1;
         undo()
      ?}
   ?};
   &_buf
!};
ZZ_KRYT.cntx_pop();
end();

: usuń plik śladowy tylko wtedy, gdy odbudowa struktury zakończyła się sukcesem
fclose(_handle);
{? _err=0
|| ferase(_name,1)
?};

: przywróć kontrolę zapisu danych
exec('chk_on','phr_dane',_off);
_err=0


\zz_kryt_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Odtwarza" strukturę kryteriów po aktualizacji danych tabeli ZZ_KRYT w indeksie UNIQUE - opis.
::   WE:
::   WY: Opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Odtworzenie struktury drzewa w tabeli ZZ_KRYT po aktualizacji'


\create_doks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Aktualizuje dane pod kątem spójości zapisow w tabeli ZZ_DOK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

exec('create_doks','phr_dane',1);
__UPG.msg('Zaktualizowano dane tabel zawierających wskazanie na wiersz tabeli ZZ_DOK');

exec('chk_on','phr_dane',_off);
1


\create_doks_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Aktualizuje dane pod kątem spójości zapisow w tabeli ZZ_DOK - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych pod kątem spójości zapisow w tabeli ZZ_DOK'


\firma_p_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Uzupełnia zawartość kolumny FIRMA tabeli (formuła biblioteczna).
::   WE:  _a  [TABLE]     - Alias tabeli.
::       [_b] [REFERENCE] - Referencja wiersza tabeli FIRMA [domyślnie: bieżąca].
::       [_c] [STRING]    - Akronim kolumny [domyślnie: 'FIRMA'].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_TAB:=_a;
_ref:={? var_pres('_b')=type_of(null()) & _b<>null() & ref_tab(_b)=FIRMA || _b || exec('ref_firma','ustawienia') ?};
_col:={? var_pres('_c')=type_of('') & _c<>'' || _c || 'FIRMA' ?};

progress(,'Uzupełnienie kolumny %1 w tabeli %2.'@ [_col,2-!_TAB],FUN.TYT,1);

_iok:=_ibad:=0;
_TAB.cntx_psh();
_MASK:=_TAB.names();
_loop:=_MASK.first();
{!
|? _loop
|! _TAB.use(_MASK.NAME);
   {? _TAB.f_active()
   || _TAB.f_clear()
   || _TAB.clear()
   ?};
   _TAB.f_set(,,'%1.%2 is null' [2-!_TAB,_col]);
   _loop:=_TAB.f_first();
   {!
   |? _loop
   |! ($('_a.%1:=_b' [_col]))(_TAB,_ref);
      {? _TAB.put()
      || _iok+=1
      || _ibad+=1
      ?};
      _loop:=_TAB.f_next()
   !};
   _TAB.f_clear();
   _loop:=_MASK.next()
!};
_TAB.cntx_pop();
__UPG.msg('Tabela: %1 (%2) | Zapisów poprawionych: %3 | Zapisów, których nie udało się poprawić: %4 |'
   [2-!_TAB,_TAB.comment(),$_iok,$_ibad]
);

prgs_clr();
{? _iok=_ibad
|| 1
|| -1
?}


\firma_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Uzupełnienie wartości kolumny FIRMA w wybranych tabelach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

::-1 -> 1
:: 0 -> 2
:: 1 -> 3
_stat:=obj_new(3);
{! _lp:=1 .. obj_len(_stat)
|! _stat[_lp]:=0
!};

_firma:=exec('ref_firma','ustawienia');
_stat[2+exec('firma_p_akt','upgrade_1802',ZZ_HIST,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',ZF_SKL,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',ZF_DEF,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',ZO_PROG,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',ZA_INST,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',ZA_ZEST,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',SZB_OKR,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',SZK_PROG,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',STN_GR,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',SZK_PLAN,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',SZK_DAT,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgrade_1802',SZK_ZAP,_firma)]+=1;

exec('chk_on','phr_dane',_off);

{? _stat[2]
:: Jeżeli wystąpił choć jeden błąd, to zgłaszany jest błąd.
|| 0
:: Jeżeli wystąpiło choć jedno ostrzeżenie, to zgłaszane jest ostrzeżenie.
|? _stat[1]
|| -1
|| 1
?}


\firma_p_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Uzupełnienie wartości kolumny FIRMA w wybranych tabelach - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wartości kolumny FIRMA w wybranych tabelach'


\zf_rpi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Transfer formatów elementów formularzy ocen pracowniczych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_arr:=obj_new(2);
{! _lp:=1 .. 2
|! _arr[_lp]:=obj_new('acr','blank','std')
!};
_arr[1].acr:='SZABLON';
_arr[2].acr:='SZABLON2';
{! _lp:=1 .. 2
|! _arr[_lp].blank:=ZF_RPI.fld_fml(_arr[_lp].acr,'BLANK',"*")
!};
_arr[1].std:=
   '|poc_zfcn01.rpi|poc_zfcw01.rpi|poc_zfdn01.rpi|poc_zfdod01.rpi|poc_zfdp01.rpi|poc_zfmb01.rpi|poc_zfok01.rpi'
   '|poc_zfopk01.rpi|poc_zfopp01.rpi|poc_zfp01.rpi||poc_zfpoo01.rpi|poc_zfps01.rpi|poc_zfrk01.rpi|poc_zfwp01.rpi|';
_arr[2].std:=
   '|poc_zrcn01.rpi|poc_zrcw01.rpi|poc_zrdn01.rpi|poc_zrdod01.rpi|poc_zrdp01.rpi|poc_zrmb01.rpi|poc_zrok01.rpi'
   '|poc_zropk01.rpi|poc_zropp01.rpi|poc_zrp01.rpi|poc_zrpoo01.rpi||poc_zrps01.rpi|poc_zrrk01.rpi|poc_zrwp01.rpi|';

ZF_SKL.cntx_psh();
ZF_SKL.f_clear();
ZF_SKL.index('KOD');
ZF_SKL.prefix(exec('ref_firma','ustawienia'));
{? ZF_SKL.first()
|| _ok:=0;
   ZF_RPI.cntx_psh();
   ZF_RPI.f_clear();
   ZF_RPI.index('NAZWA');
   {!
   |? ZF_RPI.prefix(ZF_SKL.ref());
      {? ZF_RPI.first()
      || {!
         |? _nag:='Składnik: %1 | Szablon: %2 | ' [ZF_RPI.ZF_SKL().KOD,ZF_RPI.NAZWA];
            _put:=0;
            {! _lp:=1 .. 2
            |! _fld:='ZF_RPI.'+_arr[_lp].acr;
               {? ($_fld)()=''
::                Pole nie było wypełnione - przyjmijmy wartość domyślną zwracaną przez formułę na wartość początkową.
               || _val:=_arr[_lp].blank();
                  ($(_fld+':=_a'))(_val);
                  __UPG.msg(_nag+'Uzupełniono wartość pola %1 [%2].' [_arr[_lp].acr,_val]);
                  _put+=1
               ?};
               _valb:=($_fld)();
               {? 4+_valb<>'poc_'
::                "Stara" nazwa szablonu, jeszcze bez prefisku 'poc_', który musi być doklejony.
               || _vala:='poc_'+_valb;
                  {? _arr[_lp].std*('|%1|' [_vala])=0
::                   Szablon nie jest standardowy - trzeba dokleić literkę 'q'.
                  || {? _vala+4='.rpi'
                     || _vala:=(_vala-4)+'q.rpi';
                        __UPG.msg(_nag+'Zmieniono wartość pola %1 z [%2] na [%3].' [_arr[_lp].acr,_valb,_vala])
                     || __UPG.msg(_nag+'Nieprawidłowa wartość pola %1 [%2].' [_arr[_lp].acr,_valb])
                     ?}
                  || __UPG.msg(_nag+'Zmieniono wartość pola %1 z [%2] na [%3].' [_arr[_lp].acr,_valb,_vala])
                  ?};
                  ($(_fld+':=_a'))(_vala);
                  _put+=1
               ?};
               _val:=($_fld)();
               {? ~fexists(_val,1)
               || __UPG.msg(_nag+'Brak dostępu do pliku %1.' [_val])
               ?}
            !};
            {? _put
            || ZF_RPI.put()
            || _ok+=1
            ?};
            ZF_RPI.next()
         !}
      ?};
      ZF_SKL.next()
   !};
   ZF_RPI.cntx_pop();
   {? _ok
   || __UPG.msg('Liczba szablonów, które nie wymagały zmian: %1.' [$_ok])
   ?}
?};
ZF_SKL.cntx_pop();
exec('chk_on','phr_dane',_off);
1


\zf_rpi_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Transfer formatów elementów formularzy ocen pracowniczych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Transfer formatów elementów formularzy ocen pracowniczych'


\zo_cel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_ret:=0;

ZO_CEL.cntx_psh();
ZO_CEL.index('ZO_CEL');
ZO_CEL.prefix();
{? ZO_CEL.first()
|| __UPG.msg('Wszystkich zapisów: %1.' [$ZO_CEL.size()]);
   _p1:=_p0:=_ok:=0;
   {!
   |? {? ZO_CEL.Z='?'
      || ZO_CEL.Z:='X';
         {? ZO_CEL.put()
         || _p1+=1
         || _p0+=1
         ?}
      || _ok+=1
      ?};
      ZO_CEL.next()
   !};
   __UPG.msg('Zapisów nie wymagających zmiany: %1.' [$_ok]);
   __UPG.msg('Zapisów wymagających zmiany: %1, w tym poprawionych: %2 / niepoprawionych: %3.' [$(_p1+_p0),$_p1,$_p0]);
   _ret:={? _p0 || -1 || 1 ?}

|| __UPG.msg('Brak zapisów do poprawienia.');
   _ret:=1
?};
ZO_CEL.cntx_pop();
exec('chk_on','phr_dane',_off);

_ret


\zo_cel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zamiana znaku specjalnego "?" na "X" w znaczniku realizacji celu (ZO_CEL.Z).\n'
'Powodem zamiany są zmiany technologiczne - wprowadzenie możliwości korzystania ze znaków specjalnych '
'w polach szybkiego filtra.'


\zo_celk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_ret:=0;

ZO_CELK.cntx_psh();
ZO_CELK.index('UNIQUE');
ZO_CELK.prefix();
{? ZO_CELK.first()
|| __UPG.msg('Wszystkich zapisów: %1.' [$ZO_CELK.size()]);
   _p1:=_p0:=_ok:=0;
   {!
   |? {? ZO_CELK.Z='?'
      || ZO_CELK.Z:='X';
         {? ZO_CELK.put()
         || _p1+=1
         || _p0+=1
         ?}
      || _ok+=1
      ?};
      ZO_CELK.next()
   !};
   __UPG.msg('Zapisów nie wymagających zmiany: %1.' [$_ok]);
   __UPG.msg('Zapisów wymagających zmiany: %1, w tym poprawionych: %2 / niepoprawionych: %3.' [$(_p1+_p0),$_p1,$_p0]);
   _ret:={? _p0 || -1 || 1 ?}

|| __UPG.msg('Brak zapisów do poprawienia.');
   _ret:=1
?};
ZO_CELK.cntx_pop();
exec('chk_on','phr_dane',_off);

_ret


\zo_celk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zamiana znaku specjalnego "?" na "X" w znaczniku realizacji kompetencji celu (ZO_CELK.Z).\n'
'Powodem zamiany są zmiany technologiczne - wprowadzenie możliwości korzystania ze znaków specjalnych '
'w polach szybkiego filtra.'


\slo_kod_wer_wyn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

_kod:='WER_WYN';
{? (_typ:=exec('slo_typ','ext_slo',_kod))<>null()
|| SLO_KOD.cntx_psh();
   SLO_KOD.index('KOD');
   SLO_KOD.prefix(_typ,'?');
   {? SLO_KOD.first()
   || SLO_KOD.prefix();
      SLO_KOD.KOD:='X';
      {? SLO_KOD.put()
      || __UPG.msg('Pozycję słownika udało się poprawić.')
      || __UPG.msg('Pozycji słownika nie udało się poprawić.');
         _ret:=-1
      ?}
   || __UPG.msg('Pozycja słownika nie wymaga poprawienia.')
   ?};
   SLO_KOD.cntx_pop();
   _ret
|| __UPG.msg('Nie udało się utworzyć słownika [%1].' [_kod])
?};

_ret


\slo_kod_wer_wyn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zamiana znaku specjalnego "?" na "X" w wyniku weryfikacji zapotrzebowania na szkolenia.\n'
'Powodem zamiany są zmiany technologiczne - wprowadzenie możliwości korzystania ze znaków specjalnych '
'w polach szybkiego filtra.'


\szk_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_ret:=0;

SZK_PRAC.cntx_psh();
SZK_PRAC.index('PRAC_SZK');
SZK_PRAC.prefix();
{? SZK_PRAC.first()
|| __UPG.msg('Wszystkich zapisów: %1.' [$SZK_PRAC.size()]);
   _p1:=_p0:=_ok:=0;
   {!
   |? {? SZK_PRAC.EMAIL_PO='?'
      || SZK_PRAC.EMAIL_PO:='X';
         {? SZK_PRAC.put()
         || _p1+=1
         || _p0+=1
         ?}
      || _ok+=1
      ?};
      SZK_PRAC.next()
   !};
   __UPG.msg('Zapisów nie wymagających zmiany: %1.' [$_ok]);
   __UPG.msg('Zapisów wymagających zmiany: %1, w tym poprawionych: %2 / niepoprawionych: %3.' [$(_p1+_p0),$_p1,$_p0]);
   _ret:={? _p0 || -1 || 1 ?}

|| __UPG.msg('Brak zapisów do poprawienia.');
   _ret:=1
?};
SZK_PRAC.cntx_pop();
exec('chk_on','phr_dane',_off);

_ret


\szk_prac_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zamiana znaku specjalnego "?" na "X" w potwierdzeniu uczestnictwa w szkoleniu (SZK_PRAC.EMAIL_PO).\n'
'Powodem zamiany są zmiany technologiczne - wprowadzenie możliwości korzystania ze znaków specjalnych '
'w polach szybkiego filtra.'


\zf_wid_clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Usunięcie nadmiarowych uprawnień elementów statycznych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_ret:=0;

_firma:=exec('ref_firma','ustawienia');

_i1b:=_i1a:=0;
_i2b:=_i2a:=0;
_err:=0;

ZF_SKL.cntx_psh();
ZF_SKL.index('KOD');
ZF_SKL.prefix(_firma);
{? ZF_SKL.first()
|| ZF_POZ.cntx_psh();
   ZF_POZ.index('FK_SKL');
   ZF_WID.cntx_psh();
   ZF_WID.index('TYP');
   {!
   |? {? ZF_SKL.STALY='T'
      || do();
         ZF_WID.prefix(ZF_SKL.NP_DOK,ZF_SKL.ZZ_DOK);
         _i1b+=ZF_WID.size();
         {? ZF_WID.first()
         || {!
            |? ZF_WID.del()
            !}
         ?};
         _i1a:=ZF_WID.size();

         ZF_POZ.prefix(ZF_SKL.ref());
         {? ZF_POZ.first()
         || {!
            |? ZF_WID.prefix(ZF_POZ.NP_DOK,ZF_POZ.ZZ_DOK);
               _i2b+=ZF_WID.size();
               {? ZF_WID.first()
               || {!
                  |? ZF_WID.del()
                  !}
               ?};
               _i2a:=ZF_WID.size();
               ZF_POZ.next()
            !}
         ?};
         _err+=~end()
      ?};
      ZF_SKL.next()
   !};
   ZF_WID.cntx_pop();
   ZF_POZ.cntx_pop()
?};
ZF_SKL.cntx_pop();
exec('chk_on','phr_dane',_off);

__UPG.msg('Uprawnienia elementów formularzy: zbędnych / usuniętych: %1 / %2.' [$_i1b,$(_i1b-_i1a)]);
__UPG.msg('Uprawnienia pozycji formularzy: zbędnych / usuniętych: %1 / %2.' [$_i2b,$(_i2b-_i2a)]);
{? _err
|| __UPG.msg('W trakcie przetwarzanie wystąpiły błędy zapisu. Operację należy powtórzyć.')
?};

{? _i1a=_i1b & _i2a=_i2b & ~_err
|| 1
|| -1
?}


\zf_wid_clean_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Usunięcie nadmiarowych uprawnień elementów statycznych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dla elementów statycznych i związanych z nimi pozycjami formularzy usunięcie zbędnych (nadmiarowych) uprawnień.'


\szk_tren
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Uzupełnienie informacji związanych z trenerami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_nok:=0;
_nerr:=0;
_nleft:=0;
_state:=no_msg(1);

SZK_TREN.cntx_psh();
SZK_TREN.index('ZZ_DOK');
SZK_TREN.prefix();
_loop:=SZK_TREN.first();
{!
|? _loop
|! {? SZK_TREN.put(1)
   || _nok+=1
   || _nerr+=1
   ?};
   _loop:=SZK_TREN.next()
!};
SZK_TREN.cntx_pop();

__UPG.msg('Tabela: %1 (%2) | Zapisów poprawionych: %3 | Zapisów, których nie udało się poprawić: %4 |'
   ['SZK_TREN',SZK_TREN.comment(),$_nok,$_nerr]
);

SZK_TREN.cntx_psh();
SZK_TREN.index('ZZ_OSOBA');

_nok:=0;
_nerr:=0;
_nleft:=0;
SZK_OFE.cntx_psh();
SZK_OFE.index('ZZ_DOK');
SZK_OFE.prefix();
_loop:=SZK_OFE.first();
{!
|? _loop
|! {? SZK_OFE.SZK_TREN=null & SZK_OFE.TRENER<>null
   || {? SZK_TREN.find_key(SZK_OFE.TRENER)
      || SZK_OFE.SZK_TREN:=SZK_TREN.ref();
         {? SZK_OFE.put(1)
         || _nok+=1
         || _nerr+=1
         ?}
      || _nerr+=1
      ?}
   || _nleft+=1
   ?};
   _loop:=SZK_OFE.next()
!};
SZK_OFE.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów poprawionych: %3 '
   '| Zapisów, których nie udało się poprawić: %4 '
   '| Zapisów niewymagających modyfikacji: %5'
   ['SZK_OFE',SZK_OFE.comment(),$_nok,$_nerr,$_nleft]
);

_nok:=0;
_nerr:=0;
_nleft:=0;
SZK_OPIS.cntx_psh();
SZK_OPIS.index('ZZ_DOK');
SZK_OPIS.prefix();
_loop:=SZK_OPIS.first();
{!
|? _loop
|! {? SZK_OPIS.SZK_TREN=null & SZK_OPIS.TRENER<>null
   || {? SZK_TREN.find_key(SZK_OPIS.TRENER)
      || SZK_OPIS.SZK_TREN:=SZK_TREN.ref();
         {? SZK_OPIS.put(1)
         || _nok+=1
         || _nerr+=1
         ?}
      || _nerr+=1
      ?}
   || _nleft+=1
   ?};
   _loop:=SZK_OPIS.next()
!};
SZK_OPIS.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów poprawionych: %3 '
   '| Zapisów, których nie udało się poprawić: %4 '
   '| Zapisów niewymagających modyfikacji: %5'
   ['SZK_OPIS',SZK_OPIS.comment(),$_nok,$_nerr,$_nleft]
);

SZK_TREN.cntx_pop();

exec('chk_on','phr_dane',_off);
no_msg(_state);
1


\szk_tren_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Uzupełnienie informacji związanych z trenerami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie informacji związanych z trenerami'


\zo_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Uzupełnienie danych w tabeli ZO_TEST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_nok:=0;
_nerr:=0;
_nleft:=0;
_state:=no_msg(1);

ZO_TEST.cntx_psh();
ZO_TEST.index('ZZ_DOK');
ZO_TEST.prefix();
_loop:=ZO_TEST.first();
{!
|? _loop
|! {? ZO_TEST.KOMPLET='' | ZO_TEST.P_FORM=''
   || {? ZO_TEST.put(1)
      || _nok+=1
      || _nerr+=1
      ?}
   || _nleft+=1
   ?};
   _loop:=ZO_TEST.next()
!};
ZO_TEST.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów poprawionych: %3 '
   '| Zapisów, których nie udało się poprawić: %4 '
   '| Zapisów niewymagających modyfikacji: %5'
   ['ZO_TEST',ZO_TEST.comment(),$_nok,$_nerr,$_nleft]
);

exec('chk_on','phr_dane',_off);
no_msg(_state);
1


\zo_test_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Uzupełnienie danych w tabeli ZO_TEST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie informacji w ocenach całkowitych'


\zo_osoba_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Usunięcie zapisów rodzaju O z tabeli ZO_OSOBA, dla których nie znaleziono rekordów w tabeli ZO_TEST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_ndel:=0;
_nerr:=0;
_nleft:=0;
_state:=no_msg(1);

ZO_TEST.cntx_psh();
ZO_TEST.index('UNIQUE');
ZO_OSOBA.cntx_psh();
ZO_OSOBA.index('ZO_OSOBA');
ZO_OSOBA.prefix('O');
_loop:=ZO_OSOBA.first();
{!
|? _loop
|! {? ZO_TEST.find_key(ZO_OSOBA.ref())
   || _loop:=ZO_OSOBA.next();
      _nleft+=1
   || {? ZO_OSOBA.del()
      || _loop:=1;
         _ndel+=1
      || _loop:=ZO_OSOBA.next();
         _nerr+=1
      ?}
   ?}
!};
ZO_OSOBA.cntx_pop();
ZO_TEST.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów usuniętych: %3 '
   '| Zapisów, których nie udało się usunąć: %4 '
   '| Zapisów pozostawionych: %5'
   ['ZO_TEST',ZO_TEST.comment(),$_ndel,$_nerr,$_nleft]
);

exec('chk_on','phr_dane',_off);
no_msg(_state);
1


\zo_osoba_del_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Usunięcie zapisów rodzaju O z tabeli ZO_OSOBA, dla których nie znaleziono rekordów w tabeli ZO_TEST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie nadmiarowych zapisów w tabeli ZO_OSOBA'


\stn_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Uzupełnienie danych w tabeli STN_GRP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_nput:=0;
_nerr:=0;
_nleft:=0;
_nadd:=0;
_state:=no_msg(1);

STN_GRP.cntx_psh();
STN_GRP.clear();
STN.cntx_psh();
STN.index('STANONAZ');
STN.prefix();
_loop:=STN.first();
{!
|? _loop
|! {? STN.STN_GR<>null
   || STN_GRP.blank();
      do();
      STN_GRP.STN_GR:=STN.STN_GR;
      STN_GRP.STN:=STN.ref();
      {? STN_GRP.add(1)
      || _nadd+=1;
         STN.STN_GR:=null;
         {? STN.put(1)
         || _nput+=1
         || _nerr+=1;
            undo()
         ?}
      || _nerr+=1;
         undo()
      ?};
      end()
   || _nleft+=1
   ?};
   _loop:=STN.next()
!};
STN.cntx_pop();
STN_GRP.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów poprawionych: %3 '
   '| Zapisów, których nie udało się poprawić: %4 '
   '| Zapisów niewymagających zmiany: %5 '
   '| Zapisów utworzonych: %6'
   ['STN',STN.comment(),$_nput,$_nerr,$_nleft,$_nadd]
);

exec('chk_on','phr_dane',_off);
no_msg(_state);
1


\stn_grp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Uzupełnienie danych w tabeli STN_GRP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie danych w kartotece grup stanowisk'


\rmk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Uzupełnienie pola RMK.TYP
::----------------------------------------------------------------------------------------------------------------------
_names:=RMK.names();
{? _names.first()
|| {!
   |? RMK.use(_names.NAME);
      RMK.prefix();
      RMK.for_each("
         _put:=0;
         {? RMK.TYP='' || RMK.TYP:='RMK'; _put:=1 ?};
         {? RMK.S='' || RMK.S:='M'; _put:=1 ?};
         {? _put || RMK.put() ?}
      ");
      _names.next()
   !}
?};
__UPG.msg('Uzupełniono pole typu rozliczenia międzyokresowego');
1


\rmk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Uzupełnienie pola RMK.TYP - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola typu rozliczenie międzyokresowego'


\BRAKI_GD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnienie pól w tabeli BRAKI_GD
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
BRAKI_GD.cntx_psh(); BRAKI_GD.prefix();
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   {? BRAKI_GD.MZLGD=''
   || __lmod+=1;
::    Pole BRAKI_GD.MZLGD ustawia trigger przed put
      BRAKI_GD.put()
   ?}
";
_result:=BRAKI_GD.for_each(_formula,1);
__UPG.msg('Zaktualizowano tabelę BRAKI_GD.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
BRAKI_GD.cntx_pop();
_result


\BRAKI_GD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnienie pól w tabeli BRAKI_GD - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w tabeli BRAKI_GD.'


\add_rub_graf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.02]
:: OPIS: Dodanie rubryk płacowych związanych z grafikami czasu pracy.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lista:='120,121,123,7028,7029,7037,7038,7015,7016,7011,7026,7027,7056,7057,7035,7036,7006,7050,7051,7040,7041,7052';
_lista+=',7053,7058,7059,7054,7055,7056,7057';

_wynik:=exec('import','rubryki','g',_lista,1,0);
{? +_wynik
|| __UPG.msg(_wynik);
   _result:=-1
|| _TAB:=spli_str(_lista,',');
   {? obj_len(_TAB)
   || R.cntx_psh();
      R.index('RUBKOD');
      R.prefix();
      {! _ii:=1..obj_len(_TAB)
      |! {? R.find_key(#_TAB[_ii])
         || __UPG.msg('Zaktualizowano rubrykę o numerze %1'[_TAB[_ii]])
         || __UPG.msg('Nie udało się dodać rubryki o numerze %1'[_TAB[_ii]]);
            _result:=-1
         ?}
      !};
      R.cntx_pop()
   ?}
?};

exec('__RUB','object');
_tab:=obj_new(6);
_tab[1]:=7051;
_tab[2]:=7041;
_tab[3]:=7055;
_tab[4]:=7053;
_tab[5]:=7006;
_tab[6]:=7059;
PAR_POKR.index('PAR_POKR');
PAR_POKR.prefix();
_ile:=0;
_firma:=exec('ref_firma','ustawienia');
{! _ind:=1..obj_len(_tab)
|! {? ~PAR_POKR.find_key(_firma,_tab[_ind])
   || PAR_POKR.blank();
      PAR_POKR.R:=__RUB.ref(_tab[_ind]);
      PAR_POKR.RP:='S';
      PAR_POKR.RZ:='T';
      PAR_POKR.PTZO:='T';
      PAR_POKR.FIRMA:=_firma;
      _ile+=PAR_POKR.add(1)
   ?}
!};
{? _ile=obj_len(_tab)
|| __UPG.msg('Dokonano wpisów w parametrach premii okresowej.')
|| __UPG.msg('Nie dodano %1 wpisów w parametrach premii okresowej.'[$(obj_len(_tab)-_ile)]);
   _result:=-1
?};

exec('add_use','rubatr',10022,7006,7050,7051,7040,7041,7052,7053,7054,7055,7058,7059);
__UPG.msg('Dodano atrybut %1 dla składników płacowych: %2.'
   [$10022,'7006,7050,7051,7040,7041,7052,7053,7054,7055,7058,7059']);

exec('add_use','rubatr',6313102,7006,7050,7051,7040,7041,7052,7053,7054,7055,7058,7059);
__UPG.msg('Dodano atrybut %1 dla składników płacowych: %2.'
   [$6313102,'7006,7050,7051,7040,7041,7052,7053,7054,7055,7058,7059']);

exec('add_use','rubatr',6522,7006,7051,7041,7053,7055);
__UPG.msg('Dodano atrybut %1 dla składników płacowych: %2.'
   [$6522,'7006,7051,7041,7053,7055']);

exec('add_use','rubatr',6523,7050,7040,7052,7054);
__UPG.msg('Dodano atrybut %1 dla składników płacowych: %2.'
   [$6523,'7050,7040,7052,7054']);

exec('add_use','rubatr',48,7006,7050,7051,7040,7041,7052,7053,7054,7055,7058,7059);
__UPG.msg('Dodano atrybut %1 dla składników płacowych: %2.'
   [$48,'7006,7050,7051,7040,7041,7052,7053,7054,7055,7058,7059']);

exec('add_use','rubatr',46,7050,7040,7052,7054,7058);
__UPG.msg('Dodano atrybut %1 dla składników płacowych: %2.'
   [$46,'7050,7040,7052,7054,7058']);
_result


\add_rub_graf_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.02]
:: OPIS: Dodanie rubryk płacowych związanych z grafikami czasu pracy.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk płacowych związanych z grafikami czasu pracy'


\R_CZYT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja tabeli dostawców czytników.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
R_CZYT.cntx_psh();
R_CZYT.index('R_CZYT');
R_CZYT.prefix();
{? R_CZYT.first()
|| FORMULA.cntx_psh();
   FORMULA.index('FORMULA3');
   FORMULA.prefix('X');
   {? ~FORMULA.size()
   || exec('formula_p','dane_startowe');
      {? FORMULA.size()
      || __UPG.msg('Uzupełniono w słowniku %1 formuły do odczytu danych z czytników RCP.'[$FORMULA.size()])
      || __UPG.msg('Nie udało się uzupełnić słownika z formułami do odczytu danych z czytników RCP.')
      ?}
   ?};
   {? FORMULA.first()
   || _formula:=
         "  _ok:=0;
            {!
            |? {? +R_CZYT.F & FORMULA.FORMULA*R_CZYT.F
               || R_CZYT.FORMULA:=FORMULA.ref();
                  _ok:=R_CZYT.put()
               ?};
               ~_ok & FORMULA.next()
            !};
            _ok
         ";
      {!
      |? {? _formula()
         || __UPG.msg('Dla czytnika %1 udało się dodać formułę: %2.'[R_CZYT.N,R_CZYT.F])
         || _result:=-1;
            __UPG.msg('Dla czytnika %1 nie udało się odnaleźć formuły: %2.'[R_CZYT.N,R_CZYT.F])
         ?};
         R_CZYT.next()
      !}
   || _result:=-1;
      __UPG.msg('Brak definicji formuł odczytu danych.')
   ?};
   FORMULA.cntx_pop()
|| _result:=-1;
   __UPG.msg('Brak danych w tabeli dostawców czytników.')
?};
R_CZYT.cntx_pop();
_result


\R_CZYT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja tabeli dostawców czytników.
::----------------------------------------------------------------------------------------------------------------------
'Aktualiacja informacji dotyczącej formuł odczytu danych.'


\R_ODN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja statusu wniosku o odbiór czasu wolnego.
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
R_ODN.cntx_psh();
{? R_ODN.first()
|| _lrec:=R_ODN.size();
   _lmod:=_lnmod:=0;
   {!
   |? {? R_ODN.A='A'
      || R_ODN.A:='T';
         _lmod+=R_ODN.put()
      |? R_ODN.A='T'
         || _lnmod+=1
      ?};
      R_ODN.next()
   !};
   __UPG.msg('Zaktualizowano tabelę z wnioskami o odbiór czasu wolnego.');
   __UPG.msg(
      {? _lrec=1 || 'Przetworzono: %1 zapis.'[$_lrec]
      |? _lrec<5 || 'Przetworzono: %1 zapisy.'[$_lrec]
      || 'Przetworzono: %1 zapisów.'[$_lrec]
      ?}
   );
   __UPG.msg(
      {? _lmod=1 ||'Zmodyfikowano: %1 zapis.'[$_lmod]
      |? _lmod<5 || 'Zmodyfikowano: %1 zapisy.'[$_lmod]
      || 'Zmodyfikowano: %1 zapisów.'[$_lmod]
      ?}
   );
   __UPG.msg(
      {? _lnmod=1 || '%1 zapis nie wymagał modyfikacji.'[$_lnmod]
      |? _lnmod<5 || '%1 zapisy nie wymagały modyfikacji.'[$_lnmod]
      || '%1 zapisów nie wymagało modyfikacji.'[$_lnmod]
      ?}
   );
   {? _lrec=_lmod+_lnmod || _result:=1 ?}
|| __UPG.msg('Brak danych w tabeli wniosków o odbiór czasu wolnego.')
?};
R_ODN.cntx_pop();
_result


\R_ODN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja statusu wniosku o odbiór czasu wolnego.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja statusu wniosku o odbiór czasu wolnego.'


\PRC_stale
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Uzupełnienie widoku stałych o nowe elementy związane z dziedziną PRC.
::----------------------------------------------------------------------------------------------------------------------
exec('imp_def','#stalesys');
__UPG.msg('Zakończono aktualizację konfiguracji stałych systemu.')


\PRC_stale_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Uzupełnienie widoku stałych o nowe elementy związane z dziedziną PRC.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie widoku stałych o nowe elementy z dziedziny PRC.'

\con_rodf_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.02]
:: OPIS: Dodanie wszystkich funkcji dla controllingu
::----------------------------------------------------------------------------------------------------------------------
CON_RODF.cntx_psh();
exec('add_rodf','!ctr_pdm_patd');
CON_RODF.cntx_pop();
1


\con_rodf_all_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.02]
:: OPIS: Dodanie wszystkich funkcji dla controllingu - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie wszystkich funkcji aktualizacyjnych dla controllingu'


\faks_odb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Aktualizuje danych odbiorcy do wydruku na dokumentach sprzedaży
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('czytaj','#stalesys',,INFO,'NAROD');
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKS.names();
FAKS.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("
         __lrec+=1;
         {? FAKS.KH_ODB
            & FAKS.O_NAZ='' & FAKS.O_NIP='' & FAKS.O_UL='' & FAKS.O_MIASTO='' & FAKS.O_POCZ=''
         ||
            FAKS.O_NAZ:=FAKS.KH_ODB().NAZ;
            FAKS.O_MIASTO:=KH_ODB.MIASTO;
            FAKS.O_UL:=KH_ODB.UL;
            FAKS.O_POCZ:=KH_ODB.KPOCZ+' '+KH_ODB.POCZ;
            FAKS.O_NIP:=KH_ODB.NIP;
            {? FAKS.put() || __lmod+=1 ?}
         ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();

__UPG.msg('Zaktualizowano dane odbiorcy do wydruku dla dokumentów sprzedaży.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\f_zatra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja form współpracy dla czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lrec:=_lmod:=0;
_kody:='P,Z,K,R,T';
_lista:='PRC_CZP_DKWA,PRC_CZP_DMPR,PRC_CZP_DODB,PRC_CZP_DODO,PRC_CZP_DOOK,PRC_CZP_DOPO,PRC_CZP_DPOK,PRC_CZP_DPPO,';
_lista+='PRC_CZP_DPRZ,PRC_CZP_DPSY,PRC_CZP_DROZ,PRC_CZP_DRUC,PRC_CZP_DZOK,PRC_CZP_DZPO,PRC_CZP_PKAP,PRC_CZP_PKOR,';
_lista+='PRC_CZP_PMCR,PRC_CZP_WOKR,PRC_MCR_DOMP,PRC_MCR_DWGD,PRC_MCR_DZMK,PRC_MCR_PWMC,PRC_MCR_WMRO';

_kod:=spli_str(_kody,',');
_arr:=spli_str(_lista,',');

{! _ii:=1..obj_len(_kod)
|! __UPG.msg('Kody formy współpracy: %1' [_kod[_ii]]);
   _ret:=exec('f_zatra_add','f_zatr','T',_kod[_ii],_lista,1);
   {? _ret<>''
   || __UPG.msg(_ret);
      _result:=-1
   || __UPG.msg('Dodano wszystkie czynności z listy.')
   ?}
!};
_result


\f_zatra_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja form współpracy dla czynności - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja form współpracy dla czynności'


\r_opczyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja wartości w polach "Portal" i "Słownik" tabeli R_OPCZYT.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_lrec:=_lmod:=0;
R_OPCZYT.cntx_psh();
R_OPCZYT.index('R_OPCZYS');
R_OPCZYT.prefix();
{? R_OPCZYT.first()
|| {!
   |? {? R_OPCZYT.SYMBOL='<SYSTEM>'
      || R_OPCZYT.PORTAL:=R_OPCZYT.SL:='T'
      || R_OPCZYT.PORTAL:=R_OPCZYT.SL:='N'
      ?};
      {? R_OPCZYT.put(1)
      || _lmod+=1
      ?};
      _lrec+=1;
      R_OPCZYT.next()
   !}
?};
R_OPCZYT.cntx_pop();
{? _lrec=_lmod
|| __UPG.msg('We wszystkich wierszach tabeli źródeł danych uzupełniono wartości pól "Portal" i "Słownik".');
   _result:=1
|? ~_lmod
|| __UPG.msg('W żadnym z wierszy tabeli źródeł danych nie uzupełniono wartości pól "Portal" i "Słownik".')
|| __UPG.msg('W %1 z %2 wierszy tabeli źródeł danych uzupełniono wartości w polach "Portal" i "Słownik".'[_lmod,_lrec])
?};
_result


\r_opczyt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja wartości w polach "Portal" i "Słownik" tabeli R_OPCZYT - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wartości w polach "Portal" i "Słownik" tabeli źródeł danych'


\wiki_stale_read
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnienie stałych - odczytanie adres dokumentacji
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'DOKUWIKI');
__xdoku:=XINFO.DOKUWIKI;
1


\wiki_stale_read_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnienie stałych - odczytanie adres dokumentacji
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie stałych - wspólny adres dokumentacji.'


\wiki_stale_save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnienie stałych - zapisanie adresu dokumentacji
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'DOKUWIKI');
{? XINFO.DOKUWIKI='' & __xdoku<>''
|| XINFO.DOKUWIKI:=__xdoku;
   exec('zapisz','#stalesys',1,XINFO,'DOKUWIKI')

?};
1


\wiki_stale_save_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnienie stałych - zapisanie adres dokumentacji
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie stałych - wspólny adres dokumentacji - zapis.'

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:03 7c84e02a990ecf74baa041c85519f61b3d38c8c2a677de9c3a61764a889e305f940356bc9196d5503949777e70e82b015c8cd863e105231f7aeb928bb92c44cc09df30d572c8e85f6b612fcae65307804fa5a52753c9923eb4354f1e558ca8c9d4dff5add26b9b09bc3aa83d25dc83d00d6afd525a7be5dd6c65f2667a63461b

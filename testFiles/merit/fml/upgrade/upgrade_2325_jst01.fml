:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2325_jst01.fml
:: Utworzony: 11.12.2023
:: Autor: MW
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 23.25 na 23.25_JST01
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::
::            Programowe wyłączenie zadania transferowego.
::             W plikach upgrade_rrxx.fml korzystamy z metody
::               __UPG.off_task(<task>,[<operator>],[<value>])
::                 - dodaje do listy zadanie,
::                   które ma zostać wyłączone wg warunku porównania wg operatora porównania i wartości, gdzie:
::                <task> - symbol zadania do wyłączenia;
::                [<operator>] - operator porównania - domyślnie operator '<'; dostępne opcje:
::                                   '<' - wartość mniejsza;
::                                   '<=' - wartość mniejsza lub równa;
::                                   '=' - wartość równa;
::                [<value>] - wersja systemu lub aktualizacji do porównania z wersją zadania
::                            - domyślnie aktualna wersja aktualizacji (wg pliku).
::            W pliku upgrade.fml NALEŻY umieścić wywołanie poniższej metody na końcu formuły \init
::               (po wywołaniach __UPG.add_head()):
::              __UPG.act_off() - wyłączenie wszystkich zadań wskazanych do wyłączenia.
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25_JST01]
:: OPIS: Główna formuła upgrade do 23.25_JST01
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2024,01,22),time(0,0,0));

:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('KH_ODB',,,'ZWS',,,,,'T');
__UPG.add_task('report',,,'ZWS',,,,,'T');
__UPG.add_task('upgrade_act',,,'ZPR',,,,,'T');
__UPG.add_task('POZF_ST_B',,,'ZWS',2,,,,'T');

:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('TRANS_PL',,'N','LSP',,,,0,'T');
__UPG.add_task('FO_DEF',,'N','ZWS',,,,0,'T');
__UPG.add_task('EDI',,'N','ZWS',7,,,1,'T');

:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
__UPG.add_task('procesy_jst01','N','N','ZBL',,,,1,'T');

~~


\KH_ODB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25_JST01]
:: OPIS: Wypełnia pola w tabeli KH_ODB
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_fml:="
   __lrec+=1;
   _put:=0;
   {? KH_ODB.KH_LNK<>null() & KH_ODB.NIP=KH_ODB.KH_LNK().NIP & KH_ODB.SNIP<>KH_ODB.KH_LNK().SNIP
   || KH_ODB.SNIP:=KH_ODB.KH_LNK().SNIP;
      _put:=1
   |? KH_ODB.SNIP<>exec('niptostr','#string',KH_ODB.NIP)
   || KH_ODB.SNIP:=exec('niptostr','#string',KH_ODB.NIP);
      _put:=1
   ?};
   {? KH_ODB.KH_LNK<>null() & KH_ODB.NRD='' & KH_ODB.KH_LNK().SN_DT<>''
   || KH_ODB.NRD:=KH_ODB.KH_LNK().SN_DT;
      _put:=1
   ?};
   {? _put
   || {? KH_ODB.put(1)
      || __lmod+=1
      ?}
   ?}
";
KH_ODB.cntx_psh();
KH_ODB.prefix();
_result:=KH_ODB.for_each(_fml,1);
KH_ODB.cntx_pop();
{? _result
|| __UPG.msg('Zaktualizowano dane odbiorców kontrahentów.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji danych odbiorców kontrahentów.')
?};
VAR_DEL.delete('__lrec','__lmod');
_res


\KH_ODB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25_JST01]
:: OPIS: Wypełnia pola w tabeli KH_ODB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych odbiorców kontrahentów'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25_JST01]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
{? _result
|| __UPG.msg('Zaktualizowano wydruki i zestawienia.')
|| __UPG.msg('Wystąpił błąd kompilacji wydruków.');
   _result:=-1
?};
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25_JST01]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\TRANS_PL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [23.25_JST01]
:: OPIS: Aktualizacja kodów tłumaczeń
::----------------------------------------------------------------------------------------------------------------------
exec('init','tlumaczenia');
__UPG.msg('Zaktualizowano kody tłumaczeń.');
1


\TRANS_PL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [23.25_JST01]
:: OPIS: Aktualizacja kodów tłumaczeń
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kodów tłumaczeń.'


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25_JST01]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::   WE: [_a] - dziedziny
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności (ciekawe, czy uda się alfabetycznie ...)
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.BIQ','.');
_dom_str:={? var_pres('_a')=type_of('')
          || _a
          || 'ZBL'
          ?};
_dom:=spli_str(_dom_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25_JST01]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\FO_DEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25_JST01]
:: OPIS: Aktualizacja parametrów FO aplikacyjnych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

exec('init','params');

__UPG.msg('Zaktualizowano parametry aplikacyjne');
VAR_DEL.delete('__lrec','__lmod');
_res


\FO_DEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25_JST01]
:: OPIS: Aktualizacja parametrów FO aplikacyjnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametów aplikacyjnych'


\EDI
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25_JST01]
:: OPIS: Aktualizuje definicje komunikatów EDI
::----------------------------------------------------------------------------------------------------------------------
:: transfer naprawczy ISTDEFV w wersji RR.xx, w emisyjnej do usunięcia
ISTDEFV.cntx_psh();
ISTDEFV.prefix();
ISTDEFV.for_each("{? ISTDEFV.ARCHIWUM='' || ISTDEFV.ARCHIWUM:='N'; ISTDEFV.put() ?}");
ISTDEFV.cntx_pop();
::
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
:: upgrade_edi_er_wrt_xp_2226_2207_0022.zip
:: upgrade_edi_23xx.zip
_fname:='upgrade_edi_%1.zip'['2325'];
_tmp_dir:=fmk_tmp_dir(0);
_tmp_pth:=_tmp_dir.get_path();
_sep:=exec('sep','#file',2);
_zip_name:=_tmp_pth+_sep+'zipfile.zip';
{? ~fcopy(_fname,_zip_name,1,0)
|| __UPG.msg('Nie powiodło się skopiowanie pliku %1.'[_fname]);
   return(0)
?};
{? ~funpack('zip',_zip_name,_tmp_pth)
|| __UPG.msg('Nie powiodło się rozpakowanie archiwum %1.'[_zipname]);
   return(0)
?};
_Files:=fdir(_tmp_pth,1,0);
_loop:=_Files.first();
{!
|? _loop
|!
   {? _Files.TYPE='f' & (_Files.NAME+4)='.dem' & (_Files.NAME+9)<>'_form.dem' & (_Files.NAME+8)<>'_xsd.dem'
   ||
      __lrec+=1;
      _msg:=exec('edi_imp_core','edi_def',_tmp_pth,_Files.NAME,0);
      {? _msg='' | _msg*'Nadpisano strukturę komunikatu:' || __lmod+=1 ?}
   ?};
   _loop:=_Files.next()
!};
__UPG.msg('Zaktualizowano definicje komunikatów EDI.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\EDI_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25_JST01]
:: OPIS: Aktualizuje definicje komunikatów EDI - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizuje definicje komunikatów EDI'


\POZF_ST_B
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Uzupełnia pole POZF.ST_B - stan przed korektą na podstawie pola KOR
::----------------------------------------------------------------------------------------------------------------------
POZF.cntx_psh();
POZF.trig_off('*','*');
_tab:=POZF.names();
{? _tab.first()
|| {!
   |?
      POZF.use(_tab.NAME);
      DOK.use('doku'+(8+_tab.NAME+4));
      POZF.for_each("
         {? POZF.ST_B=0
         || POZF.cntx_psh();
            _ref:=null();
            _put:=0;
            {? POZF.DOK
            || _ref:=POZF.DOK; POZF.index('KOR')
            || _ref:=POZF.EDOKUM; POZF.index('KOR2')
            ?};
            {? _ref<>null()
            || POZF.prefix(_ref,POZF.LP);
               {? POZF.first() || _put:=1 ?}
            ?};
            POZF.cntx_pop();
            {? _put || POZF.ST_B:=1; POZF.put() ?}
         ?}
      ",1);
      _tab.next()
   !}
?};
POZF.trig_on('*','*');
POZF.cntx_pop();
__UPG.msg('Zaktualizowano wartość pola stan przed korektą dla pozycji faktury (POZF.ST_B).');
1


\POZF_ST_B_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Uzupełnia pole POZF.ST_B - stan przed korektą na podstawie pola KOR - desc
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pole POZF.ST_B - stan przed korektą na podstawie pola KOR'


\procesy_jst01
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25_JST01]
:: OPIS: Import procesów
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','23.25a','ZBL_BLR','ZBL_BLR')


\procesy_jst01_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25_JST01]
:: OPIS: Import procesów - opis
::----------------------------------------------------------------------------------------------------------------------
'Zadania potransferowe gdy klient używa integracji z Businesslink.\n'
'Pozwala zaimportować zmieniony proces dziedziny ZBL:\n'
' ● ZBL_BLR - Odczyt potw. i dok. z systemu Businesslink\n'

:Sign Version 2.0 jowisz:1045 2024/02/08 14:09:22 467c58dccab2322a1d438b7bc56be689c7e297c5e5042096ead36fcabf022aaa22e8f2e4afaf257b8b67a7eb9c4e459b67fa1818d5381228800ac54ddb61c708f595a87b163b3073052a0b41964ac7e186cf89c8ab9b4c33039b3224b5a220c37c0047f4dbccfdcfd3595617efdfac1fbfc88722651776bffe8ca288c088d1e3

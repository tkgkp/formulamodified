:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2137.fml
:: Utworzony: 14.04.2021
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.14 na 21.37
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Główna formuła upgrade-ów RR.xx
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',5);
__UPG.add_task('funpar',,,'ZWS',2);
__UPG.add_task('color',,,'ZWS');
__UPG.add_task('stalesys_ini',,,'ZWS');
__UPG.add_task('USERS',,,'ZWS');
__UPG.add_task('areatitle',,,'ZWS');
__UPG.add_task('report',,,'ZWS',22,,,,'T');
__UPG.add_task('rubryki_placowe',,,'PPL');
__UPG.add_task('par_skid',,,'ZWS');
__UPG.add_task('RU',,,'PPL');
__UPG.add_task('roznice_kursowe',,,'FKS',2);
__UPG.add_task('ppsf',,,'PPL');
__UPG.add_task('kornag',,,'PRC');
__UPG.add_task('id_sync',,,'ZWS');
__UPG.add_task('sch_imp',,,'OBE');
__UPG.add_task('khodb',,,'ZWS');
__UPG.add_task('KH_DOD',,,'ZWS');
__UPG.add_task('edeklaracje_pop',,,'PPL');
__UPG.add_task('r_opczyt',,,'ZWS');
__UPG.add_task('TR_RODZ',,,'ZWS');
__UPG.add_task('NIPY',,,'ZWS');
__UPG.add_task('KH',,,'ZWS');
__UPG.add_task('statusy_abstore',,,'LSP');
__UPG.add_task('XFL_PAR',,,'ZWS');
__UPG.add_task('statusy_projekty',,,'ZWS');
__UPG.add_task('grupwyn',,,'PPL');
__UPG.add_task('add_dekr_kornag',,,'FKS');
__UPG.add_task('del_par',,,'ZWS');
__UPG.add_task('KH_ODB',,,'ZWS',1);
__UPG.add_task('JM',,,'ZWS');
__UPG.add_task('kh_dod_dob',,,'ZWS');
__UPG.add_task('rez_url',,,'PPL');
__UPG.add_task('upd_r_jpk_sch',,,'FKS');
__UPG.add_task('DOKUMZ',,,'ZWS');
__UPG.add_task('sch_color_update',,,'ZWS');
__UPG.add_task('zastepstwa',,,'PKD',2);
__UPG.add_task('upgrade_act_obe',,,'ZPR',,,,,'T');
__UPG.add_task('atrybut_cwiczenia',,,'PPL',,,,,'T');
__UPG.add_task('for_akc',,,'FKS');
__UPG.add_task('kart_url_21110024',,,'POR',,,,1,'T');
__UPG.add_task('etypy_zal',,,'OBE');
__UPG.add_task('kart_url_21120069',,,'POR',2,,,1,'T');
__UPG.add_task('rubryki_placowe_upd',,,'PPL',,,,1,'T');
__UPG.add_task('portal_ZS_DEF_clean',,,'POR',,,,,'T');
__UPG.add_task('slo_kod_2203_0025',,,'POR',,,,1,'T');
__UPG.add_task('p_pz_2203_0025',,,'POR',,,,1,'T');
__UPG.add_task('sync_mwa',,,'POR',1,,,,'T');
__UPG.add_task('etypy_2203_0061',,,'POR',,,,1,'T');
__UPG.add_task('zws_tran_bmsg',,,'ZWS');
__UPG.add_task('wz_osoba',,,'ZWS',,,,1,'T');
__UPG.add_task('portal_kart_url_napraw',,,'POR',,,,,'T');
__UPG.add_task('rk_col_act',,,'ZWS');
__UPG.add_task('rubryki_placowe_ppk',,,'PPL',2,,,1,'T');
__UPG.add_task('obe_zest_rep_tran',,,'OBE',,,,,'T');
__UPG.add_task('p_pz_klasa',,,'POR',,,,,'T');
__UPG.add_task('rubryki_placowe_PL_lip',,,'PPL',,,,,'T');
__UPG.add_task('cit8_31',,,'FKS',,,,,'T');
__UPG.add_task('slo_naz_miejscpr',,,'POR',,,,,'T');
__UPG.add_task('atr_plac_cw_woj',,,'PPL',,,,1,'T');
__UPG.add_task('s_zus',,,'PKD',,,,,'T');
:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('B_USRDOM',,'N','ZWS',,,,0);
__UPG.add_task('FO_USR',,'N','ZWS');
__UPG.add_task('KPO',,'N','LUO',,,,1);
__UPG.add_task('archiwizacja',,'N','ZWS',,,,1);
__UPG.add_task('f_zatra_add',,'N','ZWS',,,,1);
__UPG.add_task('role_personel',,'N','ZWS',,,,1);
__UPG.add_task('REJ_MAT',,'N','TTE',,,,1);
__UPG.add_task('PX_GRP',,'N','TTE',,,,1);
__UPG.add_task('ZLP',,'N','LUM',,,,1);
__UPG.add_task('PROD_REJ_ZLGD',,'N','TTE',,,,1);
__UPG.add_task('tr_rodz_perm_upd',,'N','ZWS',3,,,1,'T');
__UPG.add_task('role_portal',,'N','ZWS',,,,1);
__UPG.add_task('FAKSPOLA',,'N','LSP',,,,1);
__UPG.add_task('zws_par_kmti',,'N','ZWS');
__UPG.add_task('ogrp_perm_upd',,'N','ZWS',,,,1);
__UPG.add_task('dokum_projekty',,'N','ZWS');
__UPG.add_task('R_PRACDN',,,'PPL');
__UPG.add_task('vat_ps_sha',,'N','FKS',,,,1);
__UPG.add_task('KAL_BUFF_CZYTNIK',,'N','PRC',,,,1,'T');
__UPG.add_task('FAKS',,'N','LSP',,,,1);
__UPG.add_task('pkd_kod_4',,'T','PKD',,,,1,'T');
__UPG.add_task('pkartzas_rep_tran',,,'PPL',,,,,'T');
__UPG.add_task('porslo',,'N','POR',,,,1,'T');
__UPG.add_task('jpk_d1_upd',,'N','FKS',,,,1,'T');
__UPG.add_task('zaomedadtcreate',,'N','PED',,,,1,'T');
__UPG.add_task('formuly_placowe_PL_lip',,'N','PPL',,,,1,'T');
__UPG.add_task('weryfikacja_ZP_PL_lip',,'N','PPL',,,,1,'T');
__UPG.add_task('dokum_ob_status',,'N','OBG',,,,1,'T');
__UPG.add_task('archiwum_del_test',,'N','ZWS',,,,1,'T');
__UPG.add_task('edokum_wyc',,,'ZWS',,,,,'T');

:: Zadania manualne
__UPG.add_task('formuly_placowe','N','N','PPL',,,,1);
__UPG.add_task('formuly_placowe_ppk','N','N','PPL',2,,,1,'T');
__UPG.add_task('szablony','N',,'ZWS',6,,,1,'T');
__UPG.add_task('mechanizm_importow','N',,'ZWS',,,,1);
__UPG.add_task('RU_RKU','N',,'PPL');
__UPG.add_task('rozszerzony_filtr','N',,'ZWS');
__UPG.add_task('dks_edi_ufd_upd','N','N','ZBL',,,,1);
__UPG.add_task('ppk_zc_wn_zus','N','N','PPL',,'N',,1,'T');
__UPG.add_task('pomoc_AK_10','N','N','PKD',3,'N',,1,'T');
__UPG.add_task('kody_zus_import','N','N','ZWS',,'N',,1,'T');
__UPG.add_task('por_slowniki','N','N','POR',,,,1,'T');
__UPG.add_task('dokument_zmiany_metody','N','N','FST',,,,1,'T');
__UPG.add_task('h_overlap','N','T','PKD',,,,1,'T');
__UPG.add_task('formuly_placowe_kor_rh','N','N','PPL',,,,1,'T');

~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA','.');
_dom:=spli_str('BIQ.ZWS.ZPR.LUO.ZUI.POR.PRC.PKW.TTE.TRE.TPP.LTR.LSP.PPL.LUM.ZBL.PKD.OBG.OBE.LZK','.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\USERS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37]
:: OPIS: Aktualizacja użytkowników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
USERS.cntx_psh();
USERS.prefix();
USERS.for_each("
   __lrec+=1;
   {? USERS.PORTAL2E=''
   || USERS.PORTAL2E:='N';
      {? USERS.put(1) || __lmod+=1 ?}
   ?}",
   1
);
USERS.cntx_pop();
__UPG.msg('Zaktualizowano użytkowników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\USERS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37]
:: OPIS: Aktualizacja użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja użytkowników'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\RU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja znacznika rodzaju kosztów uzyskania dla umów cywilnoprawnych.
::----------------------------------------------------------------------------------------------------------------------
_cnt:=0;
_err:=0;

RU.cntx_psh();
RU.clear();
_loop:=RU.first();
_size:=RU.size();
{!
|? _loop
|! {? RU.RKU=''
   || RU.RKU:={? RU.K<>'5' || 'P' || 'K' ?};
      {? RU.put()
      || _cnt+=1
      || _err+=1
      ?}
   ?};
   _loop:=RU.next()
!};
RU.cntx_pop();

__UPG.msg('Liczba wszystkich wierszy: %1.'[$_size]);
__UPG.msg('Liczba zaktualizowanych wierszy: %1.'[$_cnt]);
__UPG.msg('Błędy aktualizacji wierszy: %1.'[$_err]);

{? _err>0 || -1 || 1 ?}


\RU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja znacznika rodzaju kosztów uzyskania dla umów cywilnoprawnych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika rodzaju kosztów uzyskania dla umów cywilnoprawnych.'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\RU_RKU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Weryfikacja danych dotyczących kosztów uzyskania przychodu dla umów cywilnoprawnych
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? FUN.ask('Czy zweryfikowano poprawność danych dotyczących kosztów uzyskania przychodu dla umów cywilnoprawnych?'@)
|| __UPG.msg('Zweryfikowano poprawność danych dla umów cywilnoprawnych.');
   _result:=1
|| _result:=-1
?};
_result


\RU_RKU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Weryfikacja danych dotyczących kosztów uzyskania przychodu dla umów cywilnoprawnych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Weryfikacja danych dotyczących kosztów uzyskania przychodu dla umów cywilnoprawnych.'


\rubryki_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Dodanie rubryk i atrybutów płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

:: Aktualizacja opisów składników płacowych.  - - - - - - - - - - - - - - - - - - - - - -- - - - - - - - - - - - - - - -
::7129 - Dodatek stażowy
::7130 - Staż pracy - ZUS
::7131 - Staż pracy - zdrow.
::7132 - Urlop dodatkowy
::7133 - Urlop rehabil.
::7134 - Wynagr. rehabil.
::7135 - L.g. poza siedzibą
::7136 - PA: Korzyści
::7137 - % dodatku stażowego
{? +exec('import','rubryki','g','7129,7130,7131,7132,7133,7134,7135,7136,7137',1,0)
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);
{? _result=-1 || return(_result) ?};

:: Aktualizacja atrybutów rubryk.  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: Dodatki stażowe.
exec('add_attr','rubatr',4,402,'Dodatki',1,,,7129);
   exec('add_attr','rubatr',402,4021,'Dodatki stażowe',1,,,7129);
      exec('add_attr','rubatr',4021,40211,'Wypłacane w całości',1,,,7129);
exec('add_attr','rubatr',5,59,'Pozostałe informacje',1,,,7130,7131);
   exec('add_attr','rubatr',59,591,'Podstawa pomniejszana o nieobecności',1,,,7130,7131);
      exec('add_attr','rubatr',591,5911,'Pomniejszenie wg dni kalendarzowych',1,,,7130);
         exec('add_attr','rubatr',5911,59111,'Podstawa składek ZUS',1,,,7130);
         exec('add_attr','rubatr',5911,59112,'Podstawa zdrowotna',1,,,7131);
exec('add_uses','rubatr',7129,48,6313101);

:: Urlop dodatkowy i rehabilitacyjny.
exec('add_attr','rubatr',111,1113,'Dodatkowy',1,,,7132);
exec('add_uses','rubatr',7132,14,16,19,100,113,161,192,193,194,199);
exec('add_uses','rubatr',7132,1001,1924,1932,1942,10011,14350,19421,100112);

exec('add_attr','rubatr',11,117,'Rehabilitacyjny',1);
   exec('add_attr','rubatr',117,1171,'Urlop rehabilitacyjny',1,,,7133);
exec('add_uses','rubatr',7133,11,14,15,16,19,100,117,152,161,192,193,194,197,199);
exec('add_uses','rubatr',7133,1001,1171,1923,1931,1942,10011,14350,19421,100112);

exec('add_attr','rubatr',197,1973,'Limity urlopu rehabilitacyjnego',1,,,7133);
   exec('add_attr','rubatr',1973,19731,'Limit dni',1,,21,7133);

exec('add_attr','rubatr',42,427,'Wynagrodzenie za urlop rehabilitacyjny',1,,,7134);
exec('add_uses','rubatr',7134,6,9,63,65,100,631,652,904,1002,6313,6522,9044);
exec('add_uses','rubatr',7134,48,10023,63131,65221,100231,652211,6313108);

::Praca poza siedzibą firmy.
exec('add_uses','rubatr',7135,8,83,832,8321,83211);

:: Rozbudowa modułu potrąceń
exec('add_attr','rubatr',211,2118,'Dodatkowe składniki bez potrąceń',1);
   exec('add_attr','rubatr',2118,21181,'Indywidualne składniki bez potrąceń',1);

__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\rubryki_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Dodanie rubryk i atrybutów płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk i atrybutów płacowych.'


\formuly_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Dodanie formuł płacowych.
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='436,438,7132,7133,7134,500,7129,7136';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,R,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Dodanie formuł płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuł płacowych'


\formuly_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=436 & _tp='F'
:: ==================
:: Wynagr. za urlop.
|| "
{? FUNKCJE.L_SYS(113) | FUNKCJE.L(24)
|| exec('urlop','!ppl_pll_nals')
|| FUNKCJE.LK(442,,0); FUNKCJE.LK(443,,0); FUNKCJE.LK(444,,0);
   0
?}"

|? _rn=438 & _tp='U'
:: ==================
:: Średnia urlopowa.
|| "
{? FUNKCJE.L_SYS(113) | FUNKCJE.L(24)
|| exec('urlop','!ppl_pll_nals')
|| FUNKCJE.LK(442,,0);
   FUNKCJE.LK(443,,0);
   FUNKCJE.LK(444,,0);
   0
?}"

|? _rn=500 & _tp<>'R'
:: ==================
:: Brutto
|| "
{? PAR_SKID.get(195)='T'
|| _BRUTTO:=FUNKCJE.L(100,498)+FUNKCJE.L_SYS(48);
   _nieob:=FUNKCJE.L_SYS(4011)+FUNKCJE.L(151,154)+FUNKCJE.L_SYS(41);
   _ekw:=FUNKCJE.L(440);
   _staz:=FUNKCJE.L_SYS(40211);
   _podst:=_BRUTTO-_nieob-_ekw-_staz;
   _wyr:=exec('wyr_minkraj','!ppl_pll_nals',_podst); FUNKCJE.LK(499,,_wyr);
   _BRUTTO+=FUNKCJE.L(499)
|| _BRUTTO:=FUNKCJE.L(100,499)+FUNKCJE.L_SYS(48)
?};
_BRUTTO"

|? _rn=7129 & _tp<>'R'
:: ==================
:: Dodatek stażowy
|| "exec('dodatek_stazowy','lista_licz')"

|? _rn=7132 & _tp<>'R'
:: =================
:: Urlop dodatkowy.
|| "FUNKCJE.NG(7132)"

|? _rn=7133 & _tp<>'R'
:: =================
:: Urlop rehabilitacyjny.
|| "FUNKCJE.NG(7133)"

|? _rn=7134 & (_tp='F' | _tp='U')
:: =================
:: Wynagr. za urlop rehabilitacyjny
|| "exec('wynagr_url_rehabil','!ppl_pll_nals')"

|? _rn=7136 & (_tp='F' | _tp='U')
:: ==================
:: PA: Korzyści
|| "
_kuPA:=FUNKCJE.L_SYS(9043);
{? _kuPA>0
|| _pr2:=FUNKCJE.L_SYS(9031);
   _kuPA*{? _pr2>0 || _pr2 || FUNKCJE.L_SYS(91) ?}/100$2
?}"

|? _rn=7136 & _tp='R'
:: ==================
:: PA: Korzyści
|| "exec('pa_korzysci','zlec_rh')"

|| ""
?}


\KPO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Aktualizacja pól tabeli KPO
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_msk:=KPO.names();
_msk.clear();
KPO.trig_off('*','*');
KPO.cntx_psh();
{? _msk.first()
|| {!
   |? KPO.use(_msk.NAME);
      KPO.prefix();
      KPO.for_each("
         _put:=0;
         {? KPO.KEO_GEN=''
         || KPO.KEO_GEN:='N';
            _put:=1
         ?};
         {? KPO.KEO_REC=''
         || KPO.KEO_REC:='N';
            _put:=1
         ?};
         {? _put || KPO.put() ?}
      ",1);
      _msk.next()
   !}
?};
KPO.cntx_pop();
KPO.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola kart przekazania odpadów.');
_result


\KPO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Aktualizacja pól tabeli KPO - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli kart przekazania odpadów.'


\par_skid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Aktualizacja listy parametrów
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');
PAR_SKID.set(115,5,1,'','N','OCR: Dozwolone powielanie plików','''T - tak'',''N - nie''');
::PAR_SKID.set(116,5,1,'','T',
:: 'OCR: Pobierać automatycznie dane z OCR gdy rozpoznanie na wskazanym poziomie','''T - tak'',''N - nie''');
::PAR_SKID.set(117,8,0,'','100','OCR: Procent rozpoznania danych dla pobierania automatycznego');
::PAR_SKID.ae_set(117,"_wart:=#PARAMETR.TRESC;
::                    {? PARAMETR.TRESC='0' | _wart
::                    || {? _wart<0 | _wart>100
::                       || FUN.info('Wprowadź liczbę procent w przedziale 0 - 100.'@)
::                       || 1
::                       ?}
::                    || FUN.info('Wprowadź wartość liczbową.'@)
::                    ?}");
__UPG.msg('Zaktualizowano listę parametrów');
1


\par_skid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Aktualizacja listy parametrów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy parametrów.'


\f_zatra_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Lista czynności, dla których przypisane zostaną formy współpracy. Założenia:
:: 1. Czynności są oddzielone przecinkami.
:: 2. W ramach czynności przekazywana jest również informacja o kodach form współpracy (oddzielonych znakiem dwukropka).
::    Zakładamy, że duże litery dotyczą form docelowych.
_lista:=spli_str(
   'PRC_CZP_DPPN:P:Z:K:R:T,'
   'PRC_CZP_DRPN:P:Z:K:R:T,'
   'PRC_CZP_DPHW:P:Z:K:R:T,'
   'PRC_CZP_PPHW:P:Z:K:R:T,'
   'PPL_PLL_PPSF:P:Z:K:R:T,'
   'PPL_PLL_RPSF:P:Z:K:R:T,'
   'PPL_GRP_APAR:P:Z:K:R:T,'
   'PKD_EZK_ORWS:P:Z:K:R:T,'
   'PKD_GRP_AWSP:P:Z:K:R:T,'
   'PKD_EZK_OPWS:P:Z:K:R:T',
   ','
);

_ret:=1;
B_ACTION.cntx_psh();
B_ACTION.f_clear();
B_ACTION.index('UNIK');
B_ACTION.prefix();
_lenl:=obj_len(_lista);
{! _lp:=1 .. _lenl
|! _acz:=spli_str(_lista[_lp],':');
   _uid:=_acz[1];
   {? B_ACTION.find_key(_uid,)
   || _lena:=obj_len(_acz);
      {! _lf:=2 .. _lena
      |! _fz:=_acz[_lf];
         __UPG.msg('Forma współpracy: %1 | Czynność: %2 - %3' [_fz,_uid,B_ACTION.NAME]);
         _add:=exec('f_zatra_add','f_zatr',{? -_fz=_fz || 'N' || 'T' ?},_fz,_uid);
         {? _add=''
         || __UPG.msg(' - uprawnienia nadane')
         || __UPG.msg(_add);
            _ret:=-1
         ?}
      !}
   || __UPG.msg('"%1" - nieprawidłowy kod czynności.' [_uid]);
      _ret:=-1
   ?};
   obj_del(_acz)
!};
B_ACTION.cntx_pop();
_ret


\f_zatra_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Nadanie czynnościom uprawnień do form współpracy'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',1)
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.')
?}


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\FO_USR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Aktualizacja parametrów FO użytkowników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_res:=exec('upgrade_usr','#params');

__UPG.msg('Zaktualizowano parametry użytkowników');
VAR_DEL.delete('__lrec','__lmod');
_res


\FO_USR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Aktualizacja parametrów FO użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametów użytkowników'


\archiwizacja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Formuła dodaje czynności ZWS_PAR_ARCP i ARCU do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
{? exec('roleActions','upgrade',
      'Administrator',
      'ZWS_PAR_ARCP,ZWS_PAR_ARCU'
   )
|| 1
|| -1
?}


\archiwizacja_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Formuła dodaje czynności ZWS_PAR_ARCP i ARCU do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_ARCP,ZWS_PAR_ARCU - Archiwizacja załączników" do odpowiednich ról'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\roznice_kursowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Uzupełnia znacznik różnic kursowych na rodzaju dokumentu
::----------------------------------------------------------------------------------------------------------------------
DOK_REJ.cntx_psh();
DOK_REJ.prefix();
DOK_REJ.for_each("{? DOK_REJ.RK='' || DOK_REJ.RK:='N'; DOK_REJ.put() ?}");
DOK_REJ.cntx_pop();
__UPG.msg('Uzupełniono znacznik różnic kursowych dla rodzajów dokumentów.');
KS.cntx_psh();
KS.prefix();
KS.for_each("{? KS.MW='' || KS.MW:='N'; KS.put() ?}");
KS.cntx_pop();
__UPG.msg('Uzupełniono znacznik Magazyn walut dla kont syntetycznych.');

_ret:=0;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
_add:="
   FORMULA.prefix(_a,_b,);
   {? ~FORMULA.first()
   || FORMULA.blank(1);
      FORMULA.RODZAJ:=_a;
      FORMULA.SKROT:=_b;
      FORMULA.NAZWA:=_c;
      FORMULA.FORMULA:=_d;
      FORMULA.add()
   ?}
";
_ret+=_add('G','RKBAO_BL','Formuła początkowa, czy naliczono RKB w bież. okresie (blokada)','exec(\'czy_rkb\',\'magwal\')');
_ret+=_add('G','RKBNO_BL','Formuła początkowa, czy naliczono RKB w nast. okresach (blokada)','exec(\'czy_rk\',\'magwal\',3)');
_ret+=_add('G','RK_D_BLO','Formuła początkowa, czy naliczono róznice kursowe w bieżącym dokumencie (blokada)','exec(\'czy_rk\',\'magwal\',2)');
_ret+=_add('G','RK_D_PYT','Formuła początkowa, czy naliczono róznice kursowe w bieżącym dokumencie (pytanie)','exec(\'czy_rk\',\'magwal\',1)');
FORMULA.cntx_pop();
{? _ret
|| __UPG.msg('Dodano formuły kontrolne dla wycofywania dokumentu.')
|| __UPG.msg('Nie dodano formuł kontrolnych dla wycofywania dokumentu.')
?};
_ret:=0;
ROK_F.cntx_psh(); AUTOKSIE.cntx_psh(); FORM.cntx_psh();
ROK_F.index('ROKKON'); ROK_F.prefix();
{? ROK_F.first()
|| {! |?
         AUTOKSIE.index('NAZ'); AUTOKSIE.prefix(ROK_F.ref(),'RKB_FIFO',);
         {? ~AUTOKSIE.first()
         || AUTOKSIE.blank();
            AUTOKSIE.ROK_F:=ROK_F.ref(); AUTOKSIE.NAZ:='RKB_FIFO'; AUTOKSIE.OP:='Wycena środ. pienięż. w walucie na dzień';
            AUTOKSIE.TYP:='P'; AUTOKSIE.FORMPOCZ:='exec(\'pocz_kb\',\'magwal\',\'EUR\',\'134-EUR\')';
            {? AUTOKSIE.add()
            || FORM.blank(1); FORM.AUTOKSIE:=AUTOKSIE.ref(); FORM.POZ:=1; FORM.KWN:='759-02-01'; FORM.KMA:='754-02-01';
               {? FORM.add() || FORM.memo_set('exec(\'roz_kb\',\'magwal\')'); FORM.memo_put() ?}; _ret+=1 ?}
            ?};
            ROK_F.next()
   !}
?};
ROK_F.cntx_pop(); AUTOKSIE.cntx_pop(); FORM.cntx_pop();
{? _ret
|| __UPG.msg('Dodano nowy schemat RKB_FIFO dla dokumentu.')
|| __UPG.msg('Schemat RKB_FIFO istnieje w systemie. Nie było konieczności jego dodawania.')
?};
1


\roznice_kursowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Uzupełnia znacznik róznic kursowych na rodzaju dokumentu
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie wartości N dla pola Różnice kursowe w rodzaju dokumentu księgowego. \n'
'Przypisanie wartości N pola Magazyn walut dla konta syntetycznego.\n'
'Dodanie nowych formuł kontrolnych, które warunkują wycofanie dokumentu księgowego.'


\ppsf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie słowników dla pracy poza siedzibą firmy.
::----------------------------------------------------------------------------------------------------------------------
SLO_TYP.cntx_psh();
SLO_KOD.cntx_psh();
_slo_typ:=exec('slo_typ','ext_slo','PPSFT');

:: Praca zdalna:
exec('kod_ref','ext_slo',_slo_typ,'ZDALNA','Praca zdalna',1);
:: Karmienie piersią:
exec('kod_ref','ext_slo',_slo_typ,'KAR_PIER','Karmienie piersią',1);
:: Delegacje:
exec('kod_ref','ext_slo',_slo_typ,'DELEGAC','Delegacje',1);

SLO_KOD.cntx_pop();
SLO_TYP.cntx_pop();

__UPG.msg('Dodano nowe typy słowników dla pracy poza siedzibą firmy.');

1


\ppsf_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie słowników dla pracy poza siedzibą firmy - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie słowników dla pracy poza siedzibą firmy.'


\szablony
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import zestawów danych i definicji szablonów.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_ASDZ,ZWS_PAR_ASDD',','));
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['szablony'])
|| __UPG.msg('Zaktualizowano zestawy danych i definicje szablonów.');
   _result:=1
|| _result:=-1
?};
_result


\szablony_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import zestawów danych i definicji szablonów - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import zestawów danych i definicji szablonów.'


\mechanizm_importow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Aktualizacja mechanizmu importu danych do kadr.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',obj_ntab_names(obj_new('ZWS_IMPORTY_PKD')));
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack(0).name])
|| __UPG.msg('Zaktualizowano mechanizm importu danych do kadr.');
   _result:=1
|| _result:=-1
?};
_result


\mechanizm_importow_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Aktualizacja mechanizmu importu danych do kadr - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja mechanizmu importu danych do kadr.'


\role_personel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi.
::----------------------------------------------------------------------------------------------------------------------
{? exec('roleActions','upgrade',
      'Specjalista ds. planowania czasu pracy,'
      'Specjalista ds. rozliczania czasu pracy',
      'PRC_CZP_DRPN,PRC_GRP_DRPN,PRC_CZP_DPHW,PRC_CZP_PPHW'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. planowania czasu pracy,'
      'Specjalista ds. rozliczania czasu pracy,'
      'Specjallista ds. zleceniobiorców,'
      'Specjalista ds. płac',
      'PPL_PLL_RPSF'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. planowania czasu pracy,'
      'Specjalista ds. rozliczania czasu pracy,'
      'Specjallista ds. zleceniobiorców,'
      'Specjalista ds. płac,'
      'Dyrektor HR,'
      'Kierownik ds płac',
      'PPL_GRP_PPSL'
   ) &
   exec('roleActions','upgrade',
      'Administrator',
      'ZWS_PAR_PPSF,ZWS_PAR_PDZL,ZWS_PAR_PZAK,ZWS_PAR_PZRP'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. planowania czasu pracy,'
      'Specjalista ds. rozliczania czasu pracy,'
      'Specjallista ds. zleceniobiorców,'
      'Specjalista ds. płac,'
      'Dyrektor HR,'
      'Kierownik ds płac',
      'PPL_GRP_PPSF'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. planowania czasu pracy,'
      'Specjalista ds. rozliczania czasu pracy',
      'PPL_GRP_PPSW'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. płac,'
      'Kierownik ds płac',
      'PKD_EZK_ORWS'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. płac,'
      'Kierownik ds płac',
      'PKD_GRP_AWSP'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. płac,'
      'Kierownik ds płac',
      'PPL_GRP_APAR,POR_ZRD_RZRD,POR_ZRD_PZRD'
   ) &
   exec('roleActions','upgrade',
      'Dyrektor HR',
      'PKD_EZK_OPWS'
   ) &
   exec('roleActions','upgrade',
      'Przeglądający Zleceniobiorców,'
      'Przeglądający Płace,'
      'Dyrektor HR,'
      'Kierownik ds płac',
      'PPL_PLL_PPSF'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr',
      'PKD_EZK_ABLP'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr',
      'PKD_EZK_ASRP'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr',
      'PKD_ZES_RAPK'
   )

|| 1
|| -1
?}


\role_personel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ról/uprawnień związana z czynnościami personelowymi.'


\kornag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Aktualizacja portu wejściowego KORNAG czynności LSP_FAK_DKOR w procesach
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_b_ele:=exec('FindInSet','#table','B_ELE','SYMBOL','LSP_FAK_DKOR',,,1,,null());
B_PORT.cntx_psh();
B_PORT.index('UNIK');
B_PORT.prefix(_b_ele,null(),null(),exec('kind_in','#b_port'),'KORNAG',);
{? B_PORT.first()
||
   B_VALPRT.cntx_psh();
   B_PREL.cntx_psh();
   B_PREL.index('ACCESS');
   B_PREL.prefix(_b_ele);
   _loop:=B_PREL.first();
   {!
   |? _loop
   |!
      {? B_PREL.B_PROC().MICRO<>'T'
      ||
         __lrec+=1;
         B_VALPRT.index('UNIK');
         B_VALPRT.prefix(B_PREL.ref(),B_PORT.ref());
         {? ~B_VALPRT.first()
         ||
            _value:='N';
            B_VALPRT.B_PREL:=B_PREL.ref();
            B_VALPRT.B_PORT:=B_PORT.ref();
            B_VALPRT.FORMULA:=exec('val2fml','#convert',_value,type_of(''));
            B_VALPRT.VALUE:=_value;
            {? B_VALPRT.add() || __lmod+=1 ?}
         ?}
      ?};
      _loop:=B_PREL.next()
   !};
   B_PREL.cntx_pop();
   B_VALPRT.cntx_pop()
?};
B_PORT.cntx_pop();
__UPG.msg('Zaktualizowano port wejściowy KORNAG czynności LSP_FAK_DKOR w procesach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\kornag_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Aktualizacja portu wejściowego KORNAG czynności LSP_FAK_DKOR w procesach - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja portu wejściowego KORNAG czynności LSP_FAK_DKOR w procesach.'


\id_sync
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów
::----------------------------------------------------------------------------------------------------------------------
_init:=exec('init','sync_id');
_result:=_init.RESULT;
_args:=_init.ARGS;
{? _result
|| __UPG.msg('Zaktualizowano definicje identyfikatorów rekordów.')
|| __UPG.msg('Wystąpił problem podczas aktualizacji definicji identyfikorów rekordów %1 - %2 do systemu %3.'
      [_args.KOD,_args.OPIS,_args.SYSTEM])
?};
_result


\id_sync_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie definicji identyfikatorów rekordów:\n'


\sch_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Importuje "personelowe" wnioski w obiegu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:="
{? _a
|| __UPG.msg('Zakończono import wniosku: \"%1\".'@[_b])
|| __UPG.msg('Nie udało się zaimportować wniosku: \"%1\".'@[_b])
?}
";
:: Nazwy formuł w obiegi.fml zwracających nazwę konkretnego wniosku (odzdzielone przecinkami):
_nazwy:=spli_str('nazwa_ppsf,nazwa_ppsp',',');

{! _ii:=1..obj_len(_nazwy)
|! _nazwa:=exec(_nazwy[_ii],'obiegi');
   _msg(exec('imp_wn','sch_imp',_nazwa,1,0),_nazwa)
!};

obj_del(_nazwy);
1


\sch_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Importuje "personelowe" wnioski w obiegu - opis
::----------------------------------------------------------------------------------------------------------------------
'Import typów wniosków w obiegu'


\REJ_MAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Uzupełnia pole ZLGD w tabeli REJ_MAT (do operacji rozpoczętych na e-kiosku)
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_result:=1;
REJ_MAT.cntx_psh();
REJ_MAT.index('ZL');
REJ_MAT.prefix();
{? REJ_MAT.first()
|| {!
   |?
      __lrec+=1;
      _put:=0;
      {? REJ_MAT.ZLGD=null() & REJ_MAT.RP<>''
      || _prac:=null();
         _bryg:=null();
         {? ref_tab(REJ_MAT.RP)=P
         || _prac:=exec('FindAndGet','#table',P,REJ_MAT.RP,,,null())
         |? ref_tab(REJ_MAT.RP)=ZLBR
         || _bryg:=exec('FindAndGet','#table',ZLBR,REJ_MAT.RP,,,null())
         ?};
         {? _prac<>null() | _bryg<>null()
         || _zlgd:=exec('findZlgdZ','ekioski',REJ_MAT.ZGP,_prac,_bryg,null());
            {? _zlgd<>null()
            || REJ_MAT.ZLGD:=_zlgd;
               _put:=1
            ?}
         ?}
      ?};
      {? _put
      || REJ_MAT.put();
         __lmod+=1
      ?};
      REJ_MAT.next()
   !}
?};
REJ_MAT.cntx_pop();
{? _result
|| __UPG.msg('Zaktualizowano pola rozliczeń surowców do operacji.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};

VAR_DEL.delete('__lrec','__lmod');
_result


\REJ_MAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Uzupełnia pole ZLGD w tabeli REJ_MAT (do operacji rozpoczętych na e-kiosku) - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola rozliczeń surowców do operacji.'


\PX_GRP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Uzupełnia pola w tabeli PX_GRP
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_result:=1;
PX_GRP.cntx_psh();
PX_GRP.index('UID');
PX_GRP.prefix();
{? PX_GRP.first()
|| {!
   |?
      __lrec+=1;
      _put:=0;
      {? PX_GRP.STRSTART='' & PX_GRP.STREND=''
      || {? exec('plan_times_upd','px_grp')
         || __lmod+=1
         ?}
      ?};
      PX_GRP.next()
   !}
?};
PX_GRP.cntx_pop();
{? _result
|| __UPG.msg('Zaktualizowano planowane czasy dla grup w kolejce planu.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};

VAR_DEL.delete('__lrec','__lmod');
_result


\PX_GRP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Uzupełnia pola w tabeli PX_GRP - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla grup w kolejce planu strategicznego'


\khodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Uzupełnia pola tabeli KH_ODB
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_formula:="
   __lrec+=1;
   _put:=0;
   {? KH_ODB.FV_EM=''
   || {? KH_ODB.KH_LNK<>null() & KH_ODB.EM<>''
      || KH_ODB.FV_EM:='T'
      || KH_ODB.FV_EM:='N'
      ?};
      _put:=1
   ?};
   {? KH_ODB.KH_LNK().KRAJISO & KH_ODB.KRAJISO=null()
   ||
      KH_ODB.KRAJISO:=KH.KRAJISO;
      _put:=1
   ?};
   {? _put
   || {? KH_ODB.put(1)
      || __lmod+=1
      ?}
   ?}
";
KH_ODB.cntx_psh();
KH_ODB.prefix();
_result:=KH_ODB.for_each(_formula,1);
KH_ODB.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano odbiorców kontrahenta.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji odbiorców kontrahenta.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\khodb_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Uzupełnia pola w tabeli KH_ODB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja odbiorców kontrahenta.'


\ZLP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Uzupełnia pola w tabeli ZLP
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_msk:=ZLP.names();
_msk.clear();
ZLP.trig_off('*','*');
ZLP.cntx_psh();
{? _msk.first()
|| {!
   |?
      __lrec+=1;
      ZLP.use(_msk.NAME);
      ZLP.prefix();
      ZLP.for_each("
         _put:=0;
         {? ZLP.TRN=''
         || ZLP.TRN:='T';
            _put:=1
         ?};
         {? _put
         || __lmod+=1;
            ZLP.put()
         ?}
      ",1);
      _msk.next()
   !}
?};
ZLP.cntx_pop();
ZLP.trig_on('*','*');

__UPG.msg('Zaktualizowano zgłoszenia jednorazowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\ZLP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Uzupełnia pola w tabeli ZLP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól zgłoszeń jednorazowych'


\PROD_REJ_ZLGD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Aktualizacja rejestracji wykonań na podstawie robocizny (rekordów tabeli ZLGD)
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_res:=1;
_msk:=ZLGD.names();
_msk.clear();
ZLGD.trig_off('*','*');
PROD_REJ.trig_off('*','*');
PROD_REJ.cntx_psh();
ZLGD.cntx_psh();
{? _msk.first()
|| {!
   |? ZLGD.use(_msk.NAME);
      ZLGD.cntx_psh();
      ZLGD.index('EK1');
      ZLGD.prefix('N',);
      {? ZLGD.first()
      || {!
         |?
            {? ZLGD.ZGP<>null()
            || PROD_REJ.index('ZGP');
               PROD_REJ.prefix('ZL',ZLGD.ZGP);
               {? PROD_REJ.first()
               || __lrec+=1;
::                Aktualizacja znacznika START (czy operacja rozpoczęta na e-kiosku)
                  {? PROD_REJ.START<>'T'
                  || PROD_REJ.START:='T';
                     PROD_REJ.put();
                     __lmod+=1
                  ?}
               ?}
            |? ZLGD.GROP<>null()
            || PROD_REJ.index('GROP');
               PROD_REJ.prefix('ZL',ZLGD.GROP);
               {? PROD_REJ.first()
               || __lrec+=1;
::                Aktualizacja znacznika START (czy operacja rozpoczęta na e-kiosku)
                  {? PROD_REJ.START<>'T'
                  || PROD_REJ.START:='T';
                     PROD_REJ.put();
                     __lmod+=1
                  ?}
               ?}
            ?};
            ZLGD.next()
         !}
      ?};
      ZLGD.cntx_pop();
      _msk.next()
   !}
?};
PROD_REJ.cntx_pop();
ZLGD.cntx_pop();
PROD_REJ.trig_on('*','*');
ZLGD.trig_on('*','*');
__UPG.msg('Zaktualizowano zapisy do rejestracji wykonań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\PROD_REJ_ZLGD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Aktualizacja rejestracji wykonań na podstawie robocizny (rekordów tabeli ZLGD) - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów do rejestracji wykonań'


\rozszerzony_filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Uzupełnienie definicji rozszerzonych filtrów.
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_XFL',','));
__UPG.msg('Uruchomiono formułę uzupełniającą definicje rozszerzonych filtrów.');
1


\rozszerzony_filtr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Uzupełnienie definicji rozszerzonych filtrów - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie definicji rozszerzonych filtrów'


\KH_DOD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37]
:: OPIS: Uzupełnia pola w tabeli KH_DOD
::----------------------------------------------------------------------------------------------------------------------
_formula:="KH_DOD.put()";
KH_DOD.cntx_psh();
KH_DOD.prefix();
_result:=KH_DOD.for_each(_formula,1);
KH_DOD.cntx_pop();
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w kartotece kontrahentów (dane dodatkowe dla firmy).')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w kartotece kontrahentów (dane dodatkowe dla firmy).')
?};
_result


\KH_DOD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37]
:: OPIS: Uzupełnia pola w tabeli KH_DOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli kontrahentów (dane dodatkowe dla firmy).'


\tr_rodz_perm_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Nadaje uprawnienia do danych kategorii transportu
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
exec('Perm','#object');
Perm.add('TR_RODZ','Uprawnienia do kategorii transportu',"exec('set_UserUpr','b_perm','TR_RODZ',,,_a)"
   ,"exec('copy_upr','b_perm','TR_RODZ')");
_b_perm:=exec('FindInSet','#table','B_PERM','NAME','TR_RODZ',,,1,,null());

USERS_UP.cntx_psh();
USERS.cntx_psh();
B_PERM_U.cntx_psh();
USERS.index('USR_KKOD');
USERS.prefix();
{? USERS.first() & _b_perm
|| {!
   |? {? exec('ctrlTR_RODZ','users_upraw',USERS.ref())
      || Perm.add4user(REF.FIRMA,USERS.ref(),_b_perm);
         B_PERM_U.index('USER');
         B_PERM_U.prefix(REF.FIRMA,USERS.ref(),_b_perm);
         {? B_PERM_U.first()
         || __lrec+=1;
            {? B_PERM_U.ACCESS='N'
            || B_PERM_U.ACCESS:='T';
               B_PERM_U.FULL:='T';
               {? B_PERM_U.put(1) || __lmod+=1 ?}
            ?}
         ?}
      || Perm.del4user(REF.FIRMA,USERS.ref(),_b_perm);
         USERS_UP.index('TR_RODZ');
         USERS_UP.prefix(USERS.ref(),'TR_RODZ');
         {? USERS_UP.first() || {! |? USERS_UP.del() !} ?}
      ?};
      USERS.next()
   !}
?};
B_PERM_U.cntx_pop();
USERS.cntx_pop();
USERS_UP.cntx_pop();
__UPG.msg('Nadano uprawnienia do danych w zakresie: Uprawnienia do kategorii transportu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\tr_rodz_perm_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Nadaje uprawnienia do danych kategorii transportu - opis
::----------------------------------------------------------------------------------------------------------------------
'Nadaje uprawnienia do danych kategorii transportu'


\edeklaracje_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Modyfikacje schematów e-deklaracji
::----------------------------------------------------------------------------------------------------------------------
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
::kasowanie struktur e-deklaracji IFT-1 wersja 16 oraz IFT-1R wersja 16
exec('edek_stru_del','edeklar','IFT1',16);
exec('edek_stru_del','edeklar','IFT1R',16);
::import poprawnych struktur e-deklaracji IFT-1 wersja 16
{? exec('edek_stru','edeklar','IFT1',16,'ift-1-16_v1.dfg')
|| __UPG.msg('Dodano poprawiony schemat 16 wersji e-deklaracji IFT-1.')
|| __UPG.msg('Brak aktualizacji, schemat 16 wersji e-deklaracji IFT-1 już istnieje.')
?};
::import poprawnych struktur e-deklaracji IFT-1R wersja 16
{? exec('edek_stru','edeklar','IFT1R',16,'ift-1r-16_v1.dfg')
|| __UPG.msg('Dodano poprawiony schemat 16 wersji e-deklaracji IFT-1R.')
|| __UPG.msg('Brak aktualizacji, schemat 16 wersji e-deklaracji IFT-1R już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
1


\edeklaracje_pop_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Modyfikacje schematów e-deklaracji
::----------------------------------------------------------------------------------------------------------------------
'Poprawiono schematy 16 wersji e-deklaracji IFT-1 oraz 16 wersji e-deklaracji IFT-1R.'


\r_opczyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja czytników systemu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R_OPCZYT.cntx_psh();
R_OPCZYT.index('R_OPCZYS');
R_OPCZYT.prefix();
_czyt:=
   "  {? ~R_OPCZYT.find_key(_a,'<SYSTEM>')
      || R_OPCZYT.SYMBOL:='<SYSTEM>';
         R_OPCZYT.K:=_a;
         R_OPCZYT.O:=_b;
         R_OPCZYT.PORTAL:=R_OPCZYT.SL:='T';
         R_OPCZYT.add(1)
      ?}
   ";
_czyt('SYS_5','POZA SIEDZIBĄ');
R_OPCZYT.cntx_pop();

__UPG.msg('Dodano nowy czytnik dla pracy poza siedzibą firmy.');

1


\r_opczyt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja czytników systemu - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czytników systemu.'


\TR_RODZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Uzupełnia znacznik kontroli mocy
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
TR_RODZ.cntx_psh();
TR_RODZ.prefix();
TR_RODZ.for_each("__lrec+=1;
                  {? TR_RODZ.CTRL=''
                  || TR_RODZ.CTRL:='T';
                     {? TR_RODZ.put(1) || __lmod+=1 ?}
                  ?}",1);
TR_RODZ.cntx_pop();
__UPG.msg('Zaktualizowanu znacznik kontroli mocy transportowych dla kategorii transportu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\TR_RODZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Uzupełnia znacznik kontroli mocy - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia znacznik kontroli mocy transportowych dla kategorii transportu'


\NIPY
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Uzupełnia pola w tabeli NIPY
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   _put:=0;
   {? NIPY.DEFAULT=''
   || NIPY.DEFAULT:='N';
      _put:=1
   ?};
   {? _put
   || {? NIPY.put(1)
      || __lmod+=1
      ?}
   ?}
";
NIPY.trig_off('*','*');
NIPY.cntx_psh();
NIPY.prefix();
_result:=NIPY.for_each(_formula,1);
NIPY.cntx_pop();
NIPY.trig_on('*','*');

{? _result
|| __UPG.msg('Zaktualizowano NIP dla płatników UE.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji NIP dla płatników UE.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\NIPY_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Uzupełnia pola w tabeli NIPY - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja NIP dla płatników UE'


\KH
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Uzupełnia pola dla tabeli kontrahentów
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   _put:=0;
:: Aktualizacja domyślnego nipu UE
:: Jeżeli kontrahent nie ma przypisanego nipu domyślnego oraz ma przypisany tylko jeden NIP UE,
:: to ustawiam go jako domyślny dla kontrahenta
   NIPY.cntx_psh();
   NIPY.index('DEFAULT');
   NIPY.prefix(KH.ref(),'T');
   {? ~NIPY.first()
   || NIPY.prefix(KH.ref());
      {? NIPY.first() & NIPY.size()=1
      || {? KH.NIP_UE<>NIPY.NIP
         || KH.NIP_UE:=NIPY.NIP;
            _put:=1
         ?}
      || {? KH.NIP_UE<>''
         || KH.NIP_UE:='';
            _put:=1
         ?}
      ?}
   ?};
   NIPY.cntx_pop();

   {? _put
   || {? KH.put(1)
      || __lmod+=1
      ?}
   ?}
";
KH.trig_off('*','*');
KH.cntx_psh();
KH.prefix();
_result:=KH.for_each(_formula,1);
KH.cntx_pop();
KH.trig_on('*','*');

{? _result
|| __UPG.msg('Zaktualizowano tabelę kontrahentów.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji tabeli kontrahentów.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\KH_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Uzupełnia pola dla tabeli kontrahentów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli kontrahentów'


\statusy_abstore
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [21.37]
:: OPIS: Dodanie statusów AbStore.
::----------------------------------------------------------------------------------------------------------------------
exec('abs_slo','abstore_fun')


\statusy_abstore_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [21.37]
:: OPIS: Dodanie statusów AbStore.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie statusów AbStore.'


\statusy_projekty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [21.37]
:: OPIS: Aktualizacja statusów projektów.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

PROJ_STA.cntx_psh();
PROJ_STA.index('KOD');
PROJ_STA.prefix('01_AKT',);
{? ~PROJ_STA.first()
||
   PROJ_STA.KOD:='01_AKT';
   PROJ_STA.STATUS:='Aktywna/Aktywny';
   PROJ_STA.AKT:='T';
   PROJ_STA.add()
?};
PROJ_STA.prefix('02_ZAM',);
{? ~PROJ_STA.first()
||
   PROJ_STA.KOD:='02_ZAM';
   PROJ_STA.STATUS:='Zamknięta/Zamknięty';
   PROJ_STA.AKT:='N';
   PROJ_STA.add()
?};
PROJ_STA.cntx_pop();

PROJEKTY.cntx_psh();
PROJEKTY.index('FIRMA');
PROJEKTY.prefix();
PROJEKTY.for_each("
__lrec+=1;
_put:=0;
{? PROJEKTY.STATUS=null & (_status:=exec('proj_sta_wp','projekty_view'))<>null()
||
   PROJEKTY.STATUS:=_status;
   _put:=1
?};
{? _put & PROJEKTY.put(1) || __lmod+=1 ?}
",1);
PROJEKTY.cntx_pop();

__UPG.msg('Zaktualizowano statusy projektów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\statusy_projekty_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [21.37]
:: OPIS: Aktualizacja statusów projektów.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja statusów projektów.'


\XFL_PAR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Aktualizacja parametrów rozszerzonych filtrów.
::----------------------------------------------------------------------------------------------------------------------
_cnt:=0;
_mod:=0;
_not:=0;
_err:=0;

XFL_PAR.trig_off('*','*');
XFL_PAR.cntx_psh();
XFL_PAR.index('KOD');
XFL_PAR.prefix();

_loop:=XFL_PAR.first();
{!
|? _loop
|! _cnt+=1;
   {? XFL_PAR.KOL_ZAS=0
   || XFL_PAR.KOL_ZAS:=+XFL_PAR.SYMBOL;
      {? XFL_PAR.put()
      || _mod+=1
      || _err+=1
      ?}
   || _not+=1
   ?};
   _loop:=XFL_PAR.next()
!};

XFL_PAR.cntx_pop();
XFL_PAR.trig_on('*','*');

__UPG.msg('Aktualizacja parametrów rozszerzonych filtrów:');
__UPG.msg('przetworzone: %1'[$_cnt]);
__UPG.msg('zaktualizowane: %1'[$_mod]);
__UPG.msg('pominięte: %1'[$_not]);
__UPG.msg('błędne: %1'[$_err]);

{? _err<>0 || -1 || 1 ?}


\XFL_PAR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [jaws] [21.37]
:: OPIS: Aktualizacja parametrów rozszerzonych filtrów - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametrów rozszerzonych filtrów'


\grupwyn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Dodanie słownika wykorzystywanego przy ewidencjonowaniu współczynników do wynagrodzeń.
::----------------------------------------------------------------------------------------------------------------------
SLO_TYP.cntx_psh();
exec('slo_typ','ext_slo','GRUPWYN');
SLO_TYP.cntx_pop();

__UPG.msg('Dodano nowy typy słowników dla ewidencji współczynników do wynagrodzenia.');
1


\grupwyn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Dodanie słownika wykorzystywanego przy ewidencjonowaniu współczynników do wynagrodzeń - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie słownika wykorzystywanego przy ewidencjonowaniu współczynników do wynagrodzeń.'


\add_dekr_kornag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Dekretacja korekt nagłówkowych z logistyki
::----------------------------------------------------------------------------------------------------------------------
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
_add:="FORMULA.prefix(_a,_b,);
       {? ~FORMULA.first()
       || FORMULA.blank(1);
          FORMULA.RODZAJ:=_a;
          FORMULA.SKROT:=_b;
          FORMULA.NAZWA:=_c;
          FORMULA.FORMULA:=_d;
          FORMULA.add()
       ?}";
_ret:=_add('K','200_KN','Należności - korekty nag.','(3+KONTOTXT)+SEP+POWF+KONTRKN');
{? _ret
|| __UPG.msg('Dodano formułę KONTRKN do listy formuł')
|| __UPG.msg('Formuła KONTRKN istnieje w systemie. Nie było konieczności jej dodawania.')
?};
_ret:=_add('I','FKKOR','Symbol dokumentu korygowanego.','FAKS.KOR');
{? _ret
|| __UPG.msg('Dodano formułę FKKOR do listy formuł')
|| __UPG.msg('Formuła FKKOR istnieje w systemie. Nie było konieczności jej dodawania.')
?};
FORMULA.cntx_pop();

WARLOG.cntx_psh();
WARLOG.index('KKOD');
WARLOG.prefix();
WARLOG.KOD:='KONTRKN';
WARLOG.OPIS:='Rodzaj korekty - czy rozliczane w bieżącym okresie';
WARLOG.FO:='exec(\'nalezn_kn\',\'%fks_dekr_dziedz\')';
WARLOG.DV:='N';
WARLOG.RKW:='D';
WARLOG.TD:='S';
WARLOG.SO:='N';
WARLOG.RODZ:='L';
{? WARLOG.add(1)
|| __UPG.msg('Dodano zmienną KONTRKN do zmiennych dla formuł.')
|| __UPG.msg('Zmienna KONTRKN istnieje w systemie. Nie było konieczności jej dodawania.')
?};
WARLOG.KOD:='FKKOR';
WARLOG.OPIS:='Symbol faktury korygowanej';
WARLOG.FO:='exec(\'faktura_kn\',\'%fks_dekr_dziedz\')';
{? WARLOG.add(1)
|| __UPG.msg('Dodano zmienną FKKOR do zmiennych dla formuł.')
|| __UPG.msg('Zmienna FKKOR istnieje w systemie. Nie było konieczności jej dodawania.')
?};
WARLOG.cntx_pop();
1


\add_dekr_kornag_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Dekretacja korekt nagłówkowych z logistyki - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodaje formuły niezbędne do dekretacji korekt nagłówkowych z Logistyki'


\role_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami portalowymi.
::----------------------------------------------------------------------------------------------------------------------
{? exec('roleActions','upgrade','Portal HR - Pracownik',
      'POR_PRA_WEWY,POR_PRA_ZROC,POR_PRA_ZRZA,POR_PRA_ZRZP,POR_PRA_ZRZS,POR_PRA_ZRZN'
   ) &
   exec('roleActions','upgrade','Portal HR - Przełożony (bez wynagrodzeń)',
      'POR_KIE_WEWY,POR_KIE_ZROC,POR_KIE_ZRZA,POR_KIE_ZRZP'
   ) &
   exec('roleActions','upgrade','Portal HR - Przełożony (z wynagrodzeniami)',
      'POR_KIE_WEWY,POR_KIE_ZROC,POR_KIE_ZRZA,POR_KIE_ZRZP'
   )
|| 1
|| -1
?}


\role_portal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami portalowymi - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ról/uprawnień związana z czynnościami portalowymi.'


\del_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: Usunięcie parametru ZWS_PAR_PORTAL - Parametry portalu
::----------------------------------------------------------------------------------------------------------------------
:Identyfikator parametru do usunięcia (pole ID z tabeli FP_DEF)
_id:='ZWS_PAR_PORTAL';

FP_STR.cntx_psh();
FP_DEF.cntx_psh();
FP_WYK.cntx_psh();
FP_FIR.cntx_psh();
FP_AKT.cntx_psh();
FP_INS.cntx_psh();

::Usuwanie danych z tabeli FP_DEF
FP_DEF.index('ID');
FP_DEF.prefix('T',_id,);

{? FP_DEF.first() ||
   _def:=FP_DEF.ref;
   FP_DEF.del(1);

:: Usuwanie danych z tabeli FP_STR
   FP_STR.index(FP_STR.ndx_tmp(,1,'FP_DEF',,,'ID',,));
   FP_STR.prefix(_def,);
   {? FP_STR.first() || FP_STR.del(1) ?};

:: Usuwanie powiązanych danych z tabeli FP_WYK
   FP_WYK.index(FP_WYK.ndx_tmp(,1,'FP_DEF',,,'FP_AKT',,));
   FP_WYK.prefix(_def,);
   {? FP_WYK.first() ||
      {! |?
         FP_WYK.del(1);
         FP_WYK.next()
      !}
   ?};

:: Usuwanie powiązanych danych z tabeli FP_FIR
   FP_FIR.index('UNIQUE');
   FP_FIR.prefix(_def,);
   {? FP_FIR.first() ||
      {! |?

::       Usuwanie powiązanych danych z tabeli FP_INS
         FP_INS.index('UNIQUE');
         FP_INS.prefix(FP_FIR.ref,);
         {? FP_INS.first() ||
            {! |?
               FP_INS.del(1);
               FP_INS.next()
            !}
         ?};

         FP_FIR.del(1);
         FP_FIR.next()
      !}
   ?};

:: Usuwanie powiązanych danych z tabeli FP_AKT
   FP_AKT.index(FP_AKT.ndx_tmp(,1,'FP_DEF',,,'FP_WER',,));
   FP_AKT.prefix(_def,);
   {? FP_AKT.first() ||
      {! |?
         FP_AKT.del(1);
         FP_AKT.next()
      !}
   ?};
   __UPG.msg('Parametr o identyfikatorze ''%1'' został usunięty z systemu.' [_id]);
   _status:=1
||
   __UPG.msg('Usunięcie parametru o identyfikatorze ''%1'' nie powiodło się.' [_id]);
   _status:=-1
?};

FP_STR.cntx_pop();
FP_DEF.cntx_pop();
FP_WYK.cntx_pop();
FP_FIR.cntx_pop();
FP_AKT.cntx_pop();
FP_INS.cntx_pop();
_status


\del_par_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: Usunięcie parametru ZWS_PAR_PORTAL - Prametry portalu
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie parametru ZWS_PAR_PORTAL - Parametry portalu.'


\zastepstwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: Aktualizacja pola OD tabeli P_PZ (Zastępstwo lub zależność pracowników)
::----------------------------------------------------------------------------------------------------------------------
_task:=obj_new('counter','error','getDate');
_task.counter:=0;
_task.error:=0;
_task.getDate:="
   _dates:=obj_new('phod','pzhod');
   _dates.phod:=_dates.pzhod:=date(0,0,0);
   H.cntx_psh();
   H.index('_HISTDAT');
   H.prefix(P_PZ.P);
   {? H.first() || _dates.phod:=H.OD ?};
   H.prefix(P_PZ.PZ);
   {? H.first() || _dates.pzhod:=H.OD ?};
   H.cntx_pop();
   {? _dates.phod>_dates.pzhod || _dates.phod || _dates.pzhod ?}
";
_zdate:=date(0,0,0);

P_PZ.cntx_psh();
P_PZ.index('P_PZ');
P_PZ.prefix('Z',);
_size:=P_PZ.size();
{? _loop:=P_PZ.first()
|| P_PZ.trig_off('*','*');
   {!
   |? _loop
   |! {? P_PZ.OD=_zdate
      || _od:=_task.getDate();
         {? _od<>_zdate
         || P_PZ.OD:=_od;
            {? P_PZ.put()
            || _task.counter+=1
            || _task.error+=1
            ?}
         || _task.error+=1
         ?}
      ?};
      _loop:=P_PZ.next()
   !};
   P_PZ.trig_on('*','*')
?};
P_PZ.cntx_pop();

__UPG.msg('Liczba wszystkich wierszy: %1.'[$_size]);
__UPG.msg('Liczba zaktualizowanych wierszy: %1.'[$_task.counter]);
__UPG.msg('Błędy aktualizacji wierszy: %1.'[$_task.error]);

{? _task.error || -1 || 1 ?}


\zastepstwa_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: Aktualizacja pola OD tabeli P_PZ (Zastępstwo lub zależność pracowników) - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola OD tabeli P_PZ dla zastępstw.'


\FAKSPOLA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Aktualizacja pól tabeli FAKSPOLA
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKSPOLA.names();
_msk.clear();
FAKSPOLA.cntx_psh();
{? _msk.first()
|| {!
   |? FAKSPOLA.use(_msk.NAME);
      FAKSPOLA.prefix();
      FAKSPOLA.for_each("
         __lrec+=1;
         _put:=0;
         {? FAKSPOLA.NST_RODZ='' & FAKSPOLA.NST_DATA<>date(0,0,0)
         || FAKSPOLA.NST_RODZ:=exec('nst_ladowy','jpk_log');
            _put:=1
         ?};
         {? _put
         || __lmod+=1;
            FAKSPOLA.put()
         ?}
      ",1);
      _msk.next()
   !}
?};
FAKSPOLA.cntx_pop();
__UPG.msg('Zaktualizowano dodatkowe dane faktur.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\FAKSPOLA_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Aktualizacja pól tabeli FAKSPOLA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dodatkowych danych faktur.'


\KH_ODB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Wypełnia pola w tabeli KH_ODB
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
KH_ODB.cntx_psh();
KH_ODB.prefix();
_res:=KH_ODB.for_each("
   __lrec+=1;
   _put:=0;
   {? KH_ODB.NIP='' & KH_ODB.KH_LNK<>null()
   || KH_ODB.NIP:=KH_ODB.KH_LNK().NIP;
      _put:=1
   ?};
   {? _put>0
   || {? KH_ODB.put()
      || __lmod+=1
      ?}
   ?}
");
KH_ODB.cntx_pop();
__UPG.msg('Zaktualizowano odbiorców kontrahentów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\KH_ODB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Wypełnia pola w tabeli KH_ODB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja odbiorców kontrahentów'


\zws_par_kmti
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Formuła dodaje czynność ZWS_PAR_KMTI do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZWS_PAR_KMTR'";
_uid:='ZWS_PAR_KMTI';
exec('actionRoleAction','upgrade',_uid,_cr)


\zws_par_kmti_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Formuła dodaje czynność ZWS_PAR_KMTI do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_KMTI - Edycja indeksów mater. i usług" do odpowiednich ról'


\JM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Uzupełnia pole JM.DOKL
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_uzjm:=1;

JM.cntx_psh();
JM.index('KOD');
JM.prefix();
{? JM.first()
|| {!
   |? {? JM.DOKL<>0 || _uzjm:=0 ?};
      _uzjm & JM.next()
   !}
?};
{? _uzjm || JM.for_each("__lrec+=1; JM.DOKL:=-1; {? JM.put(1) || __lmod+=1 ?}",1) ?};
JM.cntx_pop();

__UPG.msg('Zaktualizowano jednostki miary.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\JM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Uzupełnia pole JM.DOKL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli jednostek miary.'


\ogrp_perm_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Nadaje uprawnienie do danej: Uprawnienia do grup wniosków i dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
USERS.index('USR_KKOD');
USERS.prefix();
{? USERS.first()
|| B_PERM.index('NAME'); B_PERM.prefix();
   B_PERM_U.index('USER');
   {!
   |? {? B_PERM.find_key('OGRP',)
      ||
         B_PERM_U.prefix(REF.FIRMA,USERS.ref(),B_PERM.ref());
         {? B_PERM_U.first()
         || __lrec+=1;
            {? B_PERM_U.ACCESS='N'
            || B_PERM_U.ACCESS:='T';
               {? B_PERM_U.put(1) || __lmod+=1 ?}
            ?}
         ?};
         B_PERM.next()
      ?};
      USERS.next()
   !}
?};
__UPG.msg('Nadano uprawnienia do danych w zakresie: Uprawnienia do grup wniosków i dokumentów w obiegu');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ogrp_perm_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Nadaje uprawnienie do danej: Uprawnienia do grup wniosków i dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
'Nadaje uprawnienie do danej: Uprawnienia do grup wniosków i dokumentów w obiegu'


\kh_dod_dob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Aktualizacja nazwy domyślnej obsługi dokumentu Businesslink w kartotece kontrahentów dla faktury w obiegu
::----------------------------------------------------------------------------------------------------------------------
_formula:="{? KH_DOD.BL_ACT='Rejestruj dokument w obiegu'
           || KH_DOD.BL_ACT:='Rejestruj fakturę w obiegu';
              KH_DOD.put()
           ?}";
KH_DOD.cntx_psh();
KH_DOD.prefix();
_result:=KH_DOD.for_each(_formula,1);
KH_DOD.cntx_pop();
{? _result
|| __UPG.msg('Zaktualizowano pole domyślnej obługi dokumentu Businesslink w kartotece kontrahentów (dane dodatkowe dla firmy).')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji pola domyślnej obługi dokumentu Businesslink w kartotece kontrahentów (dane dodatkowe dla firmy).')
?};
_result


\kh_dod_dob_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Aktualizacja nazwy domyślnej obsługi dokumentu Businesslink w kartotece kontrahentów dla faktury w obiegu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja nazwy domyślnej obsługi dokumentu Businesslink w kartotece kontrahentów (dane dodatkowe dla firmy).'


\rez_url
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Aktualizacja rezerwy urlopowej i emerytalnej.
::----------------------------------------------------------------------------------------------------------------------
_wynik:=1;

_slo_typ:=exec('slo_typ','ext_slo','MET_REZ');
{? _slo_typ<>null()
|| exec('kod_ref','ext_slo',_slo_typ,'REZ_SKL','Według składników w przebiegu zatrudnienia',1);
   exec('kod_ref','ext_slo',_slo_typ,'REZ_EKW','Według zasad naliczania ekwiwalentu za urlop',1);
   __UPG.msg('Dodano nowy słownik dla metod obliczania podstawy rezerwy urlopowej oraz emerytalnej.')
|| __UPG.msg('Nie udało się dodać nowego słownika dla metod obliczania podstawy rezerwy urlopowej oraz emerytalnej.');
   _wynik:=0
?};

{? _wynik
|| RURL_N.trig_off('*','*');
   SLO_TYP.cntx_psh();
   SLO_KOD.cntx_psh();
   RURL_N.cntx_psh();
   ODP_EME.cntx_psh();

   _ref:=null();
   SLO_TYP.index('SYMBOL');
   SLO_TYP.prefix('MET_REZ');
   {? SLO_TYP.first()
   || SLO_KOD.index('KOD');
      SLO_KOD.prefix(SLO_TYP.ref(),'REZ_SKL');
      {? SLO_KOD.first()
      || _ref:=SLO_KOD.ref()
      ?}
   ?};

   RURL_N.prefix();
   {? RURL_N.first()
   || {!|? _akt:=0;
           {? RURL_N.D=0
           || RURL_N.D:=RURL_N.DATA~3;
              _akt:=1
           ?};
           {? RURL_N.METODA=null()
           || RURL_N.METODA:=_ref;
              _akt:=1
           ?};
           {? RURL_N.CZY_LICZ=''
           || RURL_N.CZY_LICZ:='N';
              _akt:=1
           ?};
           {? _akt=1
           || RURL_N.put()
           ?};
           RURL_N.next()
      !}
   ?};

   ODP_EME.prefix();
   {? ODP_EME.first()
   || {!|? {? ODP_EME.KP=null()
           || ODP_EME.KP:=_ref;
              ODP_EME.put()
           ?};
           ODP_EME.next()
      !}
   ?};

   ODP_EME.cntx_pop();
   SLO_KOD.cntx_pop();
   SLO_TYP.cntx_pop();
   RURL_N.cntx_pop();
   RURL_N.trig_on('*','*')
?};

1


\rez_url_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Aktualizacja rezerwy urlopowej i emerytalnej - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rezerwy urlopowej i emerytalnej.'


\dokum_projekty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Uzupełnienie pola PROJEKTY w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DOKUM.names();
_msk.clear();
DOKUM.cntx_psh();
PROJEKTY.cntx_psh();
PROJEKTY.prefix();
{? _msk.first()
|| {!
   |? DOKUM.use(_msk.NAME);
      DOKUM.index('DOKUM');
      DOKUM.prefix(REF.FIRMA,'pj_nproj');
      {? DOKUM.first()
      || {!
         |? __lrec+=1;
            _put:=0;
            {? DOKUM.PROJEKTY=null() & PROJEKTY.seek(DOKUM.REFSQL)
            || DOKUM.PROJEKTY:=PROJEKTY.ref();
               _put:=1
            ?};
            {? DOKUM.TYP='I'
            || DOKUM.TYP:='D';
               _put:=1
            ?};
            {? _put
            || __lmod+=1;
               DOKUM.put()
            ?};
            DOKUM.next()
         !}
      ?};
      _msk.next()
   !}
?};
DOKUM.cntx_pop();
PROJEKTY.cntx_pop();
__UPG.msg('Zaktualizowano załączniki projektów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\dokum_projekty_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Uzupełnienie pola PROJEKTY w tabeli DOKUM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja załączników projektów.'


\dks_edi_ufd_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Aktualizacja formuł komunikatów EDI do odczytu faktur i korekt dokumentów księgowych z businesslink
::----------------------------------------------------------------------------------------------------------------------
_choice:=0;
{! |? (_choice:=FUN.choice(
            'Należy zaaktualizować formuły komunikatów EDI do odczytu faktur i korekt dokumentów księgowych:\n'
            '1. Przejdź do ustawień i parametryzacji->wspólne->wymiana danych->komunikaty EDI.\n'
            '2. Dla komunikatów UFD_DKS* usuń i zaimportuj ponownie formuły.\n'
            'lub\n'
            '1. Uruchom formułę aktualizującą.\n\n'
            'Czy prace opisane powyżej zostały wykonane?'@
            ,0,'Komunikaty EDI'@,'Uruchom formułę'@,'Tak'@,,'Nie'@)); _choice=1 | _choice=2
   |! {? _choice=1
      || exec('edi_def','edi_def','ZWS')
      |? _choice=2
      || exec('dks_edi_ufd_upd_form','upgrade_2137')
      ?}
!};
{? _choice
|| __UPG.msg('Użytkownik %1 potwierdził wykonanie zadania.' [exec('operatorKod','#users')]);
   1
|| __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   0
?}


\dks_edi_ufd_upd_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Aktualizacja formuł komunikatów EDI do odczytu faktur i korekt dokumentów księgowych z businesslink - formuła
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh(); ISTXML.cntx_psh();
ISTDEF.index('K'); ISTDEF.prefix('ZWS','E','UFD_DKS_');
{? ISTDEF.first()
|| _tab:=tab_tmp(,'TEXT','STRING[255]','Tekst','K','STRING[255]','korekta');
   _tab.TEXT:='0936b9e6b2aac3b1a045f1a841ba1caa61b9ce48_is_fld___T'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='0936b9e6b2aac3b1a045f1a841ba1caa61b9ce48_f_text___POZF.WN'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='232ca129e36f7d5acad1b527226b91af1bf87b30_is_fld___T'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='232ca129e36f7d5acad1b527226b91af1bf87b30_f_text_____KH.DOK.NIP'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='2f3769b47321c6949180ecf5919e2d1d76153f63_is_fld___T'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='2f3769b47321c6949180ecf5919e2d1d76153f63_f_text_____KH.DOK.KH_UL'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='38aafff434df3c24b7306c9fc49297bcbed00e87_is_fld___T'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='38aafff434df3c24b7306c9fc49297bcbed00e87_f_text_____KH.DOK.KH_M'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='4b89a3f4b4c24a642b621112051374057dac6583_f_text___exec(\'check_jm\',\'edi_fo_ufd\')'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='5e974ea377e071ba1ea5da2585d92e1daf76480a_f_start__exec(\'seller_se\',\'edi_fo_ufd\')'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='5e974ea377e071ba1ea5da2585d92e1daf76480a_f_end____exec(\'seller_ee\',\'edi_fo_ufd\',\'FKS\')'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='5e974ea377e071ba1ea5da2585d92e1daf76480a_is_fld___N'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='7911846a1d11ff737cefd4544a5eff37dc60c85b_f_end____exec(\'pozf_end\',\'edi_fo_ufd\')'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='839d27504890a430314c3585132b46a98cbc0ee4_is_fld___T'; _tab.K:='N'; _tab.add;
   _tab.TEXT:='839d27504890a430314c3585132b46a98cbc0ee4_f_text___VPOZ.BRUTTO'; _tab.K:='N'; _tab.add;
   _tab.TEXT:='ca5b567d43b77219a8bad3e92def11c8b170f5fe_is_fld___T'; _tab.K:='N'; _tab.add;
   _tab.TEXT:='ca5b567d43b77219a8bad3e92def11c8b170f5fe_f_text___VPOZ.BRUTTO'; _tab.K:='T'; _tab.add;
   _tab.TEXT:='927874e45c2d592fd9f9e37ae9bd69cb2a86e859_f_end____exec(\'vpoz_end\',\'edi_fo_ufd\')'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='a376a82ff88b469359c7d1a9b6d8c53a673a1a7f_is_fld___T'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='a376a82ff88b469359c7d1a9b6d8c53a673a1a7f_f_text_____KH.DOK.KH_P'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='b1df7232d1c081acaaf2b152892af8f4055c2f0a_is_fld___T'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='b1df7232d1c081acaaf2b152892af8f4055c2f0a_f_text___POZF.WB'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='b3b331aaf700c511f64a80dfc754f9811b0280eb_is_fld___T'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='b3b331aaf700c511f64a80dfc754f9811b0280eb_f_text___POZF.WV'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='b7489ab3baca6b8a1502df657f739d4041accf8a_is_fld___T'; _tab.K:='B'; _tab.add;
   _tab.TEXT:='b7489ab3baca6b8a1502df657f739d4041accf8a_f_text_____KH.DOK.KH_NAZ'; _tab.K:='B'; _tab.add;
   ISTXML.index('TREE');
   {!
   |? ISTXML.prefix(ISTDEF.ref);
      _Hash:=exec('def_hash','edi_xml',ISTDEF.ref,0,'',~~);
      {? _tab.first()
      || {!
         |? {? (ISTDEF.K*'NOTE'>0 & (_tab.K='B' | _tab.K='T')) | (ISTDEF.K*'NOTE'=0 & (_tab.K='B' | _tab.K='N'))
            || _hash:=40+_tab.TEXT;
               _type:=10+(40-_tab.TEXT);
               _text:=50-_tab.TEXT;
               {? _Hash.find_key(_hash)
               || {? ISTXML.seek(_Hash.REF,)
                  || {? _type='_f_start__' || ISTXML.F_START:=_text
                     |? _type='_f_end____' || ISTXML.F_END:=_text
                     |? _type='_f_text___' || ISTXML.F_TEXT:=_text
                     |? _type='_is_fld___' || ISTXML.IS_FLD:=_text
                     ?};
                     ISTXML.put()
                  ?}
               ?}
            ?};
            _tab.next()
         !}
      ?};
      &_Hash;
      ISTDEF.next()
   !}
?};
ISTDEF.cntx_pop(); ISTXML.cntx_pop();
FUN.info('Zakończono aktualizację formuł komunikatów EDI.'@);
__UPG.msg('Użytkownik %1 wykonał formułę aktualizującą.' [exec('operatorKod','#users')]);
1


\dks_edi_ufd_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Aktualizacja formuł komunikatów EDI do odczytu faktur i korekt dokumentów księgowych z businesslink - OPIS
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja formuł komunikatów EDI odczytujących faktury i korekty dokumentów księgowych z Businesslink.'


\upd_r_jpk_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Ustawia formułe dla elementu R schematów JPK
::----------------------------------------------------------------------------------------------------------------------
_type:=',FA,KR,MAG,V7M,V7K,VAT,WB,';
_obj:=obj_new(
   'TAB','ONE','ALL',
   'add','get'
);
_obj.TAB:=tab_tmp(1,
   'FROM','STRING[255]','From',
   'TO','STRING[255]','TO'
);
_obj.add:="
   _tab:=.TAB;
   {? ~_tab.find_key(_a)
   || _tab.blank(1);_tab.FROM:=_a;_tab.TO:=_b;_tab.add()
   ?}
";
_obj.get:="
   _tab:=.TAB;
   {? _tab.find_key(_a,)
   || _tab.TO
   || ''
   ?}
";
_obj.ALL:=0;
_obj.add('$DK.ref()+$DK.crc()',$"exec('ref','jpk','DK')");
_obj.add('$DOK.ref()+$DOK.crc()',$"exec('ref','jpk','DOK')");
_obj.add('$ND.ref()+$ND.crc()',$"exec('ref','jpk','ND')");
_obj.add('$POZ.ref()+$POZ.crc()',$"exec('ref','jpk','POZ')");
_obj.add('$VAT_PS.ref()+$VAT_PS.crc()',$"exec('ref','jpk','VAT_PS')");
_obj.add('VAT_PS.get();$VAT_PS.ref()+$VAT_PS.crc()','VAT_PS.get();'+$"exec('ref','jpk','VAT_PS')");
_obj.add('{? JpkDane || $DOK.ref()+$DOK.crc() || \'\' ?}','{? JpkDane || '+$"exec('ref','jpk','DOK')"+' || \'\' ?}');

ISTDEF.cntx_psh();
ISTDEF.index('DATA'); ISTDEF.prefix('FKS','J',);
{? ISTDEF.first()
|| {!
   |? _obj.ONE:=0;
      {? _type*(','+ISTDEF.RODZAJ+',')>0
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'R',);
         {? ISTDEFS.first()
         || {!
            |? _to:=_obj.get(ISTDEFS.REGULY);
               {? _to<>'' & ISTDEFS.REGULY<>_to
               || ISTDEFS.REGULY:=_to;
                  _obj.ONE:=1
               ?};
               {? ISTDEFS.TYPFLD<>'STRING[58]'
               || ISTDEFS.TYPFLD:='STRING[58]';
                  _obj.ONE:=1
               ?};
               {? _obj.ONE
               || ISTDEFS.put()
               ?};
               ISTDEFS.next()
            !}
         ?};
         ISTDEFS.cntx_pop()
      ?};
      {? _obj.ONE
      || __UPG.msg('Zmodyfikowano schemat: %1'[ISTDEF.VER]);
         _obj.ALL:=1
      ?};
      ISTDEF.next()
   !}
?};
ISTDEF.cntx_pop();
{? _obj.ALL=0
|| __UPG.msg('Schematy JPK nie wymagały zmian.')
?};
1


\upd_r_jpk_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Ustawia formułe dla elementu R schematów JPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Ustawia formułę dla elementu R schematów JPK'


\DOKUMZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Uzupełnia pola tabeli DOKUMZ
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_formula:="
   __lrec+=1;
   _put:=0;
   {? DOKUMZ.DOK().BL='T' & DOKUMZ.BL_TYPE='' & -DOKUMZ.bl_info('DOKUM','EXTENSION')='xml'
   || DOKUMZ.BL_TYPE:='INV';
      _put:=1
   ?};
   {? _put
   || {? DOKUMZ.put(1)
      || __lmod+=1;
         1
      ?}
   || 1
   ?}
";
DOKUM.cntx_psh();
DOKUM.use('dokum');
DOKUM.prefix();
DOKUMZ.cntx_psh();
DOKUMZ.prefix();
_result:=DOKUMZ.for_each(_formula,1);
DOKUMZ.cntx_pop();
DOKUM.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano załączniki.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji załączników.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\B_USRDOM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Wypełnia pola w tabeli B_USRDOM
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
B_USRDOM.cntx_psh();
B_USRDOM.prefix();
_res:=exec('napraw_canal','#b_usrdom',1);
B_USRDOM.cntx_pop();
__UPG.msg('Zaktualizowano dziedziny użytkowników.');
_res


\B_USRDOM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Wypełnia pola w tabeli B_USRDOM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dziedzin dla użytkowników'


\DOKUMZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Uzupełnia pola w tabeli DOKUMZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja załączników.'


\R_PRACDN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja znacznika rodzaju pracy poza siedzibą firmy dla przepracowanych dni.
::----------------------------------------------------------------------------------------------------------------------
_cnt:=0;
_err:=0;
_size:=0;

R_PRACDN.trig_off('*','*');
R_PRACDN.cntx_psh();
R_PRACDN.clear();
_masks:=R_PRACDN.names();
{? _masks.first()
|| {!
   |? R_PRACDN.use(_masks.NAME);
      R_PRACDN.prefix();
      _size+=R_PRACDN.size();
      {? R_PRACDN.first()
      || {!
         |? {? R_PRACDN.PPSF=''
            || R_PRACDN.PPSF:='N';
               {? R_PRACDN.put()
               || _cnt+=1
               || _err+=1
               ?}
            ?};
            R_PRACDN.next()
         !}
      ?};
      _masks.next()
   !}
?};
R_PRACDN.cntx_pop();
R_PRACDN.trig_on('*','*');
obj_del(_masks);

__UPG.msg('Liczba wszystkich wierszy: %1.'[$_size]);
__UPG.msg('Liczba zaktualizowanych wierszy: %1.'[$_cnt]);
__UPG.msg('Błędy aktualizacji wierszy: %1.'[$_err]);

{? _err>0 || -1 || 1 ?}


\R_PRACDN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja znacznika rodzaju pracy poza siedzibą firmy dla przepracowanych dni - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika rodzaju pracy poza siedzibą firmy dla przepracowanych dni.'


\sch_color_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Aktualizacja domyślnych schematów kolorów systemowych.
::----------------------------------------------------------------------------------------------------------------------
UST_S.cntx_psh(); UST_S.prefix();
UST_S.for_each("
   {? UST_S.NAZWA='~Krytyczny' & UST_S.FONT_KOL='230:47:90'
   || UST_S.FONT_KOL:='188:26:34'; UST_S.put()
   |? UST_S.NAZWA='~Nieprawidłowy' & UST_S.FONT_KOL='250:148:17'
   || UST_S.FONT_KOL:='153:147:0'; UST_S.put()
   |? UST_S.NAZWA='~Ostrzeżenie' & UST_S.FONT_KOL='21:95:177'
   || UST_S.FONT_KOL:='0:69:173'; UST_S.put()
   |? UST_S.NAZWA='~Standardowy' & UST_S.FONT_KOL='21:176:32'
   || UST_S.FONT_KOL:='64:143:61'; UST_S.put()
   |? UST_S.NAZWA='~Wyróżnienie 1' & UST_S.FONT_KOL='35:3:140'
   || UST_S.FONT_KOL:='79:0:178'; UST_S.put()
   |? UST_S.NAZWA='~Wyróżnienie 2' & UST_S.FONT_KOL='0:162:255'
   || UST_S.FONT_KOL:='0:116:173'; UST_S.put()
   |? UST_S.NAZWA='~Wyszarzenie' & UST_S.FONT_KOL='147:145:144'
   || UST_S.FONT_KOL:='131:143:160'; UST_S.put()
   ?}
");
UST_S.cntx_pop();
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zaktualizowano schematy domyślnych kolorów systemowych.');
1


\sch_color_update_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Aktualizacja kolorów systemowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja domyślnych schematów kolorów systemowych.'


\vat_ps_sha
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Uzupełnia pole VAT_PS.SHA
::----------------------------------------------------------------------------------------------------------------------
_ile:=0;
_maski:=VAT_PS.names();
{? _maski.first()
|| VAT_PS.cntx_psh();
   PVAT.cntx_psh();
   VPOZ.cntx_psh();
   {!
   |? echo('Uzupełnienie VAT_PS.SHA z maski %1'@[_maski.NAME]);
      VAT_PS.use(_maski.NAME);
      VAT_PS.index('VAT_PS');
      VAT_PS.prefix();
      {? VAT_PS.first()
      || {!
         |? _chg:=0;
            {? VAT_PS.SHA=''
            || {? 4+VAT_PS.PVAT_REF='pvat'
               || PVAT.use(VAT_PS.PVAT_REF-8);
                  PVAT.prefix();
                  {? PVAT.seek(BB.sqlint(VAT_PS.PVAT_REF),)
                  || VAT_PS.SHA:=exec('sha1','#to_sha1','PVAT');
                     VAT_PS.put();
                     _chg:=1
                  ?}
               |? VAT_PS.PVAT_REF<>''
               || VPOZ.use(VAT_PS.PVAT_REF-8);
                  VPOZ.prefix();
                  {? VPOZ.seek(BB.sqlint(VAT_PS.PVAT_REF),)
                  || VAT_PS.SHA:=exec('sha1','#to_sha1','VPOZ');
                     VAT_PS.put();
                     _chg:=1
                  ?}
               ?}
            ?};
            _ile+=_chg;
            VAT_PS.next()
         !}
      ?};
      _maski.next()
   !};
   VPOZ.cntx_pop();
   PVAT.cntx_pop();
   VAT_PS.cntx_pop();
   echo()
?};
{? _ile
|| __UPG.msg('Liczba uzupełnionych rekordów VAT_PS: %1.'[$_ile])
|| __UPG.msg('Uzupełnienie pola VAT_PS.SHA nie było wymagane.')
?};
1


\vat_ps_sha_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Uzupełnia pole VAT_PS.SHA - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola VAT_PS.SHA.'


\KAL_BUFF_CZYTNIK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.14]
:: OPIS: ER/WRT/XP/21.14/2109/0015: Brak możliwości edycji dnia w harmonogramie
::----------------------------------------------------------------------------------------------------------------------
R_OPCZYT.cntx_psh();
_ndx:=R_OPCZYT.ndx_tmp('',1,'O',,0, 'O',,0);
R_OPCZYT.index(_ndx);

_result:=0;
KAL_BUFF.cntx_psh();
KAL_BUFF.index('PROKRDT');
KAL_BUFF.prefix('Z');
_lrec:=0;
{? KAL_BUFF.first()
|| _lrec:=KAL_BUFF.size();
   PROGRESS.set(_lrec,'Przetwarzanie rekordów tabeli KAL_BUFF...');
   {!
   |? {? +form(KAL_BUFF.OPIS)
      || R_OPCZYT.prefix(KAL_BUFF.OPIS,);
         {? R_OPCZYT.first()
         || KAL_BUFF.OPIS:=$R_OPCZYT.ref();
            _result+=KAL_BUFF.put()
         ?}
      ?};
      PROGRESS.next();
      KAL_BUFF.next()
   !};
   PROGRESS.close()
?};
KAL_BUFF.cntx_pop();
R_OPCZYT.cntx_pop();
R_OPCZYT.ndx_drop(_ndx);

{? _lrec>0
|| __UPG.msg('Zaktualizowano pole OPIS dla tabeli KAL_BUFF.');
   __UPG.msg('Przetworzono: %1 rekordów, z czego zmodyfikowano: %2.'[$_lrec,$_result])
|| __UPG.msg('Nie znaleziono rekordów tabeli KAL_BUFF wymagających aktualizacji pola OPIS.')
?};
1


\KAL_BUFF_CZYTNIK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.14]
:: OPIS: ER/WRT/XP/21.14/2109/0015: Brak możliwości edycji dnia w harmonogramie
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola KAL_BUFF.OPIS dla zatwierdzonych wykonań.'


\upgrade_act_obe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Aktualizacja czynności OBE
::----------------------------------------------------------------------------------------------------------------------
_dom:='OBE';
_domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom);
{? _domain<>null
|| exec('import_needed_dom','#b_design',_domain,0);
   __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom])
?};

_str:=
   'STD_PC_E_PERSONATTACHMENT_GETSIG_SLO_PRC;STD_PC_E_PERSONATTACHMENT_OVR_SLO_PRC;'
   'STD_PC_E_PERSONATTACHMENT_SIG_SLO_PRC;STD_PC_E_PERSONATTACHMENT_SIGHND_SLO_PRC;'
   'STD_PC_E_PERSONATTACHMENT_SIGSMP_SLO_PRC';
_arr:=spli_str(_str,';');
PORTALU.cntx_psh();
PORTALU.index('OPR');
{! _ii:=1..obj_len(_arr)
|! PORTALU.prefix('U',_arr[_ii]);
   {? PORTALU.first()
   || {! |? PORTALU.del() !};
      __UPG.msg('Usunięto uprawnienia do %1.'[_arr[_ii]])
   ?}
!};
PORTALU.cntx_pop();
1


\upgrade_act_obe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Aktualizacja czynności OBE
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czynności dziedziny OBE'


\atrybut_cwiczenia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie rubryki 7068 - Ćwiczenia wojskowe do atrybutów 19421, 19422 i 19431.
::       ER/WRT/XP/21.14/2110/0031 7068 ćwiczenia wojskowe w ZUS RSA powinne być wykazane w miesiącu wystąpienia
::       a nie rozliczenia na lista płac
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');
__RUB.fill();
exec('add_uses','rubatr',7068,1,19,194,1942,19421,19422,1943,19431);
__UPG.msg('Dodano rubrykę 7068 do atrybutów 19421, 19422 i 19431');

1


\atrybut_cwiczenia_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie rubryki 7068 - Ćwiczenia wojskowe do atrybutów 19421, 19422 i 19431.
::       ER/WRT/XP/21.14/2110/0031 7068 ćwiczenia wojskowe w ZUS RSA powinne być wykazane w miesiącu wystąpienia
::       a nie rozliczenia na lista płac - formuła na opis zadania
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
'Funkcja naprawcza dodająca rubrykę 7068 - Ćwiczenia wojskowe do atrybutów 19421, 19422 i 19431.'


\for_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2110/0050: Błąd uzupełnienia pola FOR_AKC.AKCJA
::----------------------------------------------------------------------------------------------------------------------
FOR_AKC.cntx_psh();
FOR_AKC.prefix();
FOR_AKC.for_each("{? FOR_AKC.AKCJA='' || FOR_AKC.AKCJA:='akceptacja'; FOR_AKC.put() ?}");
FOR_AKC.cntx_pop();
__UPG.msg('Uzupełniono znacznik miejsca wywoływania dla formuł kontrolnych.');
1


\for_akc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2110/0050: Błąd uzupełnienia pola FOR_AKC.AKCJA
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie znacznika miejsca wywoływania dla formuł kontrolnych.'


\kart_url_21110024
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.14]
:: OPIS: Poprawka błędu ER/WRT/XP/21.14/2111/0024 - brak zgodności w zaokrągleniach wykorzystania urlopów między
::       kartą urlopową a Portalem HR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_br:=date()~1;
KART_URL.cntx_psh();
KART_URL.index('ROK_PRAC');
KART_URL.trig_off('*','*');
{! _rok:=_br .. _br+1
|! KART_URL.prefix(_rok);
   __UPG.msg('Rok %1'[$_rok]);
   __UPG.msg(' - Liczba zapisów do aktualizacji: %1' [$KART_URL.size()]);
   _put0:=_put1:=0;
   {? KART_URL.first()
   || {!
      |? {? KART_URL.put(,1)
         || _put1+=1
         || _put0+=1
         ?};
         KART_URL.next()
      !}
   ?};
   __UPG.msg(' - Liczba zapisów zaktualizowanych: %1' [$_put1]);
   __UPG.msg(' - Liczba zapisów niezaktualizowanych: %1' [$_put0])
!};
KART_URL.trig_on('*','*');
KART_URL.cntx_pop();
1


\kart_url_21110024_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.14]
:: OPIS: Poprawka błędu ER/WRT/XP/21.14/2111/0024 - brak zgodności w zaokrągleniach wykorzystania urlopów między
::       kartą urlopową a Portalem HR - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Brak zgodności w zaokrągleniach wykorzystania urlopów między kartą urlopową a Portalem HR.\n'
'Poprawka błędu ER/WRT/XP/21.14/2111/0024'


\etypy_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: ER/WRT/XP/21.37/2111/0024: Błędne powiązania typów obiegu z zaliczkami.
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
ETYPY.for_each("{? ETYPY.CZY_ZALP<>'T' || ETYPY.CZY_ZALP:='N'; ETYPY.ETYPZAL:=null(); ETYPY.put() ?}");
ETYPY.cntx_pop();
__UPG.msg('Usunięto błędne powiązania typów obiegów z zaliczkami.');
1


\etypy_zal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: ER/WRT/XP/21.37/2111/0024: Błędne powiązania typów obiegu z zaliczkami. - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie błędnych powiązań typów obiegów z zaliczkami.'


\kart_url_21120069
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: ER/WRT/XP/21.37/2112/0069: Bład zasilenia metody.
::----------------------------------------------------------------------------------------------------------------------
_error:=0;
{? exec('lic','#b_domain','POR')
|| _pack_size:=500;
   _pd:='PORTAL';
   _mwac:='portal.mwac';
   _method:='chr_PersonLeaveLimitModify';
   _tabAcr:='KART_URL';
   _size:=0;
   _counter:=0;

   SYNC_PD.cntx_psh();
   SYNC_PD.index('SYM');
   SYNC_PD.prefix(_pd,);
   {? SYNC_PD.first()
   ||
      SYNC_DEF.cntx_psh();
      SYNC_DEF.index('PDNR');
      SYNC_DEF.prefix(SYNC_PD.ref,_tabAcr,);
      _war_form:={? SYNC_DEF.first() || SYNC_DEF.WAR_FORM || '' ?};
      SYNC_DEF.cntx_pop();
      {? _war_form='' || _war_form:='1' ?};

      SYNC_IDP.cntx_psh();
      SYNC_IDP.index('ACR');
      SYNC_IDP.prefix(SYNC_PD.ref(),_tabAcr,);
      {? SYNC_IDP.first()
      || SYNC_IDP.IDPUT:=time_ident(date(),time());
         SYNC_IDP.put()
      || SYNC_IDP.SYNC_PD:=SYNC_PD.ref();
         SYNC_IDP.ACR:=_tabAcr;
         SYNC_IDP.IDPUT:=time_ident(date(),time());
         SYNC_IDP.add()
      ?};
      SYNC_IDP.cntx_pop();

      _tab:=($_tabAcr)();
      _tab.trig_off('*','*');
      _args:=obj_new('size','pack_size','tab','war_form','counter','error');
      _args.size:=_size;
      _args.pack_size:=_pack_size;
      _args.tab:=_tab;
      _args.war_form:=$_war_form;
      _args.counter:=_counter;
      _args.error:=_error;
      params_set('args',_args);
      _formula:="
         _args:=params_get().args;
         {? _args.war_form('put',__Firma,REF.FIRMA)
         || _args.size+=1;
            {? _args.size>_args.pack_size
            || _args.size:=0;
               delay(1)
            ?};
            {? _args.tab.put(,1) || _args.counter+=1 || _args.error+=1 ?}
         ?};
         ~~
      ";
      params_exec('for_each','#table',_tab,_formula);
      _size:=_args.size;
      _counter:=_args.counter;
      _error:=_args.error;
      obj_del(_args);
      _tab.trig_on('*','*');
      obj_del(_tab)
   ?};
   SYNC_PD.cntx_pop();
   __UPG.msg('Liczba wszystkich wierszy: %1.'[$_size]);
   __UPG.msg('Liczba zaktualizowanych wierszy: %1.'[$_counter]);
   __UPG.msg('Błędy aktualizacji wierszy: %1.'[$_error])
?};

{? _error || -1 || 1 ?}


\kart_url_21120069_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: ER/WRT/XP/21.37/2112/0069: Bład zasilenia metody. - opis
::----------------------------------------------------------------------------------------------------------------------
'Bład zasilenia metody.\n'
'Poprawka błędu ER/WRT/XP/21.37/2112/0069'


\ppk_zc_wn_zus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Lista rachunków do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym z okresów uczestnictwa
::       współpracowników będących uczestnikami PPK.
::   WE: [_a] [NUMBER] - czy test? (0/1) domyślnie: 0 (wynikiem będzie 1- zadania nie trzeba wykonywać, 0 - trzeba)
::       [_b] [INTEGER] - 0/1 Czy wyświetlać wynikową tabelę? domyślnie: 1
::       [_c] [INTEGER] - 0/1 Czy po wyświetleniu generować dane do pliku? domyślnie: 0
::       [_d] [INTEGER] - 0/1 Czy pomijać uczestników ze złożoną DRZW (ale bez WODW/AWW po niej)? domyślnie: 1
::       [_e] [INTEGER] - 0/1 Czy pomijać uczestników ze złożonym WB25B (ale bez WB25W/DOWU25 po niej)? domyślnie: 1
::       [_f] [INTEGER] - 0/1 Czy pomijać uczestników ze złożonym WS60? domyślnie: 1
::       [_g] [INTEGER] - 0/1 Czy pomijać rachunki z policzoną podstawą PPK? domyślnie: 1
::       [_h] [INTEGER] - 0/1 Czy formuła ma zwracać wygenerowaną tabelę? domyślnie: 0
::       Uwaga: Parametry _c.._g mają znaczenie decydujące jedynie gdy parametr _b=0, w innym przypadku ustawiają
::              jedynie wartości domyślne wyświetlane użytkownikowi do wyboru.
::   WY: _WYN [TABLE] - tabela z listą osób, lub 0 jeśli brak takich zapisów
:: ~OST: INFILECHOOSER,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
_test:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_wysw:={? var_pres('_b')=type_of(0) || _b || 1 ?};
_retTab:={? var_pres('_h')=type_of(0) || _h || 0 ?};
::Wyświetl uczestników PPK, dla których wystawiono rachunki na których policzono dobrowolne ubezpieczenie emer. - rentowe
:: Pomijaj rachunki uczestniów PPK z:
_PAR:=tab_tmp(1,
   'DRZW','INTEGER','zarejestrowaną Deklaracją rezygnacji z wpłat',
   'WB25B','INTEGER','zarejestrowaną Blokadą wpłat na podstawie art. 25',
   'WS60','INTEGER','zarejestrowaną Wypłatą środków po 60. roku życia',
   'PPK','INTEGER','policzoną podstawową składką PPK pracownika i pracodawcy',
   'PLIK','INTEGER','Generowanie pliku',
   'FOLDER','STRING[255]','Folder lokalny'
   );
_PAR.DRZW:={? var_pres('_d')=type_of(0) || _d || 1 ?};
_PAR.WB25B:={? var_pres('_e')=type_of(0) || _e || 1 ?};
_PAR.WS60:={? var_pres('_f')=type_of(0) || _f || 1 ?};
_PAR.PPK:={? var_pres('_g')=type_of(0) || _g || 1 ?};
_PAR.PLIK:={? var_pres('_c')=type_of(0) || _c || 0 ?};
_PAR.FOLDER:=tmp_dir();

{? _wysw
|| _we:=_PAR.mk_edit(
      'Rachunki do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym uczestników PPK'@,0,
      '#ppk_rh_wn_zus');
   _PAR.win_esep(_we,'%1:'['Pomijaj rachunki uczestniów PPK z'@]);
   _PAR.win_efld(_we,,'DRZW',,,,,,,,'Czy pomijać uczestników ze złożoną DRZW (ale bez WODW/AWW po niej)?'@,
      'check-box','check_label="tak, pomijaj"',"1","0");
   _PAR.win_efld(_we,,'WB25B',,,,,,,,'Czy pomijać uczestników ze złożonym WB25B (ale bez WB25W/DOWU25 po niej)?'@,
      'check-box','check_label="tak, pomijaj"',"1","0");
   _PAR.win_efld(_we,,'WS60',,,,,,,,'Czy pomijać uczestników ze złożonym WS60?'@,
      'check-box','check_label="tak, pomijaj"',"1","0");
   _PAR.win_efld(_we,,'PPK',,,,,,,,'Czy pomijać rachunki z policzoną składką podstawową pracownika i pracodawcy?'@,
      'check-box','check_label="tak, pomijaj"',"1","0");
   _PAR.win_esep(_we,'Parametry raportu'@);
   _PAR.win_efld(_we,,'PLIK',,,,,,,,'Czy zapisać dane do pliku po wyświetleniu?'@,
      'check-box','check_label="tak, zapisz"',"1","0");
   _PAR.win_efld(_we,,'FOLDER',,,48,,,,,'Lokalizacja do zapisu pliku'@);
   _PAR.fld_ebtn(_we,'icon=xwin16.png:90',
      "_folder:=exec('filechooser','#file','Wybierz miejsce zapisu pliku'@,,,,,'DIRECTORIES_ONLY');
       {? +_folder
       || cur_tab().PLIK:=1;
          cur_tab().FOLDER:=_folder;
          win_disp()
       ?};
       ~~
      ");
   exec('ok_esc','#window',_PAR,_we);
   _PAR.win_edit(_we);
   {? ~_PAR.edit()
   || return(0)
   ?}
?};

_query:='
   select OSOBA.NAZWISKO as "Nazwisko", OSOBA.PIERWSZE as "Imię", OSOBA.PESEL, OSOBA.REFERENCE as "OSOBA",
          RH.DWY as "Data wypłaty", RH.REFERENCE as "RH", RH.DRA as "Data rachunku",
          cast(0 as REAL_TYPE) as "Kwota rachunku",
          ZC.NU as "Numer umowy", RU.O as "Typ umowy", ZC.DU as "Umowa od", ZC.DW as "Umowa do",
          PPK_UCZ.OD as "PPK od", PPK_UCZ.DO as "PPK do"
   from RH
   join ZC using(RH.ZLE, ZC.REFERENCE)
   join OSOBA using(ZC.OSOBA, OSOBA.REFERENCE)
   join RU using(ZC.RU, RU.REFERENCE)
   join PPK_UCZ using(ZC.OSOBA, PPK_UCZ.OSOBA)
   where ZC.WN_ZUS=\'T\' and (ZC.DW>=PPK_UCZ.OD or ZC.DU<=PPK_UCZ.DO)
   order by 1
   ';
_WNZUS:=sql(_query);

:: Ograniczenie wyników wg. parametrówm wpisanie kwoty rachunku:
{? _WNZUS.first()
|| {!
   |? _del:=0;
      {? (_PAR.DRZW+_PAR.WB25B+_PAR.WS60)>0
      || _osoba:=null();
         OSOBA.cntx_psh();
         {? OSOBA.seek(_WNZUS.OSOBA,,1)
         || _osoba:=OSOBA.ref()
         ?};
         OSOBA.cntx_pop();
         {? _osoba<>null()
         || exec('czytaj','#stalesys',_WNZUS.PPK,KST_PPK,'PPK_UMO');
            {? KST_PPK.PPK_UMO=null()
            || exec('init','ppk_umo',_WNZUS.PPK)
            ?};
            _d0:=date(0,0,0);
            {? _PAR.WS60 & exec('spr_wnu','ppk_wnu',_osoba,,_WNZUS.DATA,'WS60',0)
            || _del:=_WNZUS.del(,1)
            ?};
            {? ~_del & _PAR.DRZW
            || _dtDRZW:=exec('spr_wnu','ppk_wnu',_osoba,,_WNZUS.DATA,'DRZW',1);
::             Jeśli była dekl. rezygnacji i nie było po niej odwołania WODW lub AWW:
               {? _dtDRZW<>_d0 &
               ~exec('spr_wnu','ppk_wnu',_osoba,_dtDRZW,_WNZUS.DATA,'WODW',0) &
               ~exec('spr_wnu','ppk_wnu',_osoba,_dtDRZW,_WNZUS.DATA,'AWW',0)
               || _del:=_WNZUS.del(,1)
               ?}
            ?};
            {? ~_del & _PAR.WB25B
            || _dtWB25B:=exec('spr_wnu','ppk_wnu',_osoba,,_WNZUS.DATA,'WB25B',1);
::             Jeśli była blokada z art. 25 i nie było po niej wznowienia, ani Deklaracji opłacania wpłat uczestnika art. 25:
               {? _dtWB25B<>_d0 &
                  ~exec('spr_wnu','ppk_wnu',_osoba,_dtWB25B,_WNZUS.DATA,'WB25W',0) &
                  ~exec('spr_wnu','ppk_wnu',_osoba,_dtWB25B,_WNZUS.DATA,'DOWU25',0)
               || _del:=_WNZUS.del(,1)
               ?}
            ?}
         ?}
      ?};
::    Ew. usunięcie z policzonym PPK i przypisanie kwoty rachunku:
      {? ~_del
      || RH.cntx_psh();
         {? RH.seek(_WNZUS.RH,,1)
         || {? _PAR.PPK & (exec('licz_rhs','lista_licz',7088)+exec('licz_rhs','lista_licz',7090))>0
            || _del:=_WNZUS.del(,1)
            || _WNZUS.KWOTA:=exec('licz_rhs','lista_licz',200);
               _WNZUS.put()
            ?}
         ?};
         RH.cntx_pop()
      ?};

      _del=2 | _WNZUS.next()
   !}
?};

:: Sprawdzenie czy są dane (również na potrzeby testu):
{? ~_WNZUS.first()
|| _result:=1;
   __UPG.msg('Brak osób spełniających warunki.'@);
   {? ~_test & _wysw &
      FUN.ask('Brak osób spełniających warunki.\nCzy oznaczyć zadanie: \'%1\' jako wykonane?'@['ppk_zc_wn_zus'])
   || _result:=1
   || _result:=-1
   ?};
   return(_result)
|? _test
|| return(-1)
?};

:: Wyświetlanie tabeli:
_tytul:='Rachunki do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym uczestników PPK'@;
{? _wysw
|| _ws:=_WNZUS.mk_sel(_tytul,,,'#ppk_zc_wn_zus',,,,,'U');
   _WNZUS.win_fld(_ws,,'NAZWISKO',,,25,,,'Nazwisko');
   _WNZUS.win_fld(_ws,,'IMIĘ',,,20,,,'Imię');
   _WNZUS.win_fld(_ws,,'PESEL',,,MS.fld_len(OSOBA,'PESEL'),,,'PESEL');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(7),,,10,,,'Data rachunku');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(5),,,10,,,'Data wypłaty');
   _WNZUS.win_fld(_ws,,'KWOTA',,,10,2,,'Kwota rachunku');
   _WNZUS.win_fld(_ws,,'NUMER',,,MS.fld_len(ZC,'NU'),,,'Nr umowy');
   _WNZUS.win_fld(_ws,,'TYP',,,15,,,'Typ umowy');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(11),,,10,,,'Umowa od');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(12),,,10,,,'Umowa do');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(13),,,10,,,'PPK od');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(14),,,10,,,'PPK do');
   _WNZUS.win_act(_ws,0,'Kolejność');
   _WNZUS.win_sel(_ws);
   _WNZUS.select()
?};
:: Eksport do pliku:
{? _PAR.PLIK
|| _plik:=_PAR.FOLDER+'\\PPK_RH_WN_ZUS.csv';
   _WNZUS.export(
   '@'+_plik,,,'UTF-8,header,nopth',,
   'NAZWISKO',,1,,
   'IMIĘ',,2,,
   'PESEL',,3,,
   _WNZUS.fld_acr(7),,4,1,
   _WNZUS.fld_acr(5),,5,1,
   'KWOTA',,6,2,
   'NUMER',,7,,
   'TYP',,8,,
   _WNZUS.fld_acr(11),,9,1,
   _WNZUS.fld_acr(12),,10,1,
   _WNZUS.fld_acr(13),,11,1,
   _WNZUS.fld_acr(14),,12,1
   );
   {? _wysw
   || FUN.info('Wyeksportowano do: %1'@[_plik])
   ?}
?};

{? _retTab
|| return(_WNZUS)
?};

{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['ppk_zc_wn_zus'])
|| __UPG.msg('Wyświetlono: %1'@[_tytul]);
   _result:=1
|| _result:=-1
?};

_result


\ppk_zc_wn_zus_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Lista rachunków do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym z okresów uczestnictwa
::       współpracowników będących uczestnikami PPK.- opis
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Lista rachunków do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym z okresów uczestnictwa '
'współpracowników będących uczestnikami PPK.'


\FAKS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizuje pola FAKS.O_*
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKS.names();
FAKS.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("
         __lrec+=1;
         _put:=0;
         {? FAKS.KH_ODB
               &
            FAKS.O_NAZ=''
         ||
            exec('duplikuj_dane_khodb','faktury_wspolne');
            {? FAKS.put() || _put:=1 ?}
         ?};
         {? _put & FAKS.put() || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();

__UPG.msg('Zaktualizowano pola w nagłówku dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\FAKS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.37]
:: OPIS: Aktualizuje pola FAKS.O_*
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola w nagłówku dokumentów sprzedaży i zakupu.'


\rubryki_placowe_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja rubryk płacowych.
::       Poprawka błędu ER/WRT/XP/21.37/2201/0040 - Zmiana klasy rubryki 784 Koszty uzyskania z 'S' na '#'
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';

R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

{? +exec('import','rubryki','g','784',1,0)
|| _txt:='Aktualizacja rubryki 784 zakończyła się niepowodzeniem';
   _result:=-1
|| _txt:='Aktualizacja rubryki 784 zakończona powodzeniem'
?};

__UPG.msg(_txt);
R.trig_on('*','*');

_result


\rubryki_placowe_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja rubryk płacowych - opis.
::       Poprawka błędu ER/WRT/XP/21.37/2201/0040 - Zmiana klasy rubryki 784 Koszty uzyskania z 'S' na '#'
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rubryki 784.\n'
'Poprawka błędu ER/WRT/XP/21.37/2201/0040'


\portal_ZS_DEF_clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Oznaczenie do usunięcia niepotrzebnie wysłane rekordy zależności służbowych na Portal HR.
::       ER/WRT/XP/21.14/2111/0026 - Podwojona lista współpracowników w Portalu HR
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
{? exec('lic','#b_domain','POR')
|| _poprawionych:=_niepopr:=0;
   ZS_DEF.trig_off('*','*');
   ZS_DEF.cntx_psh();
   ZS_DEF.prefix();
   {? ZS_DEF.first()
   || {!
      |? _uidref:=ZS_DEF.uidref();
::       Jeśli nie należy zachować rekordu (zmienił typ na niedomyślny lub nie ma przełożonego i ma podwładnych):
         {? exec('preserve_ZS_DEF','portal_method_chr',_uidref)<=0
::          Czy jest odpisany w tabeli zsynchronizowanych rekordów:
         || _TAB:=exec('get_id','#sync_id','PORTAL_ZS_DEF_ID',_uidref);
            _del:=var_pres('_TAB')>100 & _TAB.first();
            obj_del(_TAB);
            {? _del
            || {? ZS_DEF.put(,1)
               || _poprawionych+=1
               || _niepopr+=1
               ?}
            ?}
         ?};

         ZS_DEF.next()
      !}
   ?};
   ZS_DEF.cntx_pop();
   ZS_DEF.trig_on('*','*');
   __UPG.msg('Liczba wpisów poprawionych:%1 , niepoprawionych:%2 .'[$_poprawionych,$_niepopr])
|| __UPG.msg('Brak licencji na Portal HR.')
?};

1


\portal_ZS_DEF_clean_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Oznaczenie do usunięcia niepotrzebnie wysłane rekordy zależności służbowych na Portal HR.
::       ER/WRT/XP/21.14/2111/0026 - Podwojona lista współpracowników w Portalu HR
::       - formuła na opis zadania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Funkcja naprawcza oznaczająca do usunięcia niepotrzebnie wysłane rekordy zależności służbowych na Portal HR.'


\pomoc_AK_10
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Pomoc przy poprawie aktualizacji 10
::       Funkcja udostępnia listę pracowników z nieobecnościami chorobowymi wprowadzonymi po 2022.1.1
::       Akcja "IDŹ DO" przekierowuje do wskazanej nieobecności pracowniczej. Z tego poziomu można poprawić
::       schemat rozliczenia nieobecności. Akcja jest zapamiętywana w pliku "pkd_akt_10.csv".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('init','pkd');

{? ~__F_ZATR.upr('P')
|| FUN.info('Brak wymaganego dostępu do formy współpracy "%1".'@ ['P']);
   return()
?};
_txt:=exec('uprawnieniawrap','pkd','Rejestracja nieobecności','PKD_EZK_OPNN','PKD_EZK_ORNN');
{? +|_txt
|| FUN.emsg('Brak wymaganych uprawnień do importu informacji o:%1'@[_txt]);
   return()
?};
P_FILTER.UD_SCH:=exec('domyslny','schemat','PODZORG');

_JOS:=exec('jos_create','pkd_grp');
_TAB:=exec('pomoc_AK_10_tab','upgrade_2137');
_cfg:=obj_new('nav','lastref','ile','akt','ogolem');
_cfg.nav:=obj_new('main','side','nwin');
_cfg.ile:=0;
_cfg.akt:=0;
_cfg.ogolem:=0;
: main - okno ze współpracownikami
: side - okno ze strukturą hierarchiczną
: nwin - okno z nieobecnościami
_cfg.nav.side:=_JOS.WS;

_ud_skl:=__PARSES.getVal('JednostkaOrganizacyjna').SYMBOL;
params_set(
   'cfg',_cfg,
   'JOS',_JOS,
   'ud_skl',_ud_skl,
   'TAB',_TAB
);
exec('pkd_conf_cntx','pkd','psh');
Cntx.psh(P,N,O,NW,TZ);
Cntx.clr(P,N,O,NW,TZ);
O.index('LISTYPZN');
O.prefix(exec('ref_firma','#firma'));
N.f_clear();

_wnd:=exec('pomoc_AK_10_wnd','upgrade_2137',_JOS,_cfg,_TAB);

_JOS.TAB.win_sel(_wnd);
_JOS.TAB.select();
_TAB.clear();
{? _TAB.first()
|| {!
   |? _cfg.ogolem+=(_TAB.A<>'A');
      _TAB.next()
   !}
?};

Cntx.pop(P,N,O,NW,TZ);
exec('pkd_conf_cntx','pkd','pop');
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['pomoc_AK_10'])
|| {? _cfg.ogolem
   || __UPG.msg('Zaktualizowano %1 schematów rozliczeń'@[$_cfg.ogolem])
   || __UPG.msg('Wyświetlono: %1'@['Nieobecności do sprawdzenia'@])
   ?};
   _result:=1
|| _result:=-1
?};
_result


\pomoc_AK_10_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Pomoc przy poprawie aktualizacji 10 - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Nieobecności typu chorobowe do sprawdzenia po 2022.1.1'


\pkd_kod_4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Przywrócenie możliwości rejestracji kodu 4 dla nieobecności
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

exec('__RUB','object');
__RUB.fill();

Cntx.psh(RA_DEF,RA_USE);
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S',);
RA_USE.index('RA_USE');
{? RA_DEF.find_key(1291)
|| RA_USE.prefix(RA_DEF.ref(),__RUB.ref(4),date(2022,1,1));
  {? RA_USE.first() || RA_USE.del() ?}
?};
Cntx.pop(RA_DEF,RA_USE);
__UPG.msg('Zakończono aktualizację wykorzystania atrybutu składnika płac.');

_result


\pkd_kod_4_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Przywrócenie możliwości rejestracji kodu 4 dla nieobecności - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Przywrócenie możliwości rejestracji kodu 4 dla nieobecności'


\pomoc_AK_10_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Definicja okna
::   WE:
::   WY:
:: ~OST: INSYSEXEC
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
_JOS:=_a;
_cfg:=_b;
_TAB:=_c;
: Najpierw zwykłe okno do prezentacji współpracowników.
_acr:='AK10WND';
_id:='pkd_akt_10';
_user:=hash(exec('username','#users'));

: użyj istniejące okienko
{? (_ws:=__WND.SEL.get(P,_acr))='';1
|| _ws:=P.mk_sel(,'P',,'#ak10wnd',,,,,'U','T');
   P.win_fld(_ws,,'T',,,9,,,,,MS.comment(P,'T'));
   P.win_fld(_ws,,'IP',,,-5,,,,,MS.comment(P,'IP'));
   P.win_fld(_ws,,'OSOBA','NAZWISKO',,20,,,,,MS.comment(OSOBA,'NAZWISKO'));
   P.win_fld(_ws,,'OSOBA','PIERWSZE',,15,,,,,MS.comment(OSOBA,'PIERWSZE'));
   P.win_fld(_ws,,'OSOBA','PESEL',,11,,,,,MS.comment(OSOBA,'PESEL'));
   P.win_fld(_ws,,'OSOBA','DOWOD',,11,,,'Dowód osobisty'@,,MS.comment(OSOBA,'DOWOD'));
   P.win_fld(_ws,,'OSOBA','PASZPORT',,11,,,'Paszport'@,,MS.comment(OSOBA,'PASZPORT'));
   P.win_fld(_ws,,'F_ZATR','KOD',,-2,,,MS.name(P,'F_ZATR'),,MS.comment(F_ZATR,'KOD'));
   P.win_fld(_ws,,'WYDZIAL','SYMBOL',,-16,,,MS.name(P,'WYDZIAL'),,'Symbol jednostki organizacyjnej'@);

   _fb:="
      params_set(_par:=params_get());
      N.index('NIEOBECX');
      N.prefix('N',P.ref());
      N.find_key(_par.TAB.OD);
      N.actions_grayed('WER','Z');
      N.actions('WER','Z');
      N.select(,1);
      1";
   P.win_act(_ws,,'Formuła','Nieobecności'@,,'Dostęp do nieobecności pracownika'@,
      _fb,,1,,,,'N');

   _fb:="
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? ~P.sel_size()
      || _par.TAB.A='A' | FUN.ask('Aktualizować ponownie?'@)
      || 1
      ?}
   ";
   _fa:="
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _TAB:=_par.TAB;
      _TAB.cntx_psh();
      _TAB.prefix($P.ref(),);
      _jest:=0;
      {? _TAB.first()
      || {!
         |? _TAB.KOM:='OK. ';
            {? _TAB.Z<>'T' & P.seek(_TAB.PREF,) & N.seek(_TAB.NREF,)
            || P.OSOBA();
               {? _TAB.TZ='T' & exec('tz_usun','nieobecnosc',1)
               || _TAB.KOM+='Usunięto rozliczenie nieobecności. '
               ?};
               exec('nw_aktualizuj','nieobecnosc',N.ref());
               _TAB.A:='P';
               {? +|_TAB.LT || _TAB.KOM+='Oblicz ponownie listę płac.' ?};
               _jest:=1
            |? _TAB.Z='T'
            || _TAB.A:='B';
               {? +|_TAB.LT || _TAB.KOM:='Lista płac jest zamknięta. Nie wykonano aktualizacji.' ?}
            || _TAB.A:='B'
            ?};
            _TAB.put();
            _TAB.next()
         !};
         {? _jest || _par.cfg.akt+=1 ?}
      ?};
      _TAB.cntx_pop()
   ";
   _fgb:="
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _cfg.ile:=P.sel_size();
      _cfg.akt:=0;
      P.cntx_psh();
      1
   ";
   _fga:="
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      P.cntx_pop();
      FUN.info('Zaktualizowano schematy rozliczeń dla %1 z %2 pracowników.'@[$_cfg.akt,$_cfg.ile])
   ";
   P.win_act(_ws,,'Formuła','Aktualizuj schematy'@,,'Aktualizuje schematy rozliczenia nieobecności dla pracownika'@,
      _fb,_fa,,1,_fgb,_fga,'A');

   _txt:='Plik z informacją o nieobecnościach już istnieje.'@;
   _fb:=$('fexists(\'%1.csv\',1) & FUN.ask(\'%2\n%3\')'[_user+_id,_txt,'Wczytać dane?'@]);
   _fa:=$("
      params_set(_par:=params_get());
      _par.TAB.cntx_psh();
      _par.TAB.clear();
      _par.TAB.erase();
      _par.TAB.import('"+_user+_id+".csv',0,1,%1+';','UTF-8',,
         'NAZWISKO',,1,,
         'PIERWSZE',,2,,
         'SYMBOL',,3,,
         'T',,4,,
         'IP',,5,,
         'OD',,6,,
         'DO',,7,,
         'RN',,8,,
         'NB',,9,,
         'LT',,10,,
         'Z',,11,,
         'A',,12,,
         'KOM',,13,,
         'PREF',,14,,
         'NREF',,15,
      );
      _par.TAB.cntx_pop()
   ");
   {? _fb() || _fa() ?};
   P.win_act(_ws,,'Formuła','Import'@,,'Import danych z pliku'@,
      _fb,_fa,,,,,'I');

   _fb:=$('~fexists(\'%1.csv\',1) | FUN.ask(\'%2\n%3\')'[_user+_id,_txt,'Nadpisać dane?'@]);
   _fa:=$("
      params_set(_par:=params_get());
      _par.TAB.cntx_psh();
      _par.TAB.clear();
      _par.TAB.export('"+_user+_id+".csv',1,%1+';','UTF-8',,
         'NAZWISKO',,1,,
         'PIERWSZE',,2,,
         'SYMBOL',,3,,
         'T',,4,,
         'IP',,5,,
         'OD',,6,,
         'DO',,7,,
         'RN',,8,,
         'NB',,9,,
         'LT',,10,,
         'Z',,11,,
         'A',,12,,
         'KOM',,13,,
         'PREF',,14,,
         'NREF',,15,
      );
      _par.TAB.cntx_pop();
      {? FUN.ask('Czy otworzyć plik danych?') || sys_exec('"+_user+_id+".csv') ?}
   ");
   P.win_act(_ws,,'Formuła','Eksport'@,,'Eksport danych do pliku'@,
      _fb,_fa,,,,,'E');

   P.win_act(_ws,,'Menu','Szukaj'@,,,,,,,,,'S');
   P.win_act(_ws,,'Formuła','Szukaj dokładnie'@,'#S','Szukanie zapisu zgodnego ze zredagowanym wzorcem'@,
      "exec('p_ustaw_ezk','szukaj')","exec('p_d','szukaj')",,,,,'S');
   P.win_act(_ws,,'Formuła','Szukaj &kontekstowo'@,'#S','Szukanie zapisu zgodnego ze zredagowanym wzorcem'@,
      "exec('p_ustaw_ezk','szukaj')","exec('p_k','szukaj')",,,,,'K');
   P.win_act(_ws,,'#F3',,,,"exec('p_ustaw_ezk','szukaj')","exec('p_f3_wnd','szukaj')");
   P.win_act(_ws,,'Kolejność');

   P.win_btn(_ws,'text='+'Nieobecności'@+',btn_label_align=center,panel=right,align=begin','menu:N');
   P.win_btn(_ws,'text='+'Aktualizuj schematy'@+',btn_label_align=center,panel=right,align=begin','menu:A');
   P.win_btn(_ws,'text='+'Import'@+',btn_label_align=center,panel=right,align=begin','menu:I');
   P.win_btn(_ws,'text='+'Eksport'@+',btn_label_align=center,panel=right,align=begin','menu:E');

   __WND.SEL.put(P,_acr,_ws)
?};

_win:=_TAB.mk_sel('Nieobecności do sprawdzenia'@,'P',0,_id,,,,,'U','T',0,0);
_TAB.win_sel(_win);
_TAB.win_fld(_win,,'RN',,,,,,,,MS.comment(R,'RN'));
_TAB.win_fld(_win,,'NB',,,,,,,,MS.comment(R,'RT'));
_TAB.win_fld(_win,,'OD',,,,,,,,MS.comment(N,'OD'));
_TAB.win_fld(_win,,'DO',,,,,,,,MS.comment(N,'DO'));
_TAB.win_fld(_win,,'LT',,,,,,'Lista'@,,MS.comment(N,'LT'));
_TAB.win_fld(_win,,'TZ',,,,,,'Obliczona na liście'@,,'Czy jest rozliczenie?'@,2,,"'T'","'N'");
_TAB.win_fld(_win,,'Z',,,,,,'Zamknięta lista płac'@,,'Zamknięta lista płac'@);
_TAB.win_fld(_win,,'A',,,,,,'Status'@,,'do (A)nalizy, (P)oprawiono, (B)ez zmian'@,2,,"'P'","'B'","'A'");
_TAB.win_fld(_win,,'KOM',,,40,,,,,'Komentarz'@);
_fb:="
   params_set(_par:=params_get());
   _cfg:=_par.cfg;
   {? ~_par.TAB.sel_size()
   || _par.TAB.A='A' | FUN.ask('Aktualizować ponownie?'@)
   || 1
   ?}
";
_fa:="
   params_set(_par:=params_get());
   _cfg:=_par.cfg;
   _TAB:=_par.TAB;
   P.clear();
   N.clear();
   _TAB.KOM:='OK. ';
   {? _TAB.Z<>'T' & P.seek(_TAB.PREF,) & N.seek(_TAB.NREF,)
   || P.OSOBA();
      {? _TAB.TZ='T' & exec('tz_usun','nieobecnosc',1)
      || _TAB.KOM+='Usunięto rozliczenie nieobecności. '
      ?};
      exec('nw_aktualizuj','nieobecnosc',N.ref());
      _TAB.A:='P';
      {? +|_TAB.LT || _TAB.KOM+='Oblicz ponownie listę płac.' ?};
      _par.cfg.akt+=1
   |? _TAB.Z='T'
   || _TAB.A:='B';
      {? +|_TAB.LT || _TAB.KOM:='Lista płac jest zamknięta. Nie wykonano aktualizacji.' ?}
   || _TAB.A:='B'
   ?};
   _TAB.put()
";
_fgb:="
   params_set(_par:=params_get());
   _cfg:=_par.cfg;
   _cfg.ile:=_par.TAB.sel_size();
   _cfg.akt:=0;
   1
";
_fga:="
   params_set(_par:=params_get());
   _cfg:=_par.cfg;
   FUN.info('Zaktualizowano schematy rozliczeń dla %1 z %2 pracowników.'@[$_cfg.akt,$_cfg.ile])
";
_TAB.win_act(_win,,'Formuła','Aktualizuj schemat'@,,'Aktualizuje schemat rozliczenia nieobecności'@,
   _fb,_fa,,1,_fgb,_fga,'A');

_fa:="
   params_set(_par:=params_get());
   _txt:=_par.TAB.A;
   _co:=FUN.choice('Zmiana statusu',,'Poprawiono','Bez zmian');
   _par.TAB.A:=
         {? _co=1 || 'P'
         |? _co=2 || 'B'
         || _txt
         ?};
   _par.TAB.put()
";
_TAB.win_act(_win,,'Formuła','S&tatus'@,,'Zmiana statusu'@,,_fa,,,,,'T');
_TAB.win_btn(_win,'text='+'Aktualizuj schemat'@+',btn_label_align=center,panel=right,align=begin','menu:A');
_TAB.win_btn(_win,'text='+'S&tatus'@+',btn_label_align=center,panel=right,align=begin','menu:T');

N.f_set(,
   'join R using(N.NB,R.REFERENCE)',
   'R.RN in (:_a) and N.KOR=\'N\' and N.OD>=to_date(\'2022-01-01\') and N.OS_N is null and N.FIRMA=:_b',
   __RUB.sys_sql(12),
   exec('ref_firma','#firma')
);
_jestTZ:="
   TZ.use('tabz'+form(N.OD~1,-4,0,'9.'));
   TZ.index('PNRM');
   TZ.prefix(N.ref());
   {? TZ.first() || 'T' || 'N' ?}
";

{? N.f_first()
|| FUN.prg_start(N.f_size(),'Analiza nieobecności');
   {!
   |? _TAB.blank(1);
      _TAB.NAZWISKO:=N.P().OSOBA().NAZWISKO;
      _TAB.PIERWSZE:=OSOBA.PIERWSZE;
      _TAB.SYMBOL:=P.WYDZIAL().SYMBOL;
      _TAB.T:=form(P.T);
      _TAB.IP:=P.IP;
      _TAB.OD:=N.OD;
      _TAB.DO:=N.DO;
      _TAB.RN:=N.NB().RN;
      _TAB.NB:=R.RT;
      _TAB.LT:=N.LT;
      _TAB.TZ:=_jestTZ();
      _TAB.Z:={? +|N.LT & O.find_key(-N.LT,) || O.Z || 'N' ?};
      _TAB.PREF:=$P.ref();
      _TAB.NREF:=$.N.ref();
      {? ~_TAB.find_tab('first',
            'NAZWISKO',,'=',_TAB.NAZWISKO,
            'PIERWSZE',,'=',_TAB.PIERWSZE,
            'SYMBOL',,'=',_TAB.SYMBOL,
            'T',,'=',_TAB.T,
            'IP',,'=',_TAB.IP,
            'OD',,'=',_TAB.OD,
            'DO',,'=',_TAB.DO,
            'PREF',,'=',_TAB.PREF,
            'NREF',,'=',_TAB.NREF
         )
      || _TAB.A:='A';
         _TAB.add()
      || _TAB.NB:=N.NB().RT;
         _TAB.LT:=N.LT;
         _TAB.Z:={? +|N.LT & O.find_key(-N.LT) || O.Z || 'N' ?};
         _TAB.TZ:=_jestTZ();
         _TAB.put()
      ?};
      FUN.prg_next();
      N.f_next()
   !};
   FUN.prg_stop()
?};
N.f_clear();

_cfg.nav.main:=_ws;
_cfg.nav.nwin:=_win;
: Teraz okno grupowe.
_mode:='maximized';

_wnd:=_JOS.TAB.grp_make('Pracownicy'@,
:  Po wyświetleniu (załaduj kontrolkę, wyświetl drzewko).
   "  _par:=params_get();
      params_set(_par);
      _cfg:=_par.cfg;
      _ud_skl:=_par.ud_skl;
      _JOS:=_par.JOS;
      exec('load','#desktop','selektor','!pkd_grp_azat.dsk',,,,,exec('pkd_grp_azat_dsk_pl','pkd_grp'),1);
      exec('jos_fill','pkd_grp',_JOS,P_FILTER.UD_SCH,_ud_skl);
      grp_disp(_JOS.TAB,_cfg.nav.side,1,1);
      grp_disp(_par.TAB,_cfg.nav.nwin)
   ",-_acr
);

: Selektor schematu.
exec('create','#desktop',_JOS.TAB,'selektor',_wnd,3,20);

: Struktura.
_JOS.TAB.grp_splt(_wnd,,'horizontal','jostab');
_JOS.TAB.grp_sel(_wnd,_JOS.TAB,_cfg.nav.side,,
:  Po odświeżeniu (wyświetl współpracowników).
   "  _par:=params_get();
      params_set(_par);
      _cfg:=_par.cfg;
      P.first();
      grp_disp(P,_cfg.nav.main,1,1);
      grp_disp(_par.TAB,_cfg.nav.nwin)
   ",,,18,,,,,_mode,_cfg.nav.side
);

: Współpracownicy.
_JOS.TAB.grp_splt(_wnd,,'vertical','p');
_JOS.TAB.grp_sel(_wnd,P,_cfg.nav.main,,"
   _par:=params_get();
   params_set(_par);
   grp_disp(_par.TAB,_par.cfg.nav.nwin)
",,,,
:  Przed obsługą (ustaw filtr na tabeli P).
: !!! Proteza. Zapamiętujemy numer rekordu tabeli _JOS.TAB, dla którego odświeżany był widok współpracowników.
   "  _par:=params_get();
      params_set(_par);
      _JOS:=_par.JOS;
      _cfg:=_par.cfg;
      {? _cfg.lastref<>_JOS.TAB.ref() & UD_DEF.seek(_JOS.TAB.UD_DEF)
      || _cfg.lastref:=_JOS.TAB.ref();
         _from:='join N using(P.REFERENCE,N.P) join R using(N.NB,R.REFERENCE)';
         _where:='R.RN in (%1) and N.KOR=''N'' and N.OD>=to_date(''2022-01-01'') and N.OS_N is null and N.FIRMA=''%2'''
            [__RUB.sys_sql(12),$exec('ref_firma','#firma')];
         exec('filtruj_p','schemat','PKD',UD_DEF.ref(),'P','','W',_from,_where)
      ?}
   ",,,,_mode,_cfg.nav.main
);

_JOS.TAB.grp_splt(_wnd,,'horizontal','n',25);
_fb:="
   _par:=params_get();
   params_set(_par);
   {? grp_empty(P,_par.cfg.nav.main)
   || return('#disable')
   ?};
   _par.TAB.prefix($P.ref())
";
_JOS.TAB.grp_sel(_wnd,_TAB,_cfg.nav.nwin,,,,,,_fb);

_wnd


\pomoc_AK_10_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Tabela na dane
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: dane analizowane
_TAB:=tab_tmp(2,
   'PREF','STRING[16]','Wskazanie na P',
   'OD','DATE',MS.name(N,'OD'),
   'DO','DATE',MS.name(N,'DO'),
   'RN','INTEGER','Kod',
   'NB','STRING['+$MS.fld_len(R,'RT')+']','Nazwa',
   'LT','STRING['+$MS.fld_len(O,'LT')+']',MS.name(O,'LT'),
   'TZ','STRING[1]','Jest rozliczenie?',
   'Z','STRING['+$MS.fld_len(O,'Z')+']',MS.name(O,'Z'),
   'A','STRING[1]','do (A)nalizy, (P)oprawiono, (B)ez zmian',
   'KOM','STRING[255]','Komentarz',
   'NAZWISKO','STRING['+$MS.fld_len(OSOBA,'NAZWISKO')+']',MS.name(OSOBA,'NAZWISKO'),
   'PIERWSZE','STRING['+$MS.fld_len(OSOBA,'PIERWSZE')+']',MS.name(OSOBA,'PIERWSZE'),
   'SYMBOL','STRING['+$MS.fld_len(UD_SKL,'SYMBOL')+']',MS.name(P,'WYDZIAL'),
   'T','STRING['+$MS.fld_len(P,'T')+']',MS.name(P,'T'),
   'IP','INTEGER',MS.name(P,'IP'),
   'NREF','STRING[16]','Wskazanie na N'
)


\kody_zus_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Import Słowniki kodów ZUS.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_PZUS',','));
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['kody_zus_import'])
|| __UPG.msg('Zaktualizowano element "Słowniki kodów ZUS".');
   _result:=1
|| _result:=-1
?};
_result


\kody_zus_import_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Import Słowniki kodów ZUS - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import elementu "Słowniki kodów ZUS".'


\pkartzas_rep_tran
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Kompilacja wydruku "Karta zasiłkowa".
::----------------------------------------------------------------------------------------------------------------------
{? rep_tran('[p]pl_pkartzas')<>0
|| __UPG.msg('Zaktualizowano wydruk.');
   1
|| __UPG.msg('Aktualizacja wydruku nie powiodła się.');
   -1
?}


\pkartzas_rep_tran_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Kompilacja wydruku "Karta zasiłkowa" - opis.
::----------------------------------------------------------------------------------------------------------------------
'Kompilacja wydruku "Karta zasiłkowa"'


\porslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie słowników wykorzystywanych na portalu
::----------------------------------------------------------------------------------------------------------------------
exec('init','portal_slowniki','J');
exec('init','portal_slowniki','W');
__UPG.msg('Dodano słowniki portalowe.');
1


\porslo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie słowników wykorzystywanych na portalu - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie słowników wykorzystywanych na portalu.'


\slo_kod_2203_0025
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0025: Błędy przy wysyłaniu zastępstw.
::----------------------------------------------------------------------------------------------------------------------
_error:=0;
{? exec('lic','#b_domain','POR')
|| _pack_size:=500;
   _pd:='PORTAL';
   _mwac:='portal.mwac';
   _tabAcr:='SLO_KOD';
   _size:=0;
   _counter:=0;

   SYNC_PD.cntx_psh();
   SYNC_PD.index('SYM');
   SYNC_PD.prefix(_pd,);
   {? SYNC_PD.first()
   ||
      SYNC_DEF.cntx_psh();
      SYNC_DEF.index('PDNR');
      SYNC_DEF.prefix(SYNC_PD.ref,_tabAcr,);
      _war_form:={? SYNC_DEF.first() || SYNC_DEF.WAR_FORM || '' ?};
      SYNC_DEF.cntx_pop();
      {? _war_form='' || _war_form:='1' ?};

      SYNC_IDP.cntx_psh();
      SYNC_IDP.index('ACR');
      SYNC_IDP.prefix(SYNC_PD.ref(),_tabAcr,);
      {? SYNC_IDP.first()
      || SYNC_IDP.IDPUT:=time_ident(date(),time());
         SYNC_IDP.put()
      || SYNC_IDP.SYNC_PD:=SYNC_PD.ref();
         SYNC_IDP.ACR:=_tabAcr;
         SYNC_IDP.IDPUT:=time_ident(date(),time());
         SYNC_IDP.add()
      ?};
      SYNC_IDP.cntx_pop();

      _tab:=($_tabAcr)();
      _tab.trig_off('*','*');
      _args:=obj_new('size','pack_size','tab','war_form','counter','error');
      _args.size:=_size;
      _args.pack_size:=_pack_size;
      _args.tab:=_tab;
      _args.war_form:=$_war_form;
      _args.counter:=_counter;
      _args.error:=_error;
      params_set('args',_args);
      _formula:="
         _args:=params_get().args;
         {? _args.war_form('put',__Firma,REF.FIRMA)
         || _args.size+=1;
            {? _args.size>_args.pack_size
            || _args.size:=0;
               delay(1)
            ?};
            {? _args.tab.put(,1) || _args.counter+=1 || _args.error+=1 ?}
         ?};
         ~~
      ";
      params_exec('for_each','#table',_tab,_formula);
      _size:=_args.size;
      _counter:=_args.counter;
      _error:=_args.error;
      obj_del(_args);
      _tab.trig_on('*','*');
      obj_del(_tab)
   ?};
   SYNC_PD.cntx_pop();
   __UPG.msg('Liczba wszystkich wierszy: %1.'[$_size]);
   __UPG.msg('Liczba zaktualizowanych wierszy: %1.'[$_counter]);
   __UPG.msg('Błędy aktualizacji wierszy: %1.'[$_error])
?};

{? _error || -1 || 1 ?}


\slo_kod_2203_0025_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0025: Błędy przy wysyłaniu zastępstw - opis
::----------------------------------------------------------------------------------------------------------------------
'Błędy przy wysyłaniu zastępstw - tabela [SLO_KOD].\n'
'Poprawka błędu ER/WRT/XP/21.37/2203/0025'


\p_pz_2203_0025
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0025: Błędy przy wysyłaniu zastępstw.
::----------------------------------------------------------------------------------------------------------------------
_error:=0;
{? exec('lic','#b_domain','POR')
|| _pack_size:=500;
   _pd:='PORTAL';
   _mwac:='portal.mwac';
   _tabAcr:='P_PZ';
   _size:=0;
   _counter:=0;

   SYNC_PD.cntx_psh();
   SYNC_PD.index('SYM');
   SYNC_PD.prefix(_pd,);
   {? SYNC_PD.first()
   ||
      SYNC_DEF.cntx_psh();
      SYNC_DEF.index('PDNR');
      SYNC_DEF.prefix(SYNC_PD.ref,_tabAcr,);
      _war_form:={? SYNC_DEF.first() || SYNC_DEF.WAR_FORM || '' ?};
      SYNC_DEF.cntx_pop();
      {? _war_form='' || _war_form:='1' ?};

      SYNC_IDP.cntx_psh();
      SYNC_IDP.index('ACR');
      SYNC_IDP.prefix(SYNC_PD.ref(),_tabAcr,);
      {? SYNC_IDP.first()
      || SYNC_IDP.IDPUT:=time_ident(date(),time());
         SYNC_IDP.put()
      || SYNC_IDP.SYNC_PD:=SYNC_PD.ref();
         SYNC_IDP.ACR:=_tabAcr;
         SYNC_IDP.IDPUT:=time_ident(date(),time());
         SYNC_IDP.add()
      ?};
      SYNC_IDP.cntx_pop();

      _tab:=($_tabAcr)();
      _tab.trig_off('*','*');
      _args:=obj_new('size','pack_size','tab','war_form','counter','error');
      _args.size:=_size;
      _args.pack_size:=_pack_size;
      _args.tab:=_tab;
      _args.war_form:=$_war_form;
      _args.counter:=_counter;
      _args.error:=_error;
      params_set('args',_args);
      _formula:="
         _args:=params_get().args;
         {? _args.war_form('put',__Firma,REF.FIRMA)
         || _args.size+=1;
            {? _args.size>_args.pack_size
            || _args.size:=0;
               delay(1)
            ?};
            {? _args.tab.put(,1) || _args.counter+=1 || _args.error+=1 ?}
         ?};
         ~~
      ";
      params_exec('for_each','#table',_tab,_formula);
      _size:=_args.size;
      _counter:=_args.counter;
      _error:=_args.error;
      obj_del(_args);
      _tab.trig_on('*','*');
      obj_del(_tab)
   ?};
   SYNC_PD.cntx_pop();
   __UPG.msg('Liczba wszystkich wierszy: %1.'[$_size]);
   __UPG.msg('Liczba zaktualizowanych wierszy: %1.'[$_counter]);
   __UPG.msg('Błędy aktualizacji wierszy: %1.'[$_error])
?};

{? _error || -1 || 1 ?}


\p_pz_2203_0025_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0025: Błędy przy wysyłaniu zastępstw - opis
::----------------------------------------------------------------------------------------------------------------------
'Błędy przy wysyłaniu zastępstw - tabela [P_PZ].\n'
'Poprawka błędu ER/WRT/XP/21.37/2203/0025'


\jpk_d1_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Naprawa pozycji D.2.1 deklaracji JPK_V7
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_lrec:=0;
VAT_DEK.cntx_psh(); VAT_POZ.cntx_psh();
VAT_DEK.index('VAT_DEK');
VAT_DEK.prefix(date(2022,01,01),'V7');
{? VAT_DEK.first()
|| {!
   |? VAT_POZ.index('POZ1');
      VAT_POZ.prefix(VAT_DEK.ref(),40);
      {? VAT_POZ.first()
      || {? VAT_POZ.TYP='R'
         || VAT_POZ.TYP:='A';
            _tmp:=VAT_POZ.put();
            {? _tmp || _lrec+=1 ?};
            {? _ret || _ret:=_tmp ?}
         ?}
      ?};
      VAT_DEK.next()
   !}
?};
VAT_DEK.cntx_pop(); VAT_POZ.cntx_pop();
{? _ret
|| __UPG.msg('Zaktualizowano pole nr 40 w %1 deklaracjach JPK_V7'[$_lrec])
|| __UPG.msg('Zaktualizowano pole nr 40 w %1 deklaracjach JPK_V7'[$_lrec]);
   __UPG.msg('Aktualizacja pola nr 40 w deklaracjach JPK_V7 nie powiodła się.')
?};
_ret


\jpk_d1_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Naprawa pozycji D.2.1 deklaracji JPK_V7 - opis
::----------------------------------------------------------------------------------------------------------------------
'Naprawa pozycji D.2.1 deklaracji JPK_V7'


\sync_mwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Dodanie komunikatów API.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na Portal HR.');
   return(1)
?};

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

_mtype:=obj_new('SEND');
_mtype.SEND:=exec('mwac_type_send','synchro');

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

_data:=obj_new(1);
{! _lpdd:=1 .. obj_len(_data)
|! _data[_lpdd]:=obj_new('PD','el')
!};

_data[1].PD:='PORTAL';
_data[1].el:=obj_new(2);

_data[1].el[1]:=obj_new('BEFORE','NEW');
_data[1].el[1].BEFORE:=obj_new('TYPE','METHOD');
_data[1].el[1].BEFORE.TYPE:=_mtype.SEND;
_data[1].el[1].BEFORE.METHOD:='chr_PersonReprimandModify';
_data[1].el[1].NEW:=obj_new('TYPE','METHOD','TAB_ACR','SYNC_REP','DESC');
_data[1].el[1].NEW.TYPE:=_mtype.SEND;
_data[1].el[1].NEW.METHOD:='chr_PersonReprimandDelete';
_data[1].el[1].NEW.TAB_ACR:='KART_DOD';
_data[1].el[1].NEW.SYNC_REP:=-1;
_data[1].el[1].NEW.DESC:='Kara / upomnienie.';

_data[1].el[2]:=obj_new('BEFORE','NEW');
_data[1].el[2].BEFORE:=obj_new('TYPE','METHOD');
_data[1].el[2].BEFORE.TYPE:=_mtype.SEND;
_data[1].el[2].BEFORE.METHOD:='chr_PersonReplacementModify';
_data[1].el[2].NEW:=obj_new('TYPE','METHOD','TAB_ACR','SYNC_REP','DESC');
_data[1].el[2].NEW.TYPE:=_mtype.SEND;
_data[1].el[2].NEW.METHOD:='chr_PersonReplacementDelete';
_data[1].el[2].NEW.TAB_ACR:='P_PZ';
_data[1].el[2].NEW.SYNC_REP:=-1;
_data[1].el[2].NEW.DESC:='Zależności służbowe: usuwanie zapisów niebędących zastępstwami.';
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_ret:=1;
SYNC_REP.cntx_psh();
SYNC_REP.index('PDNR1');
SYNC_MWA.cntx_psh();
{! _lppd:=1 .. obj_len(_data)
|! SYNC_REP.prefix(_data[_lppd].PD,);
   SYNC_MWA.index('TREE');
   SYNC_MWA.prefix(0,_data[_lppd].PD,0);
   {? SYNC_MWA.first()
   || _root:=#SYNC_MWA.ref();
      _pd:=SYNC_MWA.SYNC_PD;
      _mwac:=SYNC_MWA.MWAC;
      SYNC_MWA.index('PD_METH1');
      SYNC_MWA.prefix(_data[_lppd].PD,);
      {! _lpm:=1 .. obj_len(_data[_lppd].el)
      |! {? SYNC_MWA.find_key(_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].el[_lpm].NEW.METHOD,)
         || __UPG.msg(
               'Komunikat %1 typu "%2" dla przeznaczenia danych %3 już istniał.'
               [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
            )
         |? SYNC_MWA.find_key(_data[_lppd].el[_lpm].BEFORE.TYPE,_data[_lppd].el[_lpm].BEFORE.METHOD,)
         || do();
            _lp:=SYNC_MWA.LP;
            _sync_rep:=
               {? _data[_lppd].el[_lpm].NEW.SYNC_REP=-1
               || SYNC_MWA.SYNC_REP
               |? _data[_lppd].el[_lpm].NEW.SYNC_REP=0
               || null()
               |? SYNC_REP.find_key(_data[_lppd].el[_lpm].NEW.SYNC_REP)
               || SYNC_REP.ref()
               || __UPG.msg(
                     'Komunikat %1 typu "%2" dla przeznaczenia danych %3  - zweryfikuj informacje o replice.'
                     [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
                  );
                  null()
               ?};
            SYNC_MWA.cntx_psh();
            SYNC_MWA.index('TREE1');
            SYNC_MWA.prefix(_root);
            {? SYNC_MWA.last()
            || {!
               |? SYNC_MWA.LP+=1;
                  SYNC_MWA.put();
                  SYNC_MWA.LP>_lp+1 & SYNC_MWA.prev()
               !};
               SYNC_MWA.blank();
               SYNC_MWA.SYNC_PD:=_pd;
               SYNC_MWA.MWAC:=_mwac;
               SYNC_MWA.PARENT:=_root;
               SYNC_MWA.LP:=_lp;
               SYNC_MWA.TYPE:=_data[_lppd].el[_lpm].NEW.TYPE;
               SYNC_MWA.METHOD:=_data[_lppd].el[_lpm].NEW.METHOD;
               SYNC_MWA.TAB_ACR:=_data[_lppd].el[_lpm].NEW.TAB_ACR;
               SYNC_MWA.AKT:='T';
               SYNC_MWA.SYNC_REP:=_sync_rep;
               {? SYNC_MWA.add()
               || SYNC_MWA.memo_set(_data[_lppd].el[_lpm].NEW.DESC,'DESC');
                  {? SYNC_MWA.memo_put(,'DESC')
                  || __UPG.msg(
                        'Komunikat %1 typu "%2" dla przeznaczenia danych %3 został dodany.'
                        [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
                     )
                  || __UPG.msg(
                        'Opis komunikatu %1 typu "%2" dla przeznaczenia danych %3 nie został dodany.'
                        [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
                     );
                     _ret:=-1
                  ?}
               || __UPG.msg(
                     'Komunikat %1 typu "%2" dla przeznaczenia danych %3 nie został dodany (1).'
                     [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
                  );
                  _ret:=-1
               ?}
            ?};
            SYNC_MWA.cntx_pop();
            {? ~end()
            || __UPG.msg(
                  'Komunikat %1 typu "%2" dla przeznaczenia danych %3 został dodany (2).'
                  [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
               );
               _ret:=-1
            ?}
         || __UPG.msg(
               'Odnalezienie komunikatu %1 typu "%2" nie powiodło się.'
               [_data[_lppd].el[_lpm].BEFORE.METHOD,_data[_lppd].el[_lpm].BEFORE.TYPE]
            );
            _ret:=-1
         ?}
      !}

   || __UPG.msg('Brak przeznaczenia danych %1.\nKomunikaty nie zostaną dodane.'[_data[_lppd].PD])
   ?}
!};
SYNC_MWA.cntx_pop();
SYNC_REP.cntx_pop();
_ret


\sync_mwa_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Dodanie komunikatów API.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dodanie komunikatów API'


\etypy_2203_0061
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0061: Portal HR - Wysłane prawa do wierszy dla wniosków paperless gdy nie ma licencji PEP
::       Błąd powstał po imporcie danych wzorcowych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
{? exec('lic','#b_domain','PEP')=0
|| ETYPY.cntx_psh();
   ETYPY.prefix();
   ETYPY.for_each("
      {? ETYPY.W_PORTAL='P'
      || __lrec+=1;
         {? ETYPY.AKTYWNY='T'
         || ETYPY.AKTYWNY:='N';
            {? ETYPY.put(1) || __lmod+=1 ?}
         ?}
      ?}
   ",1);
   ETYPY.cntx_pop()
?};
{? __lrec
|| __UPG.msg('Zaktualizowano typy wniosków paperless.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Nie była konieczna aktualizacja typów wniosków paperless.')
?};
VAR_DEL.delete('__lrec','__lmod');
1


\etypy_2203_0061_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0061: Portal HR - Wysłane prawa do wierszy dla wniosków paperless gdy nie ma licencji PEP
::----------------------------------------------------------------------------------------------------------------------
'Wysłane prawa do wierszy dla wniosków paperless gdy nie ma licencji PEP.\n'
'Poprawka błędu ER/WRT/XP/21.37/2203/0061'


\zws_tran_bmsg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [21.37]
:: OPIS: Naprawa procesu ZWS_TRAN
::----------------------------------------------------------------------------------------------------------------------
B_PROC.cntx_psh();
_ndx:=B_PROC.ndx_tmp('',1,'ACTIVE',,0, 'SYMBOL',,0,'VER',,0);
B_PROC.index(_ndx);
B_PROC.prefix('T','ZWS_TRAN','2137c');
_iter:=0;
{? B_PROC.first()
|| B_MSG.index('B_PROC');
   B_MSG.prefix(B_PROC.ref);
   {? B_MSG.first()
   || {!
      |? {? B_MSG.memo_txt(,1,'TO')='z@z.pl'
         || B_MSG.memo_set('','TO');
            B_MSG.memo_put(,'TO');
            _iter+=1
         ?};
         B_MSG.next()
      !}
   ?}
?};
B_PROC.cntx_pop();
B_PROC.ndx_drop(_ndx);
{? _iter=1
|| __UPG.msg('Dokonano zmian w %1 zdarzeniu.'[$_iter])
|| __UPG.msg('Dokonano zmian w %1 zdarzeniach.'[$_iter])
?};
1


\zws_tran_bmsg_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [21.37]
:: OPIS: Naprawa procesu ZWS_TRAN
::----------------------------------------------------------------------------------------------------------------------
'Naprawa zdarzeń wysłania komunikatu w procesie ZWS_TRAN'


\wz_osoba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0042 - Nieprawidłowa definicja wzorców słowników użytkowników
::       (Pracownik, Osoba, Zleceniobiorca) - skutkuje błędnym działaniem
::       we wnioskach w webterm (gdy użyjemy taki słownik w atrybutach)
::----------------------------------------------------------------------------------------------------------------------
RS.cntx_psh();
RS.prefix();
{? RS.first()
|| {!
   |? {? RS.TR='_a:=OSOBA();_a[3]+\' \'+_a[4]+\' \'+_a[1]'
      || RS.TR:='exec(\'wz_osoba\',\'slo_slu\',OSOBA(),3,4,1)';
         RS.put
      |? RS.TR='_a:=OSOBA();_a[1]+\' \'+_a[3]+\' \'+_a[4]'
      || RS.TR:='exec(\'wz_osoba\',\'slo_slu\',OSOBA(),1,3,4)';
         RS.put
      ?};
      RS.next()
   !}
?};
RS.cntx_pop();
__UPG.msg('Zaktualizowano rodzaje wzorców.');
1


\wz_osoba_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0042 - Nieprawidłowa definicja wzorców słowników użytkowników
::       (Pracownik, Osoba, Zleceniobiorca) - skutkuje błędnym działaniem
::       we wnioskach w webterm (gdy użyjemy taki słownik w atrybutach) - OPIS
::----------------------------------------------------------------------------------------------------------------------
'Aktualizuje treść rodzaju wzorca wykorzystującego tabele OSOBA'


\rk_col_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [IFZ] [21.37]
:: OPIS: ER/WRT/XP/21.37/2202/0003 - Błędne naliczenie różnic kursowych
::       Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
exec('color','upgrade_2137')


\rk_col_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [IFZ] [21.37]
:: OPIS: ER/WRT/XP/21.37/2202/0003 - Błędne naliczenie różnic kursowych
::       Aktualizacja schematów kolorów -OPIS
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\portal_kart_url_napraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2204/0036 - nieprawidłowo wysyłane limity urlopowe.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na HR Portal - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

_ret:=1;
P.cntx_psh();
P.index('OSOBA');
P.prefix();
{? P.first()
|| _stat:=obj_new('prac','kart','do_zaznaczenia','zaznaczonych','niezaznaczonych');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};

   N.cntx_psh();
   N.index('NIEOPRAC');
   N.prefix();

   KART_URL.cntx_psh();
   KART_URL.index('PRAC_ROK');
   KART_URL.trig_off('*','*');
   {!
   |? _stat.prac+=1;

      {? ~(P.DZA~2=1 & P.DZA~3=1)
      || KART_URL.prefix(P.ref());
         {? KART_URL.first()
         || _stat.kart+=1;
            {? P.DZA=KART_URL.DATA_DOD | N.find_key(P.ref(),P.DZA)
            || _stat.do_zaznaczenia+=1;
               {? KART_URL.put(,1)
               || _stat.zaznaczonych+=1
               || _stat.niezaznaczonych+=1
               ?}
            ?}
         ?}
      ?};
      P.next()
   !};
   KART_URL.trig_on('*','*');
   KART_URL.cntx_pop();

   N.cntx_pop();

   __UPG.msg('Zweryfikowanych pracowników: %1'[$_stat.prac]);
   __UPG.msg('Zweryfikowanych kart urlopowych: %1'[$_stat.kart]);
   __UPG.msg('Kart urlopowych do zaznaczenia: %1'[$_stat.do_zaznaczenia]);
   __UPG.msg('Kart urlopowych zaznaczonych: %1'[$_stat.zaznaczonych]);
   __UPG.msg('Kart urlopowych niezaznaczonych: %1'[$_stat.niezaznaczonych]);
   _ret:={? _stat.do_zaznaczenia=_stat.zaznaczonych || 1 || -1 ?}
?};
P.cntx_pop();
_ret


\portal_kart_url_napraw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2204/0036 - nieprawidłowo wysyłane limity urlopowe - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/21.37/2204/0036 - zaznaczenie kart urlopowych, które mogły zostać nieprawidłowo wysłane na portal'


\rubryki_placowe_ppk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Aktualizacja rubryk i atrybutów płacowych.
::       PR/WRT/XP/21.37/2204/0074 - PR/WRT/XP/12.51/2203/0026 : Wyksięgowanie wpłaty PPK firma w przypadku,
::       gdy przychód nie jest rozliczany na liście ze zwrotami
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';

R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

{? +exec('import','rubryki','g','7190,7201,7202',1,0)
|| _txt:='Dodanie rubryk 7190, 7201, 7202 oraz atrybutów 771, 772 zakończyło się niepowodzeniem.';
   _result:=-1
|| _txt:='Dodanie rubryk 7190, 7201, 7202 oraz atrybutów 771, 772 zakończone powodzeniem.'
?};
R.trig_on('*','*');

{? _result=1
|| exec('add_attr','rubatr',7,77,'Zwroty wpłat',1,'Zwroty wpłat PPK.');
      exec('add_attr','rubatr',77,771,'Zwrot składek PPK firma - wyksięgowanie kosztów',1,
           'Zwrot składek PPK firma - wyksięgowanie kosztów.',,7190);
      exec('add_attr','rubatr',77,772,'Kwoty zwrotów składek PPK firma - wyksięg. kosztów',1,
        'Kwoty zwrotów składek PPK firma - wyksięgowanie kosztów.');
         exec('add_attr','rubatr',772,7721,'Kwota zwrotu składek podstawowych PPK firma',1,
              'Kwota zwrotu składek podstawowych PPK firma - wyksięgowanie kosztów.',,7201);
         exec('add_attr','rubatr',772,7722,'Kwota zwrotu składek dodatkowych PPK firma',1,
              'Kwota zwrotu składek dodatkowych PPK firma - wyksięgowanie kosztów.',,7202)
?};
__UPG.msg(_txt);

_result


\rubryki_placowe_ppk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Aktualizacja rubryk i atrybutów płacowych - opis.
::       PR/WRT/XP/21.37/2204/0074 - PR/WRT/XP/12.51/2203/0026 : Wyksięgowanie wpłaty PPK firma w przypadku,
::       gdy przychód nie jest rozliczany na liście ze zwrotami
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryki 7190, 7201, 7202 oraz atrybuty 771, 772.\n'
'Poprawka PR/WRT/XP/21.37/2204/0074'


\formuly_placowe_ppk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie formuł płacowych.
::       PR/WRT/XP/21.37/2204/0074 - PR/WRT/XP/12.51/2203/0026 : Wyksięgowanie wpłaty PPK firma w przypadku,
::       gdy przychód nie jest rozliczany na liście ze zwrotami
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='7190';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,R,D,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt_ppk',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_ppk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie formuł płacowych - opis.
::       PR/WRT/XP/21.37/2204/0074 - PR/WRT/XP/12.51/2203/0026 : Wyksięgowanie wpłaty PPK firma w przypadku,
::       gdy przychód nie jest rozliczany na liście ze zwrotami
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuł płacowych.\n'
'Poprawka PR/WRT/XP/21.37/2204/0074'


\formuly_txt_ppk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=7190
:: ==================
:: Zwrot skł. PPK firma
|| "
{? var_pres('KST_PPK')<>type_of(SYSLOG) || return(0) ?};
exec('zlicz_zwrot','ppk','Z',772)"

|| ""
?}


\zaomedadtcreate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka PR/WRT/XP/21.37/2204/0071 - data utworzenia dokumentu w metadanych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','PED')
|| __UPG.msg('Brak licencji na dziedzinę "Dokumentacja pracownicza" - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

_ret:=1;
ZAOMEDA.cntx_psh();
ZAOMEDA.index('NAG');
ZAOMEDA.prefix();
{? ZAOMEDA.first()
|| ZAOMEDA.trig_off('*','*');

   _stat:=obj_new('size','zalacz','seek0','seek1','zm0','zm1','put0','put1','del0','del1');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};
   _stat.size:=ZAOMEDA.size();

   ZALACZ.cntx_psh();
   ZALACZ.prefix();
   {!
   |? _del:=0;
      {? ref_tab(ZAOMEDA.NAG)=ZALACZ
      || _stat.zalacz+=1;
         {? ZALACZ.seek(ZAOMEDA.NAG,)
         || _stat.seek1+=1;
            {? ZAOMEDA.DTCREATE<>ZALACZ.DATA
            || _stat.zm1+=1;
               ZAOMEDA.DTCREATE:=ZALACZ.DATA;
               {? ZAOMEDA.put()
               || _stat.put1+=1
               || _stat.put0+=1
               ?}
            || _stat.zm0+=1
            ?}
         || _stat.seek0+=1;
            _del:=ZAOMEDA.del(,1);
            {? _del>0
            || _stat.del1+=1
            || _stat.del0+=1
            ?}
         ?}
      ?};
      _del=2 | ZAOMEDA.next()
   !};
   ZALACZ.cntx_pop();

   ZAOMEDA.trig_on('*','*');

   __UPG.msg('Liczba rekordów metadanych: %1'[$_stat.size]);
   __UPG.msg('Liczba rekordów metadanych do weryfikacji: %1'[$_stat.zalacz]);
   __UPG.msg('Liczba rekordów źródłowych [znalezionych / nieznalezionych]: %1 / %2'[$_stat.seek1,$_stat.seek0]);
   __UPG.msg('Liczba znalezionych rekordów źródłowych [wymagających / niewymagających] zmiany: %1 / %2'
      [$_stat.zm1,$_stat.zm0]
   );
   __UPG.msg('Liczba rekordów źródłowych wymagających zmiany i [zmienionych / niezmienionych]: %1 / %2'
      [$_stat.put1,$_stat.put0]
   );
   __UPG.msg('Liczba rekordów metadanych bez rekordów źródłowych [usuniętych / nieusuniętych]: %1 / %2'
      [$_stat.del1,$_stat.del0]
   );

   {? _stat.seek0 | _stat.put0 | _stat.del0
   || _ret:=-1
   ?}

|| __UPG.msg('Tabela metadanych nie zawiera rekordów.')
?};
ZAOMEDA.cntx_pop();

_ret


\zaomedadtcreate_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka PR/WRT/XP/21.37/2204/0071 - data utworzenia dokumentu w metadanych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'PR/WRT/XP/21.37/2204/0071 - weryfikacja daty utworzenia dokumentów w metadanych'


\obe_zest_rep_tran
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2205/0009 - Kompilacja wydruku "Zestawienie dokumentów w obiegu".
::----------------------------------------------------------------------------------------------------------------------
{? rep_tran('[o]be_zest')<>0
|| __UPG.msg('Zaktualizowano wydruk.');
   1
|| __UPG.msg('Aktualizacja wydruku nie powiodła się.');
   -1
?}


\obe_zest_rep_tran_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2205/0009 - Kompilacja wydruku "Zestawienie dokumentów w obiegu". - opis.
::----------------------------------------------------------------------------------------------------------------------
'Kompilacja wydruku "Zestawienie dokumentów w obiegu"'


\p_pz_klasa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2205/0043 - usunięcie z portalu rekordów niebędących zastępstwami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na dziedzinę "HR Portal" - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

P_PZ.cntx_psh();
P_PZ.prefix();
{? P_PZ.first()
|| P_PZ.trig_off('*','*');

   _stat:=obj_new('Z','nieZ','poprawionych','niepoprawionych');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};

   {!
   |? {? P_PZ.KLASA='Z'
      || _stat.Z+=1
      || _stat.nieZ+=1;
         {? P_PZ.put(,1)
         || _stat.poprawionych+=1
         || _stat.niepoprawionych+=1
         ?}
      ?};
      P_PZ.next()
   !};
   __UPG.msg('Rekordów w tabeli P_PZ: %1'[$P_PZ.size()]);
   __UPG.msg(' * Rekordów dotyczących zastępstw (niewymagających zmian): %1'[$_stat.Z]);
   __UPG.msg(' * Rekordów niedotyczących zastępstw (potencjalnie do usunięcia z portalu): %1'[$_stat.nieZ]);
   __UPG.msg('   - Rekordów zaznaczonych do weryfikacji: %1'[$_stat.poprawionych]);
   __UPG.msg('   - Rekordów, których nie udało się zaznaczyć: %1'[$_stat.niepoprawionych]);
   P_PZ.trig_on('*','*');

   _ret:={? _stat.niepoprawionych=0 || 1 || -1 ?}

|| __UPG.msg('Tabela P_PZ nie zawiera zapisów.');
   _ret:=1
?};
P_PZ.cntx_pop();
_ret


\p_pz_klasa_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2205/0043 - usunięcie z portalu rekordów niebędących zastępstwami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/21.37/2205/0043 - usunięcie z portalu rekordów niebędących zastępstwami'


\rubryki_placowe_PL_lip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie rubryk płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

{? +exec('import','rubryki','g','7182,7183,7184,780',1,0)
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);
{? _result=-1 || return(_result) ?};

:: Aktualizacja atrybutów rubryk. -------------------------------------------------------------------------------------
exec('add_attr','rubatr',55,552,'Zasiłki macierzyńskie',1,
   'Zasiłki macierzyńskie.',,523,524,7017);
exec('add_attr','rubatr',902,90203,'ZP: Przychód z zasiłku macierzyńskiego',1,
   'Przychód z zasiłku macierzyńskiego.',,7184);
exec('add_attr','rubatr',902,90204,'ZP: Przychód ze stosunku pracy',1,
   'Przychód ze stosunku pracy i pokrewnych.',,7182);
exec('add_attr','rubatr',902,90205,'ZP: Przychód z umowy zlecenia',1,
   'Przychód z umowy zlecenia.',,7183);
exec('add_use','rubatr',90202,7182,7183,7184);

RA_DEF.cntx_psh();
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
_tablica:=obj_new(4);
_tablica[1]:=9021;
_tablica[2]:=9028;
_tablica[3]:=9029;
_tablica[4]:=90201;

{! _ind:=1..obj_len(_tablica)
|! {? RA_DEF.find_key(_tablica[_ind])
   || RA_USE.index('RA_USE');
      {! _kor:=0..1
      |! RA_USE.prefix(RA_DEF.ref(),__RUB.ref(523+_kor));
         {? RA_USE.first()
         || RA_USE.DATA:=date(2022,7,1);
            RA_USE.STAN:='N';
            RA_USE.add(1)
         ?}
      !};
      RA_USE.prefix(RA_DEF.ref(),__RUB.ref(7017));
      {? RA_USE.first()
      || RA_USE.DATA:=date(2022,7,1);
         RA_USE.STAN:='N';
         RA_USE.add(1)
      ?}
   ?}
!};
obj_del(_tablica);
RA_DEF.cntx_pop();

R.cntx_psh();
R.index('RUBKOD');
R.prefix(780);
{? R.first() & R.RT='Przychód'
|| R.RT:='Podst. opodatkowania';
   R.put()
?};
R.cntx_pop();

_result


\rubryki_placowe_PL_lip_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie rubryk płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk płacowych.'


\weryfikacja_ZP_PL_lip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.51]
:: OPIS: Weryfikacja zapisów dodatkowych w kartotece rozliczenia zwolnienia przychodu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();
:: obiekt do wyliczenia zwolnienia z przychodu
exec('__ZW_PRZ','object');

_wyn:=0;
{? ~__ZW_PRZ.JEST_PL
|| _txt:='Brak w systemie zwolnienie od przychodu - Polski Ład';
   __UPG.msg(_txt);
   _result:=-1;
   return(_result)
?};
P.cntx_psh();
OSOBA.cntx_psh();
LS.cntx_psh();
O.cntx_psh();
O.index('LISTYPLP');
P.clear;
OS_ZWPOZ.cntx_psh();

O.prefix(exec('ref_firma','ustawienia'),'P',2022);
{? O.first()
|| {!
   |? {? LS.use(O.LT)
      || {? var_pres('_tab_P')>0 || &_tab_P ?};
         _tab_P:=sql('select LS.P from LS join R where R.RN in (:_a)',__RUB.sys_sql(9022));
         {? _tab_P.first()
         || {!
            |? {? P.seek(_tab_P.P)
               || P.OSOBA();
                  OS_ZWPOZ.index('PRACROK');
                  OS_ZWPOZ.prefix(P.ref,2022,O.MP,O.ref);
                  {? ~OS_ZWPOZ.first
                  || __ZW_PRZ.create();
                     {? __ZW_PRZ.KOD<>''
                     || _poz:=__ZW_PRZ.add_pozycja();
                        {? _poz
                        || _wyn+=1;
                           {? var_pres('_tab_rub')>0 || &_tab_rub ?};
                           _tab_rub:=__RUB.sys_rub(90202,date(O.RP,O.MP,0));
                           {? _tab_rub.first()
                           || {!
                              |? LS.index('PRACNRRU');
                                 LS.prefix(P.ref(),_tab_rub.RN);
                                 _wartosc:=0;
                                 {? LS.first() || {! |? _wartosc+=LS.KW; LS.next() !} ?};
                                 __ZW_PRZ.add_wartosc(_poz,_tab_rub.RN,_wartosc);
                                 _tab_rub.next()
                              !}
                           ?}
                        ?}
                     ?}
                  ?}
               ?};
               _tab_P.next()
            !}
         ?}
      ?};
      O.next()
   !}
?};
P.cntx_pop();
LS.cntx_pop();
O.cntx_pop();
OSOBA.cntx_pop();
OS_ZWPOZ.cntx_pop();
{? _wyn
|| _txt:='Poprawiono zapisy w kartotece rozliczenia zwolnienia przychodu.'
|| _txt:='Brak zapisów do poprawienia w kartotece rozliczenia zwolnienia przychodu.'
?};
__UPG.msg(_txt);

_result


\weryfikacja_ZP_PL_lip_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Weryfikacja zapisów dodatkowych w kartotece rozliczenia zwolnienia przychodu - opis.
::----------------------------------------------------------------------------------------------------------------------
'Weryfikacja zapisów dodatkowych w kartotece rozliczenia zwolnienia przychodu.'


\formuly_placowe_PL_lip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie formuł płacowych.
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='7182,7183,7184';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,R,D,W,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt_PL_lip',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_PL_lip_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie formuł płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuł płacowych dla zmian: Polski Ład - lipiec 2022'


\formuly_txt_PL_lip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=7182 & _tp<>'R'
:: =================
:: ZP: Prz. st. pracy
|| "exec('ZP_stosunek_pracy','lista_licz')"

|? _rn=7184 & _tp<>'R'
:: =================
:: ZP: Prz. zas. mac.
|| "exec('ZP_mac','lista_licz')"

|? _rn=7183 & _tp='R'
:: =================
:: ZP: Prz. um. zlec.
|| "exec('ZP_umowa_zlecenie','zlec_rh')"

|? _rn=7184 & _tp='R'
:: =================
:: ZP: Prz. zas. mac.
|| "exec('ZP_mac','zlec_rh')"

|| ""
?}


\cit8_31
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_imp:=exec('fiks_85','napraw_f',2);
_txt:='e-Deklaracja CIT-8 (31) obowiązująca za 2021 rok';
{? _imp=1
|| __UPG.msg(_txt+' została zaktualizowana.')
|? _imp=0
|| __UPG.msg(_txt+' nie istnieje.')
|| __UPG.msg(_txt+' nie została zaktualizowana.');
   _ok:=0
?};
_ok


\cit8_31_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji - opis.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji'


\por_slowniki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [12.51]
:: OPIS: Aktualizacja słowników portalowych.
::       ER/WRT/XP/21.37/2202/0060 - Wniosek o zatrudnienie - stanowisko nie jest przypisane do wybranej jednostki
::       organizacyjnej.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? FUN.ask(
      'Przed naniesieniem poprawki należy zakończyć obieg wszystkich aktywnie przetwarzanych w ramach Portalu HR '
      'wniosków związanych z zatrudnieniem ze względu na zmianę pola na wniosku oraz sposobu weryfikacji '
      'poprawności wprowadzonych danych:\n'
      '- Wnioski o zatrudnienie\n'
      '- Wnioski o zmianę zatrudnienia\n'
      '- Wnioski o przedłużenie lub rozwiązanie umowy\n'
      '- Wnioski o rekrutację\n\n'
      'Czy chcesz kontynuować?'@
   )
|| PORSLO.trig_off('*','*');
   PORSLO.cntx_psh();
   PORSLO.f_set(,,'PORSLO.SYSTEM=\'T\' and PORSLO.FIELD=\'DzialNazwa\' and PORSLO.TYPE=\'Request\'');
   {? PORSLO.f_first()
   || {!
      |? PORSLO.TYPE:='RequestPersonDep_Symb';
         {? ~PORSLO.put()
         || _result:=-1
         ?};

         _result<>-1 & PORSLO.f_next()
      !}
   ?};
   PORSLO.f_clear();
   {? _result<>-1
   || PORSLO.f_set(,,'PORSLO.SYSTEM=\'N\' and PORSLO.FIELD=\'DzialNazwa\' and PORSLO.TYPE=\'Request\'');
      {? PORSLO.f_first()
      || {? FUN.ask('Znaleziono nieprawidłowe niesystemowe słowniki portalowe o nazwie "DzialNazwa".\n'
                 'Czy zmienić widok na "RequestPersonDep_Symb" w znalezionych słownikach?'@)
         || {!
            |? PORSLO.TYPE:='RequestPersonDep_Symb';
               {? ~PORSLO.put()
               || _result:=-1
               ?};

               _result<>-1 & PORSLO.f_next()
            !}
         || {? FUN.ask('Czy przerwać zadanie?'@)
            || _result:=-1
            ?};
            __UPG.msg('Znaleziono nieprawidłowe niesystemowe słowniki portalowe o nazwie "DzialNazwa".')
         ?}
      ?};
      PORSLO.f_clear()
   ?};
   PORSLO.cntx_pop();
   PORSLO.trig_on('*','*');
   {? _result<>-1
   || exec('import_init','#excel_imex',spli_str('ZWS_PAR_PTYP,ZWS_PAR_PSLO',','));
      {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['por_slownik'])
      || __UPG.msg('Zaktualizowano słowniki portalowe.');
         _result:=1
      || _result:=-1
      ?}
   ?}
|| _result:=-1
?};

_result


\por_slowniki_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Aktualizacja słowników portalowych - opis.
::       ER/WRT/XP/21.37/2202/0060 - Wniosek o zatrudnienie - stanowisko nie jest przypisane do wybranej jednostki
::       organizacyjnej.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słowników portalowych.\n'
'ER/WRT/XP/21.37/2202/0060'


\dokum_ob_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Aktualizacja statusów dokumentów businesslink powiązanych z EDOKUM.
::       ER/WRT/XP/21.37/2206/0017 - powiadomienia o dokumentach z BL wymagających obsługi manulanej
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DOKUM.names();
_msk.clear();
DOKUM.cntx_psh();
{? _msk.first()
|| {!
   |? DOKUM.use(_msk.NAME);
      DOKUM.index('BL');
      DOKUM.prefix('T',REF.FIRMA,'P','T');
      {? DOKUM.first()
      || {!
         |? __lrec+=1;
            {? DOKUM.REFSQL*'skid_v'>0 & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
            || _res:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"
                  _stat:=1+@.EDOKUM.B_PRELS;
                  {? _stat<>'' & _stat<>'R'
                  || @.DOKUM.cntx_psh();
                     @.DOKUM.prefix();
                     @.DOKUM.BL_STAT:=exec('registered','zbl_stat');
                     @.DOKUM.put();
                     @.DOKUM.cntx_pop()
                  ||
                     -1
                  ?}
               ");
               {? _res=1
               || __lmod+=1
               ?}
            ?};
            DOKUM.next()
         !}
      ?};
      _msk.next()
   !}
?};
DOKUM.cntx_pop();
__UPG.msg('Zaktualizowano statusy przychodzących dokumentów Businesslink powiązanych z dokumentami w obiegu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\dokum_ob_status_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Aktualizacja statusów dokumentów businesslink powiązanych z EDOKUM - opis
::       ER/WRT/XP/21.37/2206/0017 - powiadomienia o dokumentach z BL wymagających obsługi manulanej
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja statusów dokumentów businesslink powiązanych z dokumentami w obiegu.'


\dokument_zmiany_metody
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [21.37]
:: OPIS: Formuła zmienia parametr środków trwałych dotyczący dokumentu zmiany metody z D->L
::       Domyślny dokument ustawiany jest na typ 'MA'
::----------------------------------------------------------------------------------------------------------------------
SRDT.use('srdt'+'r'+REF.FIRMA().SYMBOL);
SRDT.cntx_psh();
SRDT.index('TYP');
SRDT.prefix('MA');
{? SRDT.first()
|| exec('init_wer','#stalesys');
   exec('stalesys','#stalesys');
   FINFO.SRST_MET:=SRDT.ref();
   exec('zapisz','#stalesys',1,FINFO,'SRST_MET');
   __UPG.msg('Ustawiono dokument zmiany metody D->L na typ \'MA\'.')
?};
SRDT.cntx_pop()


\dokument_zmiany_metody_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [21.37]
:: OPIS: Formuła zmienia parametr środków trwałych dotyczący dokumentu zmiany metody z D->L
::       Domyślny dokument ustawiany jest na typ 'MA'
::----------------------------------------------------------------------------------------------------------------------
'Formuła zmienia parametr środków trwałych dotyczący dokumentu zmiany metody z D->L\n
Domyślny dokument ustawiany jest na typ \'MA\')'


\h_overlap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: kod - ER/WRT/XP/21.37/2208/0006 - Miesce pracy - zmiana stanowiska -
::             zmian daty miejsca pracy daje datę 0001/01/01
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? FUN.ask(
      'W zadaniu przeanalizowane zostaną zapisy tabeli H\n'
      'na pod kątem błędnej daty OD oraz nakładających się okresów.\n\n'
      'Czy chcesz kontynuować?'@
   )
||
   exec('KOMM','#object');
   KOMM.init();

   FIRMA.cntx_psh();
   F_ZATR.cntx_psh();
   OSOBA.cntx_psh();
   P.cntx_psh();
   P.index('FOP_ZATR');
   P.prefix();
   {? P.first()
   || H.cntx_psh();
      H.use('_hist');
      H.index('_HISTDAT');
      FUN.prg_start(P.size(),'Analiza błędnych zapisów...',5,,1);
      {!
      |? H.prefix(P.ref());
         {? H.first()
         || {!
            |?  _res:=exec('check','overlap',H.ref(),H,'OD','DO',2,,,'HISTUM',H.UMOWA);
               {? type_of('_res')=type_of('') & _res<>''
               || _txt:='Błędne zapisy w tabeli "Miejsca pracy" dla współpracownika';
                  {? ~KOMM.find_msg(_txt) || KOMM.sect_beg(_txt) ?};
                  _txt:='%3 [Firma: %1, Forma zatr.: %2]'
                        [P.FIRMA().SYMBOL,P.F_ZATR().KOD,exec('P','#to_string')];
                  {? ~KOMM.find_msg('%1 - nakładające się okresy'[_txt]) &
                     ~KOMM.find_msg('%1 - błędna data OD: %2'[_txt,$H.OD])
                  || KOMM.add('%1 - nakładające się okresy'[_txt])
                  ?}
               ?};
               {? H.OD<date(1900,1,1)
               || _txt:='%3 [Firma: %1, Forma zatr.: %2]'
                        [P.FIRMA().SYMBOL,P.F_ZATR().KOD,exec('P','#to_string')];
                  {? ~KOMM.find_msg('%1 - nakładające się okresy'[_txt]) &
                     ~KOMM.find_msg('%1 - błędna data OD: %2'[_txt,$H.OD])
                  || KOMM.add('%1 - błędna data OD: %2'[_txt,$H.OD])
                  ?}
               ?};
               H.next()
            !}
         ?};
         FUN.prg_next();
         P.next()
      !};
      FUN.prg_stop();
      H.cntx_pop()
   ?};
   P.cntx_pop();
   OSOBA.cntx_pop();
   F_ZATR.cntx_pop();
   FIRMA.cntx_pop();

   KOMM.select('Błędne zapisy',,,1);

   {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['h_overlap'])
   || __UPG.msg('Poprawiono błędne daty oraz nakładające się okresy.');
      _result:=1
   || _result:=-1
   ?}
|| _result:=-1
?};

_result


\h_overlap_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: kod - ER/WRT/XP/21.37/2208/0006 - Miesce pracy - zmiana stanowiska -
::             zmian daty miejsca pracy daje datę 0001/01/01
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/21.37/2208/0006 - Miejsca pracy - błędne daty oraz nakładające się okresy'


\slo_naz_miejscpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: ER/WRT/XP/21.37/2208/0025 - Problem z encją: chr_WorkPlaceModify_json - brak możliwości
::       modyfikowania słownika z miejscami pracy
::----------------------------------------------------------------------------------------------------------------------
exec('KOMM','#object');
KOMM.init();

:: Środowisko wywołania
_env:=exec('env','sync_mwa');
_env.LogLevel:=-1;

::wyszukujemy rekord SYNC_MWA
_sql:=''+"select REFERENCE as REF from SYNC_MWA where SYNC_MWA.METHOD='chr_WorkPlaceDelete'";
_TAB:=sql(_sql);

:: tabela do zapamiętania usuniętych uidref-ów
_UIDS:=tab_tmp(1, 'UIDREF','STRING[48]','');


SYNC_MWA.cntx_psh();
SYNC_PD.cntx_psh();
{? _TAB.first() & SYNC_MWA.seek(_TAB.REF,,1)
|| _env.MethodName:=SYNC_MWA.METHOD;
   _env.TabAcr:=SYNC_MWA.TAB_ACR;
   _sync_pd:=SYNC_MWA.SYNC_PD().SYM;
   _tab:=($(SYNC_MWA.TAB_ACR))();

   params_set('env',_env,'tab',_tab,'sync_pd',_sync_pd,'UIDS',_UIDS);
   _formula:="
      params_set(params_get());
      _UIDS:=params_get().UIDS;
      _env:=params_get().env;
      _tab:=params_get().tab;
      _sync_pd:=params_get().sync_pd;
      _uidref:=_tab.uidref();
      _env.Param:=_uidref;
      _res:=exec('run','sync_mwa');
      {? var_pres('_res')=type_of(0) & _res>0
      || _UIDS.UIDREF:=_uidref;
         _UIDS.add(1)
      ?};
      ~~
   ";
   params_exec('for_each','#table',_tab,_formula);

:: a teraz raz jeszcze put-ujemy wcześniej usunięte rekordy w celu ponownej wysyłki
   _formula:="
      _UIDS:=params_get().UIDS;
      SLO_NAZ.cntx_psh();
      {? SLO_NAZ.seek(_UIDS.UIDREF,,1)
      || SLO_NAZ.put(,1)
      ?};
      SLO_NAZ.cntx_pop()
   ";
   params_exec('for_each','#table',_UIDS,_formula)
?};
SYNC_MWA.cntx_pop();
SYNC_PD.cntx_pop();

_size:=_UIDS.size();
{? _size>0
|| __UPG.msg('Usunięto %1 %2 tabeli SLO_NAZ i oznaczono je do ponownego wysłania na Portal HR (encja chr_WorkPlace).'
             [$_size,{? _size=1 || 'rekord' |? _size<5 || 'rekordy' || 'rekordów' ?}])
|| __UPG.msg('Brak rekordów do ponownego wysłania na HR Portal.')
?};
1


\slo_naz_miejscpr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: ER/WRT/XP/21.37/2208/0025 - Problem z encją: chr_WorkPlaceModify_json - brak możliwości
::       modyfikowania słownika z miejscami pracy - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/21.37/2208/0025 - Problem z encją: chr_WorkPlaceModify_json - brak możliwości '
'modyfikowania słownika z miejscami pracy'


\archiwum_del_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Usunięcie zbędnej formuły
::----------------------------------------------------------------------------------------------------------------------
ARCH_UST.cntx_psh();
ARCH_UST.index('UNIQ');
ARCH_UST.prefix(exec('ref_firma','#firma'),'STN','ZAL',);
_zmian:=0;
_test:='exec(\'test\',\'test\')';
{? ARCH_UST.first()
|| {!
   |? {? |ARCH_UST.FORM=_test
      || ARCH_UST.FORM:='';
         _zmian+=ARCH_UST.put(1)
      ?};
      ARCH_UST.next()
   !}
?};
ARCH_UST.cntx_pop();
{? _zmian
|| __UPG.msg('Usunięto formułę "%1"'[_test])
|| __UPG.msg('Brak rekordów do zmiany.')
?};
1


\archiwum_del_test_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS:  Usunięcie zbędnej formuły - Opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie zbędnej formuły w warunku ustawień archiwizacji załączników.'


\edokum_wyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: kod - ER/WRT/XP/21.37/2207/0025 - formuła czyszcząca pozostawione wypłenione pola po wycofaniu dokumentu

::----------------------------------------------------------------------------------------------------------------------
EDOKUM.trig_off('*','*');
EDOKUM.cntx_psh();
_mask:=EDOKUM.names();
{? _mask.first()
|| {!
   |? EDOKUM.use(_mask.NAME); EDOKUM.prefix();
      {? EDOKUM.first()
      || {!
         |? _put:=0;
            {? EDOKUM.WYCOF_DO<>'' || EDOKUM.WYCOF_DO:=''; _put:=1 ?};
            {? EDOKUM.WYCOFAJ<>'' || EDOKUM.WYCOFAJ:=''; _put:=1 ?};
            {? _put || EDOKUM.put() ?};
            EDOKUM.next()
         !}
      ?};
      _mask.next()
   !}
?};
obj_del(_mask);
EDOKUM.cntx_pop();
EDOKUM.trig_on('*','*');
__UPG.msg('Naprawiono pola opisujące status wycofania dokumentów w obiegu.');
1


\edokum_wyc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: kod - ER/WRT/XP/21.37/2207/0025 - formuła czyszcząca pozostawione wypłenione pola po wycofaniu dokumentu - desc

::----------------------------------------------------------------------------------------------------------------------
'Naprawa pól opisujących status wycofania dokumentu w obiegu.'


\s_zus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja słownika kodów ZUS.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wynik:=1;
S_ZUS.cntx_psh();
S_ZUS.prefix();
{? exec('s_zus_import','personel',0)
|| __UPG.msg('Aktualizacja słownika kodów ZUS zakończyła się sukcesem.')
|| __UPG.msg('Aktualizacja słownika kodów ZUS nie powiodła się.');
   _wynik:=-1
?};
S_ZUS.cntx_pop();
_wynik


\s_zus_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja słownika kodów ZUS - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słownika kodów ZUS'


\atr_plac_cw_woj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Aktualizacja atrybutów płacowych.
::       Poprawka błędu ER/WRT/XP/21.37/2210/0021 - Kod świadczenia przerwy ZUS dla składnika 7068 - ćwiczenia wojskowe
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

RA_DEF.cntx_psh();
RA_USE.cntx_psh();
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
RA_USE.index('RA_USE');

{? RA_DEF.find_key(14151)
|| RA_USE.prefix(RA_DEF.ref(),__RUB.ref(7068));
   {? RA_USE.first()
   || RA_USE.del()
   ?}
?};
exec('add_uses','rubatr',7068,14,14111);

RA_USE.cntx_pop();
RA_DEF.cntx_pop();

R.trig_on('*','*');
__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\atr_plac_cw_woj_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Aktualizacja atrybutów płacowych - opis.
::       Poprawka błędu ER/WRT/XP/21.37/2201/0040 - Kod świadczenia przerwy ZUS dla składnika 7068 - ćwiczenia wojskowe
::----------------------------------------------------------------------------------------------------------------------
'Funkcja naprawcza przenosząca rubrykę 7068 - Ćwiczenia wojskowe z atrybutu 14151 do 14111.'
'Poprawka błędu ER/WRT/XP/21.37/2210/0021'


\formuly_placowe_kor_rh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie formuł płacowych.
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='951';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('R',',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt_kor_rh',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_kor_rh_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie formuł płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły płacowej typu R dla składnika płacowego 951 - Nadp. skł.E/R. b.rok'


\formuly_txt_kor_rh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=951
:: =================
:: Nadp. skł.E/R. b.rok
|| "exec('kor_zc_zwrot','korekty_zus')"

|| ""
?}

:Sign Version 2.0 jowisz:1045 2023/09/29 12:54:10 2eb8f3cfc8ad71337bb9e4133768b26ace277bbdf8fd4b9b88f7bab923240ccda1b65e6979ec7bb712a48f76001f25f1400082f5d7724e2f66fe9ef9d814db1c6ec92a2a58dcb25536503a8085fcd48668fa8b30c7fd3ff20a1aa40530d532c675b67fa293ceae2db0fb58ca857fb4f2c323b1272191a09f6082986748819786

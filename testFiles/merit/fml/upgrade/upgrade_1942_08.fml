:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_1942_08.fml
:: Utworzony: 15.11.2019
:: Autor: [jaws]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 19.22 na 19.42
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Główna formuła aktualizacji 19.42_08
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',,,,,'T');
__UPG.add_task('areatitle',,,'ZWS',,,,,'T');
__UPG.add_task('color',,,'ZWS',,,,,'T');
__UPG.add_task('skladniki_PPK',,,'PPL',,,,,'T');
__UPG.add_task('atrybuty_PPK',,,'PPL',,,,,'T');
__UPG.add_task('stalesys_ini',,,'ZWS',,,,,'T');
__UPG.add_task('stalesys_wid',,,'ZWS',,,,,'T');
__UPG.add_task('akt_wn_dwwd',,,'OBE',,,,,'T');
__UPG.add_task('PPK_ZDN',,,'PPK',,,,,'T');
__UPG.add_task('PPK_RPL',,,'PPK',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('ppk_role_1942_08',,'N','ZWS',,,,1,'T');
__UPG.add_task('PPK_KWN',,'N','PPK',,,,1,'T');

:: Zadania manualne, wykonywane raz dla całego systemu

:: Zadania manualne, wykonywane dla każdej firmy
__UPG.add_task('formuly_PPK_1942_08','N','N','PPL',,'N',,1,'T');
__UPG.add_task('stalesys_PPK_1942_08','N','N','PPK',,'N',,1,'T');
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.OBE','.');
_dom:=spli_str('PPK','.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.'@,,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\skladniki_PPK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja składników płacowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('__RUB','object');
__RUB.fill();

:: Definicja rubryk.
_txt:=exec('import','rubryki','g','7088,7089,7109',1,0);
{? _txt<>''
|| __UPG.msg('Aktualizacja składników płacowych nie powiodła się.');
   __UPG.msg(_txt);
   _result:=-1
|| __UPG.msg('Aktualizacja składników płacowych zakończona powodzeniem.');
:: Blokada modyfikacji rubryk systemowych.
   R.cntx_psh();
   R.prefix();
   R.for_each(
      "  {? R.RN>=7000 & R.SYSTEM<>'T'
         || R.SYSTEM:='T';
            R.put(1)
         ?}
      "
   );
   R.cntx_pop()
?};

_result


\skladniki_PPK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja składników płacowych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja składników płacowych.'


\atrybuty_PPK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja atrybutów składników PPK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');
__RUB.fill();

exec('add_attr','rubatr',72,726,'Wpłaty zwiększające przychód',1);
   exec('add_attr','rubatr',726,7261,'Wpłata podstawowa opłacana przez podmiot zatr.',1,,,7088);
   exec('add_attr','rubatr',726,7262,'Wpłata dodatkowa opłacana przez podmiot zatr.',1,,,7089);

__UPG.msg('Zakończono aktualizację atrybutów składników PPK.');
1


\atrybuty_PPK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja atrybutów składników PPK - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja atrybutów składników PPK'


\formuly_PPK_1942_08
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja formuł płacowych składników PPK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');
{? __RUB.ref(7109)=null
|| __UPG.msg('Wstrzymano aktualizację. Wymagane istnienie rubryki 7109');
   return(-1)
?};

_rn:=spli_str('7093,7109',',');
_tp:=spli_str('U,F,D,R,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_pod','upgrade_1942_08');

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_PPK_1942_08_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.42]
:: OPIS: Aktualizacja formuł płacowych składników PPK - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja formuł płacowych składników PPK'


\formuly_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Zwraca treść formuły płacowej.
::   WE: _a [STRING] - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=7093
||
"{? var_pres('KST_PPK')<>type_of(SYSLOG) || return(0) ?};
-exec('zlicz_zwrot','ppk','Z',724)"

|? _rn=7109
|| {? _tp='R'
   ||
"{? var_pres('KST_PPK')<>type_of(SYSLOG) || return(0) ?};
exec('licz_prz','ppk','z')"
   ||
"{? var_pres('KST_PPK')<>type_of(SYSLOG) || return(0) ?};
exec('licz_prz','ppk')"
   ?}
||
""
?}


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja definicji stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_kst','#stalesys',KST,KST_PPK);
__UPG.msg('Zaktualizowano definicje stałych systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja definicji stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji stałych systemu'


\stalesys_PPK_1942_08
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych dziedziny PPK
::----------------------------------------------------------------------------------------------------------------------
:: ustaw wymagane wartości początkowe
KST_PPK.POD_WMW:='T';
KST_PPK.D_PRZWPL:=0;
KST_PPK.PPKEMAIL:='S';
exec('zapisz_zes','#stalesys',date(0,0,0),KST_PPK,'POD_WMW','D_PRZWPL','PPKEMAIL');
__UPG.msg('Ustawiono wymagane wartości początkowe.');

:: ustawienia dla zgodności z wytycznymi odnośnie naliczania podatku
{? FUN.ask(
      'Czy ustawić wliczanie wpłat do przychodu w miesiącu\n'
      'następnym po miesiącu podatkowym naliczania wpłat?'
   )
|| _ok:=0;
   KST_PPK.POD_WMW:='N';
   KST_PPK.D_PRZWPL:=15;
   EDIT_VAR.DATA:=date(date()~1,date()~2,0)+1;
   _wnd:=KST_PPK.mk_edit('Wyliczenie przychodu z tytułu wpłat PPK',0);
   KST_PPK.win_efld(_wnd,EDIT_VAR,'DATA',,,,,,'Data wprowadzenia zmiany');
   KST_PPK.win_efld(_wnd,KST_PPK,'D_PRZWPL');

   KST_PPK.cntx_psh();
   KST_PPK.win_edit(_wnd);
   {!
   |? {? KST_PPK.edit("
            {? EDIT_VAR.DATA=date(0,0,0)
            || FUN.emsg('""Data wprowadzenia zmiany"" musi być podana.');
               return('DATA')
            |? KST_PPK.D_PRZWPL<0 | KST_PPK.D_PRZWPL>31
            || FUN.emsg('Błędna wartość pola ""'+MS.name(KST_PPK,'D_PRZWPL')+'"".');
               return('D_PRZWPL')
            ?};
            1
         ")
      || _ok:=1;
         0
      || ~FUN.ask('Czy na pewno zrezygnować z wprowadzenia zmian?')
      ?}
   !};
   {? _ok
   || exec('zapisz_zes','#stalesys',EDIT_VAR.DATA,KST_PPK,'POD_WMW','D_PRZWPL');
      __UPG.msg('Zaktualizowano stałe dziedziny PPK.')
   ?};
   KST_PPK.cntx_pop();
   KST_PPK.win_edel(_wnd)

|| __UPG.msg('Porzucono aktualizację stałych dziedziny PPK.');
   FUN.info(
      'Sposób wliczania przychodu z tytułu wpłat pracodawcy\n'
      'można zmienić w parametrach pracy programu.'
   )
?};

1


\stalesys_PPK_1942_08_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych dziedziny PPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych dziedziny PPK'


\stalesys_wid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja widoków stałych systemu
::----------------------------------------------------------------------------------------------------------------------
:: uzupełnienie widoków stałych systemu
KST_LST.cntx_psh();
KST_LST.prefix();
KST_LST.f_set(
   'NUMER',
   'join KST_DOM using(KST_LST.KST_DOM,KST_DOM.REFERENCE) '
   'join KST_MAP using(KST_LST.KST_MAP,KST_MAP.REFERENCE) '
   'join KST_ZES using(KST_MAP.KST_ZES,KST_ZES.REFERENCE) '
   'join B_DOMAIN using(KST_DOM.B_DOMAIN,B_DOMAIN.REFERENCE) ',
   'B_DOMAIN.SYMBOL=\'PPK\' and KST_ZES.SYMBOL=\'KST_PPK\''
);
{? KST_LST.f_last()
|| _num:=KST_LST.NUMER;
   _dom:=KST_LST.KST_DOM;
:: pola do dołączenia do widoku stałem KST_PPK
   _map:=spli_str('POD_WMW,D_PRZWPL,PPKEMAIL',',');
   KST_MAP.cntx_psh();
   KST_MAP.index('KST_MAP');
   KST_MAP.prefix('KST_PPK','KST_PPK');
   {! _ii:=1..obj_len(_map)
   |! {? KST_MAP.find_key(_map[_ii],_map[_ii])
::       znajdź pole w zmiennej reprezentujące stałą
      || {? ~KST_LST.find_tab(,'KST_DOM',,'=',_dom,'KST_MAP',,'=',KST_MAP.ref())
::          uzupełnij widok od pole w zestawie
         || KST_LST.KST_MAP:=KST_MAP.ref();
            KST_LST.NUMER:=_num+_ii;
            KST_LST.add()
         ?}
      ?}
   !};
   KST_MAP.cntx_pop()
?};
KST_LST.f_clear();
KST_LST.cntx_pop();
__UPG.msg('Zaktualizowano widoki stałych systemu.');
1


\stalesys_wid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja widoków stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja widoków stałych systemu'


\PPK_ZDN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja zawartości tabeli PPK_ZDN
::----------------------------------------------------------------------------------------------------------------------
:: Uzupełnienie kolumny AUTO.
PPK_ZDN.cntx_psh();
PPK_ZDN.prefix();
PPK_ZDN.for_each("{? PPK_ZDN.AUTO='' || PPK_ZDN.AUTO:='N'; PPK_ZDN.put() ?}");
PPK_ZDN.cntx_pop();
__UPG.msg('Zaktualizowano zawartości tabeli.');
1


\PPK_ZDN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja zawartości tabeli PPK_ZDN - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zawartości tabeli PPK_ZDN'


\PPK_RPL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja zawartości tabeli PPK_RPL
::----------------------------------------------------------------------------------------------------------------------
:: Uzupełnienie kolumny DATA;
_d0:=date(0,0,0);
PPK_RPL.cntx_psh();
PPK_RPL.index('PPK_RPL');
PPK_RPL.prefix();
_loop:=PPK_RPL.first();
{!
|? _loop
|! {? PPK_RPL.DATA=_d0
   || _dt:={? PPK_RPL.WYS_DATA=_d0 || date() || PPK_RPL.WYS_DATA ?};
      {? PPK_RPL.R=_dt~1 & PPK_RPL.M=_dt~2
      || PPK_RPL.DATA:=_dt
      |? _dt<date(PPK_RPL.R,PPK_RPL.M,1)
      || PPK_RPL.DATA:=date(PPK_RPL.R,PPK_RPL.M,1)
      || PPK_RPL.DATA:=date(PPK_RPL.R,PPK_RPL.M,0)
      ?};
      PPK_RPL.put()
   ?};
   _loop:=PPK_RPL.next()
!};
PPK_RPL.cntx_pop();
__UPG.msg('Zaktualizowano zawartości tabeli.');
1


\PPK_RPL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja zawartości tabeli PPK_RPL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zawartości tabeli PPK_RPL'


\PPK_KWN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja zawartości tabeli PPK_KWN
::----------------------------------------------------------------------------------------------------------------------
:: uzupełnienie kolumny RODZAJ dla zwrotów wpłat
:: jeśli są dane, to z założenia pochodzą od instytucji finansowej
PPK_KWN.cntx_psh();
PPK_KWN.prefix();
PPK_KWN.for_each("
   _mod:=0;
   {? PPK_KWN.TYP='Z' & PPK_KWN.RODZAJ=''
   || PPK_KWN.RODZAJ:='I';
      PPK_KWN.TYTUL:='-';
      _mod:=1
   ?};
   {? PPK_KWN.DATA_KOR=date(0,0,0)
   || PPK_KWN.DATA_KOR:=date(PPK_KWN.R,PPK_KWN.M,1);
      _mod:=1
   ?};
   {? _mod
   || PPK_KWN.put()
   ?}
");
PPK_KWN.cntx_pop();
__UPG.msg('Zaktualizowano zawartości tabeli.');
1


\PPK_KWN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizacja zawartości tabeli PPK_KWN - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zawartości tabeli PPK_KWN'


\akt_wn_dwwd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.42]
:: OPIS: Aktualizacja deklaracji wysokości wpłaty dodatkowej (wniosek w obiegu)
::----------------------------------------------------------------------------------------------------------------------
_msg:="
{? _a
|| __UPG.msg('Zakończono aktualizację wniosku: \"%1\".'@[_b])
|| __UPG.msg('Nie udało się zaktualizować wniosku: \"%1\".'@[_b])
?}
";

_typobieg:=exec('add','typ_ob','Obieg wniosków');
_nazwa:=exec('nazwa_dwwd','obiegi');
_result:=1;
{? exec('renameETYPY','sch_imp',_typobieg,_nazwa)<1
|| __UPG.msg('Nie udało się zmienić nazwy wniosku: \"%1\".'@[_nazwa]);
   _result:=-1
|| _msg(exec('imp_wn','sch_imp',_nazwa,1,0),_nazwa)
?};
_result


\akt_wn_dwwd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.42]
:: OPIS: Aktualizacja deklaracji wysokości wpłaty dodatkowej (wniosek w obiegu) - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja deklaracji wysokości wpłaty dodatkowej (wniosek w obiegu)'


\ppk_role_1942_08
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Dodanie/aktualizacja ról/czynności związanych z PPK.
::----------------------------------------------------------------------------------------------------------------------
_status:='';

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_add:="
   _rola:=_a;
   _lista:=_b;

   _ok:=1;
   _uid:=spli_str(_lista,'.');
   {! _lp:=1 .. obj_len(_uid)
   |! {? exec('add_actrol_one','#b_role',_rola,_uid[_lp])
      || __UPG.msg('Roli ''%1'' nadano uprawnienia do czynności ''%2''.' [_rola,_uid[_lp]])
      || __UPG.msg('Nadanie roli ''%1'' uprawnień do czynności ''%2'' nie powiodło się.' [_rola,_uid[_lp]]);
         _ok:=0
      ?}
   !};
   _ok";
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_rola:='Specjalista ds. PPK';
_ref:=exec('ref','#b_role',_rola);
{? _ref<>null()
|| __UPG.msg('Rola ''%1'' już istnieje.' [_rola])
|? (_ref:=exec('ref','#b_role',_rola,,1))<>null()
|| __UPG.msg('Rola ''%1'' została utworzona.' [_rola])
?};
{? _ref=null()
|| __UPG.msg('Roli ''%1'' nie udało się utworzyć.' [_rola]);
   _status+='E'
|| {? ~_add(_rola,'PPK_EWI_PPOD.PPK_EWI_RPOD')
   || _status+='E'
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_rola:='Przeglądający PPK';
_ref:=exec('ref','#b_role',_rola);
{? _ref<>null()
|| __UPG.msg('Rola ''%1'' już istnieje.' [_rola])
|? (_ref:=exec('ref','#b_role',_rola,,1))<>null()
|| __UPG.msg('Rola ''%1'' została utworzona.' [_rola])
?};
{? _ref=null()
|| __UPG.msg('Roli ''%1'' nie udało się utworzyć.' [_rola]);
   _status+='E'
|| {? ~_add(_rola,'PPK_EWI_PPOD')
   || _status+='E'
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

{? _status*'E'
|| 0
|? _status*'O'
|| -1
|| 1
?}


\ppk_role_1942_08_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Dodanie/aktualizacja ról/czynności związanych z PPK. - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie/aktualizacja ról/czynności związanych z PPK'

:Sign Version 2.0 jowisz:1045 2020/04/03 17:16:50 a1bb00f3732287485da9b92e86f37d9d504b3c5abcdf143e13493dbbf6a1bae912659c3967787b62118f90388d30b14ae2b954fd9b19d780bec1ee8d1d22878ddd2ed41948b36b2ad56615ca4509b490c56c16d863778230490ad9d631839aa0c1e0f28f5308a5497fc73560f18707e4219e608b4daf440a9ea14f075553a2e2

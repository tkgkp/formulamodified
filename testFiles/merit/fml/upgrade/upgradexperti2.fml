:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgradexperti2.fml
:: Utworzony: 06.07.2022
:: Autor: [MB]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z Xpertisa 12.41/12.51 na Merita w wersji aktualnie emitowanej
::
::            DODATKOWY PLIK DLA FORMUŁ ICH WYWOŁANIE NALEŻY ZADEKLAROWAĆ w FORMULE \init/upgradexpertis.fml.
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - realizacja zakwalifikowana jako wykonana
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\add_viudo_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Dodaje schemat deklaracji VIU-DO
::  OLD: \add_viudo_sch/upgrade_2137_05.fml
::----------------------------------------------------------------------------------------------------------------------
_imp:=-1;
{? exec('add_ver','xml','FKS',date(2021,4,1),'VIUDO',1,1)
|| _imp:=exec('upg_imp','xml','FKS','D','VIUDO',1,date(2021,4,1),'viu_do_1.dfg')
?};

_txt:='e-Deklaracja VIU-DO (1) obowiązująca od 01.04.2021';

{? _imp=1
|| __UPG.msg(_txt+' została dodana.');
   1
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.');
   1
|| __UPG.msg(_txt+' nie została dodana.');
   0
?}


\add_viudo_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Dodaje schemat deklaracji VIU-DO - opis
::  OLD: \add_viudo_sch_desc/upgrade_2137_05.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie schematu deklaracji VIU-DO (1)'


\ecb_crt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.14_09]
:: OPIS: Uzupełnianie słownika banków i oddziałów o Europejski Bank Centralny
::  OLD: \ecb_crt/upgrade_2137_05.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh(); SLO.cntx_psh(); BANORG.cntx_psh(); B.cntx_psh();
SLU.index('NAZ'); SLU.prefix('BANKI');

{? SLU.first()
|| SLO.index('SL'); SLO.prefix(SLU.ref());
   KRAJE.cntx_psh();
   KRAJE.index('KRAJE');
   KRAJE.prefix('DE');
   {? KRAJE.first() || _kraj_ref:=KRAJE.ref() || _kraj_ref:=null ?};
   KRAJE.cntx_pop();

   BANORG.index('NDX');
   BANORG.prefix('ECB','Europejski Bank Centralny',);
   _org:=_bank:=null;
   {? BANORG.first()
   || _org:=BANORG.ref()
   || BANORG.blank();
      BANORG.KOD:='ECB';
      BANORG.KODZ:='';
      BANORG.AKTYWNY:='T';
      BANORG.NAZ:='Europejski Bank Centralny';
      BANORG.NRCB:='';
      {? BANORG.add() || _org:=BANORG.ref() ?}
   ?};
   {? _org
   || B.index('BANK');
      B.prefix('Europejski Bank Centralny - Centrala','00000001');
      {? B.first()
      || _bank:=B.ref()
      || B.blank();
         B.BANORG:=_org;
         B.KOD:='00000';
         B.KODISO:=_kraj_ref;
         B.NB:='Europejski Bank Centralny - Centrala';
         B.NUMER:='00000001';
         B.STATUS:='B';
         {? B.add() || _bank:=B.ref() ?}
      ?}
   ?};

   TKRS.cntx_psh();
   TKRS.use('tabkrseb');
   TKRS.prefix();
   {? TKRS.first()
   || {!
      |? {? TKRS.count()>0
         || KRS.cntx_psh();
            KRS.use('kursy_eb');
            KRS.index('KRS_KRAJ');
            KRS.prefix(TKRS.ref);
            {? KRS.first() || {! |? KRS.del() !} ?};
            KRS.cntx_pop()
         ?};
         TKRS.del()
      !}
   ?};
   TKRS.cntx_pop();

   SLO.cntx_psh();
   SLO.index('SL_TR');
   SLO.prefix(SLU.ref,'Europejski Bank Centralny - Centrala');
   {? SLO.first()
   || {!
      |? SLO.del()
      !}
   ?};
   SLO.cntx_pop();

   {? _kraj_ref & ~SLO.find_key('99999')
   || SLO.blank();
      SLO.KOD:='99999';
      SLO.TR:='Europejski Bank Centralny - Centrala';
      SLO.add()
   ?}
?};
SLU.cntx_pop(); SLO.cntx_pop(); BANORG.cntx_pop(); B.cntx_pop();
__UPG.msg('Zaktualizowano słownik banków i oddziałów.');
1


\ecb_crt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.14_09]
:: OPIS:  Uzupełnianie słownika banków i oddziałów o Europejski Bank Centralny - opis
::  OLD: \ecb_crt_desc/upgrade_2137_05.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnianie słownika banków i oddziałów o Europejski Bank Centralny'


\jpk2_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodaje formułę kontrolną dla kontroli procedury EE
::  OLD: \jpk2_upr/upgrade_2137_08.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G','PROC_V72',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:='PROC_V72';
   FORMULA.NAZWA:='Formuła kon. sprawdzająca wymagania procedury EE';
   FORMULA.FORMULA:='exec(\'jpk_proc_ee\',\'spr_dok\')';
   {? _ret:=FORMULA.add()
   || __UPG.msg('Dodano formułę PROC_V72.')
   || __UPG.msg('Nie udało się dodać formuły PROC_V72.')
   ?}
|| __UPG.msg('Formuła kontrolna PROC_V72 jest już dodana.')
?};
FORMULA.cntx_pop();
_ret


\jpk2_upr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodaje formułę kontrolną dla kontroli procedury EE - opis.
::  OLD: \jpk2_upr_desc/upgrade_2137_08.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodaje formułę kontrolną dla kontroli procedury EE.'


\proc_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Formuła dodaje procedury dla nowej wersji JPK_V7 od 2022.01.01 oraz dodatkowo procedura SOTI.
::  OLD: \proc_add/upgrade_2137_08.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:='PROC';
_wer:=1;
_ret:=1;
JPK_SLO.cntx_psh();
JPK_SLO.index('KOD');
JPK_SLO.prefix();
_ok:=1;
{? ~JPK_SLO.find_key(_wer,_typ,'WSTO_EE',) & ~JPK_SLO.find_key(_wer,_typ,'IED',) & ~JPK_SLO.find_key(_wer,_typ,'SOTI',)
|| exec('add_slo','jpk_v',_wer,_typ,'WSTO_EE',
      'Wewnątrzwspólnotowa sprzedaż towarów na odległość',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'IED',
      'Dostawy towarów, o których mowa w art. 7a ust. 1  i 2 Ustawy, na terytorium kraju',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'SOTI',
      'Sprzedaż na odległość towarów importowanych',,'IOSS'
   );
   __UPG.msg('Dodano nowe procedury szczególne.')
|| __UPG.msg('Nowe procedury szczególne były już dodane')
?};
JPK_SLO.cntx_pop();
_ret


\proc_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodaje procedury dla nowej wersji JPK_V7 od 2022.01.01 oraz dodatkowo procedura SOTI - opis.
::  OLD: \proc_add_desc/upgrade_2137_08.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodaje procedury dla nowej wersji JPK_V7 od 2022.01.01 oraz dodatkowo procedura SOTI.'


\jpkv72_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodanie nowych schematów JPK_V7M (2) oraz JPK_V7K (2)
::  OLD: \jpkv72_sch/upgrade_2137_08.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_txt:='Schemat JPK_V7M (2)';
_imp:=exec('upg_imp','xml','FKS','J','V7M','JPK_V7M(2)_v1-0',date(2022,1,1),'jpk_v7m_2_1.dfg');
{? _imp=1
|| __UPG.msg(_txt+' został dodany.')
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.')
|| __UPG.msg(_txt+' nie został dodany.');
   _ok:=0
?};
_txt:='Schemat JPK_V7K (2)';
_imp:=exec('upg_imp','xml','FKS','J','V7K','JPK_V7K(2)_v1-0',date(2022,1,1),'jpk_v7k_2_1.dfg');
{? _imp=1
|| __UPG.msg(_txt+' został dodany.')
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.')
|| __UPG.msg(_txt+' nie został dodany.');
   _ok:=0
?};
_ok


\jpkv72_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodanie nowych schematów JPK_V7M (2) oraz JPK_V7K (2) - opis
::  OLD: \jpkv72_sch_desc/upgrade_2137_08.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych schematów: JPK_V7M (2) oraz JPK_V7K (2)\n'
'Schematy obowiązują od 1 stycznia 2022 r.'


\kontr_st_zwol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Dodaje formułę początkową formuły kontrolnej sprawdzającej podanie powodu użycia stawki zwolnionej VAT
::  OLD: \kontr_st_zwol/upgrade_2137_14.fml
::----------------------------------------------------------------------------------------------------------------------
_name:='ST_ZWOL';
_ret:=1;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G',_name,);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:=_name;
   FORMULA.NAZWA:='Formuła początkowa dla kontroli uzupełnienia powodu użycia zwolnionej stawki VAT';
   FORMULA.FORMULA:=$"exec('st_zwol','%fks_spr_dok')";
   _ret:=FORMULA.add();
   {? _ret
   || __UPG.msg('Dodano formułę %1.'[_name])
   || __UPG.msg('Nie udało się dodać formuły %1.'[_name])
   ?}
|| __UPG.msg('Istnieje już formuła %1.'[_name])
?};
FORMULA.cntx_pop();
_ret


\kontr_st_zwol_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Dodaje formułę początkową formuły kontrolnej sprawdzającej podanie powodu użycia stawki zwolnionej VAT - opis
::  OLD: \kontr_st_zwol_desc/upgrade_2137_14.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły ST_ZWOL.\n'
'Jest to początkowa formuła formuły kontrolnej dla sprawdzenia uzupełnienia powodu użycia zwolnionej stawki VAT.'


\dok_proc_upd_tt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37_14]
:: OPIS: Aktualizacja procedur szczególnych DOK.JPK_PROC
::  OLD: \dok_proc_upd_tt/upgrade_2137_14.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;

DOK.cntx_psh();
DOKPOLA.cntx_psh();
_maski:=DOKPOLA.names();
{? _maski.first()
|| {!
   |? __UPG.msg('Aktualizacja danych dodatkowych z maski: '+_maski.NAME);
      _maska:=_maski.NAME+4;
      DOKPOLA.use('dokp'+_maska);
      _i2:=DOKPOLA.ndx_tmp('',1,'TT',,0);
      DOKPOLA.index(_i2);
      DOKPOLA.prefix('T',);
      {? DOKPOLA.first()
      || {!
         |? _dokm:=ref_name(DOKPOLA.REK);
            DOK.use(_dokm);
            DOK.prefix();
            {? DOK.seek(DOKPOLA.REK)
            || VPOZ.cntx_psh();
               VPOZ.use('pozv'+(_dokm+4));
               VPOZ.index('VDOK');
               VPOZ.prefix(DOK.ref());
               {? VPOZ.first()
               || _str:=DOK.JPK_PROC;
                  {!
                  |? {? VPOZ.RVAT().RVAT().KVAT().SYM='_WWspNat'
                     || _str:=exec('add_str','dok_ksef',_str,'TT_WNT')
                     |? KVAT.SYM='_WWspDot'
                     || _str:=exec('add_str','dok_ksef',_str,'TT_D')
                     ?};
                     VPOZ.next()
                  !};
                  {? DOK.JPK_PROC<>_str
                  || DOK.JPK_PROC:=_str;
                     DOK.put()
                  ?}
               ?};
               VPOZ.cntx_pop()
            ?};
            DOKPOLA.next()
         !}
      ?};
      DOKPOLA.index('UNIK');
      DOKPOLA.ndx_drop(_i2);
      _maski.next()
   !}
?};
DOKPOLA.cntx_pop();
DOK.cntx_pop();

__UPG.msg('Zaktualizowano pole dla procedury szczególnej.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\dok_proc_upd_tt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37_14]
:: OPIS: Aktualizacja pola dla procedury szczególnej DOK.JPK_PROC
::  OLD: \dok_proc_upd_tt_desc/upgrade_2137_14.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola dla procedury szczególnej'


\jpk_fa_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Aktualizacja struktury pliku JPK_FA
::  OLD: \jpk_fa_upd/upgrade_2137_14.fml
::----------------------------------------------------------------------------------------------------------------------
_fml:="exec('jpk_fa_tt','jpk')";
_zmiana:=-1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','FA','JPK_FA(3)_v1-0',);
{? ISTDEF.first()
|| _zmiana:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref(),'P_23');
   {? ISTDEFS.first() & ISTDEFS.REGULY<>$_fml
   || ISTDEFS.REGULY:=$_fml;
      _zmiana:=ISTDEFS.put()
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
{? _zmiana=1
|| __UPG.msg('Zaktualizowano schemat JPK_FA (3).')
|? _zmiana=-1
|| __UPG.msg('Nie znaleziono schematu JPK_FA (3).')
|| __UPG.msg('Aktualizacja schematu JPK_FA (3) nie była wymagana.')
?};
1


\jpk_fa_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Aktualizacja struktury pliku JPK_FA - opis
::  OLD: \jpk_fa_upd_desc/upgrade_2137_14.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu JPK_FA (3)\n'
'Zmiana formuły dla pola JPK\\Faktura\\P_23'


\cit8_31
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS:  OPIS: Import sprawozdań na potrzeby deklaracji CIT-8 (31)
::  OLD: \cit8_31/upgrade_2137_19.fml
::----------------------------------------------------------------------------------------------------------------------
_spr:='cit-8_31_spr.dfg';
_edek:='cit-8_31.dfg';
_ok:=1;
{? fexists(_spr,1)
|| _ok:=exec('imp_spr','sprfin',_spr,2,0);
   {? _ok
   || __UPG.msg('Zaimportowano sprawozdania z pliku: %1'[_spr])
   || __UPG.msg('Nie udało się zaimportować sprawozdania z pliku: %1'[_spr])
   ?}
|| __UPG.msg('Nie znaleziono na serwerze pliku: %1'[_spr]);
   _ok:=0
?};
{? fexists(_edek,1)
|| _imp:=-1;
   {? exec('add_ver','xml','FKS',date(2021,1,1),'CIT8',31)
   || _imp:=exec('upg_imp','xml','FKS','D','CIT8',31,date(2021,1,1),'cit-8_31.dfg')
   ?};
   _txt:='e-Deklaracja CIT-8 (31) obowiązująca za 2021 rok';
   {? _imp=1
   || __UPG.msg(_txt+' została dodana.')
   |? _imp=0
   || __UPG.msg(_txt+' już istnieje.')
   || __UPG.msg(_txt+' nie została dodana.');
      _ok:=0
   ?}
|| __UPG.msg('Nie znaleziono na serwerze pliku: %1'[_edek]);
   _ok:=0
?};
_ok


\cit8_31_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Import sprawozdań na potrzeby deklaracji CIT-8 (31) - opis
::  OLD: \cit8_31_desc/upgrade_2137_19.fml
::----------------------------------------------------------------------------------------------------------------------
'Import sprawozdań CIT-8 (31) i CIT-8O (17) za 2021 rok\n'
'Import schematów e-Deklaracji CIT-8 (31) za 2021 rok'


\portal_seod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie danych na potrzeby portalu SEOD
::  OLD: \portal_seod/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');
PAR_SKID.set(465,8,0,'','TWN','SEOD: Komentarz w akceptacji, odrzuceniu i zamknięciu');
__UPG.msg('Dodano parametr 465 - SEOD: Komentarz w akceptacji, odrzuceniu i zamknięciu');

exec('init','sync_id');
__UPG.msg('Zaktualizowano identyfikatory rekordów dla portalu.');

exec('portalu_update','#portal',0);
__UPG.msg('Zaktualizowano uprawnienia użytkowników na portalu.');

exec('UD_SEOD_init','portal_seod',2);
exec('UD_SEOD_init','portal_seod',1);
__UPG.msg('Uzupełniono tabelę UD_SEOD.');

_obj:=exec('portal_hr_obj','upgradexperti2');
_obj.add('PORTAL_SEOD','Usuwanie','EDOKUM','cseod_AcceptanceHeaderDelete','Scieżka akceptacji faktur w obiegu',
   $"exec('EDOKUM_typ','portal_seod','F','S')"
);
_obj.add('PORTAL_SEOD','Usuwanie','EDOKUM','cseod_PurchaseDocumentDelete','Faktury w obiegu');
_obj.add('PORTAL_SEOD','Wysyłanie','EDOKUM','cseod_PurchaseDocumentModify','Faktury w obiegu');
_obj.add('PORTAL_SEOD','Odbieranie','EDOKUM','cseod_PurchaseDocumentGet','Faktury w obiegu');
_obj.add('PORTAL_SEOD','Wysyłanie','EDOKUM','cseod_AcceptanceHeaderModify','Ścieżka akceptacji faktury w obiegu');
_obj.add('PORTAL_SEOD','Odbieranie','EDOKUM','cseod_AcceptOperatorGet','Odbieranie informacji o akceptacji faktury');

_obj.add('PORTAL_SEOD_CFG','Usuwanie','UD_SEOD','cseod_CostCenterTypeTenantDelete',
   'Wymiary controllingowe typów faktur i wniosków',
   $"exec('UD_SEOD_ok','portal_seod')",'cseod_CostCenterTypeTenantModify'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','UD_SEOD','cseod_CostCenterTypeTenantModify',
   'Wymiary controllingowe typów faktur i wniosków',
   $"exec('UD_SEOD_ok','portal_seod')"
);
_obj.add('PORTAL_SEOD_CFG','Usuwanie','UD_DEF','cseod_CostCenterDelete',
   'Elementy wymiaru controllingowego',
   $"exec('UD_DEF_ok','portal_seod')",'cseod_CostCenterModify'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','UD_DEF','cseod_CostCenterModify',
   'Elementy wymiaru controllingowego',
   $"exec('UD_DEF_ok','portal_seod')"
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','KH','cseod_BCAddresHistoryModify','Kontrahenci');
_obj.clear();
1


\portal_seod_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie danych na potrzeby portalu SEOD - opis
::  OLD: \portal_seod_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie danych na potrzeby portalu SEOD:\n'
'- dodanie parametru 465 - SEOD: Komentarz w akceptacji, odrzuceniu i zamknięciu\n'
'- aktualizacja identyfikatorów rekordów dla portalu\n'
'- aktualizacja uprawnień użytkowników na portalu\n'
'- uzupełnienie tabeli UD_SEOD\n'
'- dodanie przestrzenie wymiany danych PORTAL_SEOD_CFG i PORTAL_SEOD'


\portal_hr_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca obiekt do obsługi definicji wymiany danych
::  OLD: \portal_hr_obj/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'STR_FROM','STR_TO',
   'FROM','TO',
   'TREE_FROM','TREE_TO',
   'TREE_FROM_M','TREE_TO_M',
   'NDX',
   'move','sort','add',
   'clear'
);
_obj.move:="
   _pd_from:=.FROM;
   _pd_to:=.TO;
   _tab:={? var_pres('_a')=type_of('') || _a || '' ?};
   _method:={? var_pres('_b')=type_of('') || _b || '' ?};
   _copy:={? var_pres('_c')=type_of(1) || _c ?};
   {? _tab<>''
   || SYNC_DEF.cntx_psh();
      SYNC_DEF.index('TREE');
      SYNC_DEF.prefix(.TREE_FROM,.STR_FROM,_tab,);
      {? SYNC_DEF.first()
      || SYNC_DEF.cntx_psh();
         SYNC_DEF.prefix();
         SYNC_DEF.REKORD:=.TREE_TO;
         SYNC_DEF.SYNC_PD:=.TO;
         {? _copy
         || SYNC_DEF.add();
            __UPG.msg('Dodano obsługę tabeli %1 dla %2.'[_tab,.STR_TO])
         || SYNC_DEF.put();
            __UPG.msg('Przeniesiono obsługę tabeli %1 z %2 do %3.'[_tab,.STR_FROM,.STR_TO])
         ?};
         SYNC_DEF.cntx_pop()
      ?};
      SYNC_DEF.cntx_pop()
   ?};
   SYNC_MWA.cntx_psh();
   {? _tab<>''
   || SYNC_MWA.index(.NDX);
      SYNC_MWA.prefix(.STR_FROM,_tab,_method)
   || SYNC_MWA.index('PD_METH');
      SYNC_MWA.prefix(.STR_FROM,_method,)
   ?};
   {? SYNC_MWA.first()
   || {!
      |? _rep:=null;
         {? SYNC_MWA.SYNC_REP
         || SYNC_REP.cntx_psh();
            SYNC_REP.index('PDNR');
            SYNC_REP.prefix();
            {? ~SYNC_REP.find_key(.TO,SYNC_MWA.SYNC_REP().NR)
            || {? SYNC_REP.seek(SYNC_MWA.SYNC_REP)
               || SYNC_REP.SYNC_PD:=.TO;
                  SYNC_REP.add()
               ?}
            ?};
            _rep:=SYNC_REP.ref();
            SYNC_REP.cntx_pop()
         ?};
         SYNC_MWA.cntx_psh();
         SYNC_MWA.prefix();
         SYNC_MWA.PARENT:=.TREE_TO_M;
         SYNC_MWA.SYNC_PD:=.TO;
         SYNC_MWA.SYNC_REP:=_rep;
         SYNC_MWA.put();
         __UPG.msg('Przeniesiono obsługę komunikatu %1 z %2 do %3.'[_method,.STR_FROM,.STR_TO]);
         SYNC_MWA.cntx_pop();
         SYNC_MWA.first()
      !}
   ?};
   SYNC_MWA.cntx_pop()
";
_obj.sort:="
   _pd:=_a;
   SYNC_MWA.cntx_psh();
   SYNC_MWA.index('PD_LP');
   SYNC_MWA.prefix(_pd);
   {? SYNC_MWA.first()
   || _lp:=-1;
      {!
      |? SYNC_MWA.LP:=(_lp+=1);
         SYNC_MWA.put();
         SYNC_MWA.next()
      !}
   ?};
   SYNC_MWA.cntx_pop();
   __UPG.msg('Przenumerowano komunikaty dla %1.'[_pd])
";
_obj.add:="
   _pd_str:=_a;
   _typ:=_b;
   _tab:=_c;
   _api:=_d;
   _desc:=_e;
   _fml:={? var_pres('_f')=type_of('') || _f || ~~ ?};
   _before:={? var_pres('_g')=type_of('') || _g || '' ?};
   _firma:={? var_pres('_h')=type_of('') || _h || ~~ ?};
   _replika:={? var_pres('_i')=type_of(1) || _i || ~~ ?};
   SYNC_PD.cntx_psh();
   SYNC_PD.index('RSYM');
   SYNC_PD.prefix();
   {? ~SYNC_PD.find_key('I',_pd_str,)
   || SYNC_PD.blank(1);
      SYNC_PD.RODZAJ:='I';
      SYNC_PD.SYM:=_pd_str;
      SYNC_PD.add();
      __UPG.msg('Dodano przeznaczenie danych %1.'[_pd_str])
   ?};
   _pd:=SYNC_PD.ref();
   SYNC_PD.cntx_pop();
   SYNC_DEF.cntx_psh();
   SYNC_DEF.index('TREE');
   SYNC_DEF.prefix();
   {? ~SYNC_DEF.find_key(0,_pd_str,)
   || SYNC_DEF.blank();
      SYNC_DEF.SYNC_PD:=_pd;
      SYNC_DEF.add()
   ?};
   _sync_def:=SYNC_DEF.ref();
   {? ~SYNC_DEF.find_key(#_sync_def,_pd_str,_tab,)
   || SYNC_DEF.blank();
      SYNC_DEF.REKORD:=_sync_def;
      SYNC_DEF.SYNC_PD:=_pd;
      SYNC_DEF.ACR_TAB:=_tab;
      SYNC_DEF.ACR_FLD:='uidref';
      SYNC_DEF.LP:=1;
      SYNC_DEF.FORM:=_tab+'.uidref()';
      SYNC_DEF.WGIDPUT:='T';
      SYNC_DEF.TYP:='S';
      SYNC_DEF.FIR:={? var_pres('_firma')>0 || _firma || 'N' ?};
      SYNC_DEF.AKT:='T';
      {? var_pres('_fml')>0
      || SYNC_DEF.WAR_FORM:=_fml
      ?};
      SYNC_DEF.add();
      __UPG.msg('Dodano obsługę tabeli %1 dla %2.'[_tab,_pd_str])
   || _put:=0;
      {? var_pres('_firma')>0
      || SYNC_DEF.FIR:=_firma;
         _put:=1
      ?};
      {? var_pres('_fml')>0
      || SYNC_DEF.WAR_FORM:=_fml;
         _put:=1
      ?};
      {? _put || SYNC_DEF.put() ?}
   ?};
   SYNC_DEF.cntx_pop();
   SYNC_MWA.cntx_psh();
   SYNC_MWA.index('TREE');
   SYNC_MWA.prefix();
   {? ~SYNC_MWA.find_key(0,_pd_str,)
   || {? SYNC_MWA.find_key(0,'PORTAL_CFG',)
      || SYNC_MWA.SYNC_PD:=_pd;
         SYNC_MWA.add()
      ?}
   ?};
   _sync_mwa:=SYNC_MWA.ref();
   _lp:=-1;
   {? _before<>''
   || SYNC_MWA.index('PD_METH'); SYNC_MWA.prefix(_pd_str,_before,);
      _lp:={? SYNC_MWA.first() || SYNC_MWA.LP || -1 ?};
      {? _lp<>-1
      || SYNC_MWA.index('TREE1'); SYNC_MWA.prefix(_sync_mwa);
         {? SYNC_MWA.last()
         || {!
            |? {? SYNC_MWA.LP>=_lp
               || SYNC_MWA.LP+=1;
                  SYNC_MWA.put();
                  SYNC_MWA.prev()
               ?}
            !}
         ?}
      ?}
   ?};
   {? _lp=-1
   || SYNC_MWA.index('TREE1');
      SYNC_MWA.prefix(_sync_mwa);
      _lp:={? SYNC_MWA.last() || SYNC_MWA.LP+1 || 1 ?}
   ?};
   SYNC_MWA.index('PD_API');
   SYNC_MWA.prefix();
   {? ~SYNC_MWA.find_key(_pd,'portal.mwac',_api,_tab,_typ,)
   || SYNC_MWA.blank();
      SYNC_MWA.PARENT:=_sync_mwa;
      SYNC_MWA.SYNC_PD:=_pd;
      SYNC_MWA.AKT:='T';
      SYNC_MWA.TAB_ACR:=_tab;
      SYNC_MWA.METHOD:=_api;
      SYNC_MWA.TYPE:=_typ;
      SYNC_MWA.LP:=_lp;
      SYNC_MWA.MWAC:='portal.mwac';
      {? var_pres('_replika')>0
      || SYNC_MWA.SYNC_REP:=exec('FindInSet','#table','SYNC_REP','PDNR',_replika,_pd,,,,null)
      ?};
      {? SYNC_MWA.add()
      || SYNC_MWA.memo_set(_desc,'DESC');
         SYNC_MWA.memo_put(,'DESC');
         __UPG.msg('Dodano obsługę komunikatu %1 dla tabeli %2 w %3.'[_api,_tab,_pd_str])
      ?}
   || __UPG.msg('Istnieje już komunikatu %1 dla tabeli %2 w %3.'[_api,_tab,_pd_str])
   ?};
   SYNC_MWA.cntx_pop()
";
_obj.clear:="
   SYNC_MWA.ndx_drop(.NDX)
";
_obj.NDX:=SYNC_MWA.ndx_tmp('',1,
   'SYNC_PD','SYM',0,
   'TAB_ACR',,0,
   'METHOD',,0
);
_obj


\portal_hr_cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zmiana przeznaczenia danych PORTAL_CFG na PORTAL_HR_CFG
::  OLD: \portal_hr_cfg/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_obj:=exec('portal_hr_obj','upgradexperti2');
_obj.STR_FROM:='PORTAL_CFG';
_obj.STR_TO:='PORTAL_HR_CFG';

SYNC_PD.cntx_psh();
SYNC_PD.index('RSYM');
SYNC_PD.prefix();
{? ~SYNC_PD.find_key('I',_obj.STR_TO,)
|| _obj.FROM:=null;
   {? SYNC_PD.find_key('I',_obj.STR_FROM)
   || SYNC_PD.SYM:=_obj.STR_TO;
      SYNC_PD.put();
      _obj.FROM:=SYNC_PD.ref();
      __UPG.msg('Zmieniono definicję wymiany danych z %1 na %2.'[_obj.STR_FROM,_obj.STR_TO])
   ?};
   {? ~SYNC_PD.find_key('I',_obj.STR_FROM)
   || SYNC_PD.blank(1);
      SYNC_PD.SYM:=_obj.STR_FROM;
      SYNC_PD.RODZAJ:='I';
      SYNC_PD.add();
      __UPG.msg('Dodano definicję wymiany danych z %1.'[_obj.STR_FROM])
   ?};
   _obj.TO:=SYNC_PD.ref();
   _hlp:=_obj.STR_FROM;
   _obj.STR_FROM:=_obj.STR_TO;
   _obj.STR_TO:=_hlp;
   {? _obj.FROM
   || SYNC_DEF.cntx_psh();
      SYNC_DEF.index('TREE');
      SYNC_DEF.prefix();
      {? ~SYNC_DEF.find_key(0,_obj.STR_TO,)
      || {? SYNC_DEF.find_key(0,_obj.STR_FROM,)
         || _obj.TREE_FROM:=SYNC_DEF.ref();
            SYNC_DEF.SYNC_PD:=_obj.TO;
            SYNC_DEF.add()
         ?}
      ?};
      _obj.TREE_TO:=SYNC_DEF.ref();
      SYNC_DEF.cntx_pop();
      SYNC_MWA.cntx_psh();
      SYNC_MWA.index('TREE');
      SYNC_MWA.prefix();
      {? ~SYNC_MWA.find_key(0,_obj.STR_TO,)
      || {? SYNC_MWA.find_key(0,_obj.STR_FROM,)
         || _obj.TREE_FROM_M:=SYNC_MWA.ref();
            SYNC_MWA.SYNC_PD:=_obj.TO;
            SYNC_MWA.add()
         ?}
      ?};
      _obj.TREE_TO_M:=SYNC_MWA.ref();
      SYNC_MWA.cntx_pop();

      _obj.move(,'SL_GETBYPOSTDATAABOUTENTITIESTOSYNC');
      _obj.move(,'sl_TenantGenericDashItemsModify');
      _obj.move('PORSLO','sl_TenantLookupCodes',1);
      _obj.move('PORSLOIT','sl_TenantLookupCodeItems',1);
      _obj.move('PORTALK');
      _obj.move('PORTALL');
      _obj.move('PORTALO');
      _obj.move('PORTALU');
      _obj.move('USERS');
      _obj.move('USR_SIGN');

      _obj.sort(_obj.STR_FROM);
      _obj.sort(_obj.STR_TO);

      _obj.add('PORTAL_CFG','Uruchomienie','FIRMA','csl_TenantCompanyModify','Firma',$"exec('FIRMA_ok','portal_seod')");
      _obj.add('PORTAL_CFG','Wysyłanie','FIRMA','csl_TenantCompanyModify','Firma');
      _obj.add('PORTAL_CFG','Uruchomienie','USERSF','csl_TenantUserCompanyModify','Użytkownicy firmy',
         $"exec('USERSF_ok','portal_seod')"
      );
      _obj.add('PORTAL_CFG','Uruchomienie','USERSF','csl_TenantUserCompanyDelete','Użytkownicy firmy');
      _obj.add('PORTAL_CFG','Wysyłanie','USERSF','csl_TenantUserCompanyModify','Użytkownicy firmy');
      ~~
   ?}
|| __UPG.msg('Istnieje już definicja wymiany danych %1.'[_obj.STR_TO])
?};
SYNC_PD.cntx_pop();
1


\portal_hr_cfg_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zmiana przeznaczenia danych PORTAL_CFG na PORTAL_HR_CFG - opis
::  OLD: \portal_hr_cfg_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'- Utworzenie przestrzeni wymiany danych PORTAL_HR_CFG\n'
'- Uzupełnienie komunikatów PORTAL_CFG o obsługę tabeli FIRMA i USERSF'


\portal_cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie komunikatów dla PORTAL_CFG
::  OLD: \portal_cfg/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init','sync_id');
__UPG.msg('Zaktualizowano identyfikatory rekordów dla portalu.');

_obj:=exec('portal_hr_obj','upgradexperti2');
_obj.add('PORTAL_CFG','Wysyłanie','PORTALAH','sl_TenantExfAvailCtrlValSetModify','Dostępność pól',,'sl_TenantDataDictionaryDelete');
_obj.add('PORTAL_CFG','Usuwanie','PORTALAH','sl_TenantExfAvailCtrlValSetDelete','Dostępność pól',,'sl_TenantExfAvailCtrlValSetModify');

exec('PORTALAH_init','portal_seod');
1


\portal_cfg_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie komunikatów dla PORTAL_CFG - opis
::  OLD: \portal_cfg_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'- aktualizacja identyfikatorów rekordów\n'
'- dodanie obsługi kounikatu sl_TenantExfAvailCtrlValSetModify\n'
'- dodanie obsługi kounikatu sl_TenantExfAvailCtrlValSetDelete'


\fiks_81
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2203/0051 - e-sprawozdanie jednostkowe inne - łączne ale nie dla spółek - pole P_4
::  OLD: \fiks_81/napraw_f.fml
::  OLD: \fiks_81/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Typ');
_typ:=Typ:=obj_new('test','res');
_typ.test:={? var_pres('_a')>0 || _a=1 ?};
_typ.res:=1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF','JednostkaInnaW');
{? ISTDEF.first()
|| _fun:="
      {? ISTDEFS.find_key(_a,)
      || {? Typ.test
         || {? ISTDEFS.REGULY<>_c || Typ.res:=0 ?}
         || {? ISTDEFS.REGULY=_b
            || ISTDEFS.REGULY:=_c;
               ISTDEFS.put();
               __UPG.msg('Zmieniono element %1 schematu %2.'[_a,ISTDEF.VER])
            || __UPG.msg('Zmiana elementu %1 schematu %2 nie była wymagana.'[_a,ISTDEF.VER])
            ?}
         ?}
      || Typ.res:=-1;
         __UPG.msg('Nie znaleziono elementu %1 schematu %2.'[_a,ISTDEF.VER])
      ?}
   ";
   {!
   |? {? ISTDEF.VER+1='2'
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS');
         ISTDEFS.prefix(ISTDEF.ref());
         _fun(
            'tns:P_6',
            $"JPK.E_SPR().L_TN='T'",
            $"JPK.E_SPR().L_PRZED<>'N'"
         );
         _fun(
            'tns:P_6A',
            $"{? JPK.E_SPR().L_PRZED='T' || 'false' || 'true' ?}",
            $"{? JPK.E_SPR().L_PRZED='B' || 'false' || 'true' ?}"
         );
         ISTDEFS.cntx_pop()
      ?};
      ISTDEF.next()
   !}
?};
ISTDEF.cntx_pop();
VAR_DEL.delete('Typ');
_typ.res


\fiks_81_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2203/0051 - e-sprawozdanie jednostkowe inne - łączne ale nie dla spółek - pole P_4
::  OLD: \fiks_81_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Poprawka błędu w schemacie e-Sprawozdań.\n'
'ER/WRT/XP/12.51/2203/0051'


\fiks_86
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2210/0023 - schemat e-deklaracji CIT8wer.31
::----------------------------------------------------------------------------------------------------------------------
_res:=-1;
VAT_VER.cntx_psh();
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','CIT8','CIT8',31);
_ver:={? VAT_VER.first() || VAT_VER.ref() || null ?};
VAT_VER.cntx_pop();
{? _ver
|| ISTDEF.cntx_psh();
   ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',_ver);
   {? ISTDEF.first()
   || _res:=0;
      _fml:="
         _tag:=_a;
         _reg:=_b;
         _wym:=_c;
         _res:=0;
         ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
         {? ISTDEFS.find_key(_tag,)
         || {? ISTDEFS.REGULY<>_reg | ISTDEFS.WYM<>_wym
            || ISTDEFS.REGULY:=_reg;
               ISTDEFS.WYM:=_wym;
               _res:=ISTDEFS.put()
            ?}
         ?};
         ISTDEFS.cntx_pop();
         _res
      ";
      _res+=_fml('P_315',$"{? exec('poz_edek_dod','xml','CIT8 ',1,316)<>'' || '1' || '2' ?}",'N');
      _res+=_fml('P_316',$"exec('poz_edek_dod','xml','CIT8 ',1,316)",'N')
   ?};
   ISTDEF.cntx_pop()
?};
{? _res=-1
|| __UPG.msg('Nie znaleziono schematu e-deklaracji CIT-8 (31).')
|? _res=0
|| __UPG.msg('Zmiany w schemacie e-deklaracji CIT-8 (31) nie były wymagane.')
|| __UPG.msg('Poprawiono schemat e-deklaracji CIT-8 (31).')
?};
1


\fiks_86_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2210/0023 - schemat e-deklaracji CIT8wer.31 - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/12.51/2210/0023\n'
'Naprawa obsługi pola P_316 w schemacie e-deklatacji CIT-8 (31)'


\fiks_87
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2303/0079 - schemat e-deklaracji CIT 8 wersja 32
::----------------------------------------------------------------------------------------------------------------------
_res:=-1;
VAT_VER.cntx_psh();
VAT_VER.index('VER_OD'); VAT_VER.prefix('FKS','CIT8','CIT8',date(2022,01,01),32);
_ver:={? VAT_VER.first() || VAT_VER.ref() || null ?};
VAT_VER.cntx_pop();
{? _ver
|| ISTDEF.cntx_psh();
   ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',_ver);
   {? ISTDEF.first()
   || _res:=0;
      _fml:="
         _tag:=_a;
         _reg:=_b;
         _wym:=_c;
         _res:=0;
         ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
         {? ISTDEFS.find_key(_tag,)
         || {? ISTDEFS.REGULY<>_reg | ISTDEFS.WYM<>_wym
            || ISTDEFS.REGULY:=_reg;
               ISTDEFS.WYM:=_wym;
               _res:=ISTDEFS.put()
            ?}
         ?};
         ISTDEFS.cntx_pop();
         _res
      ";
      _res+=_fml('P_35',$"P_36:=exec('poz_edek_dod','xml','CIT8 ',1,36); {? P_36='0' | P_36='' || '' || '1'  ?}",'N');
      _res+=_fml('P_36',$"_r:=P_36; &P_36; _r",'N')
   ?};
   ISTDEF.cntx_pop()
?};
{? _res=-1
|| __UPG.msg('Nie znaleziono schematu e-deklaracji CIT-8 (32).')
|? _res=0
|| __UPG.msg('Zmiany w schemacie e-deklaracji CIT-8 (32) nie były wymagane.')
|| __UPG.msg('Poprawiono schemat e-deklaracji CIT-8 (32).')
?};
1


\fiks_87_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2303/0079 - schemat e-deklaracji CIT 8 wersja 32 - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2303/0079\n'
'Naprawa obsługi pola P_35 w schemacie e-deklatacji CIT-8 (32)'


\warlog_lopis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0050 - zmienna dla formuł LOPIS
::       ER/WRT/XP/22.26/2303/0053 - LKONTO1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
WARLOG.cntx_psh();
WARLOG.index('KKOD');
WARLOG.prefix();
{? WARLOG.find_key('K','LOPIS',) & WARLOG.FO*'PAR_SKID.get(300)'
|| WARLOG.FO:=STR.gsub(WARLOG.FO,' | PAR_SKID.get(300)=\'N\' ',' ');
   {? WARLOG.put()
   || __UPG.msg('Zmodyfikowano treść formuły stałej LOPIS.'@); _result:=1
   || __UPG.msg('Nie powiodła się modyfikacja treść formuły stałej LOPIS.'@)
   ?}
||  __UPG.msg('Nie stwierdzono potrzeby modyfikacji treści formuły stałej LOPIS.'@); _result:=-1
?};
{? WARLOG.find_key('K','LKONTO1',) & WARLOG.FO*'kontohis'
|| _sstr:=STR.gsub(WARLOG.FO,'kontohis\'','pracownik*');
   WARLOG.FO:=STR.gsub(_sstr,'*','\',LS.P');
   {? WARLOG.put()
   || __UPG.msg('Zmodyfikowano treść formuły stałej LKONTO1.'@); _result:=1
   || __UPG.msg('Nie powiodła się modyfikacja treść formuły stałej LKONTO1.'@)
   ?}
||  __UPG.msg('Nie stwierdzono potrzeby modyfikacji treści formuły stałej LKONTO1.'@); _result:=-1
?};
WARLOG.cntx_pop();
_result


\warlog_lopis_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0050 - zmienna dla formuł LOPIS
::       opis
::----------------------------------------------------------------------------------------------------------------------
'Z treści formuły na zmienną usunięto sekwencję opartą o nieaktywny parametr 300'


\zle_dlugi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: Realizacja propozycji: Ulga na złe długi VAT tryb 05 FV sprzedaży
::  OLD: \zle_dlugi/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=-1;
{? RS.find_key('~Rozliczenie VAT')
|| _ret:=exec('new_par','slo_slu',RS.ref(),6,'Uwzględniaj w "złych długach"','TAK_NIE')
?};
{? _ret<0
|| __UPG.msg('Nie powiodło się dodanie nowego parametru (6) do słownika opartego o wzorzec ~Rozliczenie VAT.')
|? _ret>0
|| __UPG.msg('Dodano nowy parametr (6) do słownika opartego o wzorzec ~Rozliczenie VAT.')
|| __UPG.msg('Istnieje parametr (6) dot. złych długów w słowniku opartym o wzorzec ~Rozliczenie VAT.')
?};
_ret:=-1;
{? RS.find_key('~Rozliczenie VAT')
|| _ret:=exec('new_par','slo_slu',RS.ref(),7,'Rozlicz max. w dacie sprzedaży, jeśli nie wystąpiła zapłata.','TAK_NIE')
?};
{? _ret<0
|| __UPG.msg('Nie powiodło się dodanie nowego parametru (7) do słownika opartego o wzorzec ~Rozliczenie VAT.')
|? _ret>0
|| __UPG.msg('Dodano nowy parametr (7) do słownika opartego o wzorzec ~Rozliczenie VAT.')
|| __UPG.msg('Istnieje parametr (7) dot. złych długów w słowniku opartym o wzorzec ~Rozliczenie VAT.')
?};
1


\zle_dlugi_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: Realizacja propozycji: Ulga na złe długi VAT tryb 05 FV sprzedaży
::  OLD: \zle_dlugi_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych parametrów do obsługi słownika opartego o wzorzec ~Rozliczenie VAT.'


\ser_sch_ods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/21.37/2204/0063
::  OLD: \ser_sch_ods/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.cntx_psh();
SER_SCH.index('KOD'); SER_SCH.prefix();
_size:=SER_SCH.size(); ile:=0;
{? _size || SER_SCH.for_each("{? SER_SCH.CZY_ODS<>'T' || SER_SCH.CZY_ODS:='T'; ile+=SER_SCH.put() ?}") ?};
SER_SCH.cntx_pop();
__UPG.msg('Wynik dotyczy parametru, czy naliczać odsetki przy notach odsetkowych dla schematów windykacyjnych.');
{? _size=0
|| __UPG.msg('Brak schematów windykacyjnych.')
|? ile=0
|| __UPG.msg('Nie stwierdzono potrzeby zmiany wartości parametru.')
|| __UPG.msg('Zmieniono wartość parametru dla %1 schematów windykacyjnych'[$ile])
?};
1


\ser_sch_ods_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/21.37/2204/0063
::  OLD: \ser_sch_ods_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Brak możliwości zdefiniowania schematu windykacyjnego dla wezwań bez generowania odsetek'


\jpk_Asi_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Dodanie nowych schematów JPK_ASI
::  OLD: \jpk_Asi_sch/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_txt:='AsiWZlotych(1)_v1-2';
_imp:=exec('upg_imp','xml','FKS','J','SF','AsiWZlotych(1)_v1-2',date(2019,9,1),'jpk_asi_v1-2.dfg');
{? _imp=1
|| __UPG.msg(_txt+' został dodany.')
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.')
|| __UPG.msg(_txt+' nie został dodany.');
   _ok:=0
?};
_ok


\jpk_Asi_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Dodanie nowych schematów JPK_ASI - opis
::  OLD: \jpk_Asi_sch_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych schematów: JPK_ASI\n'
'Schematy obowiązują od 1 września 2019 r.'


\jpk_d1_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Naprawa pozycji D.2.1 deklaracji JPK_V7
::  OLD: \jpk_d1_upd/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_lrec:=0;
VAT_DEK.cntx_psh(); VAT_POZ.cntx_psh();
VAT_DEK.index('VAT_DEK');
VAT_DEK.prefix(date(2022,01,01),'V7');
{? VAT_DEK.first()
|| {!
   |? VAT_POZ.index('POZ1');
      VAT_POZ.prefix(VAT_DEK.ref(),40);
      {? VAT_POZ.first()
      || {? VAT_POZ.TYP='R'
         || VAT_POZ.TYP:='A';
            _tmp:=VAT_POZ.put();
            {? _tmp || _lrec+=1 ?};
            {? _ret || _ret:=_tmp ?}
         ?}
      ?};
      VAT_DEK.next()
   !}
?};
VAT_DEK.cntx_pop(); VAT_POZ.cntx_pop();
{? _ret
|| __UPG.msg('Zaktualizowano pole nr 40 w %1 deklaracjach JPK_V7'[$_lrec])
|| __UPG.msg('Zaktualizowano pole nr 40 w %1 deklaracjach JPK_V7'[$_lrec]);
   __UPG.msg('Aktualizacja pola nr 40 w deklaracjach JPK_V7 nie powiodła się.')
?};
_ret


\jpk_d1_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Naprawa pozycji D.2.1 deklaracji JPK_V7 - opis
::  OLD: \jpk_d1_upd_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Naprawa pozycji D.2.1 deklaracji JPK_V7'


\proc_ksef_emag2fiks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Dodaje formułę kontrolną do sprawdzenia zgodności procedur szczególnych KSeF między dokumentami księgowymi
::       w rejestrach, a dokumentami oznaczonymi tymi procedurami w logistyce.
::  OLD: \proc_ksef_emag2fiks/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G','PROC_KSF',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:='PROC_KSF';
   FORMULA.NAZWA:='Formuła kon. sprawdzająca zgodność procedur szczególnych';
   FORMULA.FORMULA:='exec(\'proc_ksef\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=1
?};
FORMULA.cntx_pop();
{? _ret
|| __UPG.msg('Dodano formułę kontrolną PROC_KSF.')
|| __UPG.msg('Dodanie formuły kontrolnej PROC_KSF nie powiodło się.')
?};
_ret


\proc_ksef_emag2fiks_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Dodaje formułę kontrolną do sprawdzenia zgodności procedur szczególnych KSeF między dokumentami księgowymi
::       w rejestrach, a dokumentami oznaczonymi tymi procedurami w logistyce - opis
::  OLD: \proc_ksef_emag2fiks_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły kontrolnej PROC_KSEF do sprawdzenia zgodności procedur szczególnych KSeF między dokumentami księgowym'
' w rejestrach, a dokumentami oznaczonymi tymi procedurami w logistyce.'


\cit8_31_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji
::  OLD: \cit8_31_akt/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
res:=0;
VAT_VER.cntx_psh();
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','CIT8','CIT8',31);
_ver:={? VAT_VER.first() || VAT_VER.ref() || null ?};
VAT_VER.cntx_pop();
{? _ver
|| ISTDEF.cntx_psh();
   ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',_ver);
   {? ISTDEF.first()
   || ISTDEFS.cntx_psh();
      ISTDEFS.index('LP');
      exec('pomoc_85','upgradexperti2',205,185); exec('pomoc_85','upgradexperti2',307,264); exec('pomoc_85','upgradexperti2',309,259);
      exec('pomoc_85','upgradexperti2',310,265); exec('pomoc_85','upgradexperti2',312,260); exec('pomoc_85','upgradexperti2',313,266);
      exec('pomoc_85','upgradexperti2',315,261); exec('pomoc_85','upgradexperti2',316,267); exec('pomoc_85','upgradexperti2',318,262);
      exec('pomoc_85','upgradexperti2',319,268); exec('pomoc_85','upgradexperti2',321,263); exec('pomoc_85','upgradexperti2',322,269);
      exec('pomoc_85','upgradexperti2',324,270); exec('pomoc_85','upgradexperti2',325,276); exec('pomoc_85','upgradexperti2',327,271);
      exec('pomoc_85','upgradexperti2',328,277); exec('pomoc_85','upgradexperti2',330,272); exec('pomoc_85','upgradexperti2',331,278);
      exec('pomoc_85','upgradexperti2',333,273); exec('pomoc_85','upgradexperti2',334,279); exec('pomoc_85','upgradexperti2',336,274);
      exec('pomoc_85','upgradexperti2',337,280); exec('pomoc_85','upgradexperti2',339,275); exec('pomoc_85','upgradexperti2',340,281);
      exec('pomoc_85','upgradexperti2',342,282); exec('pomoc_85','upgradexperti2',343,288); exec('pomoc_85','upgradexperti2',345,283);
      exec('pomoc_85','upgradexperti2',346,289); exec('pomoc_85','upgradexperti2',348,284); exec('pomoc_85','upgradexperti2',349,290);
      exec('pomoc_85','upgradexperti2',351,285); exec('pomoc_85','upgradexperti2',352,291); exec('pomoc_85','upgradexperti2',354,286);
      exec('pomoc_85','upgradexperti2',355,292); exec('pomoc_85','upgradexperti2',357,287); exec('pomoc_85','upgradexperti2',358,293);
      exec('pomoc_85','upgradexperti2',360,294); exec('pomoc_85','upgradexperti2',361,299); exec('pomoc_85','upgradexperti2',363,295);
      exec('pomoc_85','upgradexperti2',364,300); exec('pomoc_85','upgradexperti2',366,296); exec('pomoc_85','upgradexperti2',367,301);
      exec('pomoc_85','upgradexperti2',369,297); exec('pomoc_85','upgradexperti2',370,302); exec('pomoc_85','upgradexperti2',372,298);
      ISTDEFS.cntx_pop()
   ?};
   ISTDEF.cntx_pop()
?};

_txt:='e-Deklaracja CIT-8 (31) obowiązująca za 2021 rok';
{? res=1
|| __UPG.msg(_txt+' została naprawiona.');  _ok:=1
|? res=0
|| __UPG.msg(_txt+' nie istnieje w systemie.');  _ok:=1
|| __UPG.msg(_txt+' nie wymagała naprawy.');
   _ok:=1
?};
&res; _ok


\pomoc_85
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Pomocnicza do ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji
::   WE: _a - LP , _b - na co zmienić
::  OLD: \pomoc_85/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_b:=$_b;
ISTDEFS.prefix(ISTDEF.ref(),_a);
{? ISTDEFS.first()
|| _fml:=$"exec('poz_edek_dod','xml','CIT8 ',1,"+_b+')'; _opis:='P_'+_b;
   res:={? ISTDEFS.REGULY=_fml || 2 || ISTDEFS.REGULY:=_fml; ISTDEFS.OPIS:=_opis; res:=ISTDEFS.put() ?}
?};
res


\cit8_31_akt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji - opis.
::  OLD: \cit8_31_akt_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji'


\KH_POW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Aktualizacja danych dotyczących powiązań kontrahentów
::  OLD: \KH_POW/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
__lrec:=0;
__lmod:=0;
_ok:=0;
KH_POWR.trig_off('*','*');
KH_POWR.cntx_psh();
KH_POWR.index('KOD');
KH_POWR.prefix('NOK',);
{? ~KH_POWR.first()
|| KH_POWR.KOD:='NOK';
   KH_POWR.OPIS:='Nieokreślone powiązanie';
   KH_POWR.SYSTEM:='T';
   {? KH_POWR.add()
   || _ok:=1
   ?}
|| _ok:=1
?};
{? _ok
|| KH_POW.trig_off('*','*');
   KH_POW.cntx_psh();
   KH_POW.prefix();
   KH_POW.for_each("
      __lrec+=1;
      _put:=0;
      {? KH_POW.RODZAJ=null
      || KH_POW.RODZAJ:=KH_POWR.ref();
         {? KH_POW.put() || __lmod+=1 ?}
      ?}
   ",1);
   KH_POW.cntx_pop();
   KH_POW.trig_on('*','*')
?};
KH_POWR.cntx_pop();
KH_POWR.trig_on('*','*');
__UPG.msg('Zaktualizowano rekordy powiązania kontrahentów bez określonego rodzaju powiązania');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\KH_POW_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Aktualizacja danych dotyczących powiązań kontrahentów
::  OLD: \KH_POW_desc/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie systemowego rekord do tabeli KH_POWR\n'
'Aktualizacja danych dotyczących powiązań kontrahentów'


\etypy_2203_0061
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0061: Portal HR - Wysłane prawa do wierszy dla wniosków paperless gdy nie ma licencji PEP
::       Błąd powstał po imporcie danych wzorcowych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
{? exec('lic','#b_domain','PEP')=0
|| ETYPY.cntx_psh();
   ETYPY.prefix();
   ETYPY.for_each("
      {? ETYPY.W_PORTAL='P'
      || __lrec+=1;
         {? ETYPY.AKTYWNY='T'
         || ETYPY.AKTYWNY:='N';
            {? ETYPY.put(1) || __lmod+=1 ?}
         ?}
      ?}
   ",1);
   ETYPY.cntx_pop()
?};
{? __lrec
|| __UPG.msg('Zaktualizowano typy wniosków paperless.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Nie była konieczna aktualizacja typów wniosków paperless.')
?};
VAR_DEL.delete('__lrec','__lmod');
1


\etypy_2203_0061_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0061: Portal HR - Wysłane prawa do wierszy dla wniosków paperless gdy nie ma licencji PEP
::----------------------------------------------------------------------------------------------------------------------
'Wysłane prawa do wierszy dla wniosków paperless gdy nie ma licencji PEP.\n'
'Poprawka błędu ER/WRT/XP/21.37/2203/0061'


\portal_kart_url_napraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2204/0036 - nieprawidłowo wysyłane limity urlopowe.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na HR Portal - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

_ret:=1;
P.cntx_psh();
P.index('OSOBA');
P.prefix();
{? P.first()
|| _stat:=obj_new('prac','kart','do_zaznaczenia','zaznaczonych','niezaznaczonych');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};

   N.cntx_psh();
   N.index('NIEOPRAC');
   N.prefix();

   KART_URL.cntx_psh();
   KART_URL.index('PRAC_ROK');
   KART_URL.trig_off('*','*');
   {!
   |? _stat.prac+=1;

      {? ~(P.DZA~2=1 & P.DZA~3=1)
      || KART_URL.prefix(P.ref());
         {? KART_URL.first()
         || _stat.kart+=1;
            {? P.DZA=KART_URL.DATA_DOD | N.find_key(P.ref(),P.DZA)
            || _stat.do_zaznaczenia+=1;
               {? KART_URL.put(,1)
               || _stat.zaznaczonych+=1
               || _stat.niezaznaczonych+=1
               ?}
            ?}
         ?}
      ?};
      P.next()
   !};
   KART_URL.trig_on('*','*');
   KART_URL.cntx_pop();

   N.cntx_pop();

   __UPG.msg('Zweryfikowanych pracowników: %1'[$_stat.prac]);
   __UPG.msg('Zweryfikowanych kart urlopowych: %1'[$_stat.kart]);
   __UPG.msg('Kart urlopowych do zaznaczenia: %1'[$_stat.do_zaznaczenia]);
   __UPG.msg('Kart urlopowych zaznaczonych: %1'[$_stat.zaznaczonych]);
   __UPG.msg('Kart urlopowych niezaznaczonych: %1'[$_stat.niezaznaczonych]);
   _ret:={? _stat.do_zaznaczenia=_stat.zaznaczonych || 1 || -1 ?}
?};
P.cntx_pop();
_ret


\portal_kart_url_napraw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2204/0036 - nieprawidłowo wysyłane limity urlopowe - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/21.37/2204/0036 - zaznaczenie kart urlopowych, które mogły zostać nieprawidłowo wysłane na portal'


\p_pz_klasa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2205/0043 - usunięcie z portalu rekordów niebędących zastępstwami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na dziedzinę "HR Portal" - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

P_PZ.cntx_psh();
P_PZ.prefix();
{? P_PZ.first()
|| P_PZ.trig_off('*','*');

   _stat:=obj_new('Z','nieZ','poprawionych','niepoprawionych');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};

   {!
   |? {? P_PZ.KLASA='Z'
      || _stat.Z+=1
      || _stat.nieZ+=1;
         {? P_PZ.put(,1)
         || _stat.poprawionych+=1
         || _stat.niepoprawionych+=1
         ?}
      ?};
      P_PZ.next()
   !};
   __UPG.msg('Rekordów w tabeli P_PZ: %1'[$P_PZ.size()]);
   __UPG.msg(' * Rekordów dotyczących zastępstw (niewymagających zmian): %1'[$_stat.Z]);
   __UPG.msg(' * Rekordów niedotyczących zastępstw (potencjalnie do usunięcia z portalu): %1'[$_stat.nieZ]);
   __UPG.msg('   - Rekordów zaznaczonych do weryfikacji: %1'[$_stat.poprawionych]);
   __UPG.msg('   - Rekordów, których nie udało się zaznaczyć: %1'[$_stat.niepoprawionych]);
   P_PZ.trig_on('*','*');

   _ret:={? _stat.niepoprawionych=0 || 1 || -1 ?}

|| __UPG.msg('Tabela P_PZ nie zawiera zapisów.');
   _ret:=1
?};
P_PZ.cntx_pop();
_ret


\p_pz_klasa_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2205/0043 - usunięcie z portalu rekordów niebędących zastępstwami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/21.37/2205/0043 - usunięcie z portalu rekordów niebędących zastępstwami'


\rubryki_placowe_PL_lip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie rubryk płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
::_txt:='';
exec('__RUB','object');
__RUB.fill();

:: Wykonane po stronie Xpertis

::{? +exec('import','rubryki','g','7182,7183,7184,780',1,0)
::|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
::   _result:=-1
::|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
::?};
::__UPG.msg(_txt);
::{? _result=-1 || return(_result) ?};

:: Aktualizacja atrybutów rubryk. -------------------------------------------------------------------------------------
::exec('add_attr','rubatr',55,552,'Zasiłki macierzyńskie',1,
::   'Zasiłki macierzyńskie.',,523,524,7017);
::exec('add_attr','rubatr',902,90203,'ZP: Przychód z zasiłku macierzyńskiego',1,
::   'Przychód z zasiłku macierzyńskiego.',,7184);
::exec('add_attr','rubatr',902,90204,'ZP: Przychód ze stosunku pracy',1,
::   'Przychód ze stosunku pracy i pokrewnych.',,7182);
::exec('add_attr','rubatr',902,90205,'ZP: Przychód z umowy zlecenia',1,
::   'Przychód z umowy zlecenia.',,7183);
::exec('add_use','rubatr',90202,7182,7183,7184);

RA_DEF.cntx_psh();
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
_tablica:=obj_new(4);
_tablica[1]:=9021;
_tablica[2]:=9028;
_tablica[3]:=9029;
_tablica[4]:=90201;

{! _ind:=1..obj_len(_tablica)
|! {? RA_DEF.find_key(_tablica[_ind])
   || RA_USE.index('RA_USE');
      {! _kor:=0..1
      |! RA_USE.prefix(RA_DEF.ref(),__RUB.ref(523+_kor));
         {? RA_USE.first()
         || RA_USE.DATA:=date(2022,7,1);
            RA_USE.STAN:='N';
            RA_USE.add(1)
         ?}
      !};
      RA_USE.prefix(RA_DEF.ref(),__RUB.ref(7017));
      {? RA_USE.first()
      || RA_USE.DATA:=date(2022,7,1);
         RA_USE.STAN:='N';
         RA_USE.add(1)
      ?}
   ?}
!};
obj_del(_tablica);
RA_DEF.cntx_pop();

R.cntx_psh();
R.index('RUBKOD');
R.prefix(780);
{? R.first() & R.RT='Przychód'
|| R.RT:='Podst. opodatkowania';
   R.put()
?};
R.cntx_pop();

_result


\rubryki_placowe_PL_lip_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie rubryk płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk płacowych.'


\KAL_BUFF_CZYTNIK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.14]
:: OPIS: ER/WRT/XP/21.14/2109/0015: Brak możliwości edycji dnia w harmonogramie
::----------------------------------------------------------------------------------------------------------------------
R_OPCZYT.cntx_psh();
_ndx:=R_OPCZYT.ndx_tmp('',1,'O',,0, 'O',,0);
R_OPCZYT.index(_ndx);

_result:=0;
KAL_BUFF.cntx_psh();
KAL_BUFF.index('PROKRDT');
KAL_BUFF.prefix('Z');
_lrec:=0;
{? KAL_BUFF.first()
|| _lrec:=KAL_BUFF.size();
   PROGRESS.set(_lrec,'Przetwarzanie rekordów tabeli KAL_BUFF...');
   {!
   |? {? +form(KAL_BUFF.OPIS)
      || R_OPCZYT.prefix(KAL_BUFF.OPIS,);
         {? R_OPCZYT.first()
         || KAL_BUFF.OPIS:=$R_OPCZYT.ref();
            _result+=KAL_BUFF.put()
         ?}
      ?};
      PROGRESS.next();
      KAL_BUFF.next()
   !};
   PROGRESS.close()
?};
KAL_BUFF.cntx_pop();
R_OPCZYT.cntx_pop();
R_OPCZYT.ndx_drop(_ndx);

{? _lrec>0
|| __UPG.msg('Zaktualizowano pole OPIS dla tabeli KAL_BUFF.');
   __UPG.msg('Przetworzono: %1 rekordów, z czego zmodyfikowano: %2.'[$_lrec,$_result])
|| __UPG.msg('Nie znaleziono rekordów tabeli KAL_BUFF wymagających aktualizacji pola OPIS.')
?};
1


\KAL_BUFF_CZYTNIK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.14]
:: OPIS: ER/WRT/XP/21.14/2109/0015: Brak możliwości edycji dnia w harmonogramie
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola KAL_BUFF.OPIS dla zatwierdzonych wykonań.'


\pkd_kod_4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Przywrócenie możliwości rejestracji kodu 4 dla nieobecności
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

exec('__RUB','object');
__RUB.fill();

Cntx.psh(RA_DEF,RA_USE);
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S',);
RA_USE.index('RA_USE');
{? RA_DEF.find_key(1291)
|| RA_USE.prefix(RA_DEF.ref(),__RUB.ref(4),date(2022,1,1));
  {? RA_USE.first() || RA_USE.del() ?}
?};
Cntx.pop(RA_DEF,RA_USE);
__UPG.msg('Zakończono aktualizację wykorzystania atrybutu składnika płac.');

_result


\pkd_kod_4_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Przywrócenie możliwości rejestracji kodu 4 dla nieobecności - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Przywrócenie możliwości rejestracji kodu 4 dla nieobecności'


\weryfikacja_ZP_PL_lip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.51]
:: OPIS: Weryfikacja zapisów dodatkowych w kartotece rozliczenia zwolnienia przychodu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();
:: obiekt do wyliczenia zwolnienia z przychodu
exec('__ZW_PRZ','object');

_wyn:=0;
{? ~__ZW_PRZ.JEST_PL
|| _txt:='Brak w systemie zwolnienie od przychodu - Polski Ład';
   __UPG.msg(_txt);
   _result:=-1;
   return(_result)
?};
P.cntx_psh();
OSOBA.cntx_psh();
LS.cntx_psh();
O.cntx_psh();
O.index('LISTYPLP');
P.clear;
OS_ZWPOZ.cntx_psh();

O.prefix(exec('ref_firma','ustawienia'),'P',2022);
{? O.first()
|| {!
   |? {? LS.use(O.LT)
      || {? var_pres('_tab_P')>0 || &_tab_P ?};
         _rub9022:=__RUB.sys_sql(9022);
         {? _rub9022='' || _rub9022:='0' ?};
         _tab_P:=sql('select LS.P from LS join R where R.RN in (:_a)',_rub9022);
         {? _tab_P.first()
         || {!
            |? {? P.seek(_tab_P.P)
               || P.OSOBA();
                  OS_ZWPOZ.index('PRACROK');
                  OS_ZWPOZ.prefix(P.ref,2022,O.MP,O.ref);
                  {? ~OS_ZWPOZ.first
                  || __ZW_PRZ.create();
                     {? __ZW_PRZ.KOD<>''
                     || _poz:=__ZW_PRZ.add_pozycja();
                        {? _poz
                        || _wyn+=1;
                           {? var_pres('_tab_rub')>0 || &_tab_rub ?};
                           _tab_rub:=__RUB.sys_rub(90202,date(O.RP,O.MP,0));
                           {? _tab_rub.first()
                           || {!
                              |? LS.index('PRACNRRU');
                                 LS.prefix(P.ref(),_tab_rub.RN);
                                 _wartosc:=0;
                                 {? LS.first() || {! |? _wartosc+=LS.KW; LS.next() !} ?};
                                 __ZW_PRZ.add_wartosc(_poz,_tab_rub.RN,_wartosc);
                                 _tab_rub.next()
                              !}
                           ?}
                        ?}
                     ?}
                  ?}
               ?};
               _tab_P.next()
            !}
         ?}
      ?};
      O.next()
   !}
?};
P.cntx_pop();
LS.cntx_pop();
O.cntx_pop();
OSOBA.cntx_pop();
OS_ZWPOZ.cntx_pop();
{? _wyn
|| _txt:='Poprawiono zapisy w kartotece rozliczenia zwolnienia przychodu.'
|| _txt:='Brak zapisów do poprawienia w kartotece rozliczenia zwolnienia przychodu.'
?};
__UPG.msg(_txt);

_result


\weryfikacja_ZP_PL_lip_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Weryfikacja zapisów dodatkowych w kartotece rozliczenia zwolnienia przychodu - opis.
::----------------------------------------------------------------------------------------------------------------------
'Weryfikacja zapisów dodatkowych w kartotece rozliczenia zwolnienia przychodu.'


\ppk_zc_wn_zus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Lista rachunków do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym z okresów uczestnictwa
::       współpracowników będących uczestnikami PPK.
::   WE: [_a] [NUMBER] - czy test? (0/1) domyślnie: 0 (wynikiem będzie 1- zadania nie trzeba wykonywać, 0 - trzeba)
::       [_b] [INTEGER] - 0/1 Czy wyświetlać wynikową tabelę? domyślnie: 1
::       [_c] [INTEGER] - 0/1 Czy po wyświetleniu generować dane do pliku? domyślnie: 0
::       [_d] [INTEGER] - 0/1 Czy pomijać uczestników ze złożoną DRZW (ale bez WODW/AWW po niej)? domyślnie: 1
::       [_e] [INTEGER] - 0/1 Czy pomijać uczestników ze złożonym WB25B (ale bez WB25W/DOWU25 po niej)? domyślnie: 1
::       [_f] [INTEGER] - 0/1 Czy pomijać uczestników ze złożonym WS60? domyślnie: 1
::       [_g] [INTEGER] - 0/1 Czy pomijać rachunki z policzoną podstawą PPK? domyślnie: 1
::       [_h] [INTEGER] - 0/1 Czy formuła ma zwracać wygenerowaną tabelę? domyślnie: 0
::       Uwaga: Parametry _c.._g mają znaczenie decydujące jedynie gdy parametr _b=0, w innym przypadku ustawiają
::              jedynie wartości domyślne wyświetlane użytkownikowi do wyboru.
::   WY: _WYN [TABLE] - tabela z listą osób, lub 0 jeśli brak takich zapisów
:: ~OST: INFILECHOOSER,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
_test:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_wysw:={? var_pres('_b')=type_of(0) || _b || 1 ?};
_retTab:={? var_pres('_h')=type_of(0) || _h || 0 ?};
::Wyświetl uczestników PPK, dla których wystawiono rachunki na których policzono dobrowolne ubezpieczenie emer. - rentowe
:: Pomijaj rachunki uczestniów PPK z:
_PAR:=tab_tmp(1,
   'DRZW','INTEGER','zarejestrowaną Deklaracją rezygnacji z wpłat',
   'WB25B','INTEGER','zarejestrowaną Blokadą wpłat na podstawie art. 25',
   'WS60','INTEGER','zarejestrowaną Wypłatą środków po 60. roku życia',
   'PPK','INTEGER','policzoną podstawową składką PPK pracownika i pracodawcy',
   'PLIK','INTEGER','Generowanie pliku',
   'FOLDER','STRING[255]','Folder lokalny'
   );
_PAR.DRZW:={? var_pres('_d')=type_of(0) || _d || 1 ?};
_PAR.WB25B:={? var_pres('_e')=type_of(0) || _e || 1 ?};
_PAR.WS60:={? var_pres('_f')=type_of(0) || _f || 1 ?};
_PAR.PPK:={? var_pres('_g')=type_of(0) || _g || 1 ?};
_PAR.PLIK:={? var_pres('_c')=type_of(0) || _c || 0 ?};
_PAR.FOLDER:=tmp_dir();

{? _wysw
|| _we:=_PAR.mk_edit(
      'Rachunki do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym uczestników PPK'@,0,
      '#ppk_rh_wn_zus');
   _PAR.win_esep(_we,'%1:'['Pomijaj rachunki uczestniów PPK z'@]);
   _PAR.win_efld(_we,,'DRZW',,,,,,,,'Czy pomijać uczestników ze złożoną DRZW (ale bez WODW/AWW po niej)?'@,
      'check-box','check_label="tak, pomijaj"',"1","0");
   _PAR.win_efld(_we,,'WB25B',,,,,,,,'Czy pomijać uczestników ze złożonym WB25B (ale bez WB25W/DOWU25 po niej)?'@,
      'check-box','check_label="tak, pomijaj"',"1","0");
   _PAR.win_efld(_we,,'WS60',,,,,,,,'Czy pomijać uczestników ze złożonym WS60?'@,
      'check-box','check_label="tak, pomijaj"',"1","0");
   _PAR.win_efld(_we,,'PPK',,,,,,,,'Czy pomijać rachunki z policzoną składką podstawową pracownika i pracodawcy?'@,
      'check-box','check_label="tak, pomijaj"',"1","0");
   _PAR.win_esep(_we,'Parametry raportu'@);
   _PAR.win_efld(_we,,'PLIK',,,,,,,,'Czy zapisać dane do pliku po wyświetleniu?'@,
      'check-box','check_label="tak, zapisz"',"1","0");
   _PAR.win_efld(_we,,'FOLDER',,,48,,,,,'Lokalizacja do zapisu pliku'@);
   _PAR.fld_ebtn(_we,'icon=xwin16.png:90',
      "_folder:=exec('filechooser','#file','Wybierz miejsce zapisu pliku'@,,,,,'DIRECTORIES_ONLY');
       {? +_folder
       || cur_tab().PLIK:=1;
          cur_tab().FOLDER:=_folder;
          win_disp()
       ?};
       ~~
      ");
   exec('ok_esc','#window',_PAR,_we);
   _PAR.win_edit(_we);
   {? ~_PAR.edit()
   || return(0)
   ?}
?};

_query:='
   select OSOBA.NAZWISKO as "Nazwisko", OSOBA.PIERWSZE as "Imię", OSOBA.PESEL, OSOBA.REFERENCE as "OSOBA",
          RH.DWY as "Data wypłaty", RH.REFERENCE as "RH", RH.DRA as "Data rachunku",
          cast(0 as REAL_TYPE) as "Kwota rachunku",
          ZC.NU as "Numer umowy", RU.O as "Typ umowy", ZC.DU as "Umowa od", ZC.DW as "Umowa do",
          PPK_UCZ.OD as "PPK od", PPK_UCZ.DO as "PPK do"
   from RH
   join ZC using(RH.ZLE, ZC.REFERENCE)
   join OSOBA using(ZC.OSOBA, OSOBA.REFERENCE)
   join RU using(ZC.RU, RU.REFERENCE)
   join PPK_UCZ using(ZC.OSOBA, PPK_UCZ.OSOBA)
   where ZC.WN_ZUS=\'T\' and (ZC.DW>=PPK_UCZ.OD or ZC.DU<=PPK_UCZ.DO)
   order by 1
   ';
_WNZUS:=sql(_query);

:: Ograniczenie wyników wg. parametrówm wpisanie kwoty rachunku:
{? _WNZUS.first()
|| {!
   |? _del:=0;
      {? (_PAR.DRZW+_PAR.WB25B+_PAR.WS60)>0
      || _osoba:=null();
         OSOBA.cntx_psh();
         {? OSOBA.seek(_WNZUS.OSOBA,,1)
         || _osoba:=OSOBA.ref()
         ?};
         OSOBA.cntx_pop();
         {? _osoba<>null()
         || exec('czytaj','#stalesys',_WNZUS.PPK,KST_PPK,'PPK_UMO');
            {? KST_PPK.PPK_UMO=null()
            || exec('init','ppk_umo',_WNZUS.PPK)
            ?};
            _d0:=date(0,0,0);
            {? _PAR.WS60 & exec('spr_wnu','ppk_wnu',_osoba,,_WNZUS.DATA,'WS60',0)
            || _del:=_WNZUS.del(,1)
            ?};
            {? ~_del & _PAR.DRZW
            || _dtDRZW:=exec('spr_wnu','ppk_wnu',_osoba,,_WNZUS.DATA,'DRZW',1);
::             Jeśli była dekl. rezygnacji i nie było po niej odwołania WODW lub AWW:
               {? _dtDRZW<>_d0 &
               ~exec('spr_wnu','ppk_wnu',_osoba,_dtDRZW,_WNZUS.DATA,'WODW',0) &
               ~exec('spr_wnu','ppk_wnu',_osoba,_dtDRZW,_WNZUS.DATA,'AWW',0)
               || _del:=_WNZUS.del(,1)
               ?}
            ?};
            {? ~_del & _PAR.WB25B
            || _dtWB25B:=exec('spr_wnu','ppk_wnu',_osoba,,_WNZUS.DATA,'WB25B',1);
::             Jeśli była blokada z art. 25 i nie było po niej wznowienia, ani Deklaracji opłacania wpłat uczestnika art. 25:
               {? _dtWB25B<>_d0 &
                  ~exec('spr_wnu','ppk_wnu',_osoba,_dtWB25B,_WNZUS.DATA,'WB25W',0) &
                  ~exec('spr_wnu','ppk_wnu',_osoba,_dtWB25B,_WNZUS.DATA,'DOWU25',0)
               || _del:=_WNZUS.del(,1)
               ?}
            ?}
         ?}
      ?};
::    Ew. usunięcie z policzonym PPK i przypisanie kwoty rachunku:
      {? ~_del
      || RH.cntx_psh();
         {? RH.seek(_WNZUS.RH,,1)
         || {? _PAR.PPK & (exec('licz_rhs','lista_licz',7088)+exec('licz_rhs','lista_licz',7090))>0
            || _del:=_WNZUS.del(,1)
            || _WNZUS.KWOTA:=exec('licz_rhs','lista_licz',200);
               _WNZUS.put()
            ?}
         ?};
         RH.cntx_pop()
      ?};

      _del=2 | _WNZUS.next()
   !}
?};

:: Sprawdzenie czy są dane (również na potrzeby testu):
{? ~_WNZUS.first()
|| _result:=1;
   __UPG.msg('Brak osób spełniających warunki.'@);
   {? ~_test & _wysw &
      FUN.ask('Brak osób spełniających warunki.\nCzy oznaczyć zadanie: \'%1\' jako wykonane?'@['ppk_zc_wn_zus'])
   || _result:=1
   || _result:=-1
   ?};
   return(_result)
|? _test
|| return(-1)
?};

:: Wyświetlanie tabeli:
_tytul:='Rachunki do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym uczestników PPK'@;
{? _wysw
|| _ws:=_WNZUS.mk_sel(_tytul,,,'#ppk_zc_wn_zus',,,,,'U');
   _WNZUS.win_fld(_ws,,'NAZWISKO',,,25,,,'Nazwisko');
   _WNZUS.win_fld(_ws,,'IMIĘ',,,20,,,'Imię');
   _WNZUS.win_fld(_ws,,'PESEL',,,MS.fld_len(OSOBA,'PESEL'),,,'PESEL');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(7),,,10,,,'Data rachunku');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(5),,,10,,,'Data wypłaty');
   _WNZUS.win_fld(_ws,,'KWOTA',,,10,2,,'Kwota rachunku');
   _WNZUS.win_fld(_ws,,'NUMER',,,MS.fld_len(ZC,'NU'),,,'Nr umowy');
   _WNZUS.win_fld(_ws,,'TYP',,,15,,,'Typ umowy');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(11),,,10,,,'Umowa od');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(12),,,10,,,'Umowa do');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(13),,,10,,,'PPK od');
   _WNZUS.win_fld(_ws,,_WNZUS.fld_acr(14),,,10,,,'PPK do');
   _WNZUS.win_act(_ws,0,'Kolejność');
   _WNZUS.win_sel(_ws);
   _WNZUS.select()
?};
:: Eksport do pliku:
{? _PAR.PLIK
|| _plik:=_PAR.FOLDER+'\\PPK_RH_WN_ZUS.csv';
   _WNZUS.export(
   '@'+_plik,,,'UTF-8,header,nopth',,
   'NAZWISKO',,1,,
   'IMIĘ',,2,,
   'PESEL',,3,,
   _WNZUS.fld_acr(7),,4,1,
   _WNZUS.fld_acr(5),,5,1,
   'KWOTA',,6,2,
   'NUMER',,7,,
   'TYP',,8,,
   _WNZUS.fld_acr(11),,9,1,
   _WNZUS.fld_acr(12),,10,1,
   _WNZUS.fld_acr(13),,11,1,
   _WNZUS.fld_acr(14),,12,1
   );
   {? _wysw
   || FUN.info('Wyeksportowano do: %1'@[_plik])
   ?}
?};

{? _retTab
|| return(_WNZUS)
?};

{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['ppk_zc_wn_zus'])
|| __UPG.msg('Wyświetlono: %1'@[_tytul]);
   _result:=1
|| _result:=-1
?};

_result


\ppk_zc_wn_zus_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Lista rachunków do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym z okresów uczestnictwa
::       współpracowników będących uczestnikami PPK.- opis
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Lista rachunków do umów cywilnoprawnych z dobrowolnym ubezpieczeniem emerytalno-rentowym z okresów uczestnictwa '
'współpracowników będących uczestnikami PPK.'


\pomoc_AK_10
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Pomoc przy poprawie aktualizacji 10
::       Funkcja udostępnia listę pracowników z nieobecnościami chorobowymi wprowadzonymi po 2022.1.1
::       Akcja "IDŹ DO" przekierowuje do wskazanej nieobecności pracowniczej. Z tego poziomu można poprawić
::       schemat rozliczenia nieobecności. Akcja jest zapamiętywana w pliku "pkd_akt_10.csv".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_czyKALI:=exec('lic','#b_domain','PKD') | exec('lic','#b_domain','PPL');
exec('__KAL','object',~_czyKALI);
exec('init','pkd');

{? ~__F_ZATR.upr('P')
|| FUN.info('Brak wymaganego dostępu do formy współpracy "%1".'@ ['P']);
   return()
?};
_txt:=exec('uprawnieniawrap','pkd','Rejestracja nieobecności','PKD_EZK_OPNN','PKD_EZK_ORNN');
{? +|_txt
|| FUN.emsg('Brak wymaganych uprawnień do importu informacji o:%1'@[_txt]);
   return()
?};
P_FILTER.UD_SCH:=exec('domyslny','schemat','PODZORG');

_JOS:=exec('jos_create','pkd_grp');
_TAB:=exec('pomoc_AK_10_tab','upgradexperti2');
_cfg:=obj_new('nav','lastref','ile','akt','ogolem');
_cfg.nav:=obj_new('main','side','nwin');
_cfg.ile:=0;
_cfg.akt:=0;
_cfg.ogolem:=0;
: main - okno ze współpracownikami
: side - okno ze strukturą hierarchiczną
: nwin - okno z nieobecnościami
_cfg.nav.side:=_JOS.WS;

_ud_skl:=__PARSES.getVal('JednostkaOrganizacyjna').SYMBOL;
params_set(
   'cfg',_cfg,
   'JOS',_JOS,
   'ud_skl',_ud_skl,
   'TAB',_TAB
);
exec('pkd_conf_cntx','pkd','psh');
Cntx.psh(P,N,O,NW,TZ);
Cntx.clr(P,N,O,NW,TZ);
O.index('LISTYPZN');
O.prefix(exec('ref_firma','#firma'));
N.f_clear();

_wnd:=exec('pomoc_AK_10_wnd','upgradexperti2',_JOS,_cfg,_TAB);

_JOS.TAB.win_sel(_wnd);
{? _TAB.size()>0
|| _JOS.TAB.select()
?};
_TAB.clear();
{? _TAB.first()
|| {!
   |? _cfg.ogolem+=(_TAB.A<>'A');
      _TAB.next()
   !}
?};

Cntx.pop(P,N,O,NW,TZ);
exec('pkd_conf_cntx','pkd','pop');
_funInfo:={? ~_TAB.size() || 'Brak nieobecności spełniających warunki.\n'@ || '' ?};
{? FUN.ask('%1Czy oznaczyć zadanie: \'%2\' jako wykonane?'@[_funInfo,'pomoc_AK_10'])
|| {? _cfg.ogolem
   || __UPG.msg('Zaktualizowano %1 schematów rozliczeń'@[$_cfg.ogolem])
   || __UPG.msg('Wyświetlono: %1'@['Nieobecności do sprawdzenia'@])
   ?};
   _result:=1
|| _result:=-1
?};
_result


\pomoc_AK_10_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Pomoc przy poprawie aktualizacji 10 - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Nieobecności typu chorobowe do sprawdzenia po 2022.1.1'


\pomoc_AK_10_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Definicja okna
::   WE:
::   WY:
:: ~OST: INSYSEXEC
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
_JOS:=_a;
_cfg:=_b;
_TAB:=_c;
: Najpierw zwykłe okno do prezentacji współpracowników.
_acr:='AK10WND';
_id:='pkd_akt_10';
_user:=hash(exec('username','#users'));
_rub12:=__RUB.sys_sql(12);
{? _rub12='' || _rub12:='0' ?};

: użyj istniejące okienko
{? (_ws:=__WND.SEL.get(P,_acr))='';1
|| _ws:=P.mk_sel(,'P',,'#ak10wnd',,,,,'U','T');
   P.win_fld(_ws,,'T',,,9,,,,,MS.comment(P,'T'));
   P.win_fld(_ws,,'IP',,,-5,,,,,MS.comment(P,'IP'));
   P.win_fld(_ws,,'OSOBA','NAZWISKO',,20,,,,,MS.comment(OSOBA,'NAZWISKO'));
   P.win_fld(_ws,,'OSOBA','PIERWSZE',,15,,,,,MS.comment(OSOBA,'PIERWSZE'));
   P.win_fld(_ws,,'OSOBA','PESEL',,11,,,,,MS.comment(OSOBA,'PESEL'));
   P.win_fld(_ws,,'OSOBA','DOWOD',,11,,,'Dowód osobisty'@,,MS.comment(OSOBA,'DOWOD'));
   P.win_fld(_ws,,'OSOBA','PASZPORT',,11,,,'Paszport'@,,MS.comment(OSOBA,'PASZPORT'));
   P.win_fld(_ws,,'F_ZATR','KOD',,-2,,,MS.name(P,'F_ZATR'),,MS.comment(F_ZATR,'KOD'));
   P.win_fld(_ws,,'WYDZIAL','SYMBOL',,-16,,,MS.name(P,'WYDZIAL'),,'Symbol jednostki organizacyjnej'@);

   _fb:="
      params_set(_par:=params_get());
      N.index('NIEOBECX');
      N.prefix('N',P.ref());
      N.find_key(_par.TAB.OD);
      N.actions_grayed('WER','Z');
      N.actions('WER','Z');
      N.select(,1);
      1";
   P.win_act(_ws,,'Formuła','Nieobecności'@,,'Dostęp do nieobecności pracownika'@,
      _fb,,1,,,,'N');

   _fb:="
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? ~P.sel_size()
      || _par.TAB.A='A' | FUN.ask('Aktualizować ponownie?'@)
      || 1
      ?}
   ";
   _fa:="
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _TAB:=_par.TAB;
      _TAB.cntx_psh();
      _TAB.prefix($P.ref(),);
      _jest:=0;
      {? _TAB.first()
      || {!
         |? _TAB.KOM:='OK. ';
            {? _TAB.Z<>'T' & P.seek(_TAB.PREF,) & N.seek(_TAB.NREF,)
            || P.OSOBA();
               {? _TAB.TZ='T' & exec('tz_usun','nieobecnosc',1)
               || _TAB.KOM+='Usunięto rozliczenie nieobecności. '
               ?};
               exec('nw_aktualizuj','nieobecnosc',N.ref());
               _TAB.A:='P';
               {? +|_TAB.LT || _TAB.KOM+='Oblicz ponownie listę płac.' ?};
               _jest:=1
            |? _TAB.Z='T'
            || _TAB.A:='B';
               {? +|_TAB.LT || _TAB.KOM:='Lista płac jest zamknięta. Nie wykonano aktualizacji.' ?}
            || _TAB.A:='B'
            ?};
            _TAB.put();
            _TAB.next()
         !};
         {? _jest || _par.cfg.akt+=1 ?}
      ?};
      _TAB.cntx_pop()
   ";
   _fgb:="
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _cfg.ile:=P.sel_size();
      _cfg.akt:=0;
      P.cntx_psh();
      1
   ";
   _fga:="
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      P.cntx_pop();
      FUN.info('Zaktualizowano schematy rozliczeń dla %1 z %2 pracowników.'@[$_cfg.akt,$_cfg.ile])
   ";
   P.win_act(_ws,,'Formuła','Aktualizuj schematy'@,,'Aktualizuje schematy rozliczenia nieobecności dla pracownika'@,
      _fb,_fa,,1,_fgb,_fga,'A');

   _txt:='Plik z informacją o nieobecnościach już istnieje.'@;
   _fb:=$('fexists(\'%1.csv\',1) & FUN.ask(\'%2\n%3\')'[_user+_id,_txt,'Wczytać dane?'@]);
   _fa:=$("
      params_set(_par:=params_get());
      _par.TAB.cntx_psh();
      _par.TAB.clear();
      _par.TAB.erase();
      _par.TAB.import('"+_user+_id+".csv',0,1,%1+';','UTF-8',,
         'NAZWISKO',,1,,
         'PIERWSZE',,2,,
         'SYMBOL',,3,,
         'T',,4,,
         'IP',,5,,
         'OD',,6,,
         'DO',,7,,
         'RN',,8,,
         'NB',,9,,
         'LT',,10,,
         'Z',,11,,
         'A',,12,,
         'KOM',,13,,
         'PREF',,14,,
         'NREF',,15,
      );
      _par.TAB.cntx_pop()
   ");
   {? _fb() || _fa() ?};
   P.win_act(_ws,,'Formuła','Import'@,,'Import danych z pliku'@,
      _fb,_fa,,,,,'I');

   _fb:=$('~fexists(\'%1.csv\',1) | FUN.ask(\'%2\n%3\')'[_user+_id,_txt,'Nadpisać dane?'@]);
   _fa:=$("
      params_set(_par:=params_get());
      _par.TAB.cntx_psh();
      _par.TAB.clear();
      _par.TAB.export('"+_user+_id+".csv',1,%1+';','UTF-8',,
         'NAZWISKO',,1,,
         'PIERWSZE',,2,,
         'SYMBOL',,3,,
         'T',,4,,
         'IP',,5,,
         'OD',,6,,
         'DO',,7,,
         'RN',,8,,
         'NB',,9,,
         'LT',,10,,
         'Z',,11,,
         'A',,12,,
         'KOM',,13,,
         'PREF',,14,,
         'NREF',,15,
      );
      _par.TAB.cntx_pop();
      {? FUN.ask('Czy otworzyć plik danych?') || sys_exec('"+_user+_id+".csv') ?}
   ");
   P.win_act(_ws,,'Formuła','Eksport'@,,'Eksport danych do pliku'@,
      _fb,_fa,,,,,'E');

   P.win_act(_ws,,'Menu','Szukaj'@,,,,,,,,,'S');
   P.win_act(_ws,,'Formuła','Szukaj dokładnie'@,'#S','Szukanie zapisu zgodnego ze zredagowanym wzorcem'@,
      "exec('p_ustaw_ezk','szukaj')","exec('p_d','szukaj')",,,,,'S');
   P.win_act(_ws,,'Formuła','Szukaj &kontekstowo'@,'#S','Szukanie zapisu zgodnego ze zredagowanym wzorcem'@,
      "exec('p_ustaw_ezk','szukaj')","exec('p_k','szukaj')",,,,,'K');
   P.win_act(_ws,,'#F3',,,,"exec('p_ustaw_ezk','szukaj')","exec('p_f3_wnd','szukaj')");
   P.win_act(_ws,,'Kolejność');

   P.win_btn(_ws,'text='+'Nieobecności'@+',btn_label_align=center,panel=right,align=begin','menu:N');
   P.win_btn(_ws,'text='+'Aktualizuj schematy'@+',btn_label_align=center,panel=right,align=begin','menu:A');
   P.win_btn(_ws,'text='+'Import'@+',btn_label_align=center,panel=right,align=begin','menu:I');
   P.win_btn(_ws,'text='+'Eksport'@+',btn_label_align=center,panel=right,align=begin','menu:E');

   __WND.SEL.put(P,_acr,_ws)
?};

_win:=_TAB.mk_sel('Nieobecności do sprawdzenia'@,'P',0,_id,,,,,'U','T',0,0);
_TAB.win_sel(_win);
_TAB.win_fld(_win,,'RN',,,,,,,,MS.comment(R,'RN'));
_TAB.win_fld(_win,,'NB',,,,,,,,MS.comment(R,'RT'));
_TAB.win_fld(_win,,'OD',,,,,,,,MS.comment(N,'OD'));
_TAB.win_fld(_win,,'DO',,,,,,,,MS.comment(N,'DO'));
_TAB.win_fld(_win,,'LT',,,,,,'Lista'@,,MS.comment(N,'LT'));
_TAB.win_fld(_win,,'TZ',,,,,,'Obliczona na liście'@,,'Czy jest rozliczenie?'@,2,,"'T'","'N'");
_TAB.win_fld(_win,,'Z',,,,,,'Zamknięta lista płac'@,,'Zamknięta lista płac'@);
_TAB.win_fld(_win,,'A',,,,,,'Status'@,,'do (A)nalizy, (P)oprawiono, (B)ez zmian'@,2,,"'P'","'B'","'A'");
_TAB.win_fld(_win,,'KOM',,,40,,,,,'Komentarz'@);
_fb:="
   params_set(_par:=params_get());
   _cfg:=_par.cfg;
   {? ~_par.TAB.sel_size()
   || _par.TAB.A='A' | FUN.ask('Aktualizować ponownie?'@)
   || 1
   ?}
";
_fa:="
   params_set(_par:=params_get());
   _cfg:=_par.cfg;
   _TAB:=_par.TAB;
   P.clear();
   N.clear();
   _TAB.KOM:='OK. ';
   {? _TAB.Z<>'T' & P.seek(_TAB.PREF,) & N.seek(_TAB.NREF,)
   || P.OSOBA();
      {? _TAB.TZ='T' & exec('tz_usun','nieobecnosc',1)
      || _TAB.KOM+='Usunięto rozliczenie nieobecności. '
      ?};
      exec('nw_aktualizuj','nieobecnosc',N.ref());
      _TAB.A:='P';
      {? +|_TAB.LT || _TAB.KOM+='Oblicz ponownie listę płac.' ?};
      _par.cfg.akt+=1
   |? _TAB.Z='T'
   || _TAB.A:='B';
      {? +|_TAB.LT || _TAB.KOM:='Lista płac jest zamknięta. Nie wykonano aktualizacji.' ?}
   || _TAB.A:='B'
   ?};
   _TAB.put()
";
_fgb:="
   params_set(_par:=params_get());
   _cfg:=_par.cfg;
   _cfg.ile:=_par.TAB.sel_size();
   _cfg.akt:=0;
   1
";
_fga:="
   params_set(_par:=params_get());
   _cfg:=_par.cfg;
   FUN.info('Zaktualizowano schematy rozliczeń dla %1 z %2 pracowników.'@[$_cfg.akt,$_cfg.ile])
";
_TAB.win_act(_win,,'Formuła','Aktualizuj schemat'@,,'Aktualizuje schemat rozliczenia nieobecności'@,
   _fb,_fa,,1,_fgb,_fga,'A');

_fa:="
   params_set(_par:=params_get());
   _txt:=_par.TAB.A;
   _co:=FUN.choice('Zmiana statusu',,'Poprawiono','Bez zmian');
   _par.TAB.A:=
         {? _co=1 || 'P'
         |? _co=2 || 'B'
         || _txt
         ?};
   _par.TAB.put()
";
_TAB.win_act(_win,,'Formuła','S&tatus'@,,'Zmiana statusu'@,,_fa,,,,,'T');
_TAB.win_btn(_win,'text='+'Aktualizuj schemat'@+',btn_label_align=center,panel=right,align=begin','menu:A');
_TAB.win_btn(_win,'text='+'S&tatus'@+',btn_label_align=center,panel=right,align=begin','menu:T');

N.f_set(,
   'join R using(N.NB,R.REFERENCE)',
   'R.RN in (:_a) and N.KOR=\'N\' and N.OD>=to_date(\'2022-01-01\') and N.OS_N is null and N.FIRMA=:_b',
   _rub12,
   exec('ref_firma','#firma')
);
_jestTZ:="
   TZ.use('tabz'+form(N.OD~1,-4,0,'9.'));
   TZ.index('PNRM');
   TZ.prefix(N.ref());
   {? TZ.first() || 'T' || 'N' ?}
";

{? N.f_first()
|| FUN.prg_start(N.f_size(),'Analiza nieobecności');
   {!
   |? _TAB.blank(1);
      _TAB.NAZWISKO:=N.P().OSOBA().NAZWISKO;
      _TAB.PIERWSZE:=OSOBA.PIERWSZE;
      _TAB.SYMBOL:=P.WYDZIAL().SYMBOL;
      _TAB.T:=form(P.T);
      _TAB.IP:=P.IP;
      _TAB.OD:=N.OD;
      _TAB.DO:=N.DO;
      _TAB.RN:=N.NB().RN;
      _TAB.NB:=R.RT;
      _TAB.LT:=N.LT;
      _TAB.TZ:=_jestTZ();
      _TAB.Z:={? +|N.LT & O.find_key(-N.LT,) || O.Z || 'N' ?};
      _TAB.PREF:=$P.ref();
      _TAB.NREF:=$.N.ref();
      {? ~_TAB.find_tab('first',
            'NAZWISKO',,'=',_TAB.NAZWISKO,
            'PIERWSZE',,'=',_TAB.PIERWSZE,
            'SYMBOL',,'=',_TAB.SYMBOL,
            'T',,'=',_TAB.T,
            'IP',,'=',_TAB.IP,
            'OD',,'=',_TAB.OD,
            'DO',,'=',_TAB.DO,
            'PREF',,'=',_TAB.PREF,
            'NREF',,'=',_TAB.NREF
         )
      || _TAB.A:='A';
         _TAB.add()
      || _TAB.NB:=N.NB().RT;
         _TAB.LT:=N.LT;
         _TAB.Z:={? +|N.LT & O.find_key(-N.LT) || O.Z || 'N' ?};
         _TAB.TZ:=_jestTZ();
         _TAB.put()
      ?};
      FUN.prg_next();
      N.f_next()
   !};
   FUN.prg_stop()
?};
N.f_clear();

_cfg.nav.main:=_ws;
_cfg.nav.nwin:=_win;
: Teraz okno grupowe.
_mode:='maximized';

_wnd:=_JOS.TAB.grp_make('Pracownicy'@,
:  Po wyświetleniu (załaduj kontrolkę, wyświetl drzewko).
   "  _par:=params_get();
      params_set(_par);
      _cfg:=_par.cfg;
      _ud_skl:=_par.ud_skl;
      _JOS:=_par.JOS;
      exec('load','#desktop','selektor','!pkd_grp_azat.dsk',,,,,exec('pkd_grp_azat_dsk_pl','pkd_grp'),1);
      exec('jos_fill','pkd_grp',_JOS,P_FILTER.UD_SCH,_ud_skl);
      grp_disp(_JOS.TAB,_cfg.nav.side,1,1);
      grp_disp(_par.TAB,_cfg.nav.nwin)
   ",-_acr
);

: Selektor schematu.
exec('create','#desktop',_JOS.TAB,'selektor',_wnd,3,20);

: Struktura.
_JOS.TAB.grp_splt(_wnd,,'horizontal','jostab');
_JOS.TAB.grp_sel(_wnd,_JOS.TAB,_cfg.nav.side,,
:  Po odświeżeniu (wyświetl współpracowników).
   "  _par:=params_get();
      params_set(_par);
      _cfg:=_par.cfg;
      P.first();
      grp_disp(P,_cfg.nav.main,1,1);
      grp_disp(_par.TAB,_cfg.nav.nwin)
   ",,,18,,,,,_mode,_cfg.nav.side
);

: Współpracownicy.
_JOS.TAB.grp_splt(_wnd,,'vertical','p');
_JOS.TAB.grp_sel(_wnd,P,_cfg.nav.main,,"
   _par:=params_get();
   params_set(_par);
   grp_disp(_par.TAB,_par.cfg.nav.nwin)
",,,,
:  Przed obsługą (ustaw filtr na tabeli P).
: !!! Proteza. Zapamiętujemy numer rekordu tabeli _JOS.TAB, dla którego odświeżany był widok współpracowników.
   "  _par:=params_get();
      params_set(_par);
      _JOS:=_par.JOS;
      _cfg:=_par.cfg;
      {? _cfg.lastref<>_JOS.TAB.ref() & UD_DEF.seek(_JOS.TAB.UD_DEF)
      || _cfg.lastref:=_JOS.TAB.ref();
         _rub12:=__RUB.sys_sql(12);
         {? _rub12='' || _rub12:='0' ?};
         _from:='join N using(P.REFERENCE,N.P) join R using(N.NB,R.REFERENCE)';
         _where:='R.RN in (%1) and N.KOR=''N'' and N.OD>=to_date(''2022-01-01'') and N.OS_N is null and N.FIRMA=''%2'''
            [_rub12,$exec('ref_firma','#firma')];
         exec('filtruj_p','schemat','PKD',UD_DEF.ref(),'P','','W',_from,_where)
      ?}
   ",,,,_mode,_cfg.nav.main
);

_JOS.TAB.grp_splt(_wnd,,'horizontal','n',25);
_fb:="
   _par:=params_get();
   params_set(_par);
   {? grp_empty(P,_par.cfg.nav.main)
   || return('#disable')
   ?};
   _par.TAB.prefix($P.ref())
";
_JOS.TAB.grp_sel(_wnd,_TAB,_cfg.nav.nwin,,,,,,_fb);

_wnd


\pomoc_AK_10_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Tabela na dane
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: dane analizowane
_TAB:=tab_tmp(2,
   'PREF','STRING[16]','Wskazanie na P',
   'OD','DATE',MS.name(N,'OD'),
   'DO','DATE',MS.name(N,'DO'),
   'RN','INTEGER','Kod',
   'NB','STRING['+$MS.fld_len(R,'RT')+']','Nazwa',
   'LT','STRING['+$MS.fld_len(O,'LT')+']',MS.name(O,'LT'),
   'TZ','STRING[1]','Jest rozliczenie?',
   'Z','STRING['+$MS.fld_len(O,'Z')+']',MS.name(O,'Z'),
   'A','STRING[1]','do (A)nalizy, (P)oprawiono, (B)ez zmian',
   'KOM','STRING[255]','Komentarz',
   'NAZWISKO','STRING['+$MS.fld_len(OSOBA,'NAZWISKO')+']',MS.name(OSOBA,'NAZWISKO'),
   'PIERWSZE','STRING['+$MS.fld_len(OSOBA,'PIERWSZE')+']',MS.name(OSOBA,'PIERWSZE'),
   'SYMBOL','STRING['+$MS.fld_len(UD_SKL,'SYMBOL')+']',MS.name(P,'WYDZIAL'),
   'T','STRING['+$MS.fld_len(P,'T')+']',MS.name(P,'T'),
   'IP','INTEGER',MS.name(P,'IP'),
   'NREF','STRING[16]','Wskazanie na N'
)


\kody_zus_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Import Słowniki kodów ZUS.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Formuła importująca słowniki kodów ZUS z danych wzorcowych. Podczas importu należy wskazać'
         ' folder w którym będzie umieszczony tylko plik personel_slowniki.xlsx.'@);
exec('import_init','#excel_imex',spli_str('ZWS_PAR_PZUS',','));
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['kody_zus_import'])
|| __UPG.msg('Zaktualizowano element "Słowniki kodów ZUS".');
   _result:=1
|| _result:=-1
?};
_result


\kody_zus_import_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Import Słowniki kodów ZUS - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import elementu "Słowniki kodów ZUS".'


\zaomedadtcreate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka PR/WRT/XP/21.37/2204/0071 - data utworzenia dokumentu w metadanych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','PED')
|| __UPG.msg('Brak licencji na dziedzinę "Dokumentacja pracownicza" - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

_ret:=1;
ZAOMEDA.cntx_psh();
ZAOMEDA.index('NAG');
ZAOMEDA.prefix();
{? ZAOMEDA.first()
|| ZAOMEDA.trig_off('*','*');

   _stat:=obj_new('size','zalacz','seek0','seek1','zm0','zm1','put0','put1','del0','del1');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};
   _stat.size:=ZAOMEDA.size();

   ZALACZ.cntx_psh();
   ZALACZ.prefix();
   {!
   |? _del:=0;
      {? ref_tab(ZAOMEDA.NAG)=ZALACZ
      || _stat.zalacz+=1;
         {? ZALACZ.seek(ZAOMEDA.NAG,)
         || _stat.seek1+=1;
            {? ZAOMEDA.DTCREATE<>ZALACZ.DATA
            || _stat.zm1+=1;
               ZAOMEDA.DTCREATE:=ZALACZ.DATA;
               {? ZAOMEDA.put()
               || _stat.put1+=1
               || _stat.put0+=1
               ?}
            || _stat.zm0+=1
            ?}
         || _stat.seek0+=1;
            _del:=ZAOMEDA.del(,1);
            {? _del>0
            || _stat.del1+=1
            || _stat.del0+=1
            ?}
         ?}
      ?};
      _del=2 | ZAOMEDA.next()
   !};
   ZALACZ.cntx_pop();

   ZAOMEDA.trig_on('*','*');

   __UPG.msg('Liczba rekordów metadanych: %1'[$_stat.size]);
   __UPG.msg('Liczba rekordów metadanych do weryfikacji: %1'[$_stat.zalacz]);
   __UPG.msg('Liczba rekordów źródłowych [znalezionych / nieznalezionych]: %1 / %2'[$_stat.seek1,$_stat.seek0]);
   __UPG.msg('Liczba znalezionych rekordów źródłowych [wymagających / niewymagających] zmiany: %1 / %2'
      [$_stat.zm1,$_stat.zm0]
   );
   __UPG.msg('Liczba rekordów źródłowych wymagających zmiany i [zmienionych / niezmienionych]: %1 / %2'
      [$_stat.put1,$_stat.put0]
   );
   __UPG.msg('Liczba rekordów metadanych bez rekordów źródłowych [usuniętych / nieusuniętych]: %1 / %2'
      [$_stat.del1,$_stat.del0]
   );

   {? _stat.seek0 | _stat.put0 | _stat.del0
   || _ret:=-1
   ?}

|| __UPG.msg('Tabela metadanych nie zawiera rekordów.')
?};
ZAOMEDA.cntx_pop();

_ret


\zaomedadtcreate_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Poprawka PR/WRT/XP/21.37/2204/0071 - data utworzenia dokumentu w metadanych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'PR/WRT/XP/21.37/2204/0071 - weryfikacja daty utworzenia dokumentów w metadanych'


\wn_podat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie słowników dla wniosków dotyczących podatków.
::----------------------------------------------------------------------------------------------------------------------
SLO_TYP.cntx_psh();
SLO_KOD.cntx_psh();
_slo_typ:=exec('slo_typ','ext_slo','WN_PODAT');

:: Niepobieranie zaliczek na podatek dochodowy
exec('kod_ref','ext_slo',_slo_typ,'UZ_POD','Wniosek o niepobieranie zaliczek na podatek dochodowy',1);
:: Zaprzestanie stosowania ulgi dla klasy średniej
exec('kod_ref','ext_slo',_slo_typ,'KL_SRED','Wniosek o zaprzestanie stosowania ulgi dla klasy średniej',1);

SLO_KOD.cntx_pop();
SLO_TYP.cntx_pop();

__UPG.msg('Dodano nowe typy słowników dla wniosków dotyczących podatków.');
1


\wn_podat_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie słowników dla wniosków dotyczących podatków - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie słowników dla wniosków dotyczących podatków.'


\P_NPOD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Wprowadzenie danych dotyczących korekt do tabeli nadwyżek zaliczki na podatek.
::----------------------------------------------------------------------------------------------------------------------
:: Zapisujemy tylko te, które mają nadwyżkę
{? var_pres('P_NPOD')=type_of(SYSLOG)
|| exec('__F_ZATR','object');
   _slo_kod:=exec('kod','ext_slo','P_STAT','LIST_MOD');
   {? _slo_kod
   || _start:=1;
      _koniec:=12;
      P.cntx_psh();
      KT.cntx_psh();
      P_STAT.cntx_psh();
      LS.cntx_psh();
      ZC.cntx_psh();
      RH.cntx_psh();
      O.cntx_psh();
      _firma:=exec('ref_firma','ustawienia');
      _ndx:=O.ndx_tmp(,1,'FIRMA',,,'F_ZATR','KOD',,'RP',,,'MP',,,'D',,,'N',,);
      O.index(_ndx);
      RH.clear();
      P.clear();

::    Przeniesienie zapisów z list KPD
      {! _mc:=_start.._koniec
      |! {? var_pres('_tab')>100 || obj_del(_tab) ?};
         _tab:=sql(
            'select distinct P_STAT.P '
            'from P_STAT '
            'join O join KT '
            'where O.FIRMA=:_a and KT.F=\'P\' and P_STAT.SLO_KOD=:_b and O.RP=:_c and O.MP=:_d ',
            _firma,_slo_kod,2022,_mc);
         O.prefix(_firma,'P',2022,_mc);
         {? _tab.first()
         || {!
            |? {? P.seek(_tab.P) & O.last()
               || _next:=1;
                  {!
                  |? {? LS.use(O.LT)
                     || LS.index('PRACNRRU');
                        LS.prefix(P.ref());
                        {? LS.first()
                        || _next:=0;
                           {? O.T().F='P'
                           || {? LS.find_key(7170)
                              || {? LS.KW>0
                                 || {? exec('p_npod_del','lista_licz','P',P.ref(),O.ref())
                                    || exec('p_npod_add','lista_licz',
                                                LS.P,LS.P().OSOBA,O.ref(),null,'P',O.RP,O.MP,'N',LS.KW,'T')
                                    ?}
                                 ?}
                              ?}
                           ?}
                        ?}
                     ?};
                     O.prev() & _next
                  !}
               ?};
               _tab.next()
            !}
         ?};
::       Pobranie danych z korekt umów zleceń
         {? var_pres('_tab')>100 || obj_del(_tab) ?};
            _tab:=sql(
               'select RH.REFERENCE as REF '
               'from RH '
               'where RH.FIRMA=:_a and RH.R=:_b and RH.M=:_c and RH.KOR=\'T\' ',
               _firma,2022,_mc
            );

         {? _tab.first()
         || {!
            |? {? RH.seek(_tab.REF)
               || RH.O(); RH.ZLE();ZC.P();
                  {? LS.use(O.LT)
                  || LS.index('ZLEC');
                     LS.prefix(RH.ref(),7170);
                     {? LS.first()
                     || {? LS.KW>0
                        || {? exec('p_npod_del','lista_licz','Z',RH.ZLE().P,O.ref(),RH.ref())
                           || exec('p_npod_add','lista_licz',
                                      LS.P,LS.P().OSOBA,O.ref(),RH.ref(),'Z',O.RP,O.MP,'N',LS.KW,'T')
                           ?}
                        ?}
                     ?}
                  ?}
               ?};
               _tab.next()
            !}
         ?}
      !};
      P.cntx_pop();
      KT.cntx_pop();
      P_STAT.cntx_pop();
      LS.cntx_pop();
      ZC.cntx_pop();
      RH.cntx_pop();
      O.ndx_drop(_ndx);
      O.cntx_pop()
   ?}
?};

__UPG.msg('Zakończono uzupełnianie tabeli nadwyżek zaliczki na podatek danymi z korekt.');
1


\P_NPOD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Wprowadzenie danych dotyczących korekt do tabeli nadwyżek zaliczki na podatek - opis.
::----------------------------------------------------------------------------------------------------------------------
'Wprowadzenie danych dotyczących korekt do tabeli nadwyżek zaliczki na podatek.'


\portal_sync_met
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Błędy zasilania metod. Poprawki błędów ER/WRT/XP/21.37/2112/0069 oraz ER/WRT/XP/21.37/2203/0025
::----------------------------------------------------------------------------------------------------------------------
_errors:=0;
{? exec('lic','#b_domain','POR')
|| _pack_size:=500;
   _pd:='PORTAL';
   _acronyms:=spli_str('KART_URL,SLO_KOD,P_PZ',',');
   _size:=_counter:=_error:=0;

   SYNC_PD.cntx_psh();
   SYNC_PD.index('SYM');
   SYNC_PD.prefix(_pd,);
   {? SYNC_PD.first()
   || SYNC_DEF.cntx_psh();
      SYNC_DEF.index('PDNR');
      {! _ii:=1..obj_len(_acronyms)
      |! _tabAcr:=_acronyms[_ii];
         _size:=_counter:=_error:=0;
         SYNC_DEF.prefix(SYNC_PD.ref,_tabAcr,);
         _war_form:={? SYNC_DEF.first() || SYNC_DEF.WAR_FORM || '' ?};
         {? _war_form='' || _war_form:='1' ?};

         SYNC_IDP.cntx_psh();
         SYNC_IDP.index('ACR');
         SYNC_IDP.prefix(SYNC_PD.ref(),_tabAcr,);
         {? SYNC_IDP.first()
         || SYNC_IDP.IDPUT:=time_ident(date(),time());
            SYNC_IDP.put()
         || SYNC_IDP.SYNC_PD:=SYNC_PD.ref();
            SYNC_IDP.ACR:=_tabAcr;
            SYNC_IDP.IDPUT:=time_ident(date(),time());
            SYNC_IDP.add()
         ?};
         SYNC_IDP.cntx_pop();

         _tab:=($_tabAcr)();
         _tab.trig_off('*','*');
         _args:=obj_new('size','pack_size','tab','war_form','counter','error');
         _args.size:=_size;
         _args.pack_size:=_pack_size;
         _args.tab:=_tab;
         _args.war_form:=$_war_form;
         _args.counter:=_counter;
         _args.error:=_error;
         params_set('args',_args);
         _formula:="
            _args:=params_get().args;
            {? _args.war_form('put',__Firma,REF.FIRMA)
            || _args.size+=1;
               {? _args.size>_args.pack_size
               || _args.size:=0;
                  delay(1)
               ?};
               {? _args.tab.put(,1) || _args.counter+=1 || _args.error+=1 ?}
            ?};
            ~~
         ";
         params_exec('for_each','#table',_tab,_formula);
         _size:=_args.size;
         _counter:=_args.counter;
         _error:=_args.error;
         _tabComm:=_tab.comment();
         obj_del(_args);
         _tab.trig_on('*','*');
         obj_del(_tab);
         __UPG.msg('%1:'[_tabComm]);
         __UPG.msg('Liczba wszystkich wierszy: %1.'[$_size]);
         __UPG.msg('Liczba zaktualizowanych wierszy: %1.'[$_counter]);
         __UPG.msg('Błędy aktualizacji wierszy: %1.'[$_error]);
         _errors+=_error
      !};
      SYNC_DEF.cntx_pop()
   ?};
   SYNC_PD.cntx_pop()
?};

{? _errors || -1 || 1 ?}


\portal_sync_met_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Błędy zasilania metod. - opis
::----------------------------------------------------------------------------------------------------------------------
'Błędy zasilania metod.\n'
'Poprawki błędów ER/WRT/XP/21.37/2112/0069 oraz ER/WRT/XP/21.37/2203/0025'


\srsr_skr_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [22.26]
:: OPIS: Naprawa błędu ER/WRT/XP/22.26/2211/0047 - brak uzupełnionych pól ROZ i DOKSKR w tabeli SRSR po transferze
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh(); SRDO.cntx_psh(); SRDT.cntx_psh();
_names:=SRSR.names();
{? _names.first()
|| {! |?
      {? _names.NAME+4='____'
      || _names.del()
      || _names.next()
      ?}
   !}
?};
_size:=0;
_putsr:=0;
_maska:='';
_ndxdo:=SRDO.ndx_tmp(,1,'TYP',,);
{? _names.first()
|| {! |?
      _maska:=_names.NAME+4;
      SRSR.use(('srsr'+_maska)); SRDO.use(('srdo'+_maska)); SRDT.use(('srdt'+_maska));
      SRSR.prefix();
      SRDT.index('TYP'); SRDT.prefix('LT'); _typ:=null;
      {? SRDT.first() || _typ:=SRDT.ref() ?};
      SRDO.index(_ndxdo); SRDO.prefix(_typ); _size+=SRDO.size();
      __UPG.msg('Liczba dokumentów do przetworzenia w masce %1: %2'[('srdo'+_maska),$_size]);
      {? SRDO.first()
      || {! |?
            {? SRDO.SRSR_E=null
            || {? SRSR.seek(SRDO.SRSR) & (SRSR.DOKSKR=null | SRSR.ROZ=null)
               || SRSR.DOKSKR:=SRDO.ref();
                  SRSR.ROZ:=SRDO.ROZ;
                  SRSR.put(1);
                  _putsr+=1
               ?}
            || {? SRSR.seek(SRDO.SRSR_E) & (SRSR.DOKSKR=null | SRSR.ROZ=null)
               || SRSR.DOKSKR:=SRDO.ref();
                  SRSR.ROZ:=SRDO.ROZ;
                  SRSR.put(1);
                  _putsr+=1
               ?}
            ?};
            __UPG.msg('Liczba zaktualizowanych środków: %1.'[$_putsr]);
            SRDO.next()
         !}
      ?};
      _names.next()
   !}
?};
SRDO.ndx_drop(_ndxdo);
SRSR.cntx_pop(); SRDO.cntx_pop(); SRDT.cntx_pop();
1


\srsr_skr_roz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [22.26]
:: OPIS: Naprawa błędu ER/WRT/XP/22.26/2211/0047 - brak uzupełnionych pól ROZ i DOKSKR w tabeli SRSR po transferze
::       - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawa błędu ER/WRT/XP/22.26/2211/0047.\n'
'Uzupełnienie brakujących pól złączeniowych ROZ i DOKSKR w tabeli SRSR'


\typ_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Uzupełnienie zawartości słownika typów formuł.
::----------------------------------------------------------------------------------------------------------------------
SLO_TYP.cntx_psh();
SLO_KOD.cntx_psh();
_slo_typ:=exec('slo_typ','ext_slo','TYP_FORM');

:: Formuły do wyrównań:
exec('kod_ref','ext_slo',_slo_typ,'W','formuły do wyrównań',1);
:: Korekta podatku:
exec('kod_ref','ext_slo',_slo_typ,'P','korekta podatku',1);

SLO_KOD.cntx_pop();
SLO_TYP.cntx_pop();

__UPG.msg('Zaktualizowano zawartość słownika typów formuł.');

1


\typ_form_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Uzupełnienie zawartości słownika typów formuł - opis.
::----------------------------------------------------------------------------------------------------------------------
'Zaktualizowano zawartość słownika typów formuł.'


\for_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2110/0050: Błąd uzupełnienia pola FOR_AKC.AKCJA
::----------------------------------------------------------------------------------------------------------------------
FOR_AKC.cntx_psh();
FOR_AKC.prefix();
FOR_AKC.for_each("{? FOR_AKC.AKCJA='' || FOR_AKC.AKCJA:='akceptacja'; FOR_AKC.put() ?}");
FOR_AKC.cntx_pop();
__UPG.msg('Uzupełniono znacznik miejsca wywoływania dla formuł kontrolnych.');
1


\for_akc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2110/0050: Błąd uzupełnienia pola FOR_AKC.AKCJA
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie znacznika miejsca wywoływania dla formuł kontrolnych.'


\wz_osoba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0042 - Nieprawidłowa definicja wzorców słowników użytkowników
::       (Pracownik, Osoba, Zleceniobiorca) - skutkuje błędnym działaniem
::       we wnioskach w webterm (gdy użyjemy taki słownik w atrybutach)
::----------------------------------------------------------------------------------------------------------------------
RS.cntx_psh();
RS.prefix();
{? RS.first()
|| {!
   |? {? RS.TR='_a:=OSOBA();_a[3]+\' \'+_a[4]+\' \'+_a[1]'
      || RS.TR:='exec(\'wz_osoba\',\'slo_slu\',OSOBA(),3,4,1)';
         RS.put
      |? RS.TR='_a:=OSOBA();_a[1]+\' \'+_a[3]+\' \'+_a[4]'
      || RS.TR:='exec(\'wz_osoba\',\'slo_slu\',OSOBA(),1,3,4)';
         RS.put
      ?};
      RS.next()
   !}
?};
RS.cntx_pop();
__UPG.msg('Zaktualizowano rodzaje wzorców.');
1


\wz_osoba_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: ER/WRT/XP/21.37/2203/0042 - Nieprawidłowa definicja wzorców słowników użytkowników
::       (Pracownik, Osoba, Zleceniobiorca) - skutkuje błędnym działaniem
::       we wnioskach w webterm (gdy użyjemy taki słownik w atrybutach) - OPIS
::----------------------------------------------------------------------------------------------------------------------
'Aktualizuje treść rodzaju wzorca wykorzystującego tabele OSOBA'


\dokument_zmiany_metody
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [21.37]
:: OPIS: Formuła zmienia parametr środków trwałych dotyczący dokumentu zmiany metody z D->L
::       Domyślny dokument ustawiany jest na typ 'MA'
::----------------------------------------------------------------------------------------------------------------------
SRDT.use('srdt'+'r'+REF.FIRMA().SYMBOL);
SRDT.cntx_psh();
SRDT.index('TYP');
SRDT.prefix('MA');
{? SRDT.first()
|| exec('init_wer','#stalesys');
   exec('stalesys','#stalesys');
   FINFO.SRST_MET:=SRDT.ref();
   exec('zapisz','#stalesys',1,FINFO,'SRST_MET');
   __UPG.msg('Ustawiono dokument zmiany metody D->L na typ \'MA\'.')
?};
SRDT.cntx_pop()


\dokument_zmiany_metody_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [21.37]
:: OPIS: Formuła zmienia parametr środków trwałych dotyczący dokumentu zmiany metody z D->L
::       Domyślny dokument ustawiany jest na typ 'MA'
::----------------------------------------------------------------------------------------------------------------------
'Formuła zmienia parametr środków trwałych dotyczący dokumentu zmiany metody z D->L\n
Domyślny dokument ustawiany jest na typ \'MA\')'


\dokum_ob_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Aktualizacja statusów dokumentów businesslink powiązanych z EDOKUM.
::       ER/WRT/XP/21.37/2206/0017 - powiadomienia o dokumentach z BL wymagających obsługi manulanej
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DOKUM.names();
_msk.clear();
DOKUM.cntx_psh();
{? _msk.first()
|| {!
   |? DOKUM.use(_msk.NAME);
      DOKUM.index('BL');
      DOKUM.prefix('T',REF.FIRMA,'P','T');
      {? DOKUM.first()
      || {!
         |? __lrec+=1;
            {? DOKUM.REFSQL*'skid_v'>0 & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
            || _res:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"
                  _stat:=1+@.EDOKUM.B_PRELS;
                  {? _stat<>'' & _stat<>'R'
                  ||
                     @.DOKUM.BL_STAT:=exec('registered','zbl_stat');
                     @.DOKUM.put()
                  ||
                     -1
                  ?}
               ");
               {? _res=1
               || __lmod+=1
               ?}
            ?};
            DOKUM.next()
         !}
      ?};
      _msk.next()
   !}
?};
DOKUM.cntx_pop();
__UPG.msg('Zaktualizowano statusy przychodzących dokumentów Businesslink powiązanych z dokumentami w obiegu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\dokum_ob_status_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Aktualizacja statusów dokumentów businesslink powiązanych z EDOKUM - opis
::       ER/WRT/XP/21.37/2206/0017 - powiadomienia o dokumentach z BL wymagających obsługi manulanej
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja statusów dokumentów businesslink powiązanych z dokumentami w obiegu.'


\jpk_fa_4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2212/0010 - brak JPK_FA w wersji 4
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_imp:=exec('upg_imp','xml','FKS','J','FA','JPK_FA(4)_v1-0',date(2022,4,1),'jpk_fa_4.dfg');
_txt:='Schemat plik JPK_FA (4) obowiązujący od 01.04.2022';
{? _imp=1
|| __UPG.msg(_txt+' został dodany.');
   _ret:=1
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.');
   _ret:=1
|| __UPG.msg(_txt+' nie został dodany.')
?};
_ret


\jpk_fa_4_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2212/0010 - brak JPK_FA w wersji 4 - opis
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/22.26/2212/0010 - brak JPK_FA w wersji 4\n'
'Import schematu JPK_FA (4)'


\zam_amor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [22.26]
:: OPIS: Formuła zamykająca okresy amortyzacji dla środków trwałych, jeśli w finansach jest już zamknięty
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_counter:=_change:=_err:=0;
OKRO_F.cntx_psh();
OKRO_F.index('ROK'); OKRO_F.prefix();
OKRO_F.blank();
{? OKRO_F.first()
|| {! |?
      _counter+=1;
      {? OKRO_F.ZAM='T' & OKRO_F.AMOR<>'T'
      || OKRO_F.AMOR:='T';
         {? OKRO_F.put() || _change+=1 || _err+=1 ?}
      ?};
      OKRO_F.blank();
      OKRO_F.next()
   !}
?};
__UPG.msg('Liczba wszystkich okresów: %1.'[$_counter]);
__UPG.msg('Liczba zaktualizowanych okresów: %1.'[$_change]);
__UPG.msg('Błędy aktualizacji okresów: %1.'[$_err]);
OKRO_F.cntx_pop();
{? _err || _wyn:=-1 ?};
_wyn


\zam_amor_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [22.26]
:: OPIS: Formuła zamykająca okresy amortyzacji dla środków trwałych, jeśli w finansach jest już zamknięty - opis
::----------------------------------------------------------------------------------------------------------------------
'Zamknięcie otwartych okresów amortyzacji środków trwałych, gdy okres finansowy jest już zamknięty'


\parametr_208
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizuje wartość parametru 208 na liście parametrów
::----------------------------------------------------------------------------------------------------------------------
PARAMS.cntx_psh(); ATRUSE_E.cntx_psh();
{? ATRUSE_E.name()<>'eat_r'+REF.FIRMA().SYMBOL || ATRUSE_E.use('eat_r'+REF.FIRMA().SYMBOL) ?};
ATRUSE_E.index('SROD'); ATRUSE_E.prefix();
_wyn:=_dalej:=0;
{? ATRUSE_E.first()
|| {! |?
      {? ATRUSE_E.SRSR<>null | ATRUSE_E.SRDO<>null
      || _wyn:=1; _dalej:=0
      || _dalej:=ATRUSE_E.next()
      ?};
      _dalej
   !}
?};
{? _wyn
|| PARAMS.index('NUMER'); PARAMS.prefix(208);
   {? PARAMS.first()
   || {! |?
         PARAMS.TRESC:='T';
         PARAMS.put(1);
         PARAMS.next()
      !}
   || PAR_SKID.set(208,1,1,'','T','Środki trwałe: Włączyć obsługę cech dodatkowych?','''T'',''N''')
   ?}
|| PAR_SKID.set(208,1,1,'','N','Środki trwałe: Włączyć obsługę cech dodatkowych?','''T'',''N''')
?};
__UPG.msg('Zaktualizowano parametr 208 - Środki trwałe: Włączyć obsługę cech dodatkowych?');
PARAMS.cntx_pop(); ATRUSE_E.cntx_pop(); 1


\parametr_208_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizuje wartość parametru 208 na liście parametrów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wartości parametru numer 208 na liście parametrów - Środki trwałe: Włączyć obsługę cech dodatkowych?'


\ETYPY_P_pack
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Ustawienie grupowego zasilania uprawnień do wniosków w portalu HR
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('PACK',SYNC_MWA)>0
|| SYNC_MWA.cntx_psh();
   SYNC_MWA.index('PD_METH');
   SYNC_MWA.prefix();
   {? SYNC_MWA.find_key('PORTAL','sl_TenantUserRowRightsModify',)
   || SYNC_MWA.PACK:=1;
      SYNC_MWA.put();
      __UPG.msg('Ustawiono grupowe zasilanie uprawnień do wniosków w portalu HR')
   || __UPG.msg('Nie znaleziono metody %1 w %2.'['sl_TenantUserRowRightsModify','PORTAL'])
   ?};
   SYNC_MWA.cntx_pop()
|| __UPG.msg('Brak pola %1 w definicji systemu.'['SYNC_MWA.PACK'])
?};
1


\ETYPY_P_pack_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Ustawienie grupowego zasilania uprawnień do wniosków w portalu HR - opis
::----------------------------------------------------------------------------------------------------------------------
'Ustawienie grupowego zasilania uprawnień do wniosków w portalu HR'


\srst_korekta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [21.14]
:: OPIS: Uzupełnienie wartości pól netta po korektach w tabeli SRST
::----------------------------------------------------------------------------------------------------------------------
SRST.cntx_psh();
SRDO.cntx_psh();
SRSR.cntx_psh();
SRDT.cntx_psh();
SRST.use('srstr'+REF.FIRMA().SYMBOL);
SRDO.use('srdor'+REF.FIRMA().SYMBOL);
SRSR.use('srsrr'+REF.FIRMA().SYMBOL);
SRDT.use('srdtr'+REF.FIRMA().SYMBOL);
SRST.index('NRI');
SRST.prefix();
{? SRST.first()
|| {! |?
         SRDO.cntx_psh();
         SRDO.index('RODZOKR');
         OKRO_F.cntx_psh();
         OKRO_F.index('ROK');
         _rok:=SRST.OKRO_F().ROK;
         {? SRST.OKRO_F().NR<>12
         || _nr:=SRST.OKRO_F().NR+1
         || _nr:=1;
            ROK_F.cntx_psh();
            ROK_F.index('NAZWA');
            _rok:=(#SRST.OKRO_F().ROK().NAZ)+1;
            ROK_F.prefix(REF.FIRMA, $_rok);
            {? ROK_F.first()
            || _rok:=ROK_F.ref()
            ?};
            ROK_F.cntx_pop()
         ?};
         OKRO_F.prefix(_rok, _nr);
         {? OKRO_F.first()
         || SRDO.prefix(SRST.SRSR, OKRO_F.ref(),'W');
            SRST.NETPK:=SRST.NETP;
            SRST.NETFK:=SRST.NETF;
            {? FINFO.TOR_D='T' || SRST.NETDK:=SRST.NETD ?};
            {? SRDO.first()
            || {! |?
               {? SRDO.TYP().TYP='LT'
               || SRST.NETPK:=0;
                  SRST.NETFK:=0;
                  {? FINFO.TOR_D='T' || SRST.NETDK:=0 ?}
               |? SRDO.TYP().TYP='K-' & (fabs(SRDO.WARF)>=SRST.NETFK | fabs(SRDO.WARP)>=SRST.NETPK | fabs(SRDO.WARD)>=SRST.NETDK)
               || {? fabs(SRDO.WARF)>=SRST.NETFK
                  || SRST.NETFK:=0
                  || SRST.NETFK:=SRST.NETF+SRDO.WARF
                  ?};
                  {? fabs(SRDO.WARP)>=SRST.NETPK
                  || SRST.NETPK:=0
                  || SRST.NETPK:=SRST.NETP+SRDO.WARP
                  ?};
                  {? FINFO.TOR_D='T' & fabs(SRDO.WARD)>=SRST.NETDK
                  || SRST.NETDK:=0
                  || SRST.NETDK:=SRST.NETD+SRDO.WARD
                  ?}
               |? 1+SRDO.TYP().TYP='K'
               || SRST.NETPK:=SRST.NETP+SRDO.WARP;
                  SRST.NETFK:=SRST.NETF+SRDO.WARF;
                  {? FINFO.TOR_D='T' || SRST.NETDK:=SRST.NETD+SRDO.WARD ?}
               ?};
               SRDO.next()
               !}
            ?};
            SRST.put()
         ?};
         OKRO_F.cntx_pop();
         SRDO.cntx_pop();
         SRST.next()
   !}
?};
SRST.cntx_pop();
SRDO.cntx_pop();
SRSR.cntx_pop();
SRDT.cntx_pop();
1


\srst_korekta_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [22.26]
:: OPIS: Uzupełnienie wartości pól netta po korektach w tabeli SRST - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wartości pól netto po korektach w tabeli SRST'


\uzu_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Uzupełnienie słownika wydziałów.
::----------------------------------------------------------------------------------------------------------------------
{? ~(exec('tte_lic','tte')='T' | exec('tre_lic','tre')='T' | exec('wyp_lic','wyp')='T')
|| __UPG.msg('Brak licencji na dziedzinę - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

exec('init_wer','#stalesys');
exec('czytaj','#stalesys',date(),XINFO,INFO,FINFO);
_choice:=0;
{! |? (_choice:={? XINFO.SLWYDZIA=null
                || FUN.choice(
                   'W stałych systemu należy uzupełnić słownik wydziałów:\n'
                   '1. Przejdź do słowników systemowych\n'
                   '2. Uzupełnij słownik wydziałów\n\n'
                   'Czy prace opisane powyżej zostały wykonane?'
                   ,0,'Słowniki systemowe'@,'Tak',,,'Nie'@)
                || FUN.choice(
                   'Słownik wydziałów jest uzupełniony w stałych systemu.\n\n'
                   'Czy oznaczyć zadanie jako wykonane?'
                   ,0,'Słowniki systemowe'@,'Tak',,,'Nie'@)
                ?})=1
   |! exec('def_slo_sys','slo_sys')
!};
{? _choice
|| __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   1
|| __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   0
?}


\uzu_slo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Uzupełnienie słownika wydziałów. - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słownika wydziałów'


\fix_edek_us
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Usunięcie błędnych spacji w kodach do e-deklaracji urzędów skarbowych
::       Poprawka do ER/WRT/XP/22.26/2301/0084
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'FIRST'
);
_obj.FIRST:=0;
params_set('obj',_obj);
US.cntx_psh();
US.index('URZSKNAZ');
US.prefix();
US.for_each("
   {? US.EDEK_SYM<>form(US.EDEK_SYM)
   || US.EDEK_SYM:=form(US.EDEK_SYM);
      {? US.put()
      || _obj:=params_get().obj;
         {? _obj.FIRST=0
         || __UPG.msg('Usunięto błędne spacje w kodach do e-deklaracji następujących urzędów skarbowych:');
            _obj.FIRST:=1
         ?};
         __UPG.msg('- '+US.EDEK_SYM+' - '+US.NU)
      ?}
   ?}
");
US.cntx_pop();
{? _obj.FIRST=0
|| __UPG.msg('Nie znaleziono błędnych spacji w kodach do e-deklaracji urzędów skarbowych.')
?};
1


\fix_edek_us_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Usunięcie błędnych spacji w kodach do e-deklaracji urzędów skarbowych - opis
::       Poprawka do ER/WRT/XP/22.26/2301/0084
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie błędnych spacji w kodach do e-deklaracji urzędów skarbowych.\n'
'Poprawka do ER/WRT/XP/22.26/2301/0084'


\tam_bil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Kontrola uzupełnienia pola BIL (wskazanie na pozycję bil.) w tabeli TAM (grupy środków trwałych)
::       ER/WRT/XP/22.26/2302/0058
::----------------------------------------------------------------------------------------------------------------------
_sql:=sql('select TAB_KRST.KST, TAB_KRST.O , GR, NGS from TAM join TAB_KRST using (TAM.KST,TAB_KRST.REFERENCE)'
          ' where BIL is null');
{? _sql.size()>0
|| _win:=_sql.mk_sel('Grupy środków bez przypisanej pozycji bil.'@);
   _sql.win_fld(_win,,'KST',,,6,,,'Symbol'@);
   _sql.win_fld(_win,,'O',,,40,,,'Opis'@);
   _sql.win_fld(_win,,'GR',,,10,,,'Grupa'@);
   _sql.win_fld(_win,,'NGS',,,50,,,'Nazwa grupy środków'@);
   _sql.win_sel(_win)
?};

_choice:=0;
{! |? (_choice:={? _sql.size()>0
                || FUN.choice(
                   'W systemie istnieją grupy środków trwałych (TAM) bez uzupełnionych wskazań na pozycje bilansowe.\n'
                   'Dane należy uzupełnić ręcznie.\n\n'
                   '1. Przejdź do definiowania grup środków.\n'
                   '2. Uzupełnij wskazania na pozycję bilansowe.\n\n'
                   'Czy prace opisane powyżej zostały wykonane?'@
                   ,0,'Definiowanie grup środków'@,'Szczegóły'@,'Tak'@,,'Nie'@)
                || FUN.choice(
                   'Wszystkie wskazania na pozycje bilansowe w grupach środków trwałych są uzupełnione.\n\n'
                   'Nie jest wymagane działanie naprawcze.\n'
                   'Czy oznaczyć zadanie jako wykonane?'@
                   ,0,'Definiowanie grup środków'@,'Tak'@,,,'Nie'@)
                ?})=1 | (_sql.size()>0 & _choice=2)
   |! {? _choice=1
      || exec('krst','!zws_par_ftgr')
      |? _choice=2 & _sql.size()>0
      || _sql.select()
      ?}
!};

{? _choice
|| __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   1
|| __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   0
?}


\tam_bil_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Kontrola uzupełnienia pola BIL (wskazanie na pozycję bil.) w tabeli TAM (grupy środków trwałych)
::       ER/WRT/XP/22.26/2302/0058 - opis
::----------------------------------------------------------------------------------------------------------------------
'Kontrola uzupełnienia pola BIL (wskazanie na pozycję bil.) w tabeli TAM (grupy środków trwałych).\n'
'Poprawka do ER/WRT/XP/22.26/2302/0058'


\PORTALL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_04]
:: OPIS: Usuwa licencje portalu bez przypisanej firmy, zeruje licencje przypisane do firm
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

VAR_DEL.delete('__lrec','__ldel','__lmod');
__lrec:=__ldel:=__lmod:=0;

PORTALL.cntx_psh();
PORTALL.index('MAIN');
PORTALL.prefix(0,null());
_can_continue:=0;
{? PORTALL.first()
|| _can_continue:=1;
   {!
   |? __lrec+=1;
      _del:=PORTALL.del();
      {? _del || __ldel+=1 ?};
      _del
   !}
?};
PORTALL.prefix();
{? (_can_continue | exec('portall_chk_il','portal_engine')=0) & PORTALL.first()
|| {!
   |? __lrec+=1;
      PORTALL.BOUGHT:=PORTALL.INSTALED:=PORTALL.USED:=PORTALL.USEDG:=0;
      {? PORTALL.put() || __lmod+=1 ?};
      PORTALL.next()
   !}
?};
PORTALL.cntx_pop();

__UPG.msg('Zaktualizowano licencje portalowe.');
__UPG.msg(
   'Przetworzono: %1 zapisów, z czego usunięto: %2 zapisów, zmodyfikowano: %3 zapisów.'[$__lrec,$__ldel,$__lmod]
);
VAR_DEL.delete('__lrec','__ldel','__lmod');

_result


\PORTALL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_04]
:: OPIS: Usuwa licencje portalu bez przypisanej firmy, zeruje licencje przypisane do firm - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizuje licencje portalowe.'


\init_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_04]
:: OPIS: Inicjuje wysyłkę danych Portal HR.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('init_send_reset','sync_mwa','PORTAL');
exec('init_send_reset','sync_mwa','PORTAL_CFG');
exec('init_send_reset','sync_mwa','PORTAL_WNIOSKI');
exec('init_send_reset','sync_mwa','PORTAL_CUSTOM');
__UPG.msg('Zainicjowano wysyłkę danych dla: PORTAL, PORTAL_CFG, PORTAL_WNIOSKI, PORTAL_CUSTOM.');
_result


\init_send_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_04]
:: OPIS: Inicjuje wysyłkę danych Portal HR - opis.
::----------------------------------------------------------------------------------------------------------------------
'Inicjowanie wysyłki danych na Portal HR.'


\XINFO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_04]
:: OPIS: Wyłącza dostęp do Portal HR, jeżeli została pozostawiona konfiguracja wielofirmowa
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('czytaj_conf','portal_engine');
{? XINFO.POR_CONF='W'
|| XINFO.POR_CONF:='J';
   {? 1+XINFO.POR_TNNT<>'_' || XINFO.POR_TNNT:='_'+XINFO.POR_TNNT ?};
   {? 1+XINFO.POR0TNNT<>'_' || XINFO.POR0TNNT:='_'+XINFO.POR0TNNT ?};
   exec('zapisz','#stalesys',1,XINFO,'POR_CONF','POR_TNNT','POR0TNNT');
   __UPG.msg('Zmodyfikowano dostęp do Portal HR.')
|| __UPG.msg('Nie zmodyfikowano dostępu do Portal HR.')
?};
_result


\XINFO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_04]
:: OPIS: Wyłącza dostęp do Portal HR, jeżeli została pozostawiona konfiguracja wielofirmowa - opis
::----------------------------------------------------------------------------------------------------------------------
'Wyłączenie dostępu do Portal HR, jeżeli została pozostawiona konfiguracja wielofirmowa.'


\SYNCFMWA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_04]
:: OPIS: Aktualizacja klienta usług intergracyjnych API
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
SYNC_MWA.cntx_psh(); SYNCFMWA.cntx_psh();
SYNCFMWA.index('FIRMA');
SYNC_MWA.prefix(); SYNCFMWA.prefix();
_formula:="
   __lrec+=1;
   {? SYNC_MWA.RESULT<>''
   || SYNCFMWA.prefix(SYNC_MWA.ref(),REF.FIRMA);
      {? ~SYNCFMWA.first()
      || SYNCFMWA.blank();
         SYNCFMWA.SYNC_MWA:=SYNC_MWA.ref();
         SYNCFMWA.FIRMA:=REF.FIRMA;
         SYNCFMWA.RESULT:=SYNC_MWA.RESULT;
         {? SYNCFMWA.add() || __lmod+=1 ?}
      ?}
   ?}
";
SYNC_MWA.for_each(_formula,1);
SYNC_MWA.cntx_pop(); SYNCFMWA.cntx_pop();
__UPG.msg('Zaktualizowano klienta usług integracyjnych API.');
__UPG.msg('Przetworzono: %1 zapisów w tabeli SYNC_MWA, z czego dodano: %2 zapisów w tabeli SYNFMWA.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\SYNCFMWA_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_04]
:: OPIS: Aktualizacja klienta usług intergracyjnych API - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja klienta usług integracyjnych API - wynik przetwarzania w kontekście firmy.'


\MJM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pole MJM.MOB, MJM.MOD
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

MJM.cntx_psh();
MJM.prefix();
MJM.for_each("__lrec+=1;
             _put:=0;
             {? ~((';TN'*MJM.MOB)>1) || _put:=1 ?};
             {? ~((';TN'*MJM.MOD)>1) || _put:=1 ?};
             {? _put & MJM.put(1) || __lmod+=1 ?}",1);
MJM.cntx_pop();

__UPG.msg('Zaktualizowano przeliczniki jednostek miary.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\MJM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pole MJM.MOB, MJM.MOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli przeliczników jednostek miary dla materiałów.'


\URZ_TAG
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizuję pole URZ_TAG.RODZAJ
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod','__typwtag','__typftag');
__lrec:=__lmod:=0;
__typftag:=';<NRI>;<NST>;<GRUPA>;<ODD>;<JORG>;';
__typwtag:=';<FIRMA>;';
URZ_TAG.cntx_psh();
URZ_TAG.prefix();
URZ_TAG.for_each("_put:=0;
                 __lrec+=1;
                 {? URZ_TAG.RODZAJ='D'
                 || {? __typwtag*(';%1;'[URZ_TAG.KOD])
                    || URZ_TAG.RODZAJ:='DW';
                       _put:=1
                    |? __typftag*(';%1;'[URZ_TAG.KOD])
                    || URZ_TAG.RODZAJ:='DF';
                       _put:=1
                    || URZ_TAG.RODZAJ:='DN';
                       _put:=1
                    ?}
                 ?};
                 {? _put & URZ_TAG.put(1) || __lmod+=1 ?}
             ",0);
URZ_TAG.cntx_pop();
__UPG.msg('Zaktualizowano definicje tagów dla urządzeń.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__typwtag','__typftag');
_result


\URZ_TAG_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizuję pole URZ_TAG.RODZAJ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola rodzaju tagu dla urządzeń'


\uzupIDpal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pola IDPAL, KODPAL dla powiązanych tabel z tabelą PAL
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');


_form:="__lrec:=__lmod:=0;
        _tab:=_a;
        _msk:=_tab.names();
        _tab.cntx_psh();
        _msk.clear();
        {? _msk.first()
        || {!
           |? _tab.use(_msk.NAME);
              _tab.prefix();
              {? _tab.first()
              || {!
                 |? __lrec+=1;
                    {? ref_name(_tab.PAL)='palety' & exec('uzupIDkod','magdok_palety',_tab)
                    || {? _tab.put(1) || __lmod+=1 ?}
                    ?};
                    _tab.next()
                 !}
              ?};
              _msk.next()
           !}
        ?};
        _tab.cntx_pop();
        obj_del(_msk);
        __UPG.msg('Zaktualizowano pola w tabeli %1.'[_b]);
        __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])";

PAL.cntx_psh();
PAL.use('palety');
_form(DK_L,'DK_L');
_form(DK_LI,'DK_LI');
_form(EANP,'EANP');
_form(INY,'INY');
_form(PAL_POZ,'PAL_POZ');
_form(PAL_ZAM,'PAL_ZAM');
_form(SL,'SL');
_form(TR_ZLM,'TR_ZLM');
PAL.cntx_pop();

VAR_DEL.delete('__lrec','__lmod');
_result


\uzupIDpal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pola IDPAL, KODPAL dla powiązanych tabel z tabelą PAL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól w tabelach powiązanych z paletami'


\ilmg2ZAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Wypełnia pola ILMG w pozycjach zamówień
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
_zam:=tab_tmp(1,'SQL','STRING[16]','','ILMG','REAL','','RODZ','STRING[1]','');

__lrec:=__lmod:=0;
OKR.cntx_psh();
ND.cntx_psh();
DK.cntx_psh();
ODDZ.cntx_psh();
ODDZ.index('KOD');
ODDZ.prefix();
{? ODDZ.first()
|| {!
   |? OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      || {!
         |? exec('mag_open','open_tab',ODDZ.KOD,form(OKR.ROK-2000,-2,0,'99'));
            ND.index('NAGDT');
            ND.prefix();
            {? ND.first() & ND.find_tab('first','Z',,'=','T')
            || {!
               |? DK.index('DOKMAG');
                  DK.prefix(ND.ref());
                  {? DK.first()
                  || {!
                     |? {? DK.ZAM_RP<>''
                        || _poz:=_ptw:='';
                           _rodz:='';
                           _zre:=0;
                           {? (5+DK.ZAM_RP)='zdhip'
                           || _rodz:='D';
                              _poz:=exec('FindAndGet','#table',ZD_RP,DK.ZAM_RP,,"$ZD_POZ",'');
                              _ptw:=exec('FindAndGet','#table',ZD_RP,DK.ZAM_RP,,"form(ZDP_POZ)",'');
                              _zre:=exec('FindAndGet','#table',ZD_RP,DK.ZAM_RP,,"IL_ZRE",0)
                           |? (5+DK.ZAM_RP)='zkhip'
                           || _rodz:='Z';
                              _poz:=exec('FindAndGet','#table',ZK_RP,DK.ZAM_RP,,"$P",'');
                              _zre:=exec('FindAndGet','#table',ZK_RP,DK.ZAM_RP,,"ILR",0)
                           ?};
                           {? _poz<>''
                           || _zam.clear();
                              _zam.prefix(_poz);
                              {? _zam.first()
                              || _zam.ILMG+=_zre;
                                 _zam.put(1)
                              || _zam.blank();
                                 _zam.SQL:=_poz;
                                 _zam.ILMG:=_zre;
                                 _zam.RODZ:=_rodz;
                                 _zam.add(1)
                              ?}
                           ?};
                           {? _ptw<>''
                           || _zam.clear();
                              _zam.prefix(_ptw);
                              {? _zam.first()
                              || _zam.ILMG+=_zre;
                                 _zam.put(1)
                              || _zam.blank();
                                 _zam.SQL:=_ptw;
                                 _zam.ILMG:=_zre;
                                 _zam.RODZ:='P';
                                 _zam.add(1)
                              ?}
                           ?}
                        ?};
                        DK.next()
                     !}
                  ?};
                  ND.find_tab('next','Z',,'=','T')
               !}
            ?};
            OKR.next()
         !}
      ?};
      ODDZ.next()
   !}
?};
_bufil:=EANX.BUFIL;
ZD_POZ.trig_off('*','*');
ZDP_POZ.trig_off('*','*');
ZK_P.trig_off('*','*');
do();
_zam.clear();
{? _zam.first()
|| {!
   |? __lrec+=1;
      EANX.BUFIL:=_zam.ILMG;
      {? _zam.RODZ='Z'
      || exec('FindAndGet','#table',ZK_P,_zam.SQL,,
          "{? ILMG<>EANX.BUFIL || ILMG:=EANX.BUFIL; {? put(1) || __lmod+=1 ?} ?}",0)
      |? _zam.RODZ='D'
      || exec('FindAndGet','#table',ZD_POZ,_zam.SQL,,
          "{? ILMG<>EANX.BUFIL || ILMG:=EANX.BUFIL; {? put(1) || __lmod+=1 ?} ?}",0)
      |? _zam.RODZ='P'
      || exec('FindAndGet','#table',ZDP_POZ,_zam.SQL,,
          "{? ILMG<>EANX.BUFIL || ILMG:=EANX.BUFIL; {? put(1) || __lmod+=1 ?} ?}",0)
      ?};
      _zam.next()
   !}
?};
end();
ZD_POZ.trig_on('*','*');
ZDP_POZ.trig_on('*','*');
ZK_P.trig_on('*','*');

EANX.BUFIL:=_bufil;
ODDZ.cntx_pop();
OKR.cntx_pop();
ND.cntx_pop();
DK.cntx_pop();
__UPG.msg('Zaktualizowano informacje o ilościach zrealizowanych z zamówień.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
obj_del(_zam);
_res


\ilmg2ZAM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Wypełnia pola ILMG w pozycjach zamówień - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja informacji o ilościach zrealizowanych z zamówień'


\PAL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizuje pola domyślny status dostawy dla palety
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec');
__lrec:=0;
_msk:=PAL.names();
PAL.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? PAL.use(_msk.NAME);
      PAL.prefix();
      PAL.for_each("
         __lrec+=1;
         exec('statsPAL','magdok_palety',PAL.ref())
      ");
      _msk.next()
   !}
?};
PAL.cntx_pop();

__UPG.msg('Zaktualizowano status dostawy dla palet.');
__UPG.msg('Przetworzono: %1 zapisów.'[$__lrec]);

obj_del(_msk);
VAR_DEL.delete('__lrec');
_res


\PAL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizuje pola domyśłny status dostawy dla palety - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja statusu dostawy dla palety'


\PX_OBJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_OBJ
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PX_OBJ.trig_off('*','*');
PX_OBJ.cntx_psh();
PX_OBJ.index('SYMBOL');
PX_OBJ.prefix();
_result:=PX_OBJ.for_each("
   _put:=0;
   __lrec+=1;
   {? PX_OBJ.HAS_TAG=''
   || PX_OBJ.HAS_TAG:='N';
      _put:=1
   ?};
   {? _put || {? PX_OBJ.put(1) || __lmod+=1 ?} ?}
",0);
PX_OBJ.cntx_pop();
PX_OBJ.trig_on('*','*');
__UPG.msg('Zaktualizowano obiekty do kolejki planu strategicznego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_OBJ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_OBJ - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla obiektów do kolejki planu strategicznego'


\PX_VER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_VER
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PX_VER.trig_off('*','*');
PX_VER.cntx_psh();
PX_VER.index('TIME');
PX_VER.prefix();
_result:=PX_VER.for_each("
   _put:=0;
   __lrec+=1;
   {? PX_VER.WHATIF=''
   || PX_VER.WHATIF:='N';
      _put:=1
   ?};
   {? _put || {? PX_VER.put(1) || __lmod+=1 ?} ?}
",0);
PX_VER.cntx_pop();
PX_VER.trig_on('*','*');
__UPG.msg('Zaktualizowano wersje planu strategicznego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_VER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_VER - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla wersji planu strategicznego'


\PX_TEX4TKTL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Generuje przepisy dla nagłówków kart
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

{? var_pres('PXoTEX')<100 || exec('PXoTEX','px_tex') ?};

PX_TEX.trig_off('*','*');
PX_TEX.cntx_psh();
PX_TEX.index('TKTL');
PX_TEX.prefix('T',PXoTEX.kind.table('M'),);
{? PX_TEX.first()
|| {!
   |? {? PX_TEX.TKTL<>null()
      ||
         PX_TEX.cntx_psh();
         PX_TEX.index('TKTL');
         _rtktl:=PX_TEX.RTKTL;
         PX_TEX.prefix('T',PXoTEX.kind.table('TKTL'),PX_TEX.TKTL);
         {? PX_TEX.first()=0
         || {? exec('create4tktl','px_tex',PX_TEX.TKTL)<>null()
            || __lrec+=1
            || _result:=0
            ?}
         || PX_TEX.RTKTL:=_rtktl;
            {? PX_TEX.put()
            || __lmod+=1
            ?}
         ?};
         PX_TEX.cntx_pop()
      ?};
      PX_TEX.next()
   !}
?};
PX_TEX.cntx_pop();
PX_TEX.trig_on('*','*');
__UPG.msg('Wygenerowano nagłówki przepisów na podstawie nagłówków kart tech.');
__UPG.msg('Utworzono: %1 przepisów. Zmodyfikowano: %2 przepisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_TEX4TKTL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Generuje przepisy dla nagłówków kart - opis
::----------------------------------------------------------------------------------------------------------------------
'Generowanie nagłówków przepisów planistycznych dla nagłówków kart technologicznych'


\BADP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADP - Badania - zestaw wykonanych badań - pozycje
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=BADP.names();
_msk.clear();
BADP.trig_off('*','*');
BADP.cntx_psh();
{? _msk.first()
|| {!
   |? BADP.use(_msk.NAME);
      BADP.prefix();
      BADP.for_each("
         _put:=0;
         __lrec+=1;
         {? BADP.CZY_NAG=''
         || BADP.CZY_NAG:='N';
            _put:=1
         ?};
         {? _put || {? BADP.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
BADP.cntx_pop();
BADP.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje zestawów wykonanych badań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BADP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADP - Badania - zestaw wykonanych badań - pozycje - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla pozycji zestawów wykonanych badań'


\BADSEP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADSEP - Badania - zestawy badań - pozycje zestawu
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
BADSEP.trig_off('*','*');
BADSEP.cntx_psh();
BADSEP.prefix();
BADSEP.for_each("
   _put:=0;
   __lrec+=1;
   {? BADSEP.CZY_DEF=''
   || BADSEP.CZY_DEF:='N';
      _put:=1
   ?};
   {? BADSEP.CYKL_CO=''
   || BADSEP.CYKL_CO:='N';
      _put:=1
   ?};
   {? _put || {? BADSEP.put(1) || __lmod+=1 ?} ?}
",0);
BADSEP.cntx_pop();
BADSEP.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje zestawów badań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BADSEP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADP - Badania - zestawy badań - pozycje zestawu - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla pozycji zestawów badań'


\BADMSEP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADMSEP - Badania - zestawy badań grupy/materiału - pozycje
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
BADMSEP.trig_off('*','*');
BADMSEP.cntx_psh();
BADMSEP.prefix();
BADMSEP.for_each("
   _put:=0;
   __lrec+=1;
   {? BADMSEP.CZY_DEF=''
   || BADMSEP.CZY_DEF:='N';
      _put:=1
   ?};
   {? _put || {? BADMSEP.put(1) || __lmod+=1 ?} ?}
",0);
BADMSEP.cntx_pop();
BADMSEP.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje badań materiałów i grup materiałowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BADMSEP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADMSEP - Badania - zestawy badań grupy/materiału - pozycje - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla pozycji badań materiałów i grup materiałowych'


\update_ZLIM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja pól tabeli ZLIM na podstawie pozycji dokumentów magazynowych DK
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('update_ildok4ZLIM','zl_nlimit',0);
__UPG.msg('Zaktualizowano nowe pola limitów surowców zleceń.');
_result


\update_ZLIM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja pól tabeli ZLIM na podstawie pozycji dokumentów magazynowych DK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli limitów surowców zleceń.'


\REZ_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizuje (przepina) dedykowane rezerwacje pod zlecenia na rezerwacje pod surowce
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');

__lrec:=__lmod:=0;
_result:=1;

REZ.cntx_psh();
ZLIM.cntx_psh();
_rez_mask:=(REZ.name()-2)+'__';
{? REZ.name()<>_rez_mask || REZ.use(_rez_mask) ?};
_ndx1:=REZ.ndx_tmp(,,'TYP',,,'ZL_REZ',,);
REZ.index(_ndx1);
:: Rezerwacje do zleceń, do których nie wygenerowano jeszcze limitów - zmiana typu
REZ.prefix('T','T');
{? REZ.first()
||
   {!
   |?
      __lrec+=1;
      _next:=0;
      _ref_nxt:=null();
      REZ.cntx_psh();
      {? REZ.next() || _ref_nxt:=REZ.ref() ?};
      REZ.cntx_pop();
      REZ.TYP:='S';
      REZ.cntx_psh();
      REZ.prefix();
      {? REZ.put(1)
      || __lmod+=1
      ?};
      REZ.cntx_pop();
      {? _ref_nxt<>null()
      || _next:=REZ.seek(_ref_nxt)
      ?};
      _next>0
   !}
?};

_zlim_rez:=tab_tmp(,
   'ZLIM','STRING[16]','$ZLIM.ref()',
   'ILR','REAL','Ilość zarezerwowana',
   'TMAT_SRC','STRING[16]','Tmat źródłowy'
);
:: Rezerwacje do zleceń, do których wygenerowano limity - zmiana typu i podpięcie pod konkretny surowiec
REZ.prefix('T','P');
{? REZ.first()
||
   {!
   |?
      __lrec+=1;
      _refrea:=REZ.REFREA;
      _dorozp:=REZ.ILR;
      ZLIM.cntx_psh();
      exec('openmask','zl_common',REZ.ZL);
      ZLIM.index('ZKN');
      ZLIM.prefix(REZ.ZL,'N',REZ.M);
      {? ZLIM.first()
      || {!
         |?
            {? _zlim_rez.find_key($ZLIM.ref())
            || _tmat_src:=_zlim_rez.TMAT_SRC;
               _ilr:=ZLIM.LIL-_zlim_rez.ILR
            || _tmat_src:=exec('FindAndGet','#table',TMAT,ZLIM.TMAT,,"{? SRC<>'' || SRC || $ref() ?}",'');
               _ilr:=ZLIM.LIL
            ?};
            {? _tmat_src=_refrea & _ilr>0
            || _ilosc:={? _ilr>_dorozp || _dorozp || _ilr ?};
               REZ.ILR-=_ilosc;
               {? REZ.put()
               ||
::                Dodanie nowej rezerwacji dedykowanej na nielimity
                  REZ.TYP:='S';
                  REZ.ZL_REZ:='P';
                  REZ.REFREA:='';
                  REZ.REFSQL:=REZ.ZLIM:=$ZLIM.ref();
                  REZ.ZGP:=$ZLIM.ZGP;
                  REZ.DODT:=date(0,0,0);
                  REZ.BTERM:='T';
                  REZ.ILR:=_ilosc;
                  REZ.cntx_psh();
                  REZ.prefix();
                  {? REZ.add(1)
                  || __lmod+=1;
                     _dorozp-=_ilosc;
                     {? _zlim_rez.find_key(REZ.ZLIM)
                     || _zlim_rez.ILR+=REZ.ILR;
                        _zlim_rez.put()
                     || _zlim_rez.ZLIM:=REZ.ZLIM;
                        _zlim_rez.ILR:=REZ.ILR;
                        _zlim_rez.TMAT_SRC:=_tmat_src;
                        _zlim_rez.add()
                     ?}
                  ?};
                  REZ.cntx_pop()
               ?}
            ?};
            ZLIM.next() & _dorozp>0
         !}
      ?};
      ZLIM.cntx_pop();
      REZ.del()
   !}
?};
REZ.ndx_drop(_ndx1);
ZLIM.cntx_pop();
REZ.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano rezerwacje do surowców zleceń.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji rezerwacji do surowców zleceń.')
?};

VAR_DEL.delete('__lrec','__lmod');
_result


\REZ_update_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizuje (przepina) dedykowane rezerwacje pod zlecenia na rezerwacje pod surowce - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rezerwacji do surowców zleceń.'


\TP2TP_KOR_ZWR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Aktualizacja tabeli TP2TP - dopisanie powiązań typów dokumentów korekty i zwrotu
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

TP2TP.cntx_psh();
TP2TP.index('T2');
TP2TP.prefix(2);
_jest:=TP2TP.first();
TP2TP.cntx_pop();
{? ~_jest
|| _typy_zwr:=exec('get','#params',300501,2);
   {? _typy_zwr<>''
   || _typy_zwr:=exec('typ_dok','lmg',_typy_zwr,,,1);
      TYPYSP.cntx_psh();
      TYPYDOK.cntx_psh();
      TP2TP.cntx_psh();
      TYPYSP.index('HIS');
      TYPYSP.prefix('N','T');
      TYPYDOK.index('KRW');
      TYPYDOK.prefix('T');
      TP2TP.index('T2');
      {? TYPYSP.first()
      || {!
         |? __lrec+=1;
            TP2TP.prefix(2,TYPYSP.ref());
            {? TYPYDOK.first()
            || {!
               |? {? (' '+_typy_zwr+' ')*(' '+TYPYDOK.T+' ')>0 & TYPYDOK.Z='T' & TYPYDOK.DN='T'
                      & TYPYDOK.TD='' & TYPYDOK.DS='N' & ~TP2TP.find_key(TYPYDOK.ref())
                  || TP2TP.blank();
                     TP2TP.RODZ:=2;
                     TP2TP.TPS1:=TYPYSP.ref();
                     TP2TP.TPM2:=TYPYDOK.ref();
                     TP2TP.add();
                     __lmod+=1
                  ?};
                  TYPYDOK.next()
               !}
            ?};
            TYPYSP.next()
         !}
      ?};
      TYPYSP.cntx_pop();
      TYPYDOK.cntx_pop();
      TP2TP.cntx_pop()
   ?}
?};

__UPG.msg('Przetworzono: %1 typów dokumentów korygujacych, dodano: %2 powiązań z typami dokumentów zwrotu .'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');

_result


\TP2TP_KOR_ZWR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Aktualizacja tabeli TP2TP - dopisanie powiązań typów dokumentów korekty i zwrotu - opis
::----------------------------------------------------------------------------------------------------------------------
'Dopisanie powiązań typów dokumentów korekty i zwrotu.'


\sync_def_ud_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka definicji wymiany danych tabeli UD_DEF dla przeznaczenia PORTAL.
::  ORG: \sync_def_ud_def/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
SYNC_DEF.cntx_psh();
SYNC_DEF.index('PDNR1');
SYNC_DEF.prefix('PORTAL','UD_DEF',1);
{? SYNC_DEF.first()
|| _old:=''+"UD_DEF.UD_SCH=exec('domyslny','schemat','PODZORG',0)";
   _new:=''+"UD_DEF.UD_SCH=exec('domyslny','schemat','PODZORG',0,1)";
   {? SYNC_DEF.WAR_FORM=_new
   || __UPG.msg('Poprawka była już zastosowana.')
   |? SYNC_DEF.WAR_FORM=_old
   || SYNC_DEF.WAR_FORM:=_new;
      {? SYNC_DEF.put()
      || __UPG.msg('Zastosowanie poprawki zakończyło się sukcesem.')
      || __UPG.msg('Zastosowanie poprawki nie powiodło się.');
         _ret:=-1
      ?}
   ?}
|| __UPG.msg('Brak odpowiedniej definicji = brak danych do poprawiania.')
?};
SYNC_DEF.cntx_pop();
_ret


\sync_def_ud_def_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka definicji wymiany danych tabeli UD_DEF dla przeznaczenia PORTAL - opis.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka definicji wymiany danych tabeli UD_DEF dla przeznaczenia PORTAL'


\porsloit_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS:  ER/WRT/XP/22.26/2209/0048 - czyści błędne powiązania systemowych słowników portalowych
::  ORG: \porslo_clear/upgrade_2226.fml
::  ORG: \porslo_clear/upgrade_2226_04.fml
::  ORG: \porslo_clear/upgrade_2325.fml
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(,'UIDREF','STRING[48]','Uidref rekordu');

:: zalozenie transakcji
_mydo:=do_state()=0;
{? _mydo || do() ?};

:: dezaktywujemy wszystkie metody API dla wskazanego przeznaczenia danych i tabeli
_ok:=exec('toggle_activ_4tab','sync_mwa','PORTAL_CFG','PORSLO','N',_TAB,'add')>0 &
     exec('toggle_activ_4tab','sync_mwa','PORTAL_CFG','PORSLOIT','N',_TAB,'add')>0 &
     exec('toggle_activ_4tab','sync_mwa','PORTAL_HR_CFG','PORSLO','N',_TAB,'add')>0 &
     exec('toggle_activ_4tab','sync_mwa','PORTAL_HR_CFG','PORSLOIT','N',_TAB,'add')>0;

:: zapamiętujemy do pliku tylko te rekordy SYNC_MWA, które dezaktywowaliśmy
{? _ok
|| _file:=fopen('~upgrade_porslo.dfg','uw',1,0,1);
   {? _file.is_open()
   || _sep:=';';
      _TAB.cntx_psh();
      _TAB.prefix();
      {? _TAB.first()
      || {!
         |? _line:=_TAB.UIDREF+_sep;
            _file.fwrite(_line);
            _TAB.next()
         !}
      ?};
      _TAB.cntx_pop();
      _file.fclose();
      __UPG.msg('Zapamiętano listę dezaktywowanych rekordów tabeli SYNC_MWA.')
   || _ok:=0
   ?}
?};

:: modyfikujemy definicję wymiany danych dla tabel PORSLO i PORSLOIT
_size:=0;
_l_mod:=0;
{? _ok
|| SYNC_DEF.cntx_psh();
   SYNC_DEF.index('PDTAB');
   SYNC_DEF.prefix('PORSLO');
   {? SYNC_DEF.first()
   || _size+=SYNC_DEF.size();
      {!
      |? SYNC_DEF.WAR_FORM:='';
         SYNC_DEF.FIR:='F';
         _l_mod+=SYNC_DEF.put(1);
         SYNC_DEF.next()
      !}
   ?};
   SYNC_DEF.prefix('PORSLOIT');
   {? SYNC_DEF.first()
   || _size+=SYNC_DEF.size();
      {!
      |? SYNC_DEF.WAR_FORM:='';
         SYNC_DEF.FIR:='F';
         _l_mod+=SYNC_DEF.put(1);
         SYNC_DEF.next()
      !}
   ?};
   SYNC_DEF.cntx_pop()
?};

_result:=_ok & (_l_mod=_size);

:: jeżeli coś poszło nie tak, to wycofujemy wszystkie zmiany
{? ~_result  || undo() ?};
:: kończymy transakcję
{? _mydo || end() ?};

{? _result>0
||
:: przepisanie pozostawionych rekordów PORSLOIT z PORSLO.FIRMA<>null do PORSLO.FIRMA=null
:: wraz z wypełnieniem pola PORSLOIT.FIRMA
   _getPorslo:="
      PORSLO.prefix(null,_a,_b,_c);
      {? PORSLO.first()
      || _result:=PORSLO.ref()
      || null()
      ?}
   ";

   PORSLO.cntx_psh();
   PORSLO.index('FIELD');
   PORSLO.prefix();
   PORSLOIT.cntx_psh();
   PORSLOIT.index('SLOVAL');
   PORSLOIT.prefix();
   {? PORSLOIT.f_active() || PORSLOIT.f_clear() ?};
   PORSLOIT.f_set(,'join PORSLO','PORSLO.FIRMA is not null');
   _f_size:=PORSLOIT.f_size();
   _l_mod:=0;
   {? PORSLOIT.f_first()
   || FUN.prg_start(_f_size,'Przeniesienie powiązania z firmą z PORSLO.FIRMA do PORSLOIT.FIRMA.');
      {!
      |? _firma:=PORSLOIT.PORSLO().FIRMA;
         _porslo:=_getPorslo(PORSLO.FIELD,PORSLO.TYPE,PORSLO.CODE);
         {? _porslo<>null()
         || PORSLOIT.PORSLO:=_porslo;
            PORSLOIT.FIRMA:=_firma;
            _l_mod+=PORSLOIT.put()
         ?};
         FUN.prg_next();
         PORSLOIT.f_next()
      !};
      FUN.prg_stop()
   ?};
   PORSLOIT.cntx_pop();
   PORSLO.cntx_pop();
   {? _l_mod>0
   || _opis:="$_a+{? _a=1 || ' rekordu' || ' rekordów' ?}";
      __UPG.msg('Przeniesiono powiązania z firmą z PORSLO.FIRMA do PORSLOIT.FIRMA dla %1.'[_opis(_l_mod)])
   || __UPG.msg('Nie znaleziono rekordów tabeli PORSLOIT, które wymagałyby przeniesienia powiązania z firmą.')
   ?};

:: czyszczenie błędnych powiązań
   _PORSLO:=tab_tmp(1,
      'FIELD','STRING[255]','Lista wartości - nazwa pola',
      'CODE','STRING[100]','Kod dynamicznej listy wartości',
      'TYPE','STRING[100]','Typ listy wartości',
      'OPIS','STRING[255]','Opis słownika',
      'SYSTEM','STRING[1]','Element systemowy [T/N]'
   );
   KOMM.init();
   FIRMA.cntx_psh();
   PORSLO.cntx_psh();
   PORSLO.index('FIELD');
   PORSLO.prefix();
   {? PORSLO.first()
   || FUN.prg_start(PORSLO.size(),'Usuwanie błędnych powiązań dla systemowych słowników portalowych.');
      {!
      |? _del:=0;
         {? PORSLO.SYSTEM='T'
         || _del:={? PORSLO.FIELD=exec('customField','portal_slowniki')
                  || exec('porsloit_delete4','portal_slowniki',PORSLO.ref()) & PORSLO.count()=0
                  || PORSLO.count()=0
                  ?}
         || _del:=PORSLO.count()=0 & PORSLO.FIRMA<>null();
            _PORSLO.FIELD:=PORSLO.FIELD;
            _PORSLO.TYPE:=PORSLO.TYPE;
            _PORSLO.CODE:=PORSLO.CODE;
            _PORSLO.OPIS:=PORSLO.OPIS;
            _PORSLO.SYSTEM:=PORSLO.SYSTEM;
            _PORSLO.add(1)
         ?};
         FUN.prg_next();
         {? _del
         || PORSLO.del()
         || PORSLO.GLOBAL:='N';
            PORSLO.put();
            PORSLO.next()
         ?}
      !};
      FUN.prg_stop();
      _PORSLO.prefix();
::    dla pewności zakładamy (jeżeli nie było) wpisy nullowe dla słowników nie systemowych (wstępnie jako lokalne)
      {? _PORSLO.first()
      || {!
         |? exec('porslo_add','portal_slowniki',
                  _PORSLO.FIELD,_PORSLO.TYPE,_PORSLO.CODE,_PORSLO.OPIS,null(),_PORSLO.SYSTEM,'N','N');
            _PORSLO.next()
         !}
      ?}
   ?};
   PORSLO.cntx_pop();
   FIRMA.cntx_pop();
   __UPG.msg('Usunięto błędne powiązania dla systemowych słowników portalowych.')
|| _result:=-1;
   __UPG.msg('Nie powiodła się modyfikacja definicji wymiany danych dla tabel PORSLO oraz PORSLOIT.')
?};
_result


\porsloit_clear_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS:  ER/WRT/XP/22.26/2209/0048 - czyści błędne powiązania systemowych słowników portalowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2209/0048 - usunięcie błędnych powiązań dla systemowych słowników portalowych.'


\porslo_reinit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2209/0048 - ponowna aktualizacja elementów słowników wykorzystywanych na portalu.
::  ORG: \porslo_reinit/upgrade_2226.fml
::  ORG: \porslo_reinit/upgrade_2226_04.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init','portal_slowniki');
__UPG.msg('Zaktualizowano zawartość słowników portalowych.'@);
1


\porslo_reinit_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2209/0048 - ponowna aktualizacja elementów słowników wykorzystywanych na portalu - opis.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2209/0048 - ponowna aktualizacja elementów słowników wykorzystywanych na portalu.'


\porslo_global
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2209/0048 - dodanie wybranych słowników do listy słowników globalnych
::  ORG: \porslo_global/upgrade_2226.fml
::  ORG: \porslo_global/upgrade_2226_04.fml
::  ORG: \porslo_global/upgrade_2325.fml
::----------------------------------------------------------------------------------------------------------------------
_conf:=XINFO.POR_CONF;
exec('czytaj','#stalesys',,XINFO,'POR_CONF');
_conf==XINFO.POR_CONF;

_l_mod:=0;
PORSLO.cntx_psh();
PORSLO.index('SYSTEM');
PORSLO.prefix();
{? PORSLO.first()
|| _customField:=exec('customField','portal_slowniki');
   _sql:="select * from PORSLOIT join PORSLO using(PORSLO.REFERENCE, PORSLOIT.PORSLO)
          where PORSLO.FIRMA is not null and PORSLO.FIELD=':_a' and PORSLO.CODE=':_b'"+'';
   FUN.prg_start(PORSLO.size(),'Analiza słowników użytkownika');
   {!
   |? {? (PORSLO.FIELD<>_customField | exec('oCustomGlobal','portal_slowniki').exists(PORSLO.CODE))
      || PORSLO.GLOBAL:='T'
      ?};
      _l_mod+=PORSLO.put(1);
      FUN.prg_next();
      PORSLO.next()
   !};
   FUN.prg_stop()
?};
PORSLO.cntx_pop();
_opis:=$_l_mod+{? _l_mod=1 || ' rekordu' || ' rekordów' ?};
__UPG.msg('Ustawiono znacznik "GLOBAL" dla %1 tabeli PORSLO.'[_opis]);

:: modyfikujemy definicję wymiany danych dla tabel PORSLO i PORSLOIT
_mydo:=do_state()=0;
{? _mydo || do() ?};
_size:=0;
_l_mod:=0;

SYNC_DEF.cntx_psh();
SYNC_DEF.index('PDTAB');
SYNC_DEF.prefix('PORSLO');
{? SYNC_DEF.first()
|| _size+=SYNC_DEF.size();
   {!
   |? SYNC_DEF.WAR_FORM:=''+"exec('PORSLO_ok','portal_slowniki',_a,_b,_c)";
      SYNC_DEF.FIR:='F';
      _l_mod+=SYNC_DEF.put(1);
      SYNC_DEF.next()
   !}
?};
SYNC_DEF.prefix('PORSLOIT');
{? SYNC_DEF.first()
|| _size+=SYNC_DEF.size();
   {!
   |? SYNC_DEF.WAR_FORM:=''+"exec('PORSLOIT_ok','portal_slowniki',_a,_b,_c)";
      SYNC_DEF.FIR:='F';
      _l_mod+=SYNC_DEF.put(1);
      SYNC_DEF.next()
   !}
?};
SYNC_DEF.cntx_pop();

_result:=(_l_mod=_size);

:: jeżeli coś poszło nie tak, to wycofujemy wszystkie zmiany
{? ~_result  || undo() ?};
:: kończymy transakcję
{? _mydo || end() ?};

{? _result
||
:: putujemy wszystkie PORSLO i PORSLOIT w celu ponownego wysłania ich na portal
   PORSLO.trig_off('*','*');
   PORSLOIT.trig_off('*','*');

   PORSLO.cntx_psh();
   PORSLO.prefix();
   PORSLO.for_each("PORSLO.put(,1)",1);
   PORSLO.cntx_pop();
   PORSLOIT.cntx_psh();
   PORSLOIT.prefix();
   PORSLOIT.for_each("PORSLOIT.put(,1)",1);
   PORSLOIT.cntx_pop();

   PORSLOIT.trig_on('*','*');
   PORSLO.trig_on('*','*');

:: aktywujemy wyłączone wcześniej w zadaniu 'porsloit_clear' metody API
   _TAB:=tab_tmp(,'UIDREF','STRING[48]','Uidref rekordu');
   _fileName:='~upgrade_porslo.dfg';
   {? fexists(_fileName,1)=1
   || _TAB.import(_fileName,0,0,,'UTF-8,noheader',,'UIDREF',,1,);
      ferase(_fileName,1)
   ?};

   _ok:=exec('toggle_activ_4tab','sync_mwa','PORTAL_HR_CFG','PORSLO','T',_TAB,'check')>0 &
        exec('toggle_activ_4tab','sync_mwa','PORTAL_HR_CFG','PORSLOIT','T',_TAB,'check')>0 &
        exec('toggle_activ_4tab','sync_mwa','PORTAL_CFG','PORSLO','T',_TAB,'check')>0 &
        exec('toggle_activ_4tab','sync_mwa','PORTAL_CFG','PORSLOIT','T',_TAB,'check')>0;
   {? _ok
   || __UPG.msg('Przywrócono status aktywności dla listy zapamiętanych rekordów tabeli SYNC_MWA.')
   || __UPG.msg('Nie udało się przywrócić aktywacji dla listy zapamiętanych rekordów tabeli SYNC_MWA.')
   ?}

|| _result:=-1;
   __UPG.msg('Nie powiodła się modyfikacja definicji wymiany danych dla tabel PORSLO oraz PORSLOIT.')
?};

_result


\porslo_global_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2209/0048 - dodanie wybranych słowników do listy słowników globalnych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2209/0048 - dodanie wybranych słowników do listy słowników globalnych.'


\porslo_global_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2209/0048 - dodanie wybranych słowników do listy słowników globalnych
::        - informacja o konieczności weryfikacji listy
::  ORG: \porslo_global_info/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['porslo_global_info'])
|| _result:=1
|| _result:=-1
?};
_result


\porslo_global_info_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2209/0048 - dodanie wybranych słowników do listy słowników globalnych
::        - informacja o konieczności weryfikacji listy - opis.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2209/0048 wprowadziła nowy sposób obsługi słowników portalowych.\n'
'Dodano mechanizm w którym można określić, który ze słowników obsługujących uniwersalną listę wartości\n'
'jest słownikiem globalnym (wspólnym dla wszystkich firm).\n'
'Słowniki spoza tej listy będą traktowane jako słowniki lokalne (firmowe).\n'
'Mechanizm dostępny jest w obszarze "Ustawienia i parametryzacja" w sekcji:\n'
' ● HR Portal->Słowniki->Lista globalnych słowników portalowych\n\n'
'Po wgraniu poprawki i uruchomieniu formuł potransterowych automatycznych, należy zweryfikować\n'
'poprawność wpisów dla słowników użytkownika (nie systemowych).'


\wn_praktyka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0053 - Wniosek o praktykę - jest odrzucany z powodu nieodnalezienia opiekuna.
::  ORG: \wn_praktyka/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

_firma:=null();
_field:='OpiekunImieNazwisko';
_type:='Request';
_system:='T';

PORSLO.cntx_psh();
PORSLO.index('SYSTEM');
PORSLO.prefix(_system,_firma,_field,_type,'',);
{? PORSLO.first()
|| __UPG.msg('Słownik %1 już istniał.'[_field]);
   _porslo:=PORSLO.ref()
|? PORSLO.blank();
   PORSLO.FIRMA:=_firma;
   PORSLO.FIELD:=_field;
   PORSLO.TYPE:=_type;
   PORSLO.SYSTEM:=_system;
   PORSLO.add()
|| __UPG.msg('Utworzonie słownika %1 zakończyło się sukcesem.'[_field]);
   _porslo:=PORSLO.ref()
|| __UPG.msg('Utworzenie słownika %1 nie powiodło się.'[_field]);
   _porslo:=null()
?};
PORSLO.cntx_pop();

{? _porslo=null()
|| return(-1)
?};

ETYPY.cntx_psh();
ETYPY.index('UNIK_WP');
ETYPY.prefix(2,'T','Praktyka',);
ETYP_ATR.cntx_psh();
ETYP_ATR.index('ID_WP');
TAT.cntx_psh();
TAT.prefix();
{? ~ETYPY.first()
|| __UPG.msg('W systemie niezdefiniowano wniosku "Praktyka". Zmiany nie są wymagane.')
|? ETYP_ATR.prefix(ETYPY.ref(),'OpiekunImieNazwisko',);
   ~ETYP_ATR.first()
|| __UPG.msg(
      'Niestandardowa budowa wniosku "Praktyka" - brak pola "OpiekunImieNazwisko". '
      'Zmiany nie mogą zostać wykonane.');
   _ret:=-1
|? ETYP_ATR.TAT().PORSLO=_porslo & TAT.PORTYPE=_type
|| __UPG.msg('Atrybt nie wymagał zmiany.')
|? TAT.PORSLO:=_porslo;
   TAT.PORTYPE:=_type;

   ~TAT.put()
|| __UPG.msg('Aktualizacja atrybutu nie powiodła się.');
   _ret:=-1
|| __UPG.msg('Aktualizacja atrybutu zakończyła się sukcesem.')
?};
TAT.cntx_pop();
ETYP_ATR.cntx_pop();
ETYPY.cntx_pop();

_ret


\wn_praktyka_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0053 - Wniosek o praktykę - jest odrzucany z powodu nieodnalezienia opiekuna - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2210/0053\n'
'Wniosek o praktykę - jest odrzucany z powodu nieodnalezienia opiekuna.'


\fix_wn_zaliczka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0077 - Wniosek o zaliczkę (POR_WOL_WWW) wprowadzony przez portal HR
::       nie przenosi rachunku bankowego pracownika do Merita do pola EDOKUM.N
::  ORG: \fix_wn_zaliczka/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_sql:=''+"
   select
      EZAL.REFERENCE as REF, EZAL.N from @EZAL
   join
      @EDOKUM using(EDOKUM.REFERENCE, EZAL.EDOKUM)
   join
      @ETYPY using(EDOKUM.TYP, ETYPY.REFERENCE)
   where
      EZAL.N='' and ETYPY.ID_WP='Zaliczka'
";

_TAB:=sql(_sql);

_result:=1;

{? _TAB.first()
|| _logFile:='upgrade_fix_wn_zaliczka.log';
   _l_rek:=0;
   _size:=_TAB.size();

   FUN.prg_start(_size,'Przetwarzanie błędnych zapisów ...');
   EZAL.cntx_psh();
   {!
   |? {? EZAL.seek(_TAB.REF)
      || EDOKUM.cntx_psh();
         _name:=ref_name(EZAL.EDOKUM);
         {? +_name & EDOKUM.name()<>_name
         || EDOKUM.use(_name)
         ?};
         EZAL.EDOKUM();

         {? var_pres('_res')>0 || obj_del(_res) ?};
         _res:=exec('zaliczka_walid','portal_walidacja');
         {? ~_res.ok
         || exec('write','%logger',_logFile,'zaliczka_walid/portal_walidacja.fml',
                                   'Błąd podczas walidacji EDOKUM.ref() - %1'[$EDOKUM.ref()],TKOM13.OPIS)
         ?};

         _mydo:=do_state()=0;
         {? _mydo || do() ?};
         {? _res.ok
         || {? exec('zaliczka_akcept','portal_walidacja').ok
            || EZAL.N:=EDOKUM.N;
               {? EZAL.put()
               || _l_rek+=1;
                  exec('write','%logger',_logFile,'zaliczka_akcept/portal_walidacja.fml',
                                         'Przepisano numer rachunku z wniosku: %1 do nagłówka zaliczki: %2'
                                         [$EDOKUM.ref(),$EZAL.ref()])
               ?}
         || undo()
            ?}
         ?};
         {? _mydo || end() ?};
         EDOKUM.cntx_pop()
      ?};
      FUN.prg_next();
      _TAB.next()
   !};
   EZAL.cntx_pop();
   FUN.prg_stop();
   __UPG.msg('Naprawiono %1 z %2 zapisów'@[$_l_rek,$_size]);
   _path:=pth_dir(_logFile)+exec('sep','#file',1)+_logFile;
   __UPG.msg('Informacje o przetworzonych rekordach odpisano do pliku: %1'@[_path]);
   _result:={? _l_rek=_size || 1 || -1 ?}
|| __UPG.msg('Nie znaleziono błędnych zapisów.'@)
?};
_result


\fix_wn_zaliczka_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0077 - Wniosek o zaliczkę (POR_WOL_WWW) wprowadzony przez portal HR
::       nie przenosi rachunku bankowego pracownika do Merita do pola EDOKUM.N
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2210/0077\n'
'Wniosek o zaliczkę (POR_WOL_WWW) wprowadzony przez portal HR\n'
'nie przenosi rachunku bankowego pracownika do Merita do pola EDOKUM.N'


\map_tabs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0028 - Kłopoty z typami / podtypami nieobecności
::      (wysyłanie nieobecności i limitów / odbieranie wniosków urlopowych).
::  ORG: \map_r/upgrade_2226.fml +\zs_def_put/upgrade_2226.fml
::       Poniewaz to zadanie jest znacznie szersze w dzialaniu realizuje ono takze:
::       \portal_url_dod/upgrade_2226.fml
::       \kart_url_21110024/upgrade_2137.fml
::       \portal_ZS_DEF_clean/upgrade_2137.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

_tabs:=spli_str('R,N,KART_URL,DS,ZS_DEF',',');
_dl:=obj_len(_tabs);
{! _lt:=1 .. _dl
|! _TAB:=($_tabs[_lt])();
   _TAB.cntx_psh();
   {? _tabs[_lt]='ZS_DEF' || _TAB.index('IDADD') ?};
   _TAB.prefix();
   {? _TAB.first()
   || _TAB.trig_off('*','*');
      {? _TAB.for_each(($(_tabs[_lt]+'.put(,1)')),1)
      || __UPG.msg('Znaczniki modyfikacji rekordów tabeli %1 zostały zaktualizowane.'[_tabs[_lt]])
      ?};
      _TAB.trig_on('*','*')
   || __UPG.msg('Brak rekordów w tabeli %1. Zmiany nie są wymagane.'[_tabs[_lt]])
   ?};
   _TAB.cntx_pop();
   obj_del(_TAB)
!};

_ret


\map_r_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0028 - Kłopoty z typami / podtypami nieobecności
::      (wysyłanie nieobecności i limitów / odbieranie wniosków urlopowych) - opis.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2211/0028\n'
'Kłopoty z typami / podtypami nieobecności\n'
'(wysyłanie nieobecności i limitów / odbieranie wniosków urlopowych)'


\portal_bnftt_nag_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka błędu ER/WRT/XP/22.26/2208/0073 - Benefity - synchronizacja załączników
::  ORG: \portal_bnftt_nag_id/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
SYNC_IDD.cntx_psh();
SYNC_IDD.index('SYSTEM');
SYNC_IDD.prefix('PORTAL','PORTAL_BNFTT_NAG_ID',);
{? SYNC_IDD.first()
|| SYNC_IDM.cntx_psh();
   SYNC_IDM.index('KOD');
   SYNC_IDM.prefix(SYNC_IDD.KOD,);
   {? SYNC_IDM.first()
   || _stat:=obj_new('poprawnych','niepoprawnych','nieznalezionych');
      {! _lp:=1 .. obj_len(_stat)
      |! _stat[_lp]:=0
      !};

      BNFTT.cntx_psh();
      SYNC_ID.cntx_psh();
      {!
      |? SYNC_ID.use(SYNC_IDM.MASKA_ID);
         SYNC_ID.index('REK');
         SYNC_ID.prefix();
         {? SYNC_ID.first()
         || {!
            |? {? BNFTT.seek(SYNC_ID.REK,)
               || {? SYNC_ID.OPIS=BNFTT.HASH
                  || _stat.poprawnych+=1;
                     SYNC_ID.next()
                  || _stat.niepoprawnych+=1;
                     SYNC_ID.del(,1)=2
                  ?}
               || _stat.nieznalezionych+=1;
                  SYNC_ID.del(,1)=2
               ?}
            !}
         ?};
         SYNC_IDM.next()
      !};
      SYNC_ID.cntx_pop();
      BNFTT.cntx_pop();

      __UPG.msg('Zapisów poprawnych: %1'[$_stat.poprawnych]);
      __UPG.msg('Zapisów niepoprawnych: %1'[$_stat.niepoprawnych]);
      __UPG.msg('Nieznalezionych benefitów (usuniętych identyfikatorów): %1'[$_stat.nieznalezionych])

   || __UPG.msg('Brak danych do weryfikacji [SYNC_IDM].')
   ?};
   SYNC_IDM.cntx_pop()
|| __UPG.msg('Brak danych do weryfikacji [SYNC_IDD].')
?};
SYNC_IDD.cntx_pop();
_ret


\portal_bnftt_nag_id_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka błędu ER/WRT/XP/22.26/2208/0073 - Benefity - synchronizacja załączników
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie zbędnych identyfikatorów rekordów dla kodu PORTAL_BNFTT_NAG_ID'


\portal_wn_urlop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IGPTASZE [22.26]
:: OPIS: Zmiana kolejności przetwarzania metod API dla przeznaczenia danych PORTAL_WNIOSKI
::       ER/WRT/XP/22.26/2301/0060 - Problem z wnioskami po aktualizacji 22.26_04_Portal_HR_wielofirmowosc
::  ORG: \portal_wn_urlop/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=-1;
_pd:='PORTAL_WNIOSKI';
SYNC_MWA.cntx_psh();
SYNC_MWA.index('PD_METH');
SYNC_MWA.prefix();
:: szukanie lp metod cseod_AcceptanceHeaderModify i chr_PersonAbsenceRequestModify
{? ~SYNC_MWA.find_key(_pd,'cseod_AcceptanceHeaderModify',)
|| __UPG.msg('Nie odnaleziono metody cseod_AcceptanceHeaderModify.')
|? _header:=SYNC_MWA.LP;
   __UPG.msg('Metoda: cseod_AcceptanceHeaderModify Lp: %1.'[$_header]);
   ~SYNC_MWA.find_key(_pd,'chr_PersonAbsenceRequestModify',)
|| __UPG.msg('Nie odnaleziono metody chr_PersonAbsenceRequestModify.')
|? _request:=SYNC_MWA.LP;
   __UPG.msg('Metoda: chr_PersonAbsenceRequestModify Lp: %1.'[$_request]);
:: sprawdzenie i w razie potrzeby zmiana kolejności metod
   _request<_header
|| __UPG.msg('Zmiana kolejności przetwarzania metod nie była wymagana.');
   _ret:=1
|? __UPG.msg('Wymagana jest zmiana kolejności przetwarzania metod API.');
   _ds:=do_state();
   _ds=2
|| __UPG.msg('Zmiany nie przeprowadzone z powodu istnienia zerwanej transakcji.')
|| {? _ds=0 || do() ?};
   SYNC_MWA.LP:=-1;
   {? SYNC_MWA.put()
   || SYNC_MWA.cntx_psh();
      SYNC_MWA.index('PD_LP');
      {? SYNC_MWA.find_key(_pd,_request-1)
      || {!
         |? {? SYNC_MWA.LP>=_header
            || SYNC_MWA.LP+=1;
               SYNC_MWA.put() & SYNC_MWA.prev()
            ?}
         !}
      ?};
      SYNC_MWA.cntx_pop();
      SYNC_MWA.LP:=_header;
      SYNC_MWA.put()
   ?};
   {? {? _ds=0 || end() || do_state()=1 ?}
   || __UPG.msg('Zmiana kolejności przetwarzania metod API zakończona powodzeniem.');
      _ret:=1
   || __UPG.msg('Zmiana kolejności przetwarzania metod API niepowiodła się. Zmiany zostały wycofane.')
   ?}
?};
SYNC_MWA.cntx_pop();
_ret


\portal_wn_urlop_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IGPTASZE [22.26]
:: OPIS: Zmiana kolejności przetwarzania metod API dla przeznaczenia danych PORTAL_WNIOSKI- opis
::       ER/WRT/XP/22.26/2301/0060 - Problem z wnioskami po aktualizacji 22.26_04_Portal_HR_wielofirmowosc
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiana kolejności przetwarzania metod API chr_PersonAbsenceRequestModify i chr_PersonAbsenceRequestModify\n'
'dla przeznaczenia danych PORTAL_WNIOSKI.'


\sch_form_portal_nwu_access
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Utworzenie formuły portal_nwu_access w tabeli SCH_FORM.
::  ORG: \sch_form_portal_nwu_access/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
:: Ewentualne zmiany nanieść również w \def_wu_ogolny/etypy.fml.
::
:: Formuła walidacyjna użytkownika akceptującego (przełożonego).
_typ:='E';
_system:='T';
_skrot:='portal_nwu_access';
_nazwa:='HR Portal: Wyszukanie przełożonego (akceptanta)';
_tresc:=''+"exec('przelozony','form_ob',,,,'AKCWNIO')";

_ret:=1;
{? exec('sch_form_exists','sch_imp',_typ,_skrot)=null()
|| {? exec('sch_form_add','sch_imp',_typ,_system,_skrot,_nazwa,_tresc)
   || __UPG.msg('Utworzenie formuły "%1" powiodło się.'[_skrot])
   || __UPG.msg('Utworzenie formuły "%1" NIE powiodło się.'[_skrot]);
      _ret:=-1
   ?}
|| __UPG.msg('Utworzzenie formuły "%1" nie było wymagane - formuła już istniała.'[_skrot])
?};
_ret


\sch_form_portal_nwu_access_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Utworzenie formuły portal_nwu_access w tabeli SCH_FORM - opis.
::----------------------------------------------------------------------------------------------------------------------
'Utworzenie formuły portal_nwu_access w tabeli SCH_FORM.'


\pola_PL2023
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie wartości w nowych pola, modyfikacja w istniejących polach.
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');
__RUB.fill();
_result:=1;
_firma:=exec('ref_firma','ustawienia');

:: Rodzaje wniosków dotyczących podatków - zmiana nazw:
_modKod:="
   SLO_KOD.prefix(_a,_b,_b);
   {? SLO_KOD.first()
   || SLO_KOD.NAZWA:=_c;
      SLO_KOD.SYSTEM:=_d;
      SLO_KOD.put()
   ?}
";
SLO_TYP.cntx_psh();
SLO_KOD.trig_off('*','*');
SLO_KOD.cntx_psh();
SLO_KOD.index('KOD');
_typ:=exec('slo_typ','ext_slo','WN_PODAT');
_modKod(_typ,'UZ_POD','Wniosek o niepobieranie zaliczek na podatek dochodowy','T');
_modKod(_typ,'KL_SRED','Wniosek o zaprzestanie stosowania ulgi dla klasy średniej','T');
_modKod(_typ,'TP_ZAL_P','Wniosek o nieprzedłużanie terminów poboru zaliczek (pracownik)','T');
_modKod(_typ,'TP_ZAL_Z','Wniosek o nieprzedłużanie terminów poboru zaliczek (zleceniobiorca)','T');
&_typ;
SLO_KOD.cntx_pop();
SLO_KOD.trig_on('*','*');
SLO_TYP.cntx_pop();

:: Rodzaje wniosków dotyczących podatków - uzupełnienie słownika (przeniesienie ze SLO_KOD)
OS_ZWSLO.trig_off('*','*');
OS_ZWSLO.cntx_psh();
OS_ZWSLO.index('TYPKOD');
OS_ZWSLO.prefix(_firma);
_addOsZwslo:=
   "{? ~OS_ZWSLO.find_key(_a,_b,)
    || OS_ZWSLO.blank(1);
       OS_ZWSLO.FIRMA:=exec('ref_firma','ustawienia');
       OS_ZWSLO.TYP:=_a;
       OS_ZWSLO.KOD:=_b;
       OS_ZWSLO.RODZAJ:=_c;
       OS_ZWSLO.ROCZNE:=_d;
       OS_ZWSLO.ZDR:=_e;
       OS_ZWSLO.PODATNIK:=_f;
       OS_ZWSLO.add(1)
    ?}
   ";
_addOsZwslo('W','UZ_POD','Wniosek o niepobieranie zaliczek na podatek dochodowy','T','T','T');
_addOsZwslo('W','KL_SRED','Wniosek o zaprzestanie stosowania ulgi dla klasy średniej','T','T','T');
_addOsZwslo('W','TP_ZAL_P','Wniosek o nieprzedłużanie terminów poboru zaliczek (pracownik)','T','T','N');
_addOsZwslo('W','TP_ZAL_Z','Wniosek o nieprzedłużanie terminów poboru zaliczek (zleceniobiorca)','T','T','N');
_addOsZwslo('W','KU_50%','Wniosek o niestosowanie 50% kosztów uzyskania przychodu','N','T','T');
:: Uzupełnienie nowych pól OS_ZWSLO: TYP, ROZLICZ, RA_DEF, PO, DO_OB i PODATNIK
:: Zmiana OS_ZWSLO.ROCZNE dla ogłoszeń ('T' tylko dla powrotu z zagranicy)
OS_ZWSLO.index('KOD');
OS_ZWSLO.prefix(_firma);
{? OS_ZWSLO.first()
|| RA_DEF.cntx_psh();
   _dtUst:=date(2022,7,1);
   {!
   |? _put:=0;
      {? OS_ZWSLO.TYP=''
      || OS_ZWSLO.TYP:='O';
         _put:=1
      ?};
      {? OS_ZWSLO.PODATNIK=''
      || {? ',TP_ZAL_P,TP_ZAL_Z,'*',%1,'[OS_ZWSLO.KOD]
         || OS_ZWSLO.PODATNIK:='N'
         || OS_ZWSLO.PODATNIK:='T'
         ?};
         _put:=1
      ?};
      {? OS_ZWSLO.ROZLICZ=''
      || {? ',ZWPODE,ZWPOD,ZWPODP,ZWPODD,UZ_POD,'*',%1,'[OS_ZWSLO.KOD]
         || OS_ZWSLO.ROZLICZ:='T'
         || OS_ZWSLO.ROZLICZ:='N'
         ?};
         _put:=1
      ?};
      {? OS_ZWSLO.RA_DEF=null()
      || {? ',ZWPODE,ZWPOD,ZWPODP,ZWPODD,'*',%1,'[OS_ZWSLO.KOD]
         || {? __RUB.find_def('S',90202)
            || OS_ZWSLO.RA_DEF:=RA_DEF.ref();
               _put:=1
            ?}
         |? ',UZ_POD,'*',%1,'[OS_ZWSLO.KOD]
         || {? __RUB.find_def('S',90812)
            || OS_ZWSLO.RA_DEF:=RA_DEF.ref();
               _put:=1
            ?}
         ?}
      ?};
      {? OS_ZWSLO.PO=''
      || {? ',ZWPOD,KU_50%,'*',%1,'[OS_ZWSLO.KOD]
         || OS_ZWSLO.PO:='T'
         || OS_ZWSLO.PO:='N'
         ?};
         _put:=1
      ?};
      {? OS_ZWSLO.DO_OB=date(0,0,0) & ',TP_ZAL_P,TP_ZAL_Z,KL_SRED,'*',%1,'[OS_ZWSLO.KOD]
      || OS_ZWSLO.DO_OB:=_dtUst-1;
         _put:=1
      ?};
      {? ',ZWPODE,ZWPOD,ZWPODP,ZWPODD,'*',%1,'[OS_ZWSLO.KOD]
      || {? ',ZWPODP,'*',%1,'[OS_ZWSLO.KOD]
         || OS_ZWSLO.ROCZNE:='T'
         || OS_ZWSLO.ROCZNE:='N'
         ?};
         _put:=1
      ?};
      {? _put
      || OS_ZWSLO.put()
      ?};

      OS_ZWSLO.next()
   !};
   RA_DEF.cntx_pop()
?};
OS_ZWSLO.cntx_pop();
OS_ZWSLO.trig_on('*','*');

:: Uzupełnienie nowego pola P_IPOD.PU (Podział ulgi):
P_IPOD.trig_off('*','*');
P_IPOD.cntx_psh();
P_IPOD.index('OD');
P_IPOD.prefix();
{? P_IPOD.first()
|| {!
   |? {? P_IPOD.PU=''
      || P_IPOD.PU:='1';
         P_IPOD.put()
      ?};

      P_IPOD.next()
   !}
?};
P_IPOD.cntx_pop();
P_IPOD.trig_on('*','*');

:: Uzupełnienie nowego pola OS_ZWZAL.WYCOF:
OS_ZWZAL.trig_off('*','*');
OS_ZWZAL.cntx_psh();
OS_ZWZAL.index('DOK_WYC');
OS_ZWZAL.prefix(_firma);
{? OS_ZWZAL.first()
|| OS_ZWSLO.cntx_psh();
   OS_ZWSLO.index('TYPKOD');
   OS_ZWSLO.prefix(_firma,'W');
   SLO_KOD.cntx_psh();
   {!
   |? _put:=0;
      {? ~(',T,N,'*',%1,'[OS_ZWZAL.WYCOF])
      || OS_ZWZAL.WYCOF:='N';
         _put:=1
      ?};
::    Uzupełnienie wartości nowego pola OS_ZWZAL.OS_ZWSLO na podstawie OS_ZWZAL.SLO_KOD:
      {? OS_ZWZAL.OS_ZWSLO=null() & OS_ZWSLO.find_key(OS_ZWZAL.SLO_KOD().KOD,)
      || OS_ZWZAL.OS_ZWSLO:=OS_ZWSLO.ref();
         _put:=1
      ?};
::    Uzupełnienie nowego pola OS_ZWZAL.WYCOFANY:
      {? OS_ZWZAL.WYCOFANY=''
      || OS_ZWZAL.WYCOFANY:='N';
         _put:=1
      ?};
::    Wyczyszczenie pola ROK z wniosków nierocznego typu:
      {? OS_ZWZAL.ROK<>0 & OS_ZWZAL.OS_ZWSLO().ROCZNE='N'
      || OS_ZWZAL.ROK:=0;
         _put:=1
      ?};
      {? _put
      || OS_ZWZAL.put()
      ?};

      OS_ZWZAL.next()
   !};
   SLO_KOD.cntx_pop();
   OS_ZWSLO.cntx_pop()
?};
OS_ZWZAL.cntx_pop();
OS_ZWZAL.trig_on('*','*');

:: Uzupełnienie nowych pól RU.UP, RU.WRKU50:
:: przeniesione do \new_fld_RU/upgradexpertis1.fml

:: Zmiana znacznika REZYGN i kasowanie ROK, ROK_Z na oświadczeniach typu "ZWPOD":
OS_ZWPOD.trig_off('*','*');
OS_ZWPOD.cntx_psh();
OS_ZWPOD.index('TYP');
OS_ZWPOD.prefix(_firma);
OS_ZWSLO.cntx_psh();
{? OS_ZWPOD.first()
|| {!
   |? _put:=0;
      {? OS_ZWPOD.ROK<>0 & OS_ZWPOD.ROK>=2022 & OS_ZWPOD.OS_ZWSLO().KOD='ZWPOD'
      || OS_ZWPOD.ROK:=0;
         OS_ZWPOD.ROK_Z:=0;
         {? OS_ZWPOD.REZYGN='T'
         || OS_ZWPOD.REZYGN:='N';
            OS_ZWPOD.AKTYWNY:='T'
         ?};
         _put:=1
      ?};
::    Oświadczenia "Młody pracownik" z 2019 mogły sterować znacznikiem aktywności, dezaktywujemy:
      {? OS_ZWPOD.ROK<>0 & OS_ZWPOD.ROK=2019 & OS_ZWPOD.OS_ZWSLO().KOD='ZWPOD'
      || OS_ZWPOD.AKTYWNY:='N';
         _put:=1
      ?};
::    Uzupełnienie nowego pola OS_ZWPOD.WYCOFANY:
      {? OS_ZWPOD.WYCOFANY=''
      || OS_ZWPOD.WYCOFANY:='N';
         _put:=1
      ?};
::    Wyczyszczenie pól ROK i ROK_Z z oświadczeń nierocznego typu:
      {? (OS_ZWPOD.ROK<>0 | OS_ZWPOD.ROK_Z<>0) & OS_ZWPOD.OS_ZWSLO().ROCZNE<>'T'
      || OS_ZWPOD.ROK:=0;
         OS_ZWPOD.ROK_Z:=0;
         _put:=1
      ?};
      {? _put
      || OS_ZWPOD.put()
      ?};

      OS_ZWPOD.next()
   !}
?};
OS_ZWSLO.cntx_pop();
OS_ZWPOD.cntx_pop();
OS_ZWPOD.trig_on('*','*');

:: Przeniesienie wartości dla kwot podstawy wprowadzanych ręcznie:
OS_ZWPKW.trig_off('*','*');
OS_ZWPKW.cntx_psh();
OS_ZWPKW.index('OS_ZWPKW');
OS_ZWPOZ.cntx_psh();
OS_ZWPOZ.index('OS_RMOR');
OS_ZWPOZ.prefix(_firma);
_tab_rub:=__RUB.sys_rub(9022);
{? OS_ZWPOZ.first() & _tab_rub.RN
|| {!
   |? OS_ZWPKW.prefix(_firma,OS_ZWPOZ.ref());
      {? OS_ZWPOZ.WYK_KW>0 & ~OS_ZWPKW.first()
      || OS_ZWPKW.blank(1);
         OS_ZWPKW.FIRMA:=_firma;
         OS_ZWPKW.OS_ZWPOZ:=OS_ZWPOZ.ref();
         OS_ZWPKW.R:=__RUB.ref(_tab_rub.RN);
         OS_ZWPKW.WART:=OS_ZWPOZ.WYK_KW;
         OS_ZWPKW.add()
      ?};
      OS_ZWPOZ.next()
   !}
?};
OS_ZWPOZ.cntx_pop();
OS_ZWPKW.cntx_pop();
OS_ZWPKW.trig_on('*','*');

:: Utworzenie słowników grup przychodów:
SLO_TYP.cntx_psh();
SLO_TYP.prefix();
SLO_KOD.trig_off('*','*');
SLO_KOD.cntx_psh();
SLO_KOD.index('KOD');
_typ:=exec('slo_typ','ext_slo','RODZ_PL');
_addk:="
   SLO_KOD.prefix(_a,_b,_b);
   {? ~SLO_KOD.first()
   || SLO_KOD.blank(1);
      SLO_KOD.SLO_TYP:=_a;
      SLO_KOD.KOD:=_b;
      SLO_KOD.NAZWA:=_c;
      SLO_KOD.SYSTEM:=_d;
      SLO_KOD.add()
   ?};
   SLO_KOD.ref()
";
_addk(_typ,'UM_PRAC','Art. 32. – stosunek pracy','T');
_addk(_typ,'UM_ZLEC','Art. 41. - umowy cywilnoprawne','T');
_addk(_typ,'UM_SZKOL','Art. 35. - praktyka absolwencka lub staż uczniowski','T');
&_typ;
SLO_KOD.cntx_pop();
SLO_KOD.trig_on('*','*');
SLO_TYP.cntx_pop();

:: Uzupełnienie nowego słownika grup umów:
OS_GRRU.trig_off('*','*');
OS_GRRU.cntx_psh();
OS_GRRU.prefix();
{? ~OS_GRRU.first()
|| SLO_KOD.cntx_psh();
   SLO_KOD.index('KOD');
   SLO_KOD.prefix(exec('slo_typ','ext_slo','RODZ_PL'));
   RU.cntx_psh();
   RU.index('K');
   RU.prefix(exec('slo_typ','ext_slo','UMZLEC'));
   {? SLO_KOD.find_key('UM_SZKOL')
   || _umowy:=spli_str('v,y',',');
      {! _ii:=1..obj_len(_umowy)
      |! {? RU.find_key(_umowy[_ii])
         || OS_GRRU.SLO_KOD:=SLO_KOD.ref();
            OS_GRRU.RU:=RU.ref();
            OS_GRRU.add()
         ?}
      !};
      obj_del(_umowy)
   ?};
   {? SLO_KOD.find_key('UM_ZLEC')
   || _umowy:=spli_str('1,2,3,4,5,6',',');
      {! _ii:=1..obj_len(_umowy)
      |! {? RU.find_key(_umowy[_ii])
         || OS_GRRU.SLO_KOD:=SLO_KOD.ref();
            OS_GRRU.RU:=RU.ref();
            OS_GRRU.add()
         ?}
      !};
      obj_del(_umowy)
   ?};
   RU.cntx_pop();
   SLO_KOD.cntx_pop()
?};
OS_GRRU.cntx_pop();
OS_GRRU.trig_on('*','*');

__UPG.msg('Zakończono uzupełnianie i modyfikację danych w kartotekach związanych z Polskim Ładem.');

_result


\pola_PL2023_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie wartości w nowych pola, modyfikacja w istniejących polach - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie i modyfikacja danych w kartotekach związanych z Polskim Ładem (styczeń 2023)'


\ISTDEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli ISTDEF - Definicje komunikatów EDI
::----------------------------------------------------------------------------------------------------------------------
::_log_name:='istdef.log';
::_log:=fopen(_log_name,'Ua',1,,1);
::{? _log.is_open()
::||
::   _txt:='Firma';
::   fwrite(_log,'%1\t%2'[_txt,__Firma])
::?};
::obj_del(_log);
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ISTDEF.cntx_psh();
ISTDEF.prefix();
_result:=ISTDEF.for_each("
   _put:=0;
   {? ISTDEF.FORMAT='N'
   ||
      __lrec+=1;
      ISTDEF.cntx_psh();
      ISTDEF.index('LP');
      ISTDEF.prefix(__Firma,ISTDEF.SYSTEM,ISTDEF.IST_ISTK,ISTDEF.TM_STAM);
      _continue:=~ISTDEF.first();
      ISTDEF.cntx_pop();
      {? _continue=0
      ||
         {? ISTDEF.FIRMA<>__Firma
         ||
            exec('def_del','edi_xml',ISTDEF.ref(),null())
         ?}
      ||
         ISTXML.cntx_psh();
         ISTXML.index('TREE');
         ISTXML.prefix(ISTDEF.ref());
         {? ISTXML.first()
         ||
            {? ISTDEF.FIRMA=''
            ||
               ISTDEF.FIRMA:=__Firma;
               ISTDEF.VER:=ISTDEF.K+ISTDEF.FIRMA;
               {? ISTDEF.put() || __lmod+=1 ?}

            |? ISTDEF.FIRMA<>__Firma
            ||
               {? exec('istxml_copy','edi_xml',ISTDEF.ref(),0,1,,,1) || __lmod+=1 ?}
            ?}
         ?};
         ISTXML.cntx_pop()
      ?}
   ?}
",0);
ISTDEF.cntx_pop();
__UPG.msg('Zaktualizowano definicje komunikatów EDI.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ISTDEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli ISTDEF - Definicje komunikatów EDI - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola definicji komunikatów EDI'


\mod_dane_PL001
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie wartości nowego pola, modyfikacja słownika wniosków.
::  ORG: \mod_dane_PL001/upgrade_2226_PL001.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_firma:=exec('ref_firma','ustawienia');

OS_ZWSLO.trig_off('*','*');
OS_ZWSLO.cntx_psh();
OS_ZWSLO.index('KOD');
OS_ZWSLO.prefix(_firma,'UZ_POD',);
{? OS_ZWSLO.first()
|| OS_ZWSLO.ROCZNE:='N';
   OS_ZWSLO.put()
?};
OS_ZWSLO.cntx_pop();
OS_ZWSLO.trig_on('*','*');

ZC.trig_off('*','*');
ZC.index('ZLECDAT');
ZC.prefix(_firma);
RH.index('RACHPDWY');
{? ZC.first()
|| exec('PROGRESS','#object');
   _size:=ZC.size();
   PROGRESS.set(_size,'\nTrwa weryfikacja umów zleceń.');
   {!
   |? {? ZC.DZB=#0
      || ZC.DZB:=ZC.DW;
         RH.prefix(ZC.ref());
         {? RH.last() & RH.DWY>ZC.DW
         || ZC.DZB:=RH.DWY
         ?};
         ZC.put()
      ?};
      PROGRESS.next();
      ZC.next()
   !};
   PROGRESS.close()
?};
ZC.trig_on('*','*');
__UPG.msg('Zakończono uzupełnianie i modyfikację danych w kartotece umów zleceń i słowniku typów wniosków.');

_result


\mod_dane_PL001_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie wartości nowego pola, modyfikacja słownika wniosków - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie i modyfikacja danych związanych z Polskim Ładem (styczeń 2023) w kartotece umów zleceń'
' i słowniku typów wniosków.'


\h_overlap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: kod - ER/WRT/XP/21.37/2208/0006 - Miesce pracy - zmiana stanowiska -
::             zmian daty miejsca pracy daje datę 0001/01/01
::  ORG: \h_overlap/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? FUN.ask(
      'W zadaniu przeanalizowane zostaną zapisy tabeli H\n'
      'na pod kątem błędnej daty OD oraz nakładających się okresów.\n\n'
      'Czy chcesz kontynuować?'@
   )
||
   exec('KOMM','#object');
   KOMM.init();

   FIRMA.cntx_psh();
   F_ZATR.cntx_psh();
   OSOBA.cntx_psh();
   P.cntx_psh();
   P.index('FOP_ZATR');
   P.prefix();
   {? P.first()
   || H.cntx_psh();
      H.use('_hist');
      H.index('_HISTDAT');
      FUN.prg_start(P.size(),'Analiza błędnych zapisów...',5,,1);
      {!
      |? H.prefix(P.ref());
         {? H.first()
         || {!
            |?  _res:=exec('check','overlap',H.ref(),H,'OD','DO',2,,,'HISTUM',H.UMOWA);
               {? type_of('_res')=type_of('') & _res<>''
               || _txt:='Błędne zapisy w tabeli "Miejsca pracy" dla współpracownika';
                  {? ~KOMM.find_msg(_txt) || KOMM.sect_beg(_txt) ?};
                  _txt:='%3 [Firma: %1, Forma zatr.: %2]'
                        [P.FIRMA().SYMBOL,P.F_ZATR().KOD,exec('P','#to_string')];
                  {? ~KOMM.find_msg('%1 - nakładające się okresy'[_txt]) &
                     ~KOMM.find_msg('%1 - błędna data OD: %2'[_txt,$H.OD])
                  || KOMM.add('%1 - nakładające się okresy'[_txt])
                  ?}
               ?};
               {? H.OD<date(1900,1,1)
               || _txt:='%3 [Firma: %1, Forma zatr.: %2]'
                        [P.FIRMA().SYMBOL,P.F_ZATR().KOD,exec('P','#to_string')];
                  {? ~KOMM.find_msg('%1 - nakładające się okresy'[_txt]) &
                     ~KOMM.find_msg('%1 - błędna data OD: %2'[_txt,$H.OD])
                  || KOMM.add('%1 - błędna data OD: %2'[_txt,$H.OD])
                  ?}
               ?};
               H.next()
            !}
         ?};
         FUN.prg_next();
         P.next()
      !};
      FUN.prg_stop();
      H.cntx_pop()
   ?};
   P.cntx_pop();
   OSOBA.cntx_pop();
   F_ZATR.cntx_pop();
   FIRMA.cntx_pop();

   {? ~KOMM.empty() || KOMM.select('Błędne zapisy',,,1) ?};

   _funInfo:={? KOMM.empty() || 'Brak miejsc pracy spełniających warunki.\n' || '' ?};
   {? FUN.ask('%1Czy oznaczyć zadanie: \'%2\' jako wykonane?'@[_funInfo,'h_overlap'])
   || __UPG.msg('Poprawiono błędne daty oraz nakładające się okresy.');
      _result:=1
   || _result:=-1
   ?}
|| _result:=-1
?};

_result


\wycofania_PL2023
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Generowanie wycofań oświadczeń - wyświetlanie okna do weryfikacji
::  ORG: \wycofania_PL2023/upgrade_2226_06.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_firma:=exec('ref_firma','ustawienia');
:: Wyświetlenie okienka z osobami do weryfikacji danych w kartotece oświadczeń i dodanie wycofań dla takich:
exec('czytaj','#stalesys',,KST,'ZWPODW');
_OSW:=sql('
   select OSOBA.NAZWISKO as "Nazwisko", OSOBA.PIERWSZE as "Imię", OS_ZWPOD.D_OW as "Data zakończenia",
          OS_ZWSLO.KOD as "Kod ZP", OS_ZWSLO.RODZAJ as "Rodzaj ZP", OS_ZWPOD.REFERENCE as REF, space(255) as "Uwagi",
          case
             when (extract (day from OS_ZWPOD.D_OW)<31 or extract (month from OS_ZWPOD.D_OW)<12)
                  and (OS_ZWSLO.KOD<>\'ZWPOD\' or OS_ZWPOD.D_OW<>add_to_date(YEAR,:_a,OSOBA.UR_DATA))
                then \'T\'
                else \'N\'
          end as "Wygenerowane wycofanie"
   from OS_ZWPOD
      join OSOBA using(OS_ZWPOD.OSOBA, OSOBA.REFERENCE)
      join OS_ZWSLO using(OS_ZWPOD.OS_ZWSLO, OS_ZWSLO.REFERENCE)
   where OS_ZWPOD.FIRMA=:_b and OS_ZWPOD.AKTYWNY=\'T\' and OS_ZWPOD.D_OW is not null
   order by 8,1,2,3',KST.ZWPODW,_firma);
{? _OSW.first()
|| OS_ZWPOD.trig_off('*','*');
   OS_ZWPOD.cntx_psh();
:: Dodanie wycofań jeśli data końca jest różna od końca roku:
   OS_ZWPOD.prefix();
   _OSW.cntx_psh();
   {!
   |? {?_OSW.WYGENERO='T'
      || _dodanoWyc:=0;
         {? OS_ZWPOD.seek(_OSW.REF) & OS_ZWPOD.WYCOFANY<>'T'
         || OS_ZWPOD.cntx_psh();
            OS_ZWPOD.D_ZO:=OS_ZWPOD.D_OB:=OS_ZWPOD.D_OW+1;
            OS_ZWPOD.D_OW:=date(0,0,0);
            OS_ZWPOD.ROK:=OS_ZWPOD.ROK_Z:=0;
            OS_ZWPOD.REZYGN:='T';
            OS_ZWPOD.WYCOFANY:='N';
            OS_ZWPOD.DOK_WYC:=#OS_ZWPOD.ref();
            _dodanoWyc:=OS_ZWPOD.add();
            {? _dodanoWyc
            || _OSW.UWAGI:='Wygenerowano wycofanie oświadczenia od dnia %1'@[$(OS_ZWPOD.D_OB)]
            || _OSW.UWAGI:='Nie udało się wygenerować wycofania oświadczenia!'@;
               _ret:=-1
            ?};
            OS_ZWPOD.cntx_pop();
            {? _dodanoWyc
            || OS_ZWPOD.WYCOFANY:='T';
               OS_ZWPOD.put()
            ?}
         || {? OS_ZWPOD.WYCOFANY='T'
            || _OSW.UWAGI:='Istnieje już wycofanie dokumentu.'@
            || _OSW.UWAGI:='Błąd w danych.'@;
               _ret:=-1
            ?}
         ?}
      || _OSW.UWAGI:='Zmiana działania oświadczeń. Była data zakończenia %1, która przestała obowiązywać. '
            'Zweryfikuj dane, sprawdź naliczanie Zwolnionego przychodu.'@[$_OSW.DATA]
      ?};
      _OSW.put();

      _OSW.next()
   !};
   _OSW.cntx_pop();
   OS_ZWPOD.cntx_pop();
   OS_ZWPOD.trig_on('*','*');
   FUN.info('Zostanie wyświetlona lista oświadczeń posiadających datę zakończenia.\n'
            'Dla opisanych oświadczeń zostały automatycznie utworzone wycofania.\n'
            'Należy zweryfikować poprawność danych w kartotece oświadczeń.'@);
   _ws:=_OSW.mk_sel('Osoby dla których należy zweryfikować poprawność danych w kartotece oświadczeń'@,,0,'#napraw_osw');
   _OSW.win_fld(_ws,,'NAZWISKO',,,20,,0,'Nazwisko'@);
   _OSW.win_fld(_ws,,'IMIĘ',,,15,,0,'Imię'@);
   _OSW.win_fld(_ws,,'KOD',,,8,,0,'Kod ZP'@);
   _OSW.win_fld(_ws,,'RODZAJ',,,30,,0,'Rodzaj ZP'@);
   _OSW.win_fld(_ws,,'DATA',,,-10,,0,'Data zakończenia'@);
   _OSW.win_fld(_ws,,'UWAGI',,,90,,0,'Uwagi'@);
   _eksport:=
      "_OSW:=cur_tab();
       _tmpDir:=fmk_tmp_dir(0);
       _folder:=_tmpDir.get_path();
       _sep:=exec('sep','#file',1);
       _plik:=_folder+_sep+'osw.csv';
       _exp:=_OSW.export(
                _plik,,,'UTF-8,header,nopth',,
                'NAZWISKO',,1,,
                'IMIĘ',,2,,
                'KOD',,3,,
                'RODZAJ',,4,,
                'DATA',,5,1,
                'UWAGI',,6,
                );
       {? _exp
       || _zapis:=dlg_save(_plik);
          {? type_of(_zapis)=type_of('')
          || {? +_zapis
             || _eksp:='Wyeksportowano do: %1'@[_zapis]
             || _eksp:='Użytkownik zrezygnował z zapisania pliku'@
             ?}
          || _eksp:='Błąd podczas zapisywania pliku'@;
             _ret:=-1
          ?}
       || _eksp:='Eksport do pliku zakończył się niepowodzeniem.'@
       ?};
       obj_del(_tmpDir);
       FUN.info(_eksp)";

   _OSW.win_act(_ws,0,'Formuła','Eksport do CSV'@,,'Eksport listy do pliku CSV'@,_eksport);
   _OSW.win_btn(_ws,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Eksport do CSV'@],'menu:E');
   _OSW.win_sel(_ws);
   _OSW.select()
|| FUN.info('Brak oświadczeń wymagających ingerencji.'@)
?};
_msg:='';
{? _ret=1
|| {? FUN.ask('Czy oznaczyć zadanie "wycofania_PL2023" jako wykonane?'@)
   || _msg:='Zakończono generowanie wycofań oświadczeń - wyświetlanie okna do weryfikacji związanych z Polskim Ładem.'
   || _msg:='Użytkownik zrezygnował z oznaczenia zadania jako wykonane.';
      _ret:=-1
   ?}
|| _msg:='Nipowiodło się generowanie wycofań oświadczeń związanych z Polskim Ładem.'
?};
__UPG.msg(_msg);

_ret


\wycofania_PL2023_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Generowanie wycofań oświadczeń - wyświetlanie okna do weryfikacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Generowanie wycofań oświadczeń - wyświetlanie okna do weryfikacji'


\wn_rok_PL001
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Lista wniosków, dla których wniosków wyzerowano informację w polu "ROK"
::  ORG: \wn_rok_PL001/upgrade_2226_PL001.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_firma:=exec('ref_firma','ustawienia');

OS_ZWSLO.cntx_psh();
OS_ZWSLO.index('KOD');
OS_ZWSLO.prefix(_firma,'UZ_POD',);
{? OS_ZWSLO.first()
||
:: wyzerowanie pola ROK dla 'UZ_POD' lat od 2022 począwszy
   OS_ZWZAL.trig_off('*','*');
   OS_ZWZAL.cntx_psh();
   _ndx:=OS_ZWZAL.ndx_tmp(,1,'OS_ZWSLO',,);
   OS_ZWZAL.index(_ndx);
   OS_ZWZAL.prefix(OS_ZWSLO.ref());
   {? OS_ZWZAL.first()
   || _TAB:=sql('
         select OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL,
            OS_ZWZAL.D_OB, OS_ZWZAL.D_ZO, OS_ZWZAL.ROK, OS_ZWSLO.RODZAJ, OS_ZWSLO.KOD
         from OS_ZWZAL
            join OS_ZWSLO using(OS_ZWZAL.OS_ZWSLO, OS_ZWSLO.REFERENCE)
            join OSOBA using(OS_ZWZAL.OSOBA, OSOBA.REFERENCE)
         where 1=2
         order by 1,2
      ');
      {!
      |? {? OS_ZWZAL.ROK>=2022
         || _TAB.ROK:=OS_ZWZAL.ROK;
            _TAB.NAZWISKO:=OS_ZWZAL.OSOBA().NAZWISKO;
            _TAB.PIERWSZE:=OS_ZWZAL.OSOBA().PIERWSZE;
            _TAB.PESEL:=OS_ZWZAL.OSOBA().PESEL;
            _TAB.D_OB:=OS_ZWZAL.D_OB;
            _TAB.D_ZO:=OS_ZWZAL.D_ZO;
            _TAB.RODZAJ:=OS_ZWZAL.OS_ZWSLO().RODZAJ;
            _TAB.KOD:=OS_ZWZAL.OS_ZWSLO().KOD;
            OS_ZWZAL.ROK:=0;
            {? OS_ZWZAL.put()
            || _TAB.add(1)
            ?}
         ?};
         OS_ZWZAL.next()
      !};
      {? _TAB.first()
      || FUN.info('Zostaną pokazane wnioski za lata 2022 i 2023, które wcześniej były typu rocznego.\n'
                  'Dla tych wniosków wyzerowano informację w polu "ROK".\n'
                  'W związku z tym wnioski te stają się dla systemu bezterminowymi.\n'
                  'Należy zweryfikować wyświetlone wnioski i w razie konieczności dokonać dezaktywacji wniosku.'@);
         _ws:=_TAB.mk_sel('Zweryfikuj wnioski'@,,0,'#kalipl001');
         _TAB.win_fld(_ws,,'NAZWISKO',,,30,,,'Nazwisko'@,,'Nazwisko'@);
         _TAB.win_fld(_ws,,'PIERWSZE',,,20,,,'Imię'@,,'Pierwsze imię'@);
         _TAB.win_fld(_ws,,'PESEL',,,10,,,'PESEL'@,,'Numer PESEL'@);
         _TAB.win_fld(_ws,,'KOD',,,10,,,'Kod'@,,'Kod wniosku'@);
         _TAB.win_fld(_ws,,'RODZAJ',,,40,,,'Rodzaj'@,,'Rodzaj wniosku'@);
         _TAB.win_fld(_ws,,'ROK',,,10,,,'Rok'@,,'Rok wniosku'@);
         _TAB.win_fld(_ws,,'D_ZO',,,10,,,'Data złożenia'@,,'Data złożenia wniosku'@);
         _TAB.win_fld(_ws,,'D_OB',,,10,,,'Data stosowania'@,,'Data stosowania wniosku'@);
         _TAB.win_sel(_ws);
         _TAB.select()
      ?};
      obj_del(_TAB)
   ?};
   OS_ZWZAL.ndx_drop(_ndx);
   OS_ZWZAL.cntx_pop();
   OS_ZWZAL.trig_on('*','*')
?};
OS_ZWSLO.cntx_pop();
__UPG.msg('Wyświetlono listę wniosków, dla których wniosków wyzerowano informację w polu "ROK".');

_ret


\wn_rok_PL001_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Lista wniosków, dla których wniosków wyzerowano informację w polu "ROK" - opis
::----------------------------------------------------------------------------------------------------------------------
'Lista wniosków, dla których wniosków wyzerowano informację w polu "ROK".'


\SD_XSLO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktualizacja elementów zestawów danych
::  ORG: \SD_XSLO/upgrade_rrxx.fml
::----------------------------------------------------------------------------------------------------------------------
_insert:="
   _res:=exec('xslo_insert','szablon_std',_a,exec('_xslo_kol','szablon_std'),_b,_c,_d);
   __UPG.msg(_res.INFO);
   _res.STATUS<>~~
";

{? ~_insert('RODZAJ_KOL_1','WARTOSC','Wartość wyliczana typu prostego lub obiektowego',1) |
   ~_insert('RODZAJ_KOL_2','OBRAZ','Obraz pobrany z wiersza tabeli bazy danych',2)
|| return(-1)
?};

_kod:='RODZAJ_KOL_1';
{? (_slo:=exec('xslo_search','szablon_std',_kod))=null
|| __UPG.msg('Brak elementu "%1" w słowniku.'[_kod]);
   return(-1)
?};

_cnt:=0;
_err:=0;

SD_WKOL.trig_off('*','*');
SD_WKOL.cntx_psh();
SD_WKOL.clear();
SD_WKOL.f_set(,,'SD_XSLO is null');
_loop:=SD_WKOL.f_first();
{!
|? _loop
|! SD_WKOL.SD_XSLO:=_slo;
   {? SD_WKOL.put()
   || _cnt+=1
   || _err+=1
   ?};
   _loop:=SD_WKOL.f_next()
!};
SD_WKOL.f_clear();
SD_WKOL.cntx_pop();
SD_WKOL.trig_on('*','*');

{? _cnt=0
|| __UPG.msg('Zawartość tabeli SD_WKOL nie wymagała aktualizacji.')
|| __UPG.msg('Zaktualizowano zawartość tabeli SD_WKOL:');
   __UPG.msg('przetworzono %1 zapisów,'[$_cnt]);
   __UPG.msg('wystąpiło %1 błędów.'[$_err])
?};

{? _err<>0 || -1 || 1 ?}


\SD_XSLO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktualizacja elementów zestawów danych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja elementów zestawów danych'


\noty_ods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uzupełnienie w nagłówku noty sumy kwot odsetek z pozycji
::----------------------------------------------------------------------------------------------------------------------
_przed:=_po:=0;
SER_POZ.cntx_psh(); SER_POZ.use('ser_pxx'); SER_NAG.cntx_psh();
SER_NAG.index('SERNAG1'); SER_NAG.prefix(REF.FIRMA,'N');
SER_POZ.index('SER_POZO');
{? SER_NAG.first()
|| {! |?
      {? SER_NAG.WAL<>null
      || SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
         {? SER_POZ.first()
         || _przed:=_po:=0;
            {! |?
               {? SER_POZ.UMORZNOT=0 || {? SER_POZ.PRZEDPO ||  _przed+=SER_POZ.ODS ||  _po+=SER_POZ.ODS ?} ?};
               SER_POZ.next()
            !};
            SER_NAG.SUM_ODSA:=_po; SER_NAG.SUM_ODSB:=_przed;  SER_NAG.put()
         ?}
      ?};
      SER_NAG.next()
   !}
?};
SER_NAG.cntx_pop(); SER_POZ.cntx_pop();
{? _przed | _po
|| __UPG.msg('Uzupełnienie w nagłówku noty sumy kwot odsetek z pozycji.')
|| __UPG.msg('Brak korespondencji seryjnej typu: Noty odsetkowe.')
?};
1


\noty_ods_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uzupełnienie w nagłówku noty sumy kwot odsetek z pozycji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie w nagłówku noty sumy kwot odsetek z pozycji.'


\DOK_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ, JK, PBS [23.25]
:: OPIS: Uzupełnienie w nagłówku dokumentu sum kwot netto, vat, brutto i obrotów WN/MA z pozycji VAT dokumentu.
::       Przepisanie wartości wybranych pól z tabeli DOK do tabel VPOZ i POZ
::       Do tabeli POZ:  DOK().DOK_REJ, DOK().HAN, DOK().REJ, DOK().KH_KRISO, DOK().O_KRISO, DOK().RB, DOK().RB_V,
::                       DOK().WYS, DOK().BANK, DOK().JORG, DOK().SP_PL, DOK().KRAJ, DOK().RVAT
::       Do tabeli VPOZ: DOK().DOK_REJ, DOK().HAN, DOK().ODD, DOK().REJ, DOK().KH_KRISO, DOK().O_KRISO, DOK().RB,
::                       DOK().RB_V, DOK().WYS, DOK().WAL, DOK().BANK, DOK().JORG, DOK().SP_PL, DOK().KRAJ
::       Dodatkowo dla tabeli POZ uzupełnienie pola POZ.KON_OPIS przechowującego opis analityczny konta.
::       Uzupełnienie pól: DOK.DSP_WPOZ, VPOZ.D, POZF.DT, DOK.GVKHSTAT, DOK.GVLISTAT
::----------------------------------------------------------------------------------------------------------------------
_lpoz:=_lvpoz:=_ldok:=_lpozf:=0;
_dok_put:=_sum_vpoz:=_sum_poz:=0;
DOK.cntx_psh(); DOK.trig_off('*','*');
VPOZ.cntx_psh(); VPOZ.trig_off('*','*');
POZF.cntx_psh(); POZF.trig_off('*','*');
POZ.cntx_psh(); POZ.trig_off('*','*');
ROK_F.index('KOD'); ROK_F.prefix();
OKRO_F.index('ROK');
{? ROK_F.first()
|| _kon:=exec('obj_kon','upgradexperti2');
   {!
   |? SSTALE.AR:=ROK_F.ref();
      OKRO_F.prefix(ROK_F.ref());
      {? OKRO_F.first()
      || {!
         |? SSTALE.AO:=OKRO_F.ref();
            exec('set_var','!fks_ksr_zbob');
            _maska:=ROK_F.KOD+form(OKRO_F.NR,-2);
            VPOZ.use('pozv'+_maska); VPOZ.index('VDOK');
            POZF.use('pozf'+_maska); POZF.index('DOK');
            POZ.use('pozy'+_maska); POZ.index('DOK');
            DOK.use('doku'+_maska); DOK.index('O_REJ'); DOK.prefix();
            {? DOK.first()
            || {!
               |? _sum_poz:=_sum_vpoz:=_dok_put:=_pozf_put:=0;
                  {? DOK.KSEF_ERR=''
                  || DOK.KSEF_ERR:='N';
                     _dok_put:=1
                  ?};
                  {? DOK.DSP_WPOZ=''
                  || DOK.DSP_WPOZ:='N';
                     _dok_put:=1
                  ?};
                  {? DOK.DSP_WPZF=''
                  || DOK.DSP_WPZF:='N';
                     _dok_put:=1
                  ?};
                  {? DOK.NETTO=0 & DOK.VAT=0 & DOK.BRUTTO=0 & (DOK.DOK_REJ().SLO().KOD='VAT' | SLO.KOD='SAD')
                  || _sum_vpoz:=1
                  ?};
                  {? DOK.GVKHSTAT=''
                  || DOK.GVKHSTAT:=DOK.GVLISTAT:='N';
                     _dok_put:=1
                  ?};
                  POZF.prefix(DOK.ref);
                  {? POZF.first()
                  || {!
                     |? {? POZF.SP_WYM='' || POZF.SP_WYM:='N' ?};
                        {? POZF.DT=date(0,0,0) || POZF.DT:=DOK.DOP ?};
                        {? POZF.put(1) || _lpozf+=1 ?};
                        POZF.next()
                     !}
                  ?};
                  VPOZ.prefix(DOK.ref());
                  {? VPOZ.first()
                  || {!
                     |? VPOZ.DOK_REJ:=DOK.DOK_REJ;
                        VPOZ.HAN:=DOK.HAN;
                        VPOZ.ODD:=DOK.ODD;
                        VPOZ.REJ:=DOK.REJ;
                        VPOZ.KH_KRISO:=DOK.KH_KRISO;
                        VPOZ.O_KRISO:=DOK.O_KRISO;
                        VPOZ.RB:=DOK.RB;
                        VPOZ.RB_V:=DOK.RB_V;
                        VPOZ.WYS:=DOK.WYS;
                        VPOZ.WAL:=DOK.WAL;
                        VPOZ.BANK:=DOK.BANK;
                        VPOZ.JORG:=DOK.JORG;
                        VPOZ.SP_PL:=DOK.SP_PL;
                        {? VPOZ.D=date(0,0,0)
                        || VPOZ.D:=DOK.DOP
                        ?};
                        VPOZ.KRAJ:=DOK.KRAJ;
                        {? VPOZ.put() || _lvpoz+=1 ?};
:: Uzupełnienie w nagłówku dokumentu sum kwot netto, vat, brutto
                        {? _sum_vpoz=1
                        || _dok_put:=1;
                           DOK.NETTO+=VPOZ.NETTO;
                           DOK.VAT+=VPOZ.VAT;
                           DOK.BRUTTO+=VPOZ.BRUTTO
                        ?};
                        VPOZ.next()
                     !}
                  ?};
                  {? DOK.SUMWN=0 & DOK.SUMMA=0 || _sum_poz:=1 ?};
                  no_msg(1); lock_time(0);
                  POZ.prefix(DOK.ref());
                  {? POZ.first()
                  || {!
:: Przypisywanie wybranych wartości z DOK do POZ
                     |? POZ.DOKREJ:=DOK.DOK_REJ;
                        POZ.HAN:=DOK.HAN;
                        POZ.REJ:=DOK.REJ;
                        POZ.KH_KRISO:=DOK.KH_KRISO;
                        POZ.O_KRISO:=DOK.O_KRISO;
                        POZ.RB:=DOK.RB;
                        POZ.RB_V:=DOK.RB_V;
                        POZ.WYS:=DOK.WYS;
                        POZ.BANK:=DOK.BANK;
                        POZ.JORG:=DOK.JORG;
                        POZ.SP_PL:=DOK.SP_PL;
                        POZ.KRAJ:=DOK.KRAJ;
                        POZ.RVAT:=DOK.RVAT;
                        {? POZ.KON_OPIS=''
                        || opis_poz:=_kon.get(POZ.KON); odd_ksn:=0;
                           {? opis_poz=''
                           || ROK_F.cntx_psh(); OKRO_F.cntx_psh();
                              POZ.KOM().KS();
                              exec('opis','konto',POZ.KON);
                              exec('ka_opis','konto',0);
                              OKRO_F.cntx_pop(); ROK_F.cntx_pop();
                              _kon.set(POZ.KON,opis_poz)
                           ?};
                           POZ.KON_OPIS:=opis_poz;
                           &opis_poz; &odd_ksn
                        ?};
                        {? POZ.put() || _lpoz+=1 ?};
:: Uzupełnianie sum obrotów WN/MA
                        {? _sum_poz=1
                        || _ok_poz:=1;
                           KS.cntx_psh();
                           {? POZ.KOM=null
                           || {? POZ.KON=''
                              || _ok_poz:=0
                              || KS.index('SYM'); KS.prefix(ROK_F.ref(),ROK_F.SYNT+POZ.KON);
                                 {? ~KS.first() || _ok_poz:=0 ?}
                              ?}
                           || POZ.KOM().KS()
                           ?};
                           {? _ok_poz & -(1+KS.TYP)<>'p'
                           || {? POZ.STR='Wn' || DOK.SUMWN+=POZ.SUM || DOK.SUMMA+=POZ.SUM ?};
                              _dok_put:=1
                           ?};
                           KS.cntx_pop()
                        ?};
                        POZ.next()
                     !}
                  ?};
                  no_msg(0); lock_time(-1);
                  {? _dok_put & DOK.put() || _ldok+=1 ?};
                  DOK.next()
               !}
            ?};
            OKRO_F.next()
         !}
      ?};
      _kon.clear();
      ROK_F.next()
   !}
?};
DOK.cntx_pop(); DOK.trig_on('*','*');
VPOZ.cntx_pop(); VPOZ.trig_on('*','*');
POZF.cntx_pop(); POZF.trig_on('*','*');
POZ.cntx_pop(); POZ.trig_on('*','*');
{? _ldok | _lpoz | _lvpoz | _lpozf
|| {? _ldok || __UPG.msg('Zaktualizowano nagłówki dokumentów źródłowych. %1 zmodyfikowany(ch) rekord(ów).'[$_ldok]) ?};
   {? _lpoz || __UPG.msg('Zaktualizowano pozycje dokumentów źródłowych. %1 zmodyfikowany(ch) rekord(ów).'[$_lpoz]) ?};
   {? _lvpoz || __UPG.msg('Zaktualizowano pozycje dokumentów VAT. %1 zmodyfikowany(ch) rekord(ów).'[$_lvpoz]) ?};
   {? _lpozf || __UPG.msg('Zaktualizowano pozycje faktur. %1 zmodyfikowany(ch) rekord(ów).'[$_lpozf]) ?}
|| __UPG.msg('Nie stwierdzono potrzeby aktualizacji danych dla dokumentów księgowych.')
?};
1


\DOK_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ, JK, PBS [23.25]
:: OPIS: Uzupełnienie w nagłówku dokumentu sum kwot netto, vat i brutto z pozycji VAT dokumentu - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie w nagłówku dokumentu sum kwot netto, vat, brutto oraz obrotów WN/MA z pozycji VAT dokumentu. '
'Aktualizacja pozycji dokumentów źródłowych oraz pozycji dokumentów VAT na podstawie tabeli DOK. '


\obj_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Obiekt pomocniczy na potrzeby opisu konta
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'TAB',
   'clear',
   'get','set'
);
_obj.TAB:=tab_tmp(1,
   'K','STRING[35]','Symbol konta',
   'O','STRING[255]','Opis'
);
_obj.clear:="
   .TAB.erase()
";
_obj.get:="
   _kon:=_a;
   _tab:=.TAB;
   {? _tab.find_key(_kon,)
   || _tab.O
   || ''
   ?}
";
_obj.set:="
   _kon:=_a;
   _desc:=_b;
   _tab:=.TAB;
   _tab.blank(0);
   _tab.K:=_kon;
   _tab.O:=_desc;
   _tab.add()
";
_obj


\AN_OPIS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KW [23.25]
:: OPIS: Uzupełnia pole OPIS tabeli AN - Opis ostatniego poziomu konta
::----------------------------------------------------------------------------------------------------------------------
_licznik:=0;
_result:=1;
ROK_F.cntx_psh(); ROK_F.index('KODG'); ROK_F.prefix();
KS.cntx_psh(); KS.index('SYM'); KS.prefix();
BUD.cntx_psh(); BUD.index('KS');
SLO.cntx_psh(); SLO.index('SL'); SLO.prefix();
SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
{? ROK_F.first()
|| {! |?
      KS.prefix(ROK_F.ref());
      AN.cntx_psh(); AN.use('koan__'+ROK_F.KOD); AN.index('WALSYM'); AN.prefix();
      {? AN.first()
      || {! |?
            BUD.prefix(AN.KS);
            _opis:={? ~BUD.last()
                   || {? AN.KS || AN.KS().NAZ || '' ?}
                   || {? SLU.seek(BUD.SLU().SLU)
                      || {? SLO.find_key(BUD.SLU().SLU,|(AN.SYM+SLU.DL))
                         || SLO.TR
                         || ''
                         ?}
                      || ''
                      ?}
                   ?};
            {? AN.OPIS<>_opis
            || AN.OPIS:=_opis;
              {? AN.put() || _licznik+=1 ?}
            ?};
            AN.next()
         !}
      ?};
      AN.cntx_pop();
      ROK_F.next()
   !}
?};
ROK_F.cntx_pop();
KS.cntx_pop();
BUD.cntx_pop();
SLO.cntx_pop();
SLU.cntx_pop();
{? _licznik<>0
|| __UPG.msg('Przetworzono %1 zapisów.'[$_licznik])
|| __UPG.msg('Nie wprowadzono zmian, dane aktualne.')
?};
_result


\AN_OPIS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KW [23.25]
:: OPIS: Uzupełnia pole OPIS tabeli AN - Opis ostatniego poziomu konta
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych tabeli AN'


\OP_AN_OPIS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Aktualizacja pola AN_OPIS tabeli OP.
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh();
ROK_F.cntx_psh();ROK_F.index('KODG');ROK_F.prefix();
_licznik:=0;
OP.trig_off('*','*');
{? ROK_F.first()
|| {!
   |? rokpoz:=ROK_F.ref();
      SSTALE.AR:=ROK_F.ref();
      exec('set_var','!fks_ksr_zbob');
      OP.use('operac'+ROK_F.KOD);
      OP.index('KON_O');
      OP.prefix();
      {? OP.first()
      ||
         {!
         |? opis_poz:='';odd_ksn:=0;
            ROK_F.cntx_psh(); OKRO_F.cntx_psh();
            exec('op_konta','konto',OP.AN,null,2);
            exec('ka_opis','konto');
            OP.AN_OPIS:=opis_poz;
            {? OP.put() || _licznik+=1 ?};
            OKRO_F.cntx_pop(); ROK_F.cntx_pop();
            &opis_poz; &odd_ksn;
            OP.next()
         !}
      ?};
      ROK_F.next()
   !}
?};
OP.trig_on('*','*');
__UPG.msg('Zaktualizowano opis konta dla ' + $_licznik + ' rozrachunków.');
ROK_F.cntx_pop();
OP.cntx_pop()


\OP_AN_OPIS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Aktualizacja danych w polu AN_OPIS tabeli OP.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych w polu AN_OPIS tabeli OP.'


\f_kontrolna_poz_pln
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Dodaje formułę kontrolną która sprawdza złotówkowe pozycje 2??, podczas akceptacji dokumentu
:: w wyciągach bankowych, czy istnieje analogiczny rozrachunek w walucie obcej.
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G','POZ_PLN',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:='POZ_PLN';
   FORMULA.NAZWA:='Formuła dla pozycji sprawdzająca pozycje złotówkowe';
   FORMULA.FORMULA:='exec(\'spr_poz_pln\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=2
?};
FORMULA.cntx_pop();
{? _ret=2
|| __UPG.msg('Formuła kontrolna POZ_PLN jest już dodana w systemie.')
|? _ret=1
|| __UPG.msg('Dodano formułę kontrolną POZ_PLN.')
|| __UPG.msg('Dodanie formuły kontrolnej POZ_PLN nie powiodło się.')
?};
_ret


\f_kontrolna_poz_pln_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Dodaje formułę kontrolną która sprawdza złotówkowe pozycje 2??, podczas akceptacji dokumentu
:: w wyciągach bankowych, czy istnieje analogiczny rozrachunek w walucie obcej. - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły kontrolnej, która sprawdza złotówkowe pozycje 2??, podczas akceptacji dokumentu w '
'wyciągach bankowych. W celu sprawdzenia czy istnieje analogiczny rozrachunek w walucie obcej.'


\f_kontrolna_roz_nad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje formułę kontrolną, która przy księgowaniu sprawdzi czy nie jest płacona wyższa kwota niż istniejące
::       zobowiązanie/należność
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G','ROZ_NAD',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:='ROZ_NAD';
   FORMULA.NAZWA:='Formuła dla pozycji sprawdzająca czy nie jest płacona wyższa kwota niż istniejący rozrachunek';
   FORMULA.FORMULA:='exec(\'spr_roz_nad\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=2
?};
FORMULA.cntx_pop();
{? _ret=2
|| __UPG.msg('Formuła kontrolna ROZ_NAD jest już dodana w systemie.')
|? _ret=1
|| __UPG.msg('Dodano formułę kontrolną ROZ_NAD.')
|| __UPG.msg('Dodanie formuły kontrolnej ROZ_NAD nie powiodło się.')
?};
_ret


\f_kontrolna_roz_nad_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje formułę kontrolną, która przy księgowaniu sprawdzi czy nie jest płacona wyższa kwota niż istniejące
::       zobowiązanie/należność - opis formuły
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły kontrolnej dla pozycji, która przy księgowaniu sprawdzi czy nie jest płacona wyższa kwota niż '
'istniejące zobowiązanie/należność'


\fix_defw_uid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Aktualizacja pola UID dla tabeli związanej z definicjami wierszy sprawozdań.
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__defsize','__defmod');
__defsize:=0;
__defmod:=0;
DEFW.index('UID');
DEFW.prefix();
DEFW.for_each("
   __defsize+=1;
   _prev_uid:=DEFW.UID;
   _new_uid:='';
   {? _prev_uid=''
   || _new_uid:=exec('set_defw_uid','sprfin')
   || DEFW.cntx_psh();
      DEFW.prefix(_prev_uid,);
      {? DEFW.size>1
      || _new_uid:=exec('set_defw_uid','sprfin')
      ?};
      DEFW.cntx_pop()
   ?};
   {? _new_uid<>''
   || DEFW.UID:=_new_uid;
      {? DEFW.put() || __defmod+=1 ?}
   ?}
");
{? __defmod>0
|| __UPG.msg('Zmodyfikowano pola UID definicji wierszy sprawozdań.'
             ' Zmodyfikowano %1 z %2 rekordów.'[$__defmod,$__defsize])
|| __UPG.msg('Modyfikacja pola UID definicji wierszy sprawozdań nie jest wymagana.'
             ' Pole UID dla tabeli DEFW jest unikalne.')
?};
VAR_DEL.delete('__defsize','__defmod');
1


\fix_defw_uid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Aktualizacja pola UID dla tabeli związanej z definicjami wierszy sprawozdań. - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola UID dla tabeli związanej z definicjami wierszy sprawozdań.'


\dokum_to_dokumz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Ujednolicenie obsługi załączników w dokumentach księgowych z obsługą w dokumentach sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DOKUM.names();
_msk.clear();
DOKUM.cntx_psh();
{? _msk.first()
|| {!
   |? DOKUM.use(_msk.NAME);
      DOKUM.index('DOKUM');
      DOKUM.prefix(REF.FIRMA,'doku');
      {? DOKUM.first()
      || {!
         |? __lrec+=1;
            _put:=0;
            _ref_dok:=DOKUM.ref();
            {? DOKUM.DOKUM<>null()
            || DOKUMZ.cntx_psh();
               DOKUMZ.index('DOK');
               DOKUMZ.prefix();
               DOKUMZ.blank();
               DOKUMZ.DOK:=_ref_dok;
               DOKUMZ.DOKUM:=DOKUM.DOKUM;
               DOKUMZ.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
               DOKUMZ.add();
               DOKUMZ.cntx_pop()
            ?};
            {? DOKUM.DOKR=''
            || DOKUM.DOKR:=DOKUM.REFSQL;
               _put:=1
            ?};
            {? DOKUM.TYP='I'
            || DOKUM.TYP:='D';
               _put:=1
            ?};
            {? _put
            || __lmod+=1;
               DOKUM.put()
            ?};
            DOKUM.next()
         !}
      ?};
      _msk.next()
   !}
?};
DOKUM.cntx_pop();
__UPG.msg('Utworzono załącznik główny (DOKUMZ) do dokumentu (DOK).');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\dokum_to_dokumz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Ujednolicenie obsługi załączników w dokumentach księgowych z obsługą w dokumentach sprzedaży - opis
::----------------------------------------------------------------------------------------------------------------------
'Utworzonie załącznika głównego (DOKUMZ) do dokumentu (DOK)'


\rozlvat_dop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła uzupełniająca nowe pole ROZLVAT.DOP
::----------------------------------------------------------------------------------------------------------------------
_liczba:=0;
ROZLVAT.cntx_psh(); DOK.cntx_psh();
ROZLVAT.index('S1'); ROZLVAT.prefix();
{? ROZLVAT.first()
|| {! |?
      {? ROZLVAT.DOP=date(0,0,0)
      || DOK.use(ref_name($ROZLVAT.DOK));
         DOK.prefix();
         ROZLVAT.DOK();
         ROZLVAT.DOP:=DOK.DOP;
         {? ROZLVAT.put(1) || _liczba+=1 ?}
      ?};
      ROZLVAT.next()
   !}
?};
ROZLVAT.cntx_pop(); DOK.cntx_pop();
{? _liczba
|| __UPG.msg('Zaktualizowano zawieszone dokumenty VAT. Ilość zmodyfikowanych rekordów: %1.'[$_liczba])
|| __UPG.msg('Nie stwierdzono potrzeby aktualizacji zawieszonych dokumentów VAT do rozliczenia')
?};
1


\rozlvat_dop_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła uzupełniająca nowe pole ROZLVAT.DOP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja daty operacji w dokumentach VAT zawieszonych do rozliczenia'


\init_b_perm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Odświeża definicję uprawnień do danych
::----------------------------------------------------------------------------------------------------------------------
exec('init','b_perm');
__UPG.msg('Zaktualizowano definicje uprawnień do danych.');
1


\init_b_perm_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Odświeża definicję uprawnień do danych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji uprawnień do danych.'


\jpk_dl_sym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Wydłużenie pola symbol dokumentu do 50 znaków
::----------------------------------------------------------------------------------------------------------------------
_r:=_p:=0;
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','V7M',null,date(2022,1,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'DowodSprzedazy');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'DowodZakupu');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?}
|| _r:=-1
?};
{? _r<0
|| __UPG.msg('Nie znaleziono deklaracji JPK_V7M(2).')
|? _r>0
|| __UPG.msg('Zaktualizowano pozycje deklaracji JPK_V7M(2).');
   __UPG.msg('Przetworzono: %1 pozycji deklaracji JPK_V7M(2), z czego zmodyfikowano: %2 pozycji.'[$_r,$_p])
?};

_r:=_p:=0;
ISTDEF.prefix('FKS','J','KR',null,date(2016,3,11));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'NrDowoduKsiegowego');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?}
|| _r:=-1
?};
{? _r<0
|| __UPG.msg('Nie znaleziono deklaracji JPK_KR(1).')
|? _r>0
|| __UPG.msg('Zaktualizowano pozycje deklaracji JPK_KR(1).');
   __UPG.msg('Przetworzono: %1 pozycji deklaracji JPK_KR(1), z czego zmodyfikowano: %2 pozycji.'[$_r,$_p])
?};
_r:=_p:=0;
ISTDEF.prefix('FKS','J','MAG',null,date(2016,3,11));
{? ISTDEF.first()
|| ISTDEFS.prefix(ISTDEF.ref,'NumerFaPZ');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?}
|| _r:=-1
?};
{? _r<0
|| __UPG.msg('Nie znaleziono deklaracji JPK_MAG(1).')
|? _r>0
|| __UPG.msg('Zaktualizowano pozycje deklaracji JPK_MAG(1).');
   __UPG.msg('Przetworzono: %1 pozycji deklaracji JPK_MAG(1), z czego zmodyfikowano: %2 pozycji.'[$_r,$_p])
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
1


\jpk_dl_sym_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS:  Wydłużenie pola symbol dokumentu do 50 znaków - opis
::----------------------------------------------------------------------------------------------------------------------
' Wydłużenie pola symbol dokumentu do 50 znaków'


\long_sym_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uaktualnienie pola SYM_ROK
::----------------------------------------------------------------------------------------------------------------------
__lrec:=__lmod:=0; _NAMES:=POZ.names; POZ.trig_off('*','*');
{? _NAMES.first()
|| POZ.cntx_psh();
   {! |?
      POZ.use(_NAMES.NAME); POZ.prefix();
      POZ.for_each("__lrec+=1;
                   {? POZ.SYM_ROK<>'' & +POZ.SYM_ROK<55 || POZ.SYM_ROK:=exec('op_unik_sym','rozrach',POZ.ID,,POZ.SYM_ROK+4);
                      {? POZ.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   POZ.cntx_pop()
?};
POZ.trig_on('*','*');
__UPG.msg('Zaktualizowano SYM_ROK w pozycjach dokumentów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=OP.names; OP.trig_off('*','*');
{? _NAMES.first()
|| OP.cntx_psh();
   {! |?
      OP.use(_NAMES.NAME); OP.prefix();
      OP.for_each("__lrec+=1;
                      {? OP.SYM_ROK<>'' & +OP.SYM_ROK<55 || OP.SYM_ROK:=exec('op_unik_sym','rozrach',OP.SYM,,OP.SYM_ROK+4);
                      {? OP.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   OP.cntx_pop()
?};
OP.trig_on('*','*');
__UPG.msg('Zaktualizowano SYM_ROK w rozrachunkach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=RMK.names;
{? _NAMES.first()
|| RMK.cntx_psh();
   {! |?
      RMK.use(_NAMES.NAME); RMK.prefix();
      RMK.for_each("__lrec+=1;
                      {? RMK.SYM_ROK<>'' & +RMK.SYM_ROK<55 || RMK.SYM_ROK:=exec('op_unik_sym','rozrach',RMK.SYM,,RMK.SYM_ROK+4);
                      {? RMK.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   RMK.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli RMK.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=VAT_PS.names;
{? _NAMES.first()
|| VAT_PS.cntx_psh();
   {! |?
      VAT_PS.use(_NAMES.NAME); VAT_PS.prefix();
      VAT_PS.for_each("__lrec+=1;
                      {? VAT_PS.SYM_ROK<>'' & +VAT_PS.SYM_ROK<55 || VAT_PS.SYM_ROK:=exec('op_unik_sym','rozrach',VAT_PS.SYM,,VAT_PS.SYM_ROK+4);
                      {? VAT_PS.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   VAT_PS.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli VAT_PS.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=OPTMP.names;
{? _NAMES.first()
|| OPTMP.cntx_psh();
   {! |?
      OPTMP.use(_NAMES.NAME); OPTMP.prefix();
      OPTMP.for_each("__lrec+=1;
                      {? OPTMP.SYM_ROK<>'' & +OPTMP.SYM_ROK<55 || OPTMP.SYM_ROK:=exec('op_unik_sym','rozrach',OPTMP.SYM,,OPTMP.SYM_ROK+4);
                      {? OPTMP.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   OPTMP.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli OPTMP.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=PAR_NAG.names;
{? _NAMES.first()
|| PAR_NAG.cntx_psh();
   {! |?
      PAR_NAG.use(_NAMES.NAME); PAR_NAG.prefix();
      PAR_NAG.for_each("__lrec+=1;
                      {? PAR_NAG.SYM_ROK<>'' & +PAR_NAG.SYM_ROK<55 || PAR_NAG.SYM_ROK:=exec('op_unik_sym','rozrach',PAR_NAG.SYMDOK,,PAR_NAG.SYM_ROK+4);
                      {? PAR_NAG.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   PAR_NAG.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli PAR_NAG.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=PAR_POZ.names;
{? _NAMES.first()
|| PAR_POZ.cntx_psh();
   {! |?
      PAR_POZ.use(_NAMES.NAME); PAR_POZ.prefix();
      PAR_POZ.for_each("__lrec+=1;
                      {? PAR_POZ.SYM_ROK<>'' & +PAR_POZ.SYM_ROK<55 || PAR_POZ.SYM_ROK:=exec('op_unik_sym','rozrach',PAR_POZ.OP,,PAR_POZ.SYM_ROK+4);
                      {? PAR_POZ.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   PAR_POZ.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli PAR_POZ.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=PB.names;
{? _NAMES.first()
|| PB.cntx_psh();
   {! |?
      PB.use(_NAMES.NAME); PB.prefix();
      PB.for_each("__lrec+=1;
                      {? PB.SYM_ROK<>'' & +PB.SYM_ROK<55 || PB.SYM_ROK:=exec('op_unik_sym','rozrach',PB.SYM,,PB.SYM_ROK+4);
                      {? PB.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   PB.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli PB.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=PB_OP.names;
{? _NAMES.first()
|| PB_OP.cntx_psh();
   {! |?
      PB_OP.use(_NAMES.NAME); PB_OP.prefix();
      PB_OP.for_each("__lrec+=1;
                      {? PB_OP.SYM_ROK<>'' & +PB_OP.SYM_ROK<55 || PB_OP.SYM_ROK:=exec('op_unik_sym','rozrach',PB_OP.SYM,,PB_OP.SYM_ROK+4);
                      {? PB_OP.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   PB_OP.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli PB_OP.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=SER_POZ.names;
{? _NAMES.first()
|| SER_POZ.cntx_psh();
   {! |?
      SER_POZ.use(_NAMES.NAME); SER_POZ.prefix();
      SER_POZ.for_each("__lrec+=1;
                      {? SER_POZ.SYM_ROK<>'' & +SER_POZ.SYM_ROK<55 || SER_POZ.SYM_ROK:=exec('op_unik_sym','rozrach',SER_POZ.DOK,,SER_POZ.SYM_ROK+4);
                      {? SER_POZ.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   SER_POZ.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli SER_POZ.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=WIND_OP.names;
{? _NAMES.first()
|| WIND_OP.cntx_psh();
   {! |?
      WIND_OP.use(_NAMES.NAME); WIND_OP.prefix();
      WIND_OP.for_each("__lrec+=1;
                      {? WIND_OP.SYM_ROK<>'' & +WIND_OP.SYM_ROK<55 || WIND_OP.SYM_ROK:=exec('op_unik_sym','rozrach',WIND_OP.SYM,,WIND_OP.SYM_ROK+4);
                      {? WIND_OP.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   WIND_OP.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli WIND_OP.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=POZDOK.names;
{? _NAMES.first()
|| POZDOK.cntx_psh();
   {! |?
      POZDOK.use(_NAMES.NAME); POZDOK.prefix();
      POZDOK.for_each("__lrec+=1;
                      {? POZDOK.SYM_ROK<>'' & +POZDOK.SYM_ROK<55 || POZDOK.SYM_ROK:=exec('op_unik_sym','rozrach',POZDOK.MF_FIKS-5,,POZDOK.SYM_ROK+4);
                      {? POZDOK.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   POZDOK.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli POZDOK.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;
1


\long_sym_rok_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uaktualnienie pola SYM_ROK - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie pola SYM_ROK'


\portalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizacja znakcznika PORTALK.ENABLED
::----------------------------------------------------------------------------------------------------------------------
_wym:=_akt:=0;
ETYP_ATR.cntx_psh();
ETYP_ATR.index('ID_WP');
PORTALK.cntx_psh();
PORTALK.index('FLD');
ETYPY.cntx_psh();
ETYPY.index('GRUPA_WP');
ETYPY.prefix('S');
{? ETYPY.first()
|| {!
   |? ETYP_ATR.prefix(ETYPY.ref());
      {? ETYP_ATR.first()
      || {!
         |? PORTALK.prefix(ETYP_ATR.TAT().ID_WP,);
            {? PORTALK.first() & PORTALK.ENABLED<>'T'
            || _wym+=1;
               PORTALK.ENABLED:='T';
               _akt+=PORTALK.put()
            ?};
            ETYP_ATR.next()
         !}
      ?};
      ETYPY.next()
   !}
?};
ETYPY.cntx_pop();
PORTALK.cntx_pop();
ETYP_ATR.cntx_pop();
__UPG.msg('Liczna rekordów do aktualizacji: %1'[$_wym]);
__UPG.msg('Liczna rekordów zaktualizowanych: %1'[$_akt]);
1


\portalk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizacja znakcznika PORTALK.ENABLED - opis
::----------------------------------------------------------------------------------------------------------------------
'Włączenie dodatkowych pól na fakturze SEOD.\n'
'Aktualizacja pola PORTALK.ENABLED.'


\deleg_seod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Formuły uzupełniające dane na potrzeby delegacji SEOD
::----------------------------------------------------------------------------------------------------------------------
:: Typy delegacji
_dk:=exec('ETYPY_deleg_add','portal_seod','K');
_dz:=exec('ETYPY_deleg_add','portal_seod','Z');
__UPG.msg('Utworzenie typu delegacji krajowej: %1'[{? _dk || 'Tak' || 'Nie' ?}]);
__UPG.msg('Utworzenie typu delegacji zagranicznej: %1'[{? _dz || 'Tak' || 'Nie' ?}]);

:: Środki transportu
INNESRTR.cntx_psh();
INNESRTR.index('NAZWA');
INNESRTR.prefix();
INNESRTR.for_each("{? INNESRTR.DODAT='' || INNESRTR.DODAT:='N'; INNESRTR.put() ?}");
INNESRTR.cntx_pop();
__UPG.msg('Uzupełniono pole INNESRTR.DODAT');
_add_st:="
   _naz:=_a;
   INNESRTR.cntx_psh();
   INNESRTR.index('NAZWA'); INNESRTR.prefix();
   {? INNESRTR.find_key(_naz,)
   || INNESRTR.DODAT:='T';
      INNESRTR.put();
      __UPG.msg('Uaktualniono środek transportu: %1'[_naz])
   || INNESRTR.blank(1);
      INNESRTR.NAZWA:=_naz;
      INNESRTR.DODAT:='T';
      INNESRTR.add();
      __UPG.msg('Dodano środek transportu: %1'[_naz])
   ?};
   INNESRTR.cntx_pop()
";
_add_st('Samochód służbowy');
_add_st('Samochód prywatny');

:: Nowe komunikaty
_obj:=exec('portal_hr_obj','upgradexperti2');

:: PORTAL_SEOD_CFG
_obj.add('PORTAL_SEOD_CFG','Usuwanie','SLO','cseod_BusinessTripReasonDelete',
   'Cel delegacji',$"exec('SLO_ok','portal_seod')",,'F'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','SLO','cseod_BusinessTripReasonModify',
   'Cel delegacji',$"exec('SLO_ok','portal_seod')",,'F'
);
_obj.add('PORTAL_SEOD_CFG','Usuwanie','INNESRTR','cseod_BusinessTripVehicleDelete',
   'Środki transportu',,,'F'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','INNESRTR','cseod_BusinessTripVehicleModify',
   'Środki transportu',,,'F'
);
_obj.add('PORTAL_SEOD_CFG','Usuwanie','ETYPWYD','cseod_CostCenterDelete',
   'Typy wydatków',,,'F'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','ETYPWYD','cseod_CostCategoryModify',
   'Typy wydatków',,,'F'
);
:: PORTAL_SEOD
_obj.add('PORTAL_SEOD','Odbieranie','EDOKUM','cseod_BusinessTripGet',
   'Delegacja',$"exec('EDOKUM_seod','portal_seod')",'cseod_AcceptanceHeaderModify','T'
);
_obj.add('PORTAL_SEOD','Wysyłanie','EDOKUM','cseod_BusinessTripModify',
   'Delegacja',$"exec('EDOKUM_seod','portal_seod')",'cseod_BusinessTripGet','T'
);
_obj.add('PORTAL_SEOD','Usuwanie','EDOKUM','cseod_BusinessTripDelete',
   'Delegacja',$"exec('EDOKUM_seod','portal_seod')",'cseod_BusinessTripModify','T'
);

_obj.add('PORTAL_SEOD','Odbieranie','EDOK_ZAL','cseod_PurchaseDocumentGet',
   'Wydatki delegacji',$"exec('EDOK_ZAL_ok','portal_seod')",'cseod_BusinessTripGet','T'
);
_obj.add('PORTAL_SEOD','Usuwanie','EDOK_ZAL','cseod_PurchaseDocumentDelete',
   'Wydatki delegacji',$"exec('EDOK_ZAL_ok','portal_seod')",'cseod_PurchaseDocumentDelete','T'
);
1


\deleg_seod_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Formuły uzupełniające dane na potrzeby delegacji SEOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie danych na potrzeby delegacji SEOD:\n'
'- dodanie typów delegacji'
'- uzupełnienie pola INNESRTR.DODAT\n'
'- dodanie innych środków transportu: samochodu służbowego i prywatnego\n'
'- dodanie obsługi nowych komunikatów API\n'


\jpk_fa_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja struktury pliku JPK_FA
::----------------------------------------------------------------------------------------------------------------------
_fml:="exec('jpk_fa_dsp','jpk')";
_zmiana:=-1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','FA','JPK_FA(4)_v1-0',);
{? ISTDEF.first()
|| _zmiana:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref(),'P_6');
   {? ISTDEFS.first() & ISTDEFS.REGULY<>$_fml
   || ISTDEFS.REGULY:=$_fml;
      _zmiana:=ISTDEFS.put()
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
{? _zmiana=1
|| __UPG.msg('Zaktualizowano schemat JPK_FA (4).')
|? _zmiana=-1
|| __UPG.msg('Nie znaleziono schematu JPK_FA (4).')
|| __UPG.msg('Aktualizacja schematu JPK_FA (4) nie była wymagana.')
?};
1


\jpk_fa_dsp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja struktury pliku JPK_FA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu JPK_FA (4)\n'
'Zmiana formuły dla pola JPK\\Faktura\\P_6'


\mwa_wnioski
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie metod API dla PORTAL_WNIOSKI
::----------------------------------------------------------------------------------------------------------------------
exec('init','sync_id');
__UPG.msg('Zaktualizowano identyfikatory rekordów dla portalu.');


_obj:=exec('portal_hr_obj','upgradexperti2');

:: PORTAL_HR_CFG
_obj.add('PORTAL_HR_CFG','Wysyłanie','ETYPY','chr_RequestTypeParDefParamModify',
   'Wysłanie pól wyświetlanych w szczegółach wniosku inTerm.',,'chr_RequestTypeParDefParamModify',,3
);

:: PORTAL_WNIOSKI
_obj.add('PORTAL_WNIOSKI','Uruchomienie','EDOKUM','chr_PersonRequestModify',
   'Wysyłanie wniosków.',,'chr_PersonRequestModify'
);
_obj.add('PORTAL_WNIOSKI','Uruchomienie','EDOKUM','cseod_AcceptanceHeaderModify',
   'Wysyłanie ścieżki akceptacji wniosków.',,'cseod_AcceptanceHeaderModify'
);
1


\mwa_wnioski_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie metod API dla PORTAL_WNIOSKI - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja identyfikatorów rekordów na portalu.
Dołączenie metod API do:
- PORTAL_HR_CFG
- PORTAL_WNIOSKI'


\mwa_faktury
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie metod API dla PORTAL_SEOD_CFG
::----------------------------------------------------------------------------------------------------------------------
exec('init','sync_id');
__UPG.msg('Zaktualizowano identyfikatory rekordów dla portalu.');


_obj:=exec('portal_hr_obj','upgrade_2226');

:: PORTAL_SEOD_CFG
_obj.add('PORTAL_SEOD_CFG','Usuwanie','SLO','cseod_PaymentTypeTenantDelete',
   'Sposób płatności'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','SLO','cseod_PaymentTypeTenantModify',
   'Sposób płatności'
);

{? XINFO.SLP=null
|| exec('czytaj','#stalesys',,XINFO,'SLP')
?};
{? XINFO.SLP
|| SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(XINFO.SLP);
   {? SLO.first()
   || {!
      |? SLO.put(,1);
         SLO.next()
      !}
   ?};
   SLO.cntx_pop();
   __UPG.msg('Oznaczono sposoby płatności do ponownej wysyłki na portal.')
?};

1


\mwa_faktury_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie metod API dla PORTAL_SEOD_CFG - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja identyfikatorów rekordów na portalu.\n'
'Dołączenie metod API do:\n'
'- PORTAL_SEOD_CFG\n'
'Oznaczenie sposobów płatności do ponownej wysyłki.'


\tat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja nieuzupełnionych pól TAT.MIEJSCE
::----------------------------------------------------------------------------------------------------------------------
_ret:=-1;
_tab:=sql(
   'select REFERENCE as REF, NA, OPIS, \'B\' as M, \'           \' as S, \'                   \' as O, 0 as Z '
   'from TAT '
   'where TAT.MIEJSCE=\'\' '
   'order by NA,OPIS'
);
{? _tab.first()
|| TAT.cntx_psh();
   TAT.prefix();
   {!
   |? {? TAT.seek(_tab.REF)
      || {? TAT.count()=0 & TAT.W_PORTAL='N'
         || _tab.next()
         || _tab.del()
         ?}
      || _tab.del()
      ?}
   !};
   TAT.cntx_pop()
?};
{? _tab.first()
|| VAR_DEL.delete('TatM');
   TatM:=obj_new(
      'FLD','SEL_EXIT','D0','D1','P0','P1','N0','N1',
      'I0','I1'
   );
   TatM.SEL_EXIT:=0;
   TatM.I0:=_tab.index('?');
   TatM.I1:=_tab.ndx_tmp(,,'Z',,);
   _tab.fld_fml('M','AFTER_EDIT',"TatM.FLD:=fld(); 1");
   _red:=_tab.mk_edit('Miejsce',,'#tatupdmiejsce');
   _tab.win_efld(_red,,'M',,,,,,'Miejsce',,,'radio-buttons',,
      'Brak',"'B'",
      'Logistyka',"'L'",
      'Obieg dokumentów',"'W'",
      'Produkcja',"'U'",
      'Środki trwałe',"'T'"
   );
   _ok:=_tab.win_ebtn(_red,'text=%1'['OK'@],'key:F2');
   _anuluj:=_tab.win_ebtn(_red,'text=%1'['Anuluj'@],'key:Esc');
   _tab.btn_eopt(_red,_ok,'tooltip='+exec('help_red_ok','#window','P'));
   _tab.btn_eopt(_red,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   _tab.win_edit(_red);
   _wer:=_tab.mk_sel('Atrybuty','P',,'#tatupdmiejsce',,,,,'U');
   _tab.win_fld(_wer,,'NA',,,,,,'Nazwa');
   _tab.win_fld(_wer,,'OPIS',,,100,,,'Opis');
   _tab.win_fld(_wer,,'S',,,11,,,'Operacja');
   _tab.win_fld(_wer,,'O',,,20,,,'Miejsce');
   _tab.win_act(_wer,,'Formuła','Popraw',,,"
      _tab:=cur_tab();
      {? _tab.sel_size() | _tab.edit()
      || _tab.M:=TatM.FLD;
         _tab.S:={? _tab.M<>'B' || 'Poprawienie' || '' ?};
         _tab.Z:=_tab.M<>'B';
         _tab.O:={? _tab.M='L' || 'Logistyka'
                 |? _tab.M='W' || 'Obieg dokumentów'
                 |? _tab.M='U' || 'Produkcja'
                 |? _tab.M='T' || 'Środki trwałe'
                 || ''
                 ?};
         _tab.put()
      ?}
   ",,1,1,"
      _tab:=cur_tab();
      {? _tab.edit()
      || 1
      ?}
   ");
   _tab.win_act(_wer,,'Formuła','Usuń',,,"
      _tab:=cur_tab();
      {? _tab.sel_size() | FUN.ask('Czy usunąć atrybut?'@)
      || _tab.S:='Usunięcie';
         _tab.M:='D';
         _tab.O:='';
         _tab.Z:=1;
         _tab.put()
      ?}
   ",,,1,"
      {? FUN.ask('Czy usunąć zaznaczone atrybuty?'@)
      || 1
      ?}
   ");
   _tab.win_act(_wer,,'Formuła','Zakończ',,,"TatM.SEL_EXIT:=1; sel_exit()");
   _tab.win_act(_wer,,'Okienko',,,,,"
      _tab:=cur_tab();
      {? TatM.SEL_EXIT
      || _tab.cntx_psh();
         _tab.index(TatM.I1);
         _tab.prefix(0);
         _not:=_tab.first();
         _tab.cntx_pop();
         {? _not
         || FUN.ask('Istnieją atrybuty bez uzupełnionej operacji.\nCzy kontynuować?'@)
         || 1
         ?}
      || _tab.cntx_psh();
         _tab.index(TatM.I1);
         _tab.prefix(1);
         _set:=_tab.first();
         _tab.cntx_pop();
         {? _set
         || FUN.ask('Istnieją atrybuty z uzupełnioną operacją.\nAnulować zmiany?'@)
         || 1
         ?}
      ?}
   ");
   _tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['&Popraw'@],'menu:P');
   _tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['&Usuń'@],'menu:U');
   _tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Zakończ'@],'menu:Z');
   _tab.win_sel(_wer);
   {? _tab.select()
   || TatM.D0:=TatM.D1:=TatM.P0:=TatM.P1:=0;
      TatM.N0:=TatM.N1:=0;
      _tab.cntx_psh();
      _tab.index(TatM.I1);
      _tab.prefix(1);
      {? _tab.first()
      || TAT.cntx_psh();
         TAT.prefix();
         {!
         |? {? TAT.seek(_tab.REF)
            || {? _tab.M='D'
               || TatM.D0+=1;
                  {? TAT.count()=0
                  || {? TAT.del(,1)
                     || TatM.D1+=1
                     ?}
                  ?}
               |? TAT.MIEJSCE=''
               || TatM.P0+=1;
                  TAT.MIEJSCE:=_tab.M;
                  {? TAT.put()
                  || TatM.P1+=1
                  ?}
               || TatM.N1+=1
               ?}
            || TatM.N0+=1
            ?};
            _tab.next()
         !};
         TAT.cntx_pop()
      ?};
      _tab.prefix(0);
      _bez:=_tab.size();
      {? _bez=0 || _ret:=1 ?};
      _tab.cntx_pop();
      {? TatM.D0
      || __UPG.msg('Usunięto: %1 z %2'[$TatM.D1,$TatM.D0])
      ?};
      {? TatM.P0
      || __UPG.msg('Poprawiono: %1 z %2'[$TatM.P1,$TatM.P0])
      ?};
      {? TatM.N0
      || __UPG.msg('Nie znaleziono: %1'[$TatM.N0])
      ?};
      {? TatM.N1
      || __UPG.msg('Miejsce już uzupełnione: %1'[$TatM.N1])
      ?};
      {? _bez
      || __UPG.msg('Do uzupełnienia: %1'[$_bez])
      ?}
   ?}
|| _info:='Brak atrybutów do uzupełnienia.'@;
   __UPG.msg(_info);
   FUN.info(_info);
   _ret:=1
?};
VAR_DEL.delete('TatM');
_ret


\tat_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja nieuzupełnionych pól TAT.MIEJSCE - opis
::----------------------------------------------------------------------------------------------------------------------
'Umożliwia uzupełnienie znaczników miejsca użycia typów atrybutów (TAT.MIEJSCE), które nie zostały wypełnione formułą'
' automatyczną lub usunięcie tych rekordów.'


\rozrach_waluta_obca
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła przechodzi po wszystich rozrachunkach i dla każdego szuka jego odpowiednika w obcej walucie
::       W przypadku znalezieniu bliźniaczego rozrachunku oba rozrachunki są aktualizowane o obroty,salda i walutę
::       drugiego rozrachunku.
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh();
_licznik:=0;
_msk:=OP.names();
_msk.clear();
{? _msk.first()
||
:: Pętla dla wszystkih masek
   {!
   |? OP.use(_msk.NAME);
      OP.index('KON_O');
      OP.prefix();
      {? OP.first()
      ||
:: Pętla dla wszystkich rozrachunków w obcej walucie
          OP.trig_off('*','*');
         {!
         |?
:: Wyszukanie bliźniaczego rozrachunku
            {? OP.WAL<>FINFO.NAROD
            || _wal:=OP.WAL;
               _symbol:=OP.SYM;
               _konto:=OP.AN;
               _sal_ma:=OP.SMA;
               _sal_wn:=OP.SWN;
               _ob_ma:=OP.MA;
               _ob_wn:=OP.WN;
               OP.cntx_psh();
               OP.index('KON_O');
               OP.prefix(FINFO.NAROD,OP.ODD,_konto,_symbol,_symbol,OP.SYM_ROK);
               {? OP.first()
               ||
:: Aktualizacja znalezionego rozrachunku w obcej walucie
                  OP.WAL_OB:=_wal;
                  OP.SMA_WAL:=_sal_ma;
                  OP.SWN_WAL:=_sal_wn;
                  OP.MA_WAL:=_ob_ma;
                  OP.WN_WAL:=_ob_wn;
                  _wal:=OP.WAL;
                  _sal_ma:=OP.SMA;
                  _sal_wn:=OP.SWN;
                  _ob_ma:=OP.MA;
                  _ob_wn:=OP.WN;
                  OP.put();
:: Aktualizacja aktualnego rozrachunku
                  OP.cntx_pop();
                  OP.WAL_OB:=_wal;
                  OP.SMA_WAL:=_sal_ma;
                  OP.SWN_WAL:=_sal_wn;
                  OP.MA_WAL:=_ob_ma;
                  OP.WN_WAL:=_ob_wn;
                  OP.put();
                  _licznik+=1
               || OP.cntx_pop()
               ?}
            ?};
            OP.next()
         !};
         OP.trig_on('*','*')
      ?};
      _msk.next()
   !};
   __UPG.msg('Znaleziono i zaktualizowano '+$_licznik +' par bliźniaczych rozrachunków.')
?};
OP.cntx_pop()


\rozrach_waluta_obca_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła przechodzi po wszystich rozrachunkach i dla każdego szuka jego odpowiednika w obcej walucie
::       W przypadku znalezieniu bliźniaczego rozrachunku oba rozrachunki są aktualizowane o obroty,salda i walutę
::       drugiego rozrachunku.
::----------------------------------------------------------------------------------------------------------------------
'Formuła aktualizuje rozrachunki o dane z bliźniaczego rozrachunku w obcej walucie'


\TAT_miejsce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Uzupełnia pole TAT.MIEJSCE
::----------------------------------------------------------------------------------------------------------------------
_sql:=sql(
:: Wnioski
   'select TAT.REFERENCE as REF, \'W\' as MIEJSCE from ETYP_ATR join TAT order by 1 '
   'union all '

:: Definicje atrybutów dla tabel - KH, SRSR
   'select TAT.REFERENCE as REF, \'T\' as MIEJSCE from CDDEF join TAT order by 1 '
   'union all '

:: Używane atrybuty
   'select TAT.REFERENCE as REF, \'U\' as MIEJSCE from UAT join TAT order by 1 '
);
_res:=obj_new('TO_SET','SET');
_res.TO_SET:=_res.SET:=0;
params_set('tab',_sql,'res',_res);
TAT.trig_off('*','*');
TAT.prefix();
TAT.for_each("
   {? TAT.MIEJSCE=''
   || _res:=params_get().res;
      _res.TO_SET+=1;
      {? TAT.W_PORTAL='N'
      || _tab:=params_get().tab;
         TAT.MIEJSCE:={? _tab.find_key($TAT.ref()) || _tab.MIEJSCE |? TAT.count()>0 || 'L' || '' ?}
      || TAT.MIEJSCE:='W'
      ?};
      {? TAT.MIEJSCE<>''
      || TAT.put();
         _res.SET+=1
      ?}
   ?}
",1);
TAT.trig_on('*','*');
__UPG.msg('Liczba rekordów do uzupełnienia: %1'[$_res.TO_SET]);
__UPG.msg('Liczba rekordów uzupełnionych: %1'[$_res.SET]);
__UPG.msg('Liczba rekordów nieuzupełnionych: %1'[$(_res.TO_SET-_res.SET)]);
1


\TAT_miejsce_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Uzupełnia pole TAT.MIEJSCE - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola z miejscem użycia typu atrybutu: TAT.MIEJSCE'


\przenies_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Dodanie formuły rejestrowania automatycznego PRZENIES
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_naz:='PRZENIES';
_opis:='Przeniesienie dokumentu do innego okresu';
_formpocz:='exec(\'wyb_dok\',\'fks_auto\',,1)';
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.last() || _rokf:=ROK_F.ref || _rokf:=null() ?};
ROK_F.cntx_pop();

AUTOKSIE.index('NAZ'); AUTOKSIE.prefix(_rokf,_naz,);
{? AUTOKSIE.first()=0
|| AUTOKSIE.blank(0);
   AUTOKSIE.NAZ:=_naz;
   AUTOKSIE.OP:=_opis;
   AUTOKSIE.ROK_F:=_rokf;
   AUTOKSIE.FORMPOCZ:=_formpocz;
   AUTOKSIE.TYP:='N';
   {? AUTOKSIE.add()
   || FORM.blank();
      FORM.AUTOKSIE:=AUTOKSIE.ref();
      FORM.POZ:=1;
      {? FORM.add() || FORM.memo_set('exec(\'storno\',\'fks_auto\',3)'); FORM.memo_put() ?};
      __UPG.msg('Dodano formułę rejestrowania automatycznego PRZENIES.')
   || _result:=0;
      __UPG.msg('Błąd podczas dodawania formuły rejestrowania automatycznego PRZENIES.')
   ?}
|| __UPG.msg('Aktualizacja formuł rejestrowania automatycznego nie jest wymagana.')
?};
_result


\przenies_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Dodanie formuły rejestrowania automatycznego PRZENIES
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły rejestrowania automatycznego PRZENIES'


\cit8_32
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS:  OPIS: Import sprawozdań na potrzeby deklaracji CIT-8 (32)
::----------------------------------------------------------------------------------------------------------------------
_spr:='cit-8_32_spr.dfg';
_edek:='cit-8_32.dfg';
_ok:=1;
{? fexists(_spr,1)
|| _ok:=exec('imp_spr','sprfin',_spr,2,0);
   {? _ok
   || __UPG.msg('Zaimportowano sprawozdania z pliku: %1'[_spr])
   || __UPG.msg('Nie udało się zaimportować sprawozdania z pliku: %1'[_spr])
   ?}
|| __UPG.msg('Nie znaleziono na serwerze pliku: %1'[_spr]);
   _ok:=0
?};
{? fexists(_edek,1)
|| _imp:=-1;
   {? exec('add_ver','xml','FKS',date(2022,1,1),'CIT8',32)
   || _imp:=exec('upg_imp','xml','FKS','D','CIT8',32,date(2022,1,1),'cit-8_32.dfg')
   ?};
   _txt:='e-Deklaracja CIT-8 (32) obowiązująca za 2022 rok';
   {? _imp=1
   || __UPG.msg(_txt+' została dodana.')
   |? _imp=0
   || __UPG.msg(_txt+' już istnieje.')
   || __UPG.msg(_txt+' nie została dodana.');
      _ok:=0
   ?}
|| __UPG.msg('Nie znaleziono na serwerze pliku: %1'[_edek]);
   _ok:=0
?};
_ok


\cit8_32_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Import sprawozdań na potrzeby deklaracji CIT-8 (32) - opis
::----------------------------------------------------------------------------------------------------------------------
'Import sprawozdań CIT-8 (32) i CIT-8O (18) za 2022 rok\n'
'Import schematów e-Deklaracji CIT-8 (32) za 2022 rok'


\akt_jpk_crok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Dostosowanie schematów JPK do tworzenia danych z całego roku
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
BPMN.SYM_DOM:='FKS';
SKID.ISTDEF:='J';
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','KR','JPK_KR(1)_v1-0',);
{? ISTDEF.first()
|| {? exec('erase_jpk','upgradexperti2')
   || _ok:=exec('edek_imp','xml','jpk_kr_1.dfg',1,1)
   ?}
?};
__UPG.msg('Schemat JPK_KR - %1 do tworzenia danych z całego roku.'@[{? _ok || 'dostosowano' || 'nie dostosowano' ?}]);

_ok:=0;
ISTDEF.prefix('J','WB','JPK_WB(1)_v1-0',);
{? ISTDEF.first()
|| {? exec('erase_jpk','upgradexperti2')
   || _ok:=exec('edek_imp','xml','jpk_wb_1.dfg',1,1)
   ?}
?};
ISTDEF.cntx_pop();
__UPG.msg('Schemat JPK_WB - %1 do tworzenia danych z całego roku.'@[{? _ok || 'dostosowano' || 'nie dostosowano' ?}]);
1


\akt_jpk_crok_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Podmienia we wskazanej strukturze JPK regułę - opis
::----------------------------------------------------------------------------------------------------------------------
'Dostosowanie schematów JPK do tworzenia danych z całego roku'


\erase_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Usuwa elementy schematu JPK
::   WE: [_a] - element nadrzędny
::----------------------------------------------------------------------------------------------------------------------
_tree:={? var_pres('_a')>0 || _a || 0 ?};
ISTDEFS.cntx_psh();
ISTDEFS.index('DRZEWO');
ISTDEFS.prefix(ISTDEF.ref(),_tree);
{? ISTDEFS.first()
|| {!
   |? ISTDEFI.cntx_psh();
      ISTDEFI.index('LP');
      ISTDEFI.prefix(ISTDEFS.ref());
      {? ISTDEFI.first() || {! |? ISTDEFI.del() !} ?};
      ISTDEFI.cntx_pop();
      exec('erase_jpk','upgradexperti2',#ISTDEFS.ref());
      ISTDEFS.del()
   !}
?};
_ok:=~ISTDEFS.first();
ISTDEFS.cntx_pop();
_ok


\stalesys_PPK_VI
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Aktualizacja stałych dziedziny PPK
::  ORG: \stalesys_PPK_VI/upgrade_rrxx.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? var_pres('POP_ZAW',KST_PPK)>0
|| _zap:=obj_new('TAB','add');
   _zap.TAB:=tab_tmp(1,'DATE','DATE','Data'@,'WAR','INTEGER','Wartość'@);
   _zap.add:="
      .TAB.DATE:=_a;
      .TAB.WAR:=_b;
      .TAB.add()";
:: Zapisy do dodania (konieczne zachowanie kolejności)
   _zap.add(date(0,0,0),0);
   _zap.add(date(2022,6,4),14);

   {? _zap.TAB.first()
   || {!
      |? exec('czytaj','#stalesys',_zap.TAB.DATE,KST_PPK,'POP_ZAW');
         {? KST_PPK.POP_ZAW=0
         || KST_PPK.POP_ZAW:=_zap.TAB.WAR;
            exec('zapisz_zes','#stalesys',_zap.TAB.DATE,KST_PPK,'POP_ZAW');
            _info:='Ustawiono wartość %1 dla daty %2.'@[$_zap.TAB.WAR,_zap.TAB.DATE$1]
         || _info:='Ustawienie wartości dla daty %1 nie było konieczne.'@[_zap.TAB.DATE$1]
         ?};
         __UPG.msg(_info);

         _zap.TAB.next()
      !}
   ?};
   obj_del(_zap)
|| __UPG.msg('Brak pola POP_ZAW w stałych systemu (KST_PPK).');
   _ret:=-1
?};

_ret


\stalesys_PPK_VI_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Aktualizacja stałych dziedziny PPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych dziedziny PPK'


\sync_mwa_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: Import definicji synchronizacji dla przeznaczenia danych PORTAL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fun:=spli_str('ZWS_PAR_DOK_SYNCHRO,',',');
exec('import_init','#excel_imex',_fun,,1,,0,,'SYNC_MWA_PORTAL');

_choice:=FUN.choice(
   'Czy dane zostały zaimportowane?\n\n'
   ' * Tak = zmiana statusu zadania na "Wykonane"\n'
   ' * Nie = pozostawienie zadanie do ponownego uruchomienia\n'
   ' * Anuluj = rezygnacja z wykonywania, zmiana statusu zadania na "Wykonane"\n'@
   ,2,'Tak'@,'Nie'@
);

USERS.cntx_psh();
__UPG.msg('Użytkownik: %1 | Data: %2 | Godzina: %3'[OPERATOR.USER().KOD,date()$1,time()$3]);
USERS.cntx_pop();

{? _choice=1
|| __UPG.msg('Zadanie zostało wykonane.')
|? _choice=2
|| __UPG.msg('Zadanie zostało odłożone do ponownego uruchomienia.')
|? _choice=0
|| __UPG.msg('Zrezygnowano z wykonania zadania.')
|| __UPG.msg('Nierozpoznana odpowiedź.')
?};

{? _choice<=1
|| 1
|| -1
?}


\sync_mwa_portal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: Import definicji synchronizacji dla przeznaczenia danych PORTAL - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Import definicji synchronizacji dla przeznaczenia danych PORTAL.'


\imp_wzor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Komunikat o konieczności zaimportowania danych wzorcowych.
::   WE:
::   WY:
::  OLD: \imp_wzor_2226_04/upgrade_2226_04.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask(
      'Formuła importująca dane wzorcowe wymagane przez aktualizację 2226_04 Portal HR Wielofirmowość\n'
      'Podczas importu należy wskazać folder w którym będą umieszczone tylko pliki: wymiana_danych_PORTAL.xlsx,\n'
      'wymiana_danych_PORTAL_CFG.xlsx, wymiana_danych_PORTAL_HR_CFG.xlsx, wymiana_danych_PORTAL_SEOD.xlsx,\n'
      'wymiana_danych_PORTAL_SEOD_CFG.xlsx, wymiana_danych_PORTAL_WNIOSKI.xlsx.\n'
      'Import należy wykonać w trybie: \'Nadpisuj istniejące\'.\n'
      '\n'
      'Czy import danych wzorcowych aktualizacji 22.26_04 został wykonany?'@
   )
|| __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   1
|| __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   0
?}


\imp_wzor_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Komunikat o konieczności zaimportowania danych wzorcowych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Komunikat o konieczności zaimportowania danych wzorcowych.'


\PORTALU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja uprawnień do funkcjonalności
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PORTALU.trig_off('*','*');
PORTALU.cntx_psh();
PORTALU.prefix();
PORTALU.for_each("
   __lrec+=1;
   _put:=0;
   {? PORTALU.TYPE='' || PORTALU.TYPE:='U'; _put:=1 ?};
   {? _put
   || {? PORTALU.put(1) || __lmod+=1 ?}
   ?}",
   1
);
PORTALU.cntx_pop();
PORTALU.trig_on('*','*');
__UPG.msg('Zaktualizowano uprawnienia do funkcjonalności.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\PORTALU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja uprawnień do funkcjonalności - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień do funkcjonalności'


\folp_std
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Standaryzacja formuł obliczeniowych list płac.
::----------------------------------------------------------------------------------------------------------------------
exec('folp_std_upd','lista_plac');
1


\folp_std_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Standaryzacja formuł obliczeniowych list płac - opis.
::----------------------------------------------------------------------------------------------------------------------
'Standaryzacja formuł obliczeniowych list płac.\n\n'
'Zadanie przeprowadza analizę treści formuł obliczeniowych list płac pod kątem\n'
'zgodności ich treści z formułami zapisanymi w odpowiednich plikach ppl_formuly?.fml.\n\n'
'Treść formuły klienta porównywana jest z formułami zarówno w pliku właściwym\n'
'dla typu formuły jak i plikiem formuł wspólnych. Przykładowo standaryzacja formuł typu U\n'
'będzie polegała na porównaniu treści formuł zapisanych w tabeli z formułami w plikach\n'
'ppl_formulyu.fml i ppl_formuly.fml.\n\n'
'Wynik analizy jest udostępniany w oknie wertowania. Na listach prezentowane są tylko te formuły,\n'
'których treść udało się dopasować do treści formuł standardowych.'


\OPAK_N
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje OPAK_N, OPAK_P
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=OPAK_N.names();
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |?
      OPAK_N.use(_msk.NAME);
      OPAK_N.prefix();
      OPAK_N.for_each("
         __lrec+=1;
         _put:=0;
         OPAK_P.use('opakp'+(OPAK_N.name()+3));
         OPAK_P.index('OPAK_N');
         OPAK_P.prefix(OPAK_N.ref());
         _loop:=OPAK_P.first();
         {!
         |? _loop
         |!
            {? OPAK_P.KH=null() | OPAK_P.KH_ODB=null()
            ||
               OPAK_P.KH:=OPAK_N.KH;
               OPAK_P.KH_ODB:=OPAK_N.KH_ODB;
               OPAK_P.put();
               _put:=1
            ?};
            _loop:=OPAK_P.next()
         !};
         {? _put || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
OPAK_P.cntx_pop();
OPAK_N.cntx_pop();

__UPG.msg('Zaktualizowano pola w dokumentach opakowań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\OPAK_N_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje OPAK_N, OPAK_P - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól dokumentów opakowań.'


\OPAK_ZWR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli OPAK_ZWR - tabela zwrotów opakowań
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=OPAK_ZWR.names();
_msk.clear();
OPAK_ZWR.cntx_psh();
{? _msk.first()
|| {!
   |? OPAK_ZWR.use(_msk.NAME);
      OPAK_ZWR.prefix();
      OPAK_ZWR.for_each("
         _put:=0;
         __lrec+=1;
         {? OPAK_ZWR.TMP=''
         ||
            OPAK_ZWR.TMP:='N';
            _put:=1
         ?};
         {? _put || {? OPAK_ZWR.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
OPAK_ZWR.cntx_pop();
__UPG.msg('Zaktualizowano tabelę zwrotów opakowań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\OPAK_ZWR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli OPAK_ZWR - tabela zwrotów opakowań - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola w tabeli zwrotów opakowań'


\base_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Działania związane z obsługą identyfikatora bazy danych
::----------------------------------------------------------------------------------------------------------------------
_res:=exec('base_id_manage','#system',1);
_res


\base_id_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Działania związane z obsługą identyfikatora bazy danych - opis
::----------------------------------------------------------------------------------------------------------------------
exec('base_id_info_transfer','#system')


\sync_mwa_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_KSEF1]
:: OPIS: Dopisuje definicję nowych metod
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_pd_sym:='BLMT';
_method1:='BLMT_DOCUMENT_SEND';
_method2:='BLMT_DOCUMENT_GETINFO';

SYNC_MWA.cntx_psh();
SYNC_MWA.index('PD_METH');
SYNC_MWA.prefix(_pd_sym,_method2,);
{? ~SYNC_MWA.first()
|| SYNC_MWA.prefix(_pd_sym,_method1,);
   {? SYNC_MWA.first()
   || _lp:=SYNC_MWA.LP;
::    Przenumerowanie z pozostawieniem dziury na nową metodę
      SYNC_MWA.cntx_psh();
      SYNC_MWA.index('TREE1');
      SYNC_MWA.prefix(SYNC_MWA.PARENT);
      {? SYNC_MWA.last()
      || {!
         |? {? SYNC_MWA.LP>_lp
            || SYNC_MWA.LP+=1;
               SYNC_MWA.put()
            ?};
            SYNC_MWA.prev()
         !}
      ?};
      SYNC_MWA.cntx_pop();
::    Dodanie nowej metody
      SYNC_MWA.LP+=1;
      SYNC_MWA.METHOD:=_method2;
      SYNC_MWA.prefix();
      {? SYNC_MWA.add()
      || __UPG.msg('Dopisano metodę %1.'[_method2])
      || __UPG.msg('Nie dopisano metody %1.'[_method2]);
         _res:=0
      ?}
   || __UPG.msg('Brak metody %1, nie można dopisać metody %2.'[_method1,_method2]);
      _res:=-1
   ?}
|| __UPG.msg('Metoda %1 była już dopisana.'[_method2])
?};
SYNC_MWA.cntx_pop();

_res


\sync_mwa_ksef_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26_KSEF1]
:: OPIS: Dopisuje definicję nowych metod - opis
::----------------------------------------------------------------------------------------------------------------------
'Dopisanie definicji metod:\n'
'BLMT_DOCUMENT_GETINFO'


\users_upr_dane_napraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Usuwa błędne zapisy w USERS_UP i TARUP
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('user_upraw_usun_blad','users_upraw');
__UPG.msg('Zaktualizowano uprawnienia użytkowników do danych.');
_res


\users_upr_dane_napraw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Usuwa błędne zapisy w USERS_UP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do danych'


\rubryki_placowe_PZD01
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przypisanie dodanych rubryk do nowych typów pracy poza siedzibą
::  OLD: \rubryki_placowe/upgrade_2226_pzd01.fml
::----------------------------------------------------------------------------------------------------------------------
PPSFT.trig_off('*','*');
PPSFT.cntx_psh();
PPSFT.index('NAZWA');
PPSFT.prefix(exec('firma','ustawienia'));
R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? PPSFT.find_key('Praca zdalna okazjonalna',) & PPSFT.SYSTEM='T' & PPSFT.R=null() & R.find_key(7135)
|| PPSFT.R:=R.ref();
   PPSFT.put()
?};
{? PPSFT.find_key('Praca zdalna stała',) & PPSFT.SYSTEM='T' & PPSFT.R=null() & R.find_key(7138)
|| PPSFT.R:=R.ref();
   PPSFT.put()
?};
R.cntx_pop();
PPSFT.cntx_pop();
PPSFT.trig_on('*','*');

__UPG.msg('Zakończono przypisanie rubryk do nowych typów pracy poza siedzibą.');

1


\rubryki_placowe_PZD01_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przypisanie dodanych rubryk do nowych typów pracy poza siedzibą - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie dodanych rubryk do nowych typów pracy poza siedzibą.'


\fap2dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przeniesienie złączenia pozycji dokumentów sprzedaży z pozycjami magazynowymi do tabeli FAP2DK
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DK.names();
exec('fap2dk_psh','open_tab');
_msk.clear();
{? _msk.first()
||
   {!
   |?
      _kod:=1+(_msk.NAME+3);
      _rok:=_msk.NAME+2;
      exec('fap2dk_open','open_tab',_kod,_rok,'sp');
      DK.prefix();
      FAP.prefix();
      FAKS.prefix();
      FAP2DK.index('NDK');
      DK.for_each("
         __lrec+=1;
         {? DK.FAP<>'' & ~(FAP2DK.prefix($DK.ref());FAP2DK.first()) & FAP.seek(DK.FAP,ref_name(DK.FAP))
         & FAP.FAKS<>null() & FAKS.seek(FAP.FAKS, ref_name(FAP.FAKS))
         || {? exec('adfap2dk','magdok_wspolne',$FAP.ref(),$DK.ref(),$FAP.FAKS,$DK.N,DK.IL,FAP.FAKS().WHERE)
            || __lmod+=1
            ?}
         |? DK.FAP<>'' & (FAP2DK.prefix($DK.ref());FAP2DK.first()) & FAP.seek(DK.FAP,ref_name(DK.FAP)) & (FAP2DK.FAKS='' | FAP2DK.FAP='')
         || FAP2DK.FAKS:=$FAP.FAKS;
            FAP2DK.FAP:=$FAP.ref();
            FAP2DK.prefix();
            {? FAP2DK.put(1) || __lmod+=1 ?}
         ?}
      ");
      _msk.next()
   !}
?};
exec('fap2dk_pop','open_tab');

__UPG.msg('Przeniesienie złączenia pozycji magazynowych z pozycjami magazynowymi do tabeli FAP2DK.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\fap2dk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przeniesienie złączenia pozycji dokumentów sprzedaży z pozycjami magazynowymi do tabeli FAP2DK
::----------------------------------------------------------------------------------------------------------------------
'Przeniesienie złączenia pozycji dokumentów sprzedaży z pozycjami magazynowymi do tabeli FAP2DK.'

\tlum_popr_wf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktualizacja struktury tłumaczeń.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__trans_gr_old','__trans_gr_new');
__lrec:=__lmod:=0;

TRANS_GR.cntx_psh();
TRANS_GR.index('KOD');
TRANS_GR.prefix('Wydruki dokumentów sprzedaży i korekty',);
{? TRANS_GR.first() || __trans_gr_old:=TRANS_GR.ref() ?};
TRANS_GR.prefix('Wydruki dokumentów sprzedaży i korekt',);
{? TRANS_GR.first() || __trans_gr_new:=TRANS_GR.ref() ?};
TRANS_GR.cntx_pop();

TRANS_PL.cntx_psh();
TRANS_PL.prefix();
{? var_press('__trans_gr_old')=7 & var_press('__trans_gr_new')=7
||
   TRANS_PL.for_each("
   __lrec+=1;
   {? TRANS_PL.TRANS_GR=__trans_gr_old
   ||
      _put:=0;
      TRANS_PL.TRANS_GR:=__trans_gr_new;
      {? TRANS_PL.put(1) || __lmod+=1 ?}
   ?}
   ",1);

   TRANS_GR.cntx_psh();
   TRANS_GR.prefix();
   {? TRANS_GR.seek(__trans_gr_old) || TRANS_GR.del() ?};
   TRANS_GR.cntx_pop()
?};
TRANS_PL.cntx_pop();

VAR_DEL.delete('__trans_gr_old','__trans_gr_new');

TRANS_GR.cntx_psh();
TRANS_GR.index('KOD');
TRANS_GR.prefix('Wydruki dok. sprzedaży i korekty',);
{? TRANS_GR.first() || __trans_gr_old:=TRANS_GR.ref() ?};
TRANS_GR.prefix('Wydruki dokumentów sprzedaży i korekt',);
{? TRANS_GR.first() || __trans_gr_new:=TRANS_GR.ref() ?};
TRANS_GR.cntx_pop();

TRANS_PL.cntx_psh();
TRANS_PL.prefix();
{? var_press('__trans_gr_old')=7 & var_press('__trans_gr_new')=7
||
   TRANS_PL.for_each("
   __lrec+=1;
   {? TRANS_PL.TRANS_GR=__trans_gr_old
   ||
      _put:=0;
      TRANS_PL.TRANS_GR:=__trans_gr_new;
      {? TRANS_PL.put(1) || __lmod+=1 ?}
   ?}
   ",1);

   TRANS_GR.cntx_psh();
   TRANS_GR.prefix();
   {? TRANS_GR.seek(__trans_gr_old) || TRANS_GR.del() ?};
   TRANS_GR.cntx_pop()
?};
TRANS_PL.cntx_pop();

exec('init','tlumaczenia');

__UPG.msg('Zaktualizowano strukturę tłumaczeń.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__trans_gr_old','__trans_gr_new');
_res


\tlum_popr_wf_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktualizacja struktury tłumaczeń.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja struktury tłumaczeń.'

:Sign Version 2.0 jowisz:1045 2023/11/30 07:58:32 a59528122c52c2f20e3173f875a8f7925f1689300b49cf3219326ef5f51290dfaca12795b57c7f079d69dce2d54e5f8a9d0e7036c5ce92b6db172c9add8d5df91ed718813b63085d4d5061e68e80c0272ac0c12f1fdf42bb37071aec7b1e4ae0708ee8a0f6b1ab11559b71eadbd8feba789e1db4c3ed26ad758e54189e108fc3

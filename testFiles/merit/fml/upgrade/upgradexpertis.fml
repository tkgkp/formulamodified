:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgradexpertis.fml
:: Utworzony: 08.01.2019
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z Xpertisa 12.41/12.51 na Merita w wersji aktualnie emitowanej
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: Główna formuła upgrade-ów z Xpertisa do 22.26
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('stalesys_ini',,,'ZWS');
__UPG.add_task('report',,,'ZWS');
__UPG.add_task('areatitle',,,'ZWS');
__UPG.add_task('upgrade_act',,,'ZPR');
__UPG.add_task('upgrade_event',,,'ZPR');
__UPG.add_task('color',,,'ZWS',);
__UPG.add_task('funpar',,,'ZWS',);
__UPG.add_task('par_skid_rbk',,,'ZWS');
__UPG.add_task('edeklaracje',,,'ZWS');
__UPG.add_task('f_03',,,'ZWS');
__UPG.add_task('jpk',,,'ZWS');
__UPG.add_task('wz_proj',,,'ZWS');
__UPG.add_task('USERS',,,'ZWS');
__UPG.add_task('B_SIGNAL',,,'ZPR');
__UPG.add_task('vat7_sch',,,'FKS');
__UPG.add_task('fiks_27',,,'ZWS');
__UPG.add_task('projekty_ref',,,'FKS');
__UPG.add_task('MATERIAL',,,'ZWS');
__UPG.add_task('pozf',,,'FKS');
__UPG.add_task('m_kh_sv',,,'ZWS');
__UPG.add_task('del140',,,'ZWS');
__UPG.add_task('eancAKT',,,'LMG');
__UPG.add_task('RS_zl',,,'ZWS');
__UPG.add_task('trn_rs',,,'ZWS');
__UPG.add_task('rs',,,'ZWS');
__UPG.add_task('alg_par',,,'FKS');
__UPG.add_task('skid_mbn',,,'CTR');
__UPG.add_task('con_rodf',,,'CTR');
__UPG.add_task('formuly',,,'ZWS');
__UPG.add_task('kom_rp',,,'PPL');
__UPG.add_task('slo_typ',,,'PKD');
__UPG.add_task('kh_nasz_kod',,,'ZWS');
__UPG.add_task('stale_once',,,'ZWS');
__UPG.add_task('rmk',,,'FKS');
__UPG.add_task('con_rodf_all',,,'CTR');
__UPG.add_task('CDTAB',,,'ZWS');
__UPG.add_task('cit25',,,'FKS');
__UPG.add_task('sp',,,'FKS');
__UPG.add_task('KH_DOD_SP',,,'ZWS');
__UPG.add_task('par_pokr_graf',,,'PPL');
__UPG.add_task('vat_ue',,,'FKS');
__UPG.add_task('zz_kryt_exp',,,'POC');
__UPG.add_task('move_doks',,,'POC');
__UPG.add_task('zz_kryt_imp',,,'POC');
__UPG.add_task('create_doks',,,'POC');
__UPG.add_task('firma_p',,,'POC');
__UPG.add_task('szk_tren',,,'PSZ');
__UPG.add_task('zo_test',,,'POC');
__UPG.add_task('zo_cel',,,'POC');
__UPG.add_task('zo_celk',,,'POC');
__UPG.add_task('szk_prac',,,'POC');
__UPG.add_task('slo_kod_wer_wyn',,,'PSZ');
__UPG.add_task('zo_osoba_del',,,'POC');
__UPG.add_task('stn_grp',,,'PSZ');
__UPG.add_task('B_PROC',,,'ZPR');
__UPG.add_task('B_PREL',,,'ZPR');
__UPG.add_task('B_KEYREF',,,'ZPR');
__UPG.add_task('MGRP',,,'ZWS');
__UPG.add_task('FIRMA',,,'ZWS');
__UPG.add_task('DOKUM',,,'ZWS');
__UPG.add_task('KH_OSOB',,,'ZWS');
__UPG.add_task('za_nota',,,'PBA');
__UPG.add_task('zo_form',,,'POC');
__UPG.add_task('matkan',,,'ZWS');
__UPG.add_task('new_fld_H',,,'PKD');
__UPG.add_task('new_fld_P',,,'PKD');
__UPG.add_task('new_fld_O',,,'PPL');
__UPG.add_task('kal_wzor',,,'ZWS');
__UPG.add_task('N_NDOK_do_CU',,,'PKD');
__UPG.add_task('control_01',,,'CTR');
__UPG.add_task('zasw_ZUS_do_oddel',,,'ZWS');
__UPG.add_task('gr_sik',,,'FKS');
__UPG.add_task('p_04',,,'PPL');
__UPG.add_task('sch_imp',,,'OBE');
__UPG.add_task('zws_par_fspr',,,'ZWS');
__UPG.add_task('ROKP',,,'FST');
__UPG.add_task('form_plat_pos',,,'ZWS');
__UPG.add_task('zdo_w',,,'ROD');
__UPG.add_task('f_06',,,'FKS');
__UPG.add_task('KART_DOD_ZUS_RIA',,,'ZWS');
__UPG.add_task('edok_atk',,,'OBE');
__UPG.add_task('etypy',,,'ZWS');
__UPG.add_task('sam_el',,,'FST');
__UPG.add_task('uzup_finfo',,,'FST');
__UPG.add_task('httpbank',,,'ZWS');
__UPG.add_task('par6_plat',,,'ZWS');
__UPG.add_task('del_ocr',,,'ZWS');
__UPG.add_task('nbp_kursy_fsp',,,'HBN');
__UPG.add_task('form_plat_typ',,,'ZWS');
__UPG.add_task('abstore_field',,,'ZWS');
__UPG.add_task('parametry_del',,,'ZWS');
__UPG.add_task('formula_spr',,,'FKS');
__UPG.add_task('N',,,'PKD');
__UPG.add_task('OS_N',,,'PKD');
__UPG.add_task('zmiana_R',,,'PPL');
__UPG.add_task('KAL_OPIS',,,'ZWS');
__UPG.add_task('ZA_INST',,,'PBA');
__UPG.add_task('espr_o',,,'FKS');
__UPG.add_task('jpk_url',,,'FKS');
__UPG.add_task('fix_espr',,,'FKS',);
__UPG.add_task('fix_xsd',,,'ZWS');
__UPG.add_task('upd_espr',,,'FKS');
__UPG.add_task('pba_act',,,'PBA');
__UPG.add_task('istdefs_act',,,'FKS');
__UPG.add_task('obywatel',,,'PPK');
__UPG.add_task('start_PPK',,,'PPK',);
__UPG.add_task('akt_1922_05',,,'PPL');
__UPG.add_task('addr_cho',,,'LTR',);
__UPG.add_task('MPKW',,,'ZWS');
__UPG.add_task('fiks_59',,,'FKS');
__UPG.add_task('UD_TYP',,,'ZWS');
__UPG.add_task('UD_SCH',,,'ZWS');
__UPG.add_task('espr2',,,'FKS');
__UPG.add_task('sp_l',,,'FKS');
__UPG.add_task('param_nagrodajubileuszowa',,,'PKD');
__UPG.add_task('rub_52',,,'PPL');
__UPG.add_task('zalacz',,,'PED');
__UPG.add_task('slo_kod_add',,,'ZWS');
__UPG.add_task('stanowiska',,,'PKD');
__UPG.add_task('plugins_init',,,'ZWS');
__UPG.add_task('kst_map',,,'ZWS');
__UPG.add_task('kst_lst',,,'ZWS');
__UPG.add_task('PPK_ZDN',,,'PPK');
__UPG.add_task('PPK_RPL',,,'PPK');
__UPG.add_task('id_sync',,,'ZWS');
__UPG.add_task('xinfo_ter_widok',,,'ZWS');
__UPG.add_task('KH_ODB',,,'ZWS');
__UPG.add_task('zdarzt',,,'ZWS');
__UPG.add_task('upd_par76',,,'ZWS');
__UPG.add_task('tlumaczenia',,,'ZWS');
__UPG.add_task('stalesys_wid',,,'ZWS');
__UPG.add_task('edek_vat',,,'FKS');
__UPG.add_task('blp',,,'FKS');
__UPG.add_task('wersje_raportow',,,'PPK');
__UPG.add_task('tat_w_portal',,,'ZWS');
__UPG.add_task('POCZTA',,,'ZWS');
__UPG.add_task('POCZTAM',,,'ZWS');
__UPG.add_task('M_ATR',,,'ZWS');
__UPG.add_task('dokum_bl',,,'ZWS');
__UPG.add_task('USERSF',,,'ZWS');
__UPG.add_task('jpk_przyczyna_korekty',,,'FKS');
__UPG.add_task('dok_td_fix',,,'FKS');
__UPG.add_task('vat_ue_f',,,'FKS');
__UPG.add_task('akt_2014_12',,,'FKS');
__UPG.add_task('jpk_v7_gtu',,,'FKS');
__UPG.add_task('jpk_v7_stale',,,'ZWS');
__UPG.add_task('jpk_v7_sch',,,'FKS');
__UPG.add_task('jpk_v7_report',,,'FKS');
__UPG.add_task('jpk_v7_sch2',,,'FKS');
__UPG.add_task('kor_cit',,,'ZWS');
__UPG.add_task('zatory',,,'ZWS');
__UPG.add_task('akt_2014_09',,,'PPL');
__UPG.add_task('kal_def_typws',,,'ZWS');
__UPG.add_task('pm_budz',,,'PPL');
__UPG.add_task('users_osoba_unique',,,'ZWS');
__UPG.add_task('B_TIMER',,,'ZPR');
__UPG.add_task('B_HARM',,,'ZPR');
__UPG.add_task('term_plat',,,'LSP');
__UPG.add_task('projekty_etapy',,,'ZWS');
__UPG.add_task('footer',,,'ZWS');
__UPG.add_task('dokum_arch_one',,,'ZWS');
__UPG.add_task('akt_2042_19',,,'PPL');
__UPG.add_task('kart_dod_TARC',,,'PPL');
__UPG.add_task('wnurl',,,'POR');
__UPG.add_task('PKALSYNC',,,'POR');
__UPG.add_task('slo_wn_pap',,,'ZWS');
__UPG.add_task('ZZ_DOKZ_fill',,,'ZWS');
__UPG.add_task('typy_kom_list',,,'ZWS');
__UPG.add_task('SLO_NAZ',,,'ZWS');
__UPG.add_task('lista_par','N',,'PPL');
__UPG.add_task('slimvat_2042_07',,,'FKS');
__UPG.add_task('fiks77',,,'FKS');
__UPG.add_task('jpk_v7_staleus',,,'ZWS');
__UPG.add_task('etypy_w_portal',,,'ZWS');
__UPG.add_task('dokum_typ_k',,,'ZWS');
__UPG.add_task('kontr_upr_add',,,'FKS');
__UPG.add_task('akt_proc',,,'FKS');
__UPG.add_task('fix_por_proc',,,'ZWS');
__UPG.add_task('par_skid',,,'ZWS');
__UPG.add_task('roznice_kursowe',,,'FKS');
__UPG.add_task('add_dekr_kornag',,,'FKS');
__UPG.add_task('upd_r_jpk_sch',,,'FKS');
__UPG.add_task('sch_color_update',,,'ZWS');
__UPG.add_task('NIPY',,,'ZWS');
__UPG.add_task('JM',,,'ZWS');
__UPG.add_task('DOKUMZ',,,'ZWS');
__UPG.add_task('statusy_abstore',,,'LSP');
__UPG.add_task('add_kom_del',,,'ZUI');
__UPG.add_task('edeklaracje_2114_02',,,'PPL');
__UPG.add_task('bipkat',,,'POR');
__UPG.add_task('potr_zal',,,'PPL');
__UPG.add_task('ppsf',,,'PPL');
__UPG.add_task('r_opczyt_add',,,'ZWS');
__UPG.add_task('grupwyn',,,'PPL');
__UPG.add_task('del_par',,,'ZWS');
__UPG.add_task('zastepstwa',,,'PKD');
__UPG.add_task('rez_url',,,'PPL');
__UPG.add_task('PORTALO',,,'ZPR');
__UPG.add_task('add_viudo_sch',,,'FKS');
__UPG.add_task('ecb_crt',,,'HBN');
__UPG.add_task('jpk2_upr',,,'FKS');
__UPG.add_task('proc_add',,,'FKS');
__UPG.add_task('jpkv72_sch',,,'FKS');
__UPG.add_task('dok_proc_upd_tt',,,'FKS');
__UPG.add_task('jpk_fa_upd',,,'FKS');
__UPG.add_task('portal_hr_cfg',,,'ZWS');
__UPG.add_task('portal_cfg',,,'ZWS');
__UPG.add_task('portal_seod',,,'SEO');
__UPG.add_task('odd_pola',,,'PKD');
__UPG.add_task('portalwu_init',,,'POR');
__UPG.add_task('sync_mwa',,,'POR');
__UPG.add_task('p_zdjecie',,,'POR');
__UPG.add_task('new_fld_RU',,,'PPL');
__UPG.add_task('zal_wdp',,,'PPL');
__UPG.add_task('fiks_81',,,'FKS');
__UPG.add_task('zle_dlugi',,,'FKS');
__UPG.add_task('ser_sch_ods',,,'FKS');
__UPG.add_task('jpk_Asi_sch',,,'FKS');
__UPG.add_task('jpk_d1_upd',,,'FKS');
__UPG.add_task('proc_ksef_emag2fiks',,,'ZWS');
__UPG.add_task('cit8_31_akt',,,'FKS');
__UPG.add_task('KH_POW',,,'FKS');
__UPG.add_task('srsr_skr_roz',,,'FST');
__UPG.add_task('etypy_2203_0061',,,'POR');
__UPG.add_task('portal_kart_url_napraw',,,'POR');
__UPG.add_task('p_pz_klasa',,,'POR');
__UPG.add_task('rubryki_placowe_PL_lip',,,'PPL');
__UPG.add_task('wn_podat',,,'PPL');
__UPG.add_task('portal_sync_met',,,'POR');
__UPG.add_task('typ_form',,,'PPL');
__UPG.add_task('for_akc',,,'FKS');
__UPG.add_task('wz_osoba',,,'ZWS');
__UPG.add_task('jpk_fa_4',,,'FKS');
__UPG.add_task('akt_jpk_crok',,,'FKS');
__UPG.add_task('zam_amor',,,'FST');
__UPG.add_task('rozrach_waluta_obca',,,'FKS');
__UPG.add_task('jpk_dl_sym',,,'FKS');
__UPG.add_task('long_sym_rok',,,'FKS');
__UPG.add_task('DOK_upd',,,'FKS');
__UPG.add_task('AN_OPIS',,,'FKS');
__UPG.add_task('OP_AN_OPIS',,,'FKS');
__UPG.add_task('f_kontrolna_poz_pln',,,'FKS');
__UPG.add_task('f_kontrolna_roz_nad',,,'FKS');
__UPG.add_task('jpk_fa_dsp',,,'FKS');
__UPG.add_task('deleg_seod',,,'SEO');
__UPG.add_task('mwa_wnioski',,,'POR');
__UPG.add_task('mwa_faktury',,,'SEO');
__UPG.add_task('TAT_miejsce',,,'ZWS');
__UPG.add_task('ETYPY_P_pack',,,'POR');
__UPG.add_task('fix_edek_us',,,'ZWS');
__UPG.add_task('PORTALL',,'T','POR',,,,1);
__UPG.add_task('MJM',,,'ZWS');
__UPG.add_task('URZ_TAG',,,'ZWS');
__UPG.add_task('portal_bnftt_nag_id',,,'POR');
__UPG.add_task('sync_def_ud_def',,,'POR');
__UPG.add_task('fix_wn_zaliczka',,,'POR');
__UPG.add_task('porsloit_clear',,,'POR');
__UPG.add_task('wn_praktyka',,,'POR');
__UPG.add_task('map_tabs',,,'POR');
__UPG.add_task('portal_wn_urlop',,,'POR');
__UPG.add_task('SD_XSLO',,,'ZWS');
__UPG.add_task('sync_mwa_ksef',,,'ZBL');
__UPG.add_task('fiks_86',,,'FKS');
__UPG.add_task('fiks_87',,,'FKS');
__UPG.add_task('init_b_perm',,,'ZWS');
__UPG.add_task('portalk',,,'SEO');
__UPG.add_task('fix_defw_uid',,,'FKS');
__UPG.add_task('f_zatr',,,'PKD');

:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('stale_many',,'N','ZWS',,,,1);
__UPG.add_task('f_zatra',,'N','PKD',,,,1);
__UPG.add_task('typy',,'N','ZWS',,,,1);
__UPG.add_task('u2uwagi',,'N','LSP',,,,1);
__UPG.add_task('importTypDokMag',,'N','ZWS',,,,1);
__UPG.add_task('sam2poj',,'N','ZWS',,,,1);
__UPG.add_task('updateFAKSO',,'N','ZWS',,,,1);
__UPG.add_task('plusREZ',,'N','LMG',,,,1);
__UPG.add_task('edi_i_fill',,'N','ZWS',,,,1);
__UPG.add_task('actOperMob',,'N','LMG',,,,1);
__UPG.add_task('actZlecFakt',,'N','LSP',,,,1);
__UPG.add_task('KHODB2position',,'N','LSP',,,,1);
__UPG.add_task('updateMDOST',,'N','ZWS',,,,1);
__UPG.add_task('plat_zkn',,'N','LSP',,,,1);
__UPG.add_task('faks_odb',,'N','LSP',,,,1);
__UPG.add_task('actualMG',,'N','ZWS',,,,1);
__UPG.add_task('StanPROD',,'N','LMG',,,,1);
__UPG.add_task('rp7_os',,'N','PPL',,,,1);
__UPG.add_task('rp7_kw',,'N','PPL',,,,1);
__UPG.add_task('rp7_blok',,'N','PPL',,,,1);
__UPG.add_task('sch_xd',,'N','ZWS',,,,1);
__UPG.add_task('okr_obsg',,'N','FST',,,,1);
__UPG.add_task('ROK_F',,'N','ZWS',,,,1);
__UPG.add_task('zus_one',,'N','HBN',,,,1);
__UPG.add_task('PB_SP',,'N','HBN',,,,1);
__UPG.add_task('PW_SP',,'N','HBN',,,,1);
__UPG.add_task('updateKAL_BUFF',,'N','PRC',,,,1);
__UPG.add_task('R_ZMIANY',,'N','ZWS',,,,1);
__UPG.add_task('zf_rpi',,'N','POC',,,,1);
__UPG.add_task('zf_wid_clean',,'N','POC',,,,1);
__UPG.add_task('R_CZYT',,'N','ZWS',,,,1);
__UPG.add_task('R_ODN',,'N','PRC',,,,1);
__UPG.add_task('r_opczyt',,'N','PRC',,,,1);
__UPG.add_task('ZLBR',,'N','ZWS',,,,1);
__UPG.add_task('PL_OPER',,'N','TPP',,,,1);
__UPG.add_task('PX_POZ',,'N','TPP',,,,1);
__UPG.add_task('ZTP',,'N','ZWS',,,,1);
__UPG.add_task('PX_WYK',,'N','TPP',,,,1);
__UPG.add_task('ZL',,'N','TTE',,,,1);
__UPG.add_task('ZL1',,'N','TTE',,,,1);
__UPG.add_task('ZLZAM',,'N','TTE',,,,1);
__UPG.add_task('GROP',,'N','TTE',,,,1);
__UPG.add_task('PROD_REJ',,'N','TTE',,,,1);
__UPG.add_task('ZLGD',,'N','TTE',,,,1);
__UPG.add_task('PLIKIDOC',,'N','TTE',,,,1);
__UPG.add_task('dokumTOPER2ZGP',,'N','TTE',,,,1);
__UPG.add_task('ZGP',,'N','TTE',,,,1);
__UPG.add_task('BRAKI_GD',,'N','TTE',,,,1);
__UPG.add_task('TOPER',,'N','TTE',,,,1);
__UPG.add_task('FO_DEF',,'N','ZWS',,,,1);
__UPG.add_task('FO_USR',,'N','ZWS',,,,1);
__UPG.add_task('UAT',,'N','TTE',,,,1);
__UPG.add_task('DATY',,'N','ZWS',,,,1);
__UPG.add_task('RD_OKR_zalacz',,'N','PKD',,,,1);
__UPG.add_task('RD_slowniki',,'N','PKD',,,,1);
__UPG.add_task('rap_zew',,'N','PKD',,,,1);
__UPG.add_task('fo_100227',,'N','ZWS',,,,1);
__UPG.add_task('rodzaje_oddelegowania',,'N','PKD',1,,,1);
__UPG.add_task('A_OKRD',,'N','PRC',,,,1);
__UPG.add_task('PL_RES',,'N','ZWS',,,,1);
__UPG.add_task('PX_CUP',,'N','TPP',,,,1);
__UPG.add_task('convEANU',,'N','LMG',,,,1);
__UPG.add_task('WARP100',,'N','FST',,,,1);
__UPG.add_task('odd_dok_update',,'N','FST',,,,1);
__UPG.add_task('zdo_f',,'N','ROD',,,,1);
__UPG.add_task('potwZAM',,'N','LZK',,,,1);
__UPG.add_task('umowy_miejskie',,'N','LUM',,,,1);
__UPG.add_task('HN',,'N','LUM',,,,1);
__UPG.add_task('HARMON',,'N','LUM',,,,1);
__UPG.add_task('HGEN',,'N','LUM',,,,1);
__UPG.add_task('UM',,'N','LUM',,,,1);
__UPG.add_task('UP',,'N','LUM',,,,1);
__UPG.add_task('UPM',,'N','LUM',,,,1);
__UPG.add_task('UPMPOJ',,'N','LUM',,,,1);
__UPG.add_task('ZLP',,'N','LUM',,,,1);
__UPG.add_task('POS',,'N','LUM',,,,1);
__UPG.add_task('OKR',,'N','ZWS',,,,1);
__UPG.add_task('FPACZ',,'N','LUM',,,,1);
__UPG.add_task('FAPOW',,'N','LSP',,,,1);
__UPG.add_task('REM_KAT',,'N','ZWS',,,,1);
__UPG.add_task('REM_ZAS',,'N','ZWS',,,,1);
__UPG.add_task('REM_ZGL',,'N','TRE',,,,1);
__UPG.add_task('REM_GWAR',,'N','TRE',,,,1);
__UPG.add_task('REM_CYKL',,'N','TRE',,,,1);
__UPG.add_task('zws_par_pstn',,'N','ZWS',,,,1);
__UPG.add_task('SZK_WZO',,'N','PBA',,,,1);
__UPG.add_task('KPO',,'N','LUO',,,,1);
__UPG.add_task('PERM_UPD',,'N','ZWS',,,,1);
__UPG.add_task('ppk_role',,'N','ZWS',,,,1);
__UPG.add_task('pd_k',,'N','LZK',,,,1);
__UPG.add_task('spal2RODZPAL',,'N','ZWS',,,,1);
__UPG.add_task('rem_zas',,'N','TRE',,,,1);
__UPG.add_task('sp_g',,'N','FKS',,,,1);
__UPG.add_task('premie_okresowe',,'N','PPL',,,,1);
__UPG.add_task('zaoh2',,'N','PED',,,,1);
__UPG.add_task('zaots',,'N','PED',,,,1);
__UPG.add_task('PPK_KWN',,'N','PPK',,,,1);
__UPG.add_task('dokum_kh_osob',,'N','ZWS',,,,1);
__UPG.add_task('KR2AR',,'N','TTE',,,,1);
__UPG.add_task('SYNC_DEF',,'N','ZWS',,,,1);
__UPG.add_task('logo',,'N','ZWS',,,,1);
__UPG.add_task('PL_WZOR',,'N','TPP',,,,1);
__UPG.add_task('PL_OPIS',,'N','TPP',,,,1);
__UPG.add_task('PL_WZORU',,'N','TPP',,,,1);
__UPG.add_task('rok_f_akt_roz',,'N','ZWS',,,,1);
__UPG.add_task('tzl_ppl_ppk',,'N','HBN',,,,1);
__UPG.add_task('UMLOJN_zak_kon',,'N','PKD',,,,1);
__UPG.add_task('wnioski_komunikaty',,'N','PPK',,,,1);
__UPG.add_task('stalesys_PPK',,'N','PPK',,,,1);
__UPG.add_task('fix_dokzrodl',,'N','FKS',,,,1);
__UPG.add_task('DK_C',,'N','LMG',,,,0);
__UPG.add_task('actKoszt',,'N','LZK',,,,1);
__UPG.add_task('dokump_rodz',,'N','ZWS',,,,1);
__UPG.add_task('TMAT',,'N','TTE',,,,1);
__UPG.add_task('TCHMAT',,'N','TTE',,,,1);
__UPG.add_task('kparam',,'N','ZWS',,,,0);
__UPG.add_task('dk_mar_nar',,'N','LMG',,,,1);
__UPG.add_task('zal_ucp',,'N','ZWS',,,,1);
__UPG.add_task('ppk_06',,'N','PPK',,,,1);
__UPG.add_task('status_pracownika',,'N','PPL',,,,1);
__UPG.add_task('dok_jpk_upgr',,'N','FKS',,,,1);
__UPG.add_task('SM',,'N','LMG',,,,1);
__UPG.add_task('ZK_RN',,'N','LSP',,,,1);
__UPG.add_task('ZD_RN',,'N','LZK',,,,1);
__UPG.add_task('PX_KONT',,'N','TPP',,,,1);
__UPG.add_task('GROPS',,'N','TPP',,,,1);
__UPG.add_task('samk_time',,'N','LTR',,,,1);
__UPG.add_task('dokum_arch_firma',,'N','ZWS',,,,1);
__UPG.add_task('porslo',,'N','POR',,,,1);
__UPG.add_task('KTRWP',,'N','CTR',,,,1);
__UPG.add_task('srst_korekta',,'N','FST',,,,1);
__UPG.add_task('REJ_MAT',,'N','TTE',,,,1);
__UPG.add_task('PX_GRP',,'N','TTE',,,,1);
__UPG.add_task('vat_ps_sha',,'N','FKS',,,,1);
__UPG.add_task('build_etypy_p',,'N','POR',,,,1);
__UPG.add_task('OFE',,'N','LSP',,,,1);
__UPG.add_task('STATS',,'N','LMG',,,,1);
__UPG.add_task('usr_upr_sts',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_stz',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_zam',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_zaw',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_trrodz',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_tars',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_tard',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_mkas',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_pelne',,'N','ZWS',,,,1);
__UPG.add_task('ZPARN',,'N','TTE',,,,1);
__UPG.add_task('cenniki_akt',,'N','LSP',,,,1);
__UPG.add_task('OZ',,'N','LZK',,,,1);
__UPG.add_task('KEOP',,'N','LUO',,,,1);
__UPG.add_task('WYDRUKIL',,'N','ZWS',,,,1);
__UPG.add_task('fakskh',,'N','LSP',,,,1);
__UPG.add_task('porsloit',,'N','POR',,,,1);
__UPG.add_task('GRUPPRAC',,'N','ZWS',,,,1);
__UPG.add_task('zaomeda',,'N','PED',,,,1);
__UPG.add_task('OS_ZWSLO',,'N','PPL',,,,1);
__UPG.add_task('OS_ZWPOD',,'N','PPL',,,,1);
__UPG.add_task('vat_poz_2137_11',,'N','PPL',,,,1);
__UPG.add_task('kryterium_listy_plac',,'N','PPL',,,,1);
__UPG.add_task('KAL_BUFF_CZYTNIK',,'N','PRC',,,,1);
__UPG.add_task('pkd_kod_4',,'T','PKD',,,,1);
__UPG.add_task('weryfikacja_ZP_PL_lip',,'N','PPL',,,,1);
__UPG.add_task('zaomedadtcreate',,'N','PED',,,,1);
__UPG.add_task('P_NPOD',,'N','PPL',,,,1);
__UPG.add_task('tar_perm_upd',,'N','ZWS',,,,1);
__UPG.add_task('parametr_208',,'N','FST',,,,1);
__UPG.add_task('dokum_ob_status',,'N','OBG',,,,1);
__UPG.add_task('init_send','T','N','POR',,,,1);
__UPG.add_task('XINFO','T','N','POR',,,,1);
__UPG.add_task('SYNCFMWA','T','N','ZWS',,,,1);
__UPG.add_task('uzupIDpal',,'N','LMG',,,,1);
__UPG.add_task('ilmg2ZAM',,'N','LZK',,,,1);
__UPG.add_task('PAL',,'N','LMG',,,,1);
__UPG.add_task('PX_VER',,'N','TPP',,,,1);
__UPG.add_task('PX_OBJ',,'N','TPP',,,,1);
__UPG.add_task('PX_TEX4TKTL',,'N','TPP',,,,1);
__UPG.add_task('noty_ods',,'N','FKS',,,,1);
__UPG.add_task('BADP',,'N','TTE',,,,1);
__UPG.add_task('BADSEP',,'N','TTE',,,,1);
__UPG.add_task('BADMSEP',,'N','TTE',,,,1);
__UPG.add_task('update_ZLIM',,'N','TTE',,,,1);
__UPG.add_task('REZ_update',,'N','TTE',,,,1);
__UPG.add_task('TP2TP_KOR_ZWR',,'N','ZWS',,,,1);
__UPG.add_task('porslo_reinit',,'N','POR',,,,1);
__UPG.add_task('sch_form_portal_nwu_access',,'N','POR',,,,1);
__UPG.add_task('pola_PL2023',,'N','PPL',,,,1);
__UPG.add_task('ISTDEF',,'N','ZWS',,,,1);
__UPG.add_task('mod_dane_PL001',,'N','PPL',,,,1);
__UPG.add_task('stalesys_PPK_VI',,'N','PPK');
__UPG.add_task('PORTALU',,'N','ZWS',,,,1);
__UPG.add_task('OPAK_N',,'N','LMG',,,,1);
__UPG.add_task('OPAK_ZWR',,'N','LMG',,,,1);
__UPG.add_task('users_upr_dane_napraw',,'N','ZWS',,,,1);
__UPG.add_task('dokum_to_dokumz',,'N','FKS',,,,1);
__UPG.add_task('rozlvat_dop',,'N','FKS',,,,1);
__UPG.add_task('przenies_add',,'N','FKS',,,,1);
__UPG.add_task('warlog_lopis',,'N','FKS',,,,1);
__UPG.add_task('rubryki_placowe_PZD01',,'N','PPL');
__UPG.add_task('fap2dk',,'N','LSP',,,,1);
__UPG.add_task('tlum_popr_wf',,'N','ZWS',,,,1);
__UPG.add_task('par_wyd',,'N','FKS');

:: Zadania globalne, które muszą być wykonane po zadaniach dla firm
__UPG.add_task('porslo_global',,,'POR');

:: Zadania manualne
__UPG.add_task('form_wal','N','N','ZWS',0,'N',,1);
__UPG.add_task('zaoh','N','N','PKD',0,'N',,1);
__UPG.add_task('rodo','N','T','ZWS',1,'N',,1);
__UPG.add_task('users_fz','N','N','ZWS',0,'N',,0);
__UPG.add_task('abs_upd','N',,'ZWS');
__UPG.add_task('edi_imp','N','N','ZWS',0,'N',,1);
__UPG.add_task('mech_imp','N','N','ZWS',0,'N',,1);
__UPG.add_task('czysc_osoba','N','T','ZWS',,'N',,1);
__UPG.add_task('usp','N','N','PKD',,'N',,1);
__UPG.add_task('KPO_manual','N','N','LUO',,'N',,1);
__UPG.add_task('ud_upr_i','N','T','ZWS',1,'N',,1);
__UPG.add_task('akc_sch','N','N','FKS',0,'N',,1);
__UPG.add_task('nazwy_typy','N','N','ZWS',0,'N',,1);
__UPG.add_task('fks_form','N','N','FKS',0,'N',,1);
__UPG.add_task('uzu_slo','N','N','ZWS',0,'N',,1);
__UPG.add_task('fo_xpertis','N','N','ZWS',,'N',,1);
__UPG.add_task('h2n_rap','N','N','PKD',0,'N',,1);
__UPG.add_task('akt_1922_08','N','T','ZWS',1,,,1);
__UPG.add_task('progi_nagrodajubileuszowa','N','N','PPL',1,'N',,1);
__UPG.add_task('usp_staz','N','N','PKD',,'N',,1);
__UPG.add_task('trans_sp','N','N','ZWS',,'N',,1,1);
__UPG.add_task('stalesys_PPK_1942_08','N','N','PPK',,'N',,1);
__UPG.add_task('szablony','N',,'ZWS',,,,1);
__UPG.add_task('jpk_fa_3','N',,'FKS',,,,1);
__UPG.add_task('kal_def_dataw','N','N','ZWS',2,,,1);
__UPG.add_task('ZS_DEF','N','N','PKD');
__UPG.add_task('PARAMS','N','T','ZWS',,,,1);
__UPG.add_task('ctrlParFisk','N','N','LSP',,'N',,1);
__UPG.add_task('rozszerzony_filtr','N',,'ZWS',,,,1);
__UPG.add_task('inwentaryzacja_lt','N','N','FST',,,,1);
__UPG.add_task('dks_edi_ufd_upd','N','N','ZBL',,,,1);
__UPG.add_task('cit8_30','N','N','FKS',,,,1);
__UPG.add_task('obg_edi_ufd_upd','N','N','ZBL',,,,1);
__UPG.add_task('wnioski_portal','N',,'POR',,,,1);
__UPG.add_task('RU_RKU','N',,'PPL',,,,1);
__UPG.add_task('cit8_31','N','N','FKS',,,,1);
__UPG.add_task('cit8_32','N','N','FKS',,,,1);
__UPG.add_task('tat','N',,'ZWS',,,,1);
__UPG.add_task('pakiet_mobilnosci','N','N','PPL',,,,1);
__UPG.add_task('paperless_dane_wzorcowe','N','N','PEP',1,,,1);
__UPG.add_task('nieobecnosci_2022','N','N','PKD',,,,1);
__UPG.add_task('ppk_zc_wn_zus','N','N','PPL',,'N',,1);
__UPG.add_task('pomoc_AK_10','N','N','PKD',3,'N',,1);
__UPG.add_task('kody_zus_import','N','N','ZWS',,'N',,1);
__UPG.add_task('dokument_zmiany_metody','N','N','FST',,,,1);
__UPG.add_task('tam_bil','N','N','FST',,,,1);
__UPG.add_task('h_overlap','N','T','PKD',,,,1);
__UPG.add_task('wycofania_PL2023','N','N','PPL',,,,1);
__UPG.add_task('wn_rok_PL001','N','N','PPL',,,,1);
__UPG.add_task('imp_wzor','N','T','POR',,,,1);
__UPG.add_task('sync_mwa_portal','N','T','POR',,,,1);
__UPG.add_task('folp_std','N','N','PPL',,,,1);
__UPG.add_task('base_id','N',,'ZWS',,,,1);
~~


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::  ORG: \stalesys_ini/upgrade_1902.fml
::  ORG: \stalesys_ini/upgrade_2325.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
KST_DEF.cntx_psh();
KST_DEF.clear();
KST_MAP.cntx_psh();
KST_MAP.index('KST_MAP');
KST_MAP.prefix('XINFO','XINFO');
:: zmień domyślne wartości
{? KST_MAP.find_key('BASE_ID',) & (KST_MAP.KST_DEF().HISTORIA<>'T' | KST_DEF.WSPOLNA<>'T')
|| KST_DEF.HISTORIA:='T';
   KST_DEF.WSPOLNA:='T';
   KST_DEF.put()
?};
KST_MAP.cntx_pop();
KST_DEF.cntx_pop();
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::  ORG: \stalesys_ini_desc/upgrade_1902.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::  ORG: \upgrade_act/upgrade_1714.fml
::       \upgrade_act/upgrade_1728.fml
::       \upgrade_act/upgrade_1742.fml
::       \upgrade_act/upgrade_1802.fml
::       \upgrade_act/upgrade_1822.fml
::       \upgrade_act/upgrade_1842.fml
::       \upgrade_act/upgrade_1902.fml
::       \upgrade_act/upgrade_1942_08.fml
::       \upgrade_act/upgrade_2137_01.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.PKD.PKW.PPL.TTE.WYP.ZWS.FST.FKS.KON.OBE.OBG','.');
_dom:=spli_str('BIQ.'
               'FKS.FST.'
               'HBN.'
               'KKH.KON.'
               'LMG.LMO.LSP.LSS.LTR.LUM.LUO.LZK.'
               'OBE.OBG.'
               'PBA.PED.PEP.PKD.PKW.POC.POR.PPK.PPL.PRC.PSZ.'
               'ROD.'
               'SEO.'
               'TPP.TRE.TTE.'
               'WYP.'
               'ZPR.ZUI.ZWS.ZBL','.');
_len:=obj_len(_dom);
FUN.prg_start(_len+2,'Aktualizacja czynności.'@,,,1);

:: Uzupełnienie domen (zawsze)
FUN.prg_next();
exec('add_domain','#b_action');
__UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
FUN.prg_next();
exec('fill_tab','#b_type',0);
__UPG.msg('Zaktualizowano typy danych.');

{! _i.._len
|! FUN.prg_next();
   _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
   {? _domain<>null()
   || exec('import_needed_dom','#b_design',_domain,0);
      __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
   ?}
!};
FUN.prg_stop();
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Aktualizacja schematów kolorów
::  ORG: \upgrade_act/upgrade_1714.fml
::       \upgrade_act/upgrade_1728.fml
::       \upgrade_act/upgrade_1742.fml
::       \upgrade_act/upgrade_1802.fml
::       \upgrade_act/upgrade_1822.fml
::       \upgrade_act/upgrade_1842.fml
::       \upgrade_act/upgrade_1902.fml
::       \upgrade_act/upgrade_1942_08.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::  ORG: \paramdef_act/upgrade_1714.fml
::  ORG: \upgrade_act/upgrade_1728.fml
::  ORG: \upgrade_act/upgrade_1742.fml
::  ORG: \upgrade_act/upgrade_1802.fml
::  ORG: \upgrade_act/upgrade_1822.fml
::  ORG: \funpar/upgrade_1842.fml
::  ORG: \funpar/upgrade_1902.fml
::  ORG: \funpar/upgrade_2042.fml
::  ORG: \funpar/upgrade_2137.fml
::  ORG: \funpar/upgrade_2226.fml
::  ORG: \funpar/upgrade_2325.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? exec('aktualizuj','funpar',1)
|| FP_DEF.cntx_psh();
   FP_DEF.index('ID');
   FP_DEF.prefix('T','ZWS_PAR_PPNW',);
   _del:={? FP_DEF.first() || FP_DEF.del(,1) || 1 ?};
   FP_DEF.prefix('T','ZWS_PAR_PORCAS',);
   _del*={? FP_DEF.first() || FP_DEF.del(,1) || 1 ?};
   FP_DEF.cntx_pop();
   {? _del>0
   || __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
   || __UPG.msg('Usunięcie czynności parametryzacyjnej ZWS_PAR_PPNW nie powiodło się.');
      _ret:=-1
   ?}
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.');
   _ret:=-1
?};
_ret


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\f_zatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja definicji form współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('dod_f_zatr','f_zatr');
__UPG.msg('Zaktualizowano definicje form współpracy.');
1


\f_zatr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja definicji form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji form współpracy'


\f_zatra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja czynności dla form współpracy.
::   WE:
::   WY:
::  ORG: \f_zatra/upgrade_1728.fml
::  ORG: \f_zatra/upgrade_1742.fml
::  ORG: \f_zatra/upgrade_1802.fml
::  ORG: \f_zatra/upgrade_1842.fml
::  ORG: \f_zatra/upgrade_1902.fml
::  ORG: \f_zatra/upgrade_1942.fml
::  ORG: \f_zatra_add/upgrade_2014.fml
::  ORG: \f_zatra_add/upgrade_2114.fml
::  ORG: \f_zatra_add/upgrade_2137.fml
::  ORG: \f_zatra_add/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
exec('f_zatra_init','f_zatr',1);
__UPG.msg('Zaktualizowano czynności dla form współpracy');
1


\f_zatra_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja czynności dla form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czynności dla form współpracy'


\edeklaracje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Dodanie nowych e-deklaracji
::  ORG: \edeklaracje/upgrade_1714.fml
::  ORG: \edeklaracje/upgrade_1842.fml
::  ORG: \edeklaracje_2019/upgrade_1902.fml
::  ORG: \edeklaracje_2020/upgrade_1942_04.fml
::  ORG: \edeklaracje_2042_14/upgrade_2042.fml
::  ORG: \edeklaracje_2042_06/upgrade_2042_06.fml
::  ORG: \edeklaracje_2042_08/upgrade_2042_08.fml
::  ORG: \edeklaracje_2042_08_pop/upgrade_2114.fml
::  ORG: \edeklaracje_2114_02/upgrade_2114_02.fml
::  ORG: \edeklaracje_pop/upgrade_2137.fml
::  ORG: \edeklaracje_2137_11/upgrade_2137_11.fml
::  ORG: \PIT01_2226/upgrade_2226.fml
::  ORG: \PIT_2022_akt_2226_11/upgrade_2226.fml
::  ORG: \PIT_4R_akt_2226_19/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
TrFml:=1;
E_DEK.prefix();
E_DEK.for_each("
   {? E_DEK.SYSTEM='FIKS'
   || E_DEK.SYSTEM:='FKS'
   |? E_DEK.SYSTEM='KALI'
   || E_DEK.SYSTEM:='PPL'
   ?};
   E_DEK.put()
");
ISTDEF.prefix();
ISTDEF.for_each("
   {? ISTDEF.SYSTEM='FIKS'
   || ISTDEF.SYSTEM:='FKS';
      ISTDEF.put()
   |? ISTDEF.SYSTEM='KALI'
   || ISTDEF.SYSTEM:='PPL';
      ISTDEF.put()
   ?}
");
VAT_VER.prefix();
VAT_VER.for_each("
   {? VAT_VER.SYSTEM='FIKS'
   || VAT_VER.SYSTEM:='FKS'
   |? VAT_VER.SYSTEM='KALI'
   || VAT_VER.SYSTEM:='PPL'
   ?};
   VAT_VER.put(1)
");
VAR_DEL.delete('TrFml');
_dod_ver:="
   VAT_VER.index('VER_OD'); VAT_VER.prefix(_a,_c,_c,_b,_d);
   _jest:=VAT_VER.first();
   {? ~_jest
   || _old:={? _a='FKS' || 'FIKS' |? _a='PPL' || 'KALI' || '' ?};
      VAT_VER.prefix(_old,_c,_c,_b,_d);
      _jest:=VAT_VER.first()
   ?};
   {? _jest
   || _add:=0
   || _add:=1;
      VAT_VER.blank();
      VAT_VER.SYSTEM:=_a;
      VAT_VER.OD:=_b;
      VAT_VER.NAZWA:=_c;
      VAT_VER.NR:=_d
   ?};
   {? _<6 || _f:='' ?};
   {? _<5 || _e:=0 ?};
   _put:=0;
   {? VAT_VER.NRF<>_e || VAT_VER.NRF:=_e; _put+=1 ?};
   {? VAT_VER.WIERSZ<>_f || VAT_VER.WIERSZ:=_f; _put+=1 ?};
   VAT_VER.prefix();
   {? _add || VAT_VER.add(1) |? _put || VAT_VER.put(1) ?}
";

_dod_ver('FKS',date(2017,01,11),'POD',4);
_dod_ver('FKS',date(2017,01,11),'PODK',4);
_dod_ver('FKS',date(2017,01,11),'VAT27',2);

_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
_dod_ver('PPL',date(2018,1,1),'PIT4R',7,7,',7,8,');
_dod_ver('PPL',date(2018,12,6),'PIT4R',8,8,',3,4,');
_dod_ver('PPL',date(2018,1,1),'PIT8AR',8,7,',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,');
_dod_ver('PPL',date(2018,1,1),'PIT11',9,24,',2,3,4,5,6,7,8,9,10,');
_dod_ver('PPL',date(2018,1,1),'PIT8C',9,9);
_dod_ver('PPL',date(2018,1,1),'IFT1',14,14,',1,2,3,4,5,6,7,8,9,10,');
_dod_ver('PPL',date(2018,1,1),'IFT1R',14,14,',1,2,3,4,5,6,7,8,9,10,');
:: z upgrade_1942_04.fml :
_dod_ver('PPL',date(2019,1,1),'PIT4R',9,9,',2,3,4,6,7,10,');
_dod_ver('PPL',date(2019,1,1),'PIT8AR',9,8,',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,');
_dod_ver('PPL',date(2019,1,1),'PIT11',10,25,',3,4,5,7,8,');
_dod_ver('PPL',date(2019,1,1),'PIT8C',10,10);
_dod_ver('PPL',date(2019,1,1),'IFT1',15,15,',1,2,3,4,5,6,7,8,9,10,');
_dod_ver('PPL',date(2019,1,1),'IFT1R',15,15,',1,2,3,4,5,6,7,8,9,10,');
:: z upgrade_2042.fml
_dod_ver('PPL',date(2021,1,1),'IFT1',16,16,',1,2,3,4,5,6,7,8,9,10,11,12,');
_dod_ver('PPL',date(2021,1,1),'IFT1R',16,16,',1,2,3,4,5,6,7,8,9,10,11,12,');
:: z upgrade_2042_06.fml
_dod_ver('PPL',date(2020,11,13),'PIT4R',10,10,',2,3,4,6,7,9,');
_wakt:=',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,';
_dod_ver('PPL',date(2020,11,13),'PIT8AR',10,9,_wakt);
_dod_ver('PPL',date(2020,11,13),'PIT11',11,26,',3,4,5,7,8,');
::  z upgrade_2042_08.fml
_dod_ver('PPL',date(2021,1,1),'PIT4R',11,11,',2,3,4,6,7,9,');
_wakt:=',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,';
_dod_ver('PPL',date(2021,1,1),'PIT8AR',11,10,_wakt);
:: z upgrade_2114_02.fml
_dod_ver('PPL',date(2021,3,4),'PIT11',12,27,',1,3,4,5,7,8,10,');
_dod_ver('PPL',date(2021,4,2),'PIT4R',12,12,',2,3,4,6,7,9,');
_wakt:=',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,';
_dod_ver('PPL',date(2021,4,2),'PIT8AR',12,11,_wakt);
:: z upgrade_2137_11.fml
_dod_ver('PPL',date(2022,1,1),'PIT11',13,28,',1,4,5,6,9,10,12,');
_wakt:=',1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,';
_dod_ver('PPL',date(2022,1,1),'PIT8AR',13,12,_wakt);
:: z upgrade_2226.fml
_dod_ver('PPL',date(2022,11,9),'PIT11',14,29,',1,4,5,6,9,10,15,');
_dod_ver('PPL',date(2022,8,10),'PIT8C',11,11);

::import poprawnych struktur e-deklaracji
exec('edek_stru','edeklar','PIT4R',8,'pit-4r8_v1.dfg');
exec('edek_stru','edeklar','PIT8AR',8,'pit-8ar8_v1.dfg');
exec('edek_stru','edeklar','PIT11',9,'pit-11_24_v1.dfg');
exec('edek_stru','edeklar','PIT8C',9,'pit-8c-9v1.dfg');
exec('edek_stru','edeklar','IFT1',14,'ift-1-14_v1.dfg');
exec('edek_stru','edeklar','IFT1R',14,'ift-1r-14_v1.dfg');
:: z upgrade_1942_04.fml :
exec('edek_stru','edeklar','PIT4R',9,'pit-4r9_v1.dfg');
exec('edek_stru','edeklar','PIT8AR',9,'pit-8ar9_v1.dfg');
exec('edek_stru_del','edeklar','PIT11',10);
exec('edek_stru','edeklar','PIT11',10,'pit-11_25_v1.dfg');
exec('edek_stru','edeklar','PIT8C',10,'pit-8c-10v1.dfg');
exec('edek_stru','edeklar','IFT1',15,'ift-1-15_v1.dfg');
exec('edek_stru','edeklar','IFT1R',15,'ift-1r-15_v1.dfg');
:: z \edeklaracje_2020_07/upgrade_2014.fml
exec('edek_stru','edeklar','PIT4R',9,'pit-4r9_v1-2.dfg',date(2020,8,19));
exec('edek_stru','edeklar','PIT11',10,'pit-11_25_v1-1.dfg',date(2020,7,10));
exec('edek_stru','edeklar','PIT8C',10,'pit-8c-10v1-1.dfg',date(2020,7,8));
:: z upgrade_2042.fml+upgrade_2137.fml
exec('edek_stru_del','edeklar','IFT1',16);
exec('edek_stru_del','edeklar','IFT1R',16);
exec('edek_stru','edeklar','IFT1',16,'ift-1-16_v1.dfg');
exec('edek_stru','edeklar','IFT1R',16,'ift-1r-16_v1.dfg');
:: z upgrade_2042_06.fml
exec('edek_stru','edeklar','PIT4R',10,'pit-4r10_v1.dfg');
exec('edek_stru','edeklar','PIT8AR',10,'pit-8ar10_v1.dfg');
exec('edek_stru_del','edeklar','PIT11',11);
exec('edek_stru','edeklar','PIT11',11,'pit-11_26_v1.dfg');
::  z upgrade_2042_08.fml + upgrade_2114.fml
exec('edek_stru_del','edeklar','PIT4R',11);
exec('edek_stru','edeklar','PIT4R',11,'pit-4r11_v1.dfg');
exec('edek_stru_del','edeklar','PIT8AR',11);
exec('edek_stru','edeklar','PIT8AR',11,'pit-8ar11_v1.dfg');
:: z upgrade_2114_02.fml
exec('edek_stru','edeklar','PIT11',12,'pit-11_27_v1.dfg');
exec('edek_stru','edeklar','PIT4R',12,'pit-4r12_v1.dfg');
exec('edek_stru','edeklar','PIT8AR',12,'pit-8ar12_v1.dfg');
:: z upgrade_2137_11.fml
exec('edek_stru','edeklar','PIT11',13,'pit-11_28_v1.dfg');
exec('edek_stru','edeklar','PIT8AR',13,'pit-8ar13_v1.dfg');
exec('edek_stru','edeklar','PIT4R',12,'pit-4r12_v1-2.dfg',date(2022,4,15));
exec('edek_stru','edeklar','IFT1',16,'ift-1-16_v1-2.dfg',date(2022,1,1));
exec('edek_stru','edeklar','IFT1R',16,'ift-1r-16_v1-2.dfg',date(2022,1,1));
:: z upgrade_2226.fml
exec('edek_stru','edeklar','PIT8AR',13,'pit-8ar13_v1-2.dfg',date(2022,12,12));
exec('edek_stru','edeklar','PIT11',14,'pit-11_29_v1-1.dfg');
exec('edek_stru','edeklar','PIT8C',11,'pit-8c-11v1-0.dfg');
exec('edek_stru','edeklar','PIT4R',12,'pit-4r12_v1-3.dfg',date(2022,12,12));

BPMN.SYM_DOM:=_dom;
__UPG.msg('Wykonano uaktualnienie schematów e-deklaracji.');
1


\edeklaracje_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Dodanie nowych e-deklaracji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie schematów e-deklaracji'


\jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: kod - PR/WRT/XP/12.41/1703/0043
::       Agregacja pozycji VAT na potrzeby JPK_VAT
::  OLD: \fiks_24/napraw_f.fml
::       \jpk/upgrade_1714.fml
::       \jpk_01/upgrade_1714.fml
::       \jpk_01/upgrade_1728.fml
::       \jpk/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','VAT',null,date(2017,1,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref,'R',);
   {? ISTDEFS.first()
   || {!
      |? {? ISTDEFS.REGULY='$VAT_PS.ref()+$VAT_PS.crc()'
         || ISTDEFS.REGULY:='VAT_PS.get();$VAT_PS.ref()+$VAT_PS.crc()';
            ISTDEFS.put()
         ?};
         ISTDEFS.next()
      !}
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'DowodSprzedazy');
   {? ISTDEFS.first()
   || ISTDEFS.REGULY:='VAT_PS.SYM_ZEW';
      ISTDEFS.TYPFLD:='STRING[35]';
      ISTDEFS.put()
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'DowodZakupu');
   {? ISTDEFS.first()
   || ISTDEFS.REGULY:='VAT_PS.SYM_ZEW';
      ISTDEFS.TYPFLD:='STRING[35]';
      ISTDEFS.put()
   ?}
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
exec('czytaj','#stalesys',,XINFO,'HTTPPATH');
SKID.ISTDEF:='J';
BPMN.SYM_DOM:='FKS';
ISTDEF.cntx_psh(); ISTDEF.prefix();
_wyn:=exec('edek_upd1','xml',0);
ISTDEF.cntx_pop();
{? _wyn=-1
|| __UPG.msg('Nie udało się zaktualizować schematu JPK_VAT.');
   -1
|| __UPG.msg('Liczba wykonanych aktualizacji schematów JPK_VAT: %1.'[$_wyn]);
   1
?}


\jpk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: kod - PR/WRT/XP/12.41/1703/0043
::       Agregacja pozycji VAT na potrzeby JPK_VAT - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacje schematów JPK'


\wz_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Aktualizacja wzorca projekty
::  ORG: \wz_proj/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
RS.index('TAB_POLE');
RS.prefix();
{? RS.find_key('UD_SKL','UD_SKL','SYMBOL','SYMBOL','Projekty','Projekty')
|| RS.WZ:='Projekty_schemat';
   RS.put();
   SLU.prefix();
   SLU.for_each("
      {? SLU.WZ='Projekty'
      || SLU.WZ:='Projekty_schemat';
         SLU.put()
      ?}
   ",1)
?};
__UPG.msg('Zaktualizowano wzorce słowników: projekty.');
1


\wz_proj_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Aktualizacja wzorca projekty - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wzorca słowników: projekty'


\USERS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli USERS
::  ORG: \USERS/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=__lmop:=0;
_result:=1;
USERS.cntx_psh();
USERS.prefix();
_formula:="
   _put:=0;
   __lrec+=1;
   {? USERS.EKIOSK='' || USERS.EKIOSK:='N'; _put:=1 ?};
   {? USERS.PORTAL2='' || USERS.PORTAL2:='N'; _put:=1 ?};
   {? USERS.PORTAL2E='' || USERS.PORTAL2E:='N'; _put:=1 ?};
   {? USERS.OF365='' || USERS.OF365:='N'; _put:=1 ?};
   {? USERS.LOG2='' || USERS.LOG2:='N'; _put:=1 ?};
   {? _put || {? USERS.put(1) || __lmod+=1 ?} ?}
";
_result:=USERS.for_each(_formula,1);
USERS.cntx_pop();
__UPG.msg('Zaktualizowano użytkowników - uprawnienia dostępowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

VAR_DEL.delete('__lrec','__lmod');
exec('synchronize','users');
_result


\USERS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli USERS - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja i synchronizacja tabeli użytkowników.'


\B_SIGNAL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli B_SIGNAL
::  ORG: \B_SIGNAL/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_result:=1;
B_SIGNAL.cntx_psh();
B_SIGNAL.prefix();
_formula:="
   _put:=0;
   __lrec+=1;
   {? B_SIGNAL.FIRM=''
   || B_SIGNAL.FIRM:='N';
      _put:=1
   ?};
   {? _put || {? B_SIGNAL.put(1) || __lmod+=1 ?} ?}
";
_result:=B_SIGNAL.for_each(_formula,1);
B_SIGNAL.cntx_pop();
__UPG.msg('Zaktualizowano sygnały (procesowość).');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\B_SIGNAL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli B_SIGNAL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli sygnałów o znacznik wielofirmowości.'


\vat7_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [17.14]
:: OPIS: ER/WRT/XP/12.41/1702/0066 : Powtórzony bład  ER/WRT/XP/12.41/1604/0039 w e-deklaracji VAT(17)
::       ER/WRT/XP/12.41/1703/0031 : błąd e-deklaracji VAT 7 w przypadku wystąpienia załącznika VAT-ZD
::  ORG: \vat7_sch/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fiks_06a','upgradexpertis','VAT7',17,'vzz:P_9',57);
exec('fiks_06a','upgradexpertis','VAT7',17,'vzt:P_10',58);
exec('fiks_22a','upgradexpertis',17,69);
__UPG.msg('Wykonano poprawki spisowe z 12.41 dotyczące zmian w schemacie deklaracji VAT-7.');
1


\fiks_06a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula pomocnicza do transferu
::   WE: _a - typ deklaracji
::       _b - nr deklaracji
::       _c - pozycja deklaracji
::       _d - nr pola
::  ORG: \fiks_06a/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_a,_a,_b);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_a,VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key(_c,)
      || ISTDEFS.REGULY:='exec(\'poz_edek\',\'xml\','+$_d+')';
         ISTDEFS.put()
      ?}
   ?}
?}


\fiks_22a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Formula pomocnicza do transferu
::   WE: _a - nr deklaracji
::       _b - pozycja deklaracji
::  ORG: \fiks_22a/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','VAT7','VAT7',_a);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','VAT7',VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('P_'+$_b,)
      || ISTDEFS.REGULY:='{? exec(\'vat_zd_size\',\'vat\',\'N\') || \'1\' || 0 ?}';
         ISTDEFS.put()
      ?}
   ?}
?};
1


\vat7_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [17.14]
:: OPIS: ER/WRT/XP/12.41/1702/0066 : Powtórzony bład  ER/WRT/XP/12.41/1604/0039 w e-deklaracji VAT(17) - OPIS
::       ER/WRT/XP/12.41/1703/0031 : błąd e-deklaracji VAT 7 w przypadku wystąpienia załącznika VAT-ZD
::----------------------------------------------------------------------------------------------------------------------
'Poprawki spisowe z 12.41 dotyczące zmian w schemacie deklaracji VAT-7'


\fiks_27
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1704/0010
::       Błąd w strukturze deklaracji CIT_8
::  OLD: \fiks_27/napraw_f.fml
::  ORG: \fiks_27/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAT_VER.index('VER_NR'); VAT_VER.prefix('FIKS','CIT8','CIT8',24);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FIKS','D','CIT8',VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('P_66',)
      || {? ISTDEFS.seek(ISTDEFS.TREE,) & ISTDEFS.OPIS='grupa'
         || ISTDEFS.WYM:='N';
            ISTDEFS.HIDDEN:='T';
            ISTDEFS.put()
         ?}
      ?}
   ?}
?};
__UPG.msg('Poprawiono błędy w strukturze deklaracji CIT-8.');
_res


\fiks_27_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1704/0010 - opis
::       Błąd w strukturze deklaracji CIT_8
::----------------------------------------------------------------------------------------------------------------------
'Poprawka błędu w strukturze deklaracji CIT-8'


\typy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.14]
:: OPIS: Aktualizacja typów dokumentów i zamówień
::   WE:
::   WY:
::  ORG: \typy/upgrade_1714.fml
::       \updateTYPYSP/upgrade_1802.fml
::       \updateTYPYSP/upgrade_1902.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
TYPYDOK.prefix();
TYPYDOK.for_each("
   _put:=0;
   __lrec+=1;
   {? TYPYDOK.PROJZAKR=''
   || _put:=1;
      TYPYDOK.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty');
      TYPYDOK.PROJZKH:='N';
      TYPYDOK.PROJZKH:='N'
   ?};
   {? (';INW+;INW-;'*(';%1;'[TYPYDOK.T]))>0  || TYPYDOK.INW:='I'; _put:=1
   |? (';PRC+;PRC-;'*(';%1;'[TYPYDOK.T]))>0  || TYPYDOK.INW:='E'; _put:=1
   |? TYPYDOK.INW='' || TYPYDOK.INW:='N'; _put:=1
   ?};
   {? _put || {? TYPYDOK.put() || __lmod+=1 ?} ?}
",1);
__UPG.msg('Zaktualizowano typy dokumentów magazynowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
__lrec:=__lmod:=0;
TYPYSP.prefix();
TYPYSP.for_each("
   _put:=0;
   __lrec+=1;
   {? TYPYSP.PROJZAKR='' || _put:=1; TYPYSP.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty'); TYPYSP.PROJZKH:='N' ?};
   {? TYPYSP.NDVAT='' || _put:=1; TYPYSP.NDVAT:='N' ?};
   {? TYPYSP.SPPK='' || _put:=1; TYPYSP.SPPK:='N' ?};
   {? TYPYSP.TOW_USL<>'T' & TYPYSP.TOW_USL<>'U' || TYPYSP.TOW_USL:='T'; _put:=1 ?};
   {? TYPYSP.KSEF='' || TYPYSP.KSEF:='N'; _put:=1 ?};
   {? TYPYSP.KSEFZDOK='' || TYPYSP.KSEFZDOK:='N'; _put:=1 ?};
   {? TYPYSP.UE_WSU='' || TYPYSP.UE_WSU:='N'; _put:=1 ?};
   {? _put || {? TYPYSP.put() || __lmod+=1 ?} ?}
",1);
__UPG.msg('Zaktualizowano typy dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
__lrec:=__lmod:=0;
TYPYZAM.prefix();
TYPYZAM.for_each("
   _put:=0;
   __lrec+=1;
   {? TYPYZAM.PROJZAKR=''
   || _put:=1; TYPYZAM.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty'); TYPYZAM.PROJZKH:='N'; TYPYZAM.PROJZKH:='N'
   ?};
   {? TYPYZAM.PROJ='' || TYPYZAM.PROJ:='N'; _put:=1 ?};
   {? _put || {? TYPYZAM.put() || __lmod+=1 ?} ?}
",1);
__UPG.msg('Zaktualizowano typy zamówień.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\typy_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.14]
:: OPIS: Aktualizacja typów dokumentów i zamówień - opis
::   WE:
::   WY:
::  ORG: \typy_desc/upgrade_1714.fml
::       \typy_desc/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów dokumentów i zamówień.'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::  TAG: <PRYWATNA>
::  ORG: \areatitle/upgrade_1728.fml
::       \areatitle/upgrade_1742.fml
::       \areatitle/upgrade_1802.fml
::       \areatitle/upgrade_1822.fml
::       \areatitle/upgrade_1942_08.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\projekty_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Uzupełnienie danych dla projektów
::  ORG: \projekty_ref/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=SKIDXD.names();
{? _tab.first()
|| SKIDXD.cntx_psh();
   DOK.cntx_psh();
   {!
   |? SKIDXD.use(_tab.NAME);
      DOK.use('doku'+(8+_tab.NAME+4));
      echo('Przetwarzanie tabeli SKIDXD maska %1 ...'@[_tab.NAME]);
      SKIDXD.for_each("
         {? SKIDXD.DOK_REJ=null
         || SKIDXD.DOK_REJ:=SKIDXD.DOK().REJ;
            SKIDXD.ODD:=DOK.ODD;
            SKIDXD.put()
         ?}
      ");
      _tab.next()
   !};
   DOK.cntx_pop();
   SKIDXD.cntx_pop()
?};
VAR_DEL.delete('KonProj0','KonProj','KonProj2');
KonProj0:=tab_tmp(1,
   'KON','STRING[35]',
);
KonProj:=tab_tmp(1,
   'KON','STRING[35]',
);
KonProj2:=tab_tmp(1,
   'REF','INTEGER',,
   'PROJ','INTEGER',
);
_rok:=SSTALE.AR;
DOK.cntx_psh();
POZ.cntx_psh();
ROK_F.index('KOD'); ROK_F.prefix();
OKRO_F.index('ROK');
{? ROK_F.first()
|| {!
   |? {? exec('projekty_in_kon','upgradexpertis')
      || KonProj2.erase();
         KonProj.erase();
         SSTALE.AR:=ROK_F.ref();
         OKRO_F.prefix(ROK_F.ref());
         {? OKRO_F.first()
         || {!
            |? _maska:=ROK_F.KOD+form(OKRO_F.NR,-2);
               POZ.use('pozy'+_maska);
               POZ.index('KON');
               DOK.use('doku'+_maska);
               echo('Przetwarzanie tabeli POZ maska %1 ...'@['pozy'+_maska]);
               {? KonProj0.first()
               || {!
                  |? POZ.prefix(KonProj0.KON);
                     {? POZ.first()
                     || {!
                        |? exec('projekty_poz','upgradexpertis');
                           POZ.next()
                        !}
                     ?};
                     KonProj0.next()
                  !}
               ?};
               OKRO_F.next()
            !}
         ?}
      ?};
      ROK_F.next()
   !}
?};
VAR_DEL.delete('KonProj0','KonProj','KonProj2');
DOK.cntx_pop();
POZ.cntx_pop();
SSTALE.AR:=_rok;
exec('czytaj','#stalesys',,XINFO,'SLU_PROJ');
{? XINFO.SLU_PROJ().WZ='Projekty_id' | XINFO.SLU_PROJ().WZ='Projekty_symbol'
|| &_tab;
   _tab:=OP_PROJ.names();
   {? _tab.first()
   || OP_PROJ.cntx_psh();
      {!
      |? OP_PROJ.use(_tab.NAME);
         OP_PROJ.prefix();
         echo('Przetwarzanie tabeli OP_PROJ maska %1 ...'@[_tab.NAME]);
         OP_PROJ.for_each("OP_PROJ.put()");
         _tab.next()
      !};
      OP_PROJ.cntx_pop()
   ?}
?};
&_tab;
_tab:=EDOKUMPR.names();
{? _tab.first()
|| EDOKUMPR.cntx_psh();
   EDOKUM.cntx_psh();
   {!
   |? EDOKUM.use('skid_v'+(_tab.NAME+2));
      EDOKUM.prefix();
      EDOKUMPR.use(_tab.NAME);
      EDOKUMPR.prefix();
      echo('Przetwarzanie tabeli EDOKUMPR maska %1 ...'@[_tab.NAME]);
      EDOKUMPR.for_each("{? EDOKUMPR.ETYPY=null || EDOKUMPR.ETYPY:=EDOKUMPR.EDOKUM().TYP; EDOKUMPR.put() ?}");
      _tab.next()
   !};
   EDOKUM.cntx_pop();
   EDOKUMPR.cntx_pop()
?};
echo('Przetwarzanie tabeli PROJEKTY ...'@);
PROJEKTY.for_each("exec('trig_proj','projekty_ref',1,1)",1);
echo();
__UPG.msg('Uzupełniono dane dla projektów.');
1


\projekty_ref_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.02]
:: OPIS: Uzupełnienie danych dla projektów - opis
::  ORG: \projekty_ref/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia dane dla projektów.'


\projekty_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Uzupełnienie informacji o projektach pozycji księgowych
::  ORG: \projekty_poz/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
PROJ_REF.index('IDTIME'); PROJ_REF.prefix(POZ.IDADD,'Z');
{? PROJ_REF.first() || {! |? PROJ_REF.del !} ?};
KonProj.prefix(POZ.KON,);
{? ~KonProj.first()
|| exec('projekty_kon_proj','upgradexpertis',POZ.KON)
?};
PROJEKTY.prefix();
KonProj2.prefix(KonProj.ref());
{? KonProj2.first()
|| {!
   |? {? PROJEKTY.seek(KonProj2.PROJ,)
      || PROJ_REF.IDTIME:=POZ.IDADD;
         PROJ_REF.TYPE:='Z';
         PROJ_REF.PROJEKT:=PROJEKTY.ref();
         DOK.cntx_psh();
         PROJ_REF.ODD:=POZ.DOK().ODD;
         DOK.cntx_pop();
         PROJ_REF.add()
      ?};
      KonProj2.next()
   !}
?}


\projekty_kon_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Funkcja do pobierania informacji o projektach konta księgowym
::   WE: _a - symbol konta analitycznego
::   WY: gdy podano symbol konta - zwraca tablice tab[][] z
::           tab[1][1] - symbol konta syntetycznego
::  ORG: \projekty_kon_proj/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
KonProj.KON:=_a;
KonProj.add();
KS.index('SYM'); KS.prefix(ROK_F.ref(),SSTALE.AR().SYNT+_a);
{? KS.first()
|| BUD.index('KS'); BUD.prefix(KS.ref());
   {? BUD.first()
   || _ile:=BUD.size();
      _tab:=obj_new(_ile);
      SLO.index('SL'); SLO.prefix();
      _a:=ROK_F.SYNT-_a;
      {!
      |? {? ROK_F.SEP<>',' || _a:=1-_a ?};
         {? BUD.SLU().SLU().WZ='Projekty_id' | SLU.WZ='Projekty_symbol'
         || _kod:=BUD.SLU().SLU().DL+_a;
            {? SLU.WZ='Projekty_id'
            || PROJEKTY.index('ID_KSG');
               PROJEKTY.prefix();
               {? PROJEKTY.find_key(#_kod)
               || KonProj2.prefix();
                  KonProj2.REF:=KonProj.ref();
                  KonProj2.PROJ:=PROJEKTY.ref();
                  KonProj2.add()
               ?}
            || PROJEKTY.index('SYM');
               PROJEKTY.prefix();
               {? PROJEKTY.find_key(_kod,)
               || KonProj2.prefix();
                  KonProj2.REF:=KonProj.ref();
                  KonProj2.PROJ:=PROJEKTY.ref();
                  KonProj2.add()
               ?}
            ?}
         ?};
         _a:=SLU.DL-_a;
         BUD.next()
      !}
   ?}
?}


\projekty_in_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Czy istnieja słowniki projektów w kontach
::  ORG: \projekty_in_kon/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
KonProj0.erase();
_tak:=0;
KS.index('SYM'); KS.prefix(ROK_F.ref());
{? KS.first()
|| {!
   |? _tak:=0;
      BUD.index('KS'); BUD.prefix(KS.ref());
      {? BUD.first()
      || {!
         |? _tak:=BUD.SLU().SLU().WZ='Projekty_id' | SLU.WZ='Projekty_symbol';
            _tak=0 & BUD.next()
         !}
      ?};
      {? _tak
      || KonProj0.prefix();
         KonProj0.KON:=KS.SYM;
         KonProj0.add()
      ?};
      KS.next()
   !}
?};
KonProj0.prefix();
KonProj0.first()


\u2uwagi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Przypisuję zawartość pola FAP.U do pola FAP.UWAGI
::  ORG: \u2uwagi/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_mask:=FAKS.names();
FAKS.cntx_psh();
FAP.cntx_psh();
_mask.clear();
{? _mask.first()
|| {!
   |? FAKS.use(_mask.NAME);
      FAP.use('fakpo'+(_mask.NAME+3));
      FAKS.prefix();
      FAP.prefix();
      FAP.for_each("__lrec+=1;
                    {? FAP.memo_lin('UWAGI')='\n' & +form(FAP.U)
                    || FAP.memo_get();
                       FAP.memo_set(FAP.U);
                       FAP.memo_put(,'UWAGI');
                       __lmod+=1
                    ?}",1);
      _mask.next()
   !}
?};
FAKS.cntx_pop();
FAP.cntx_pop();
__UPG.msg('Zaktualizowano uwagi dla pozycji doskumentów sprzedaży i dokumentów zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\u2uwagi_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Przypisuję zawartość pola FAP.U do pola FAP.UWAGI - opis
::  ORG: \u2uwagi_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uwag dla pozycji dokumentów sprzedaży i dokumentów zakupu.'


\MATERIAL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Przypisuję wartość pól DOKL_S, DOKL_Z w kartotece materiałowej i grupach materiałowych
::       na 2 miejsca po przecinku
::  ORG: \doklMsprzak/upgrade_1728.fml
::       \MATERIAL/upgrade_1822.fml
::       \m_fill/upgrade_1742.fml
::       \abstore_m/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

MGR.cntx_psh();
MGR.prefix();
_result1:=MGR.for_each("
   __lrec+=1;
   _put:=0;
   {? MGR.MOD<>'T'
   || MGR.DOKL_S:=2;
      MGR.DOKL_Z:={? MGR.RODZ='T' || MGR.DOKL_C || 2 ?};
      MGR.MOD:='T';
      _put:=1
   ?};
   {? _put || {? MGR.put(1) || __lmod+=1 ?} ?}",0);
MGR.cntx_pop();
{? _result1
|| __UPG.msg('Zaktualizowano zapisy w kartotece grup materiałowych.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji pól w kartotece grup materiałowych.')
?};

__lrec:=__lmod:=0;
M.cntx_psh();
M.prefix();
_result2:=M.for_each("
   __lrec+=1;
   {? M.MOD<>'T'
   || M.DOKL_S:=2;
      M.DOKL_Z:={? M.RODZ='T' || M.DOKL_C || 2 ?};
      M.MOD:='T'
   ?};
   {? M.OO='' || M.OO:='N' ?};
   {? M.SP_WYM='' || M.SP_WYM:='N' ?};
   {? M.MGAT<>null() & M.GATKOD<>M.MGAT().KOD || M.GATKOD:=M.MGAT().KOD ?};
   {? M.ATEST='' || M.ATEST:='N' ?};
:: Wymagane pola nawijane są triggerem, więc wystarczy przeputować rekordy
   {? M.put() || __lmod+=1 ?}",0);
M.cntx_pop();
{? _result2
|| __UPG.msg('Zaktualizowano zapisy w kartotece materiałowej.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w kartotece materiałowej.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result:=_result1 & _result2;
_result


\MATERIAL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Przypisuję wartość pól DOKL_S, DOKL_Z w kartotece materiałowej na 2 miejsca po przecinku - opis
::  ORG: \doklMsprzak_desc/upgrade_1728.fml
::       \MATERIAL_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki materiałowej.'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Kompilacja wydruków
::  ORG: \report/upgrade_1742.fml
::       \report/upgrade_1728.fml
::       \report/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Kompilacja wydruków - opis
::  ORG: \report_desc/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\importTypDokMag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Uaktualnienie typów dokumentów o typ KR+ i KR-
::  ORG: \importTypDokMag/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_krp:='KR+^Korekta ceny +^N^T^^T^N^^N^^^N^N^MAG^N^N^^^N^N^N^^N^^^^^^';
_krm:='KR-^Korekta ceny -^N^N^^T^N^^N^^^N^N^MAG^N^N^^^N^N^^^T^T^T^^^^^N^';
_dkom:='DKOM^Dekompletacja^N^N^DSUR^N^N^^N^^^N^D^MAG^N^N^^^N^N^^^T^N^N^^^^^N^';
_dsur:='DSUR^Przyjęcie surowców z dekompletacji^N^T^^N^N^^N^^^N^N^MAG^N^N^^^N^N^^^N^N^N^^^^^N^';
_rru:='RRU^Rejestracja realizacji usługi^N^N^^N^N^^N^^^N^N^MAG^N^N^^^T^U^^^T^T^T^^^^^N^';
_ch:='^';
_len:=TYPYDOK.fld_num();
TYPYDOK.cntx_psh();
TYPYDOK.index('TYP');
TYPYDOK.prefix('KR+',);
{? ~TYPYDOK.first()
|| TYPYDOK.prefix();
   TYPYDOK.blank();
   _i:=1;
   _buf:=_krp;
   {!
   |? _wsk:=_buf*_ch;
      _war:=(_wsk-1)+_buf;
      _buf:=_wsk-_buf;
      {? _war<>'' || ($('TYPYDOK['+$_i+']'))():=_war ?};
      _i+=1;
      _i<=_len & _buf<>''
   !};
   TYPYDOK.add(1)
?};
TYPYDOK.prefix('KR-',);
{? ~TYPYDOK.first()
|| TYPYDOK.prefix();
   TYPYDOK.blank();
   _i:=1;
   _buf:=_krm;
   {!
   |? _wsk:=_buf*_ch;
      _war:=(_wsk-1)+_buf;
      _buf:=_wsk-_buf;
      {? _war<>'' || ($('TYPYDOK['+$_i+']'))():=_war ?};
      _i+=1;
      _i<=_len & _buf<>''
   !};
   TYPYDOK.add(1)
?};
TYPYDOK.prefix('DKOM',);
{? ~TYPYDOK.first()
|| TYPYDOK.prefix();
   TYPYDOK.blank();
   _i:=1;
   _buf:=_dkom;
   {!
   |? _wsk:=_buf*_ch;
      _war:=(_wsk-1)+_buf;
      _buf:=_wsk-_buf;
      {? _war<>'' || ($('TYPYDOK['+$_i+']'))():=_war ?};
      _i+=1;
      _i<=_len & _buf<>''
   !};
   TYPYDOK.add(1)
?};
TYPYDOK.prefix('DSUR',);
{? ~TYPYDOK.first()
|| TYPYDOK.prefix();
   TYPYDOK.blank();
   _i:=1;
   _buf:=_dsur;
   {!
   |? _wsk:=_buf*_ch;
      _war:=(_wsk-1)+_buf;
      _buf:=_wsk-_buf;
      {? _war<>'' || ($('TYPYDOK['+$_i+']'))():=_war ?};
      _i+=1;
      _i<=_len & _buf<>''
   !};
   TYPYDOK.add(1)
?};
TYPYDOK.prefix('RRU',);
{? ~TYPYDOK.first()
|| TYPYDOK.prefix();
   TYPYDOK.blank();
   _i:=1;
   _buf:=_rru;
   {!
   |? _wsk:=_buf*_ch;
      _war:=(_wsk-1)+_buf;
      _buf:=_wsk-_buf;
      {? _war<>'' || ($('TYPYDOK['+$_i+']'))():=_war ?};
      _i+=1;
      _i<=_len & _buf<>''
   !};
   _res:=TYPYDOK.add(1)
?};
TYPYDOK.cntx_pop();
__UPG.msg('Zaktualizowano typy dokumentów KR+, KR-, DKOM, DSUR, RRU.');
_res


\importTypDokMag_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Uaktualnienie typów dokumentów o typ KR+ i KR- - opis
::  ORG: \importTypDokMag_desc/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie typów dokumentów magazynowych KR+, KR-, DKOM, DSUR, RRU'


\sam2poj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Formuła kopiuje dane samochodów z lokalnej tabeli SAM do globalnej POJAZDY
::       Formuła wykonywana w każdej firmie!
::   WY: 1/0
::  ORG: \sam2poj/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__sampoj');
__sampoj:=tab_tmp(1,'SAM','STRING[16]','','POJ','STRING[16]','');

:: modele pojazdów
POJMODEL.index('NAZWA');
SAMMOD.prefix();
SAMMOD.for_each("POJMODEL.prefix(SAMMOD.MAR,SAMMOD.NAZ,);
                 {? ~POJMODEL.first()
                 || POJMODEL.blank();
                    POJMODEL.MAR:=SAMMOD.MAR;
                    POJMODEL.NAZWA:=SAMMOD.NAZ;
                    POJMODEL.add(1)
                 ?}
");
:: typy pojazdów
POJTYPY.index('NAZWA');
SAMT.prefix();
SAMT.for_each("POJTYPY.prefix(SAMT.NAZ,);
               {? ~POJTYPY.first()
               || POJTYPY.blank();
                  POJTYPY.NAZWA:=SAMT.NAZ;
                  POJTYPY.POJ_CIEZ:=SAMT.LADUNEK;
                  POJTYPY.add(1)
               ?}
              ");

SAM.cntx_psh(); POJAZDY.cntx_psh(); SRSR.cntx_psh(); SRST.cntx_psh(); SRDO.cntx_psh(); SRDT.cntx_psh();
SAM.index('NAZ'); SAM.prefix();
SRSR.use('srsrr'+REF.FIRMA().SYMBOL);SRSR.prefix();
SRST.use('srstr'+REF.FIRMA().SYMBOL);SRST.prefix();
SRDO.use('srdor'+REF.FIRMA().SYMBOL);SRDO.prefix();
SRDT.use('srdtr'+REF.FIRMA().SYMBOL);SRDT.prefix();
VAR_DEL.delete('__idx_poj');
__idx_poj:=POJAZDY.ndx_tmp(,,'FIRMA',,,'ODDZ',,,'NREW',,);
SAM.for_each("_sam:=$SAM.ref();
              _poj:='';
              POJAZDY.index(__idx_poj);
              POJAZDY.prefix(REF.FIRMA,SAM.ODDZ,SAM.NREW,);
              {? ~POJAZDY.first()
              || POJAZDY.blank();
                 POJAZDY.NRREJ:=SAM.NRREJ;
                 _add:=1
              || _poj:=$POJAZDY.ref();
                 _add:=0
              ?};
              POJAZDY.FIRMA:=REF.FIRMA;
              POJAZDY.ODDZ:=SAM.ODDZ;
              POJAZDY.NREW:=SAM.NREW;
              POJAZDY.A:=SAM.A;
              POJAZDY.DPR:=SAM.DPR;
              POJAZDY.DPS:=SAM.DPS;
              POJAZDY.WLASNY:=SAM.WLASNY;
              POJAZDY.SRSR:=SAM.SRSR;
              {? POJAZDY.SRSR<>null()
              || SRST.index('PODAT');
                 SRST.prefix(POJAZDY.SRSR);
                 {? SRST.last()
                 || POJAZDY.JORG:=SRST.JORG;
                    POJAZDY.OSOBA:=SRST.OSOBA;
                    {? POJAZDY.DPS=date(0,0,0) || POJAZDY.DPS:=SRST.SRSR().DE ?}
                 ?}
              ?};
              POJAZDY.MAR:=SAM.MAR;
              POJAZDY.MIEJSCA:=SAM.MIEJSCA;
              POJAZDY.MOC:=SAM.MOC;
              POJAZDY.MREJ:=SAM.MREJ;
              POJAZDY.MW:=SAM.MW;
              POJAZDY.NADW:=SAM.NADW;
              POJAZDY.NAZ:=SAM.NAZ;
              POJAZDY.POJE:=SAM.POJE;
              {? SAM.SAMMOD<>null
              || POJMODEL.index('NAZWA');
                 POJMODEL.prefix(SAM.SAMMOD().MAR,SAM.SAMMOD().NAZ);
                 {? POJMODEL.first() || POJAZDY.POJMODEL:=POJMODEL.ref() ?}
              ?};
              {? SAM.SAMT<>null
              || POJTYPY.index('NAZWA');
                 POJTYPY.prefix(SAM.SAMT().NAZ,);
                 {? POJTYPY.first() || POJAZDY.POJTYPY:=POJTYPY.ref() ?}
              ?};

              POJAZDY.ROK:=SAM.ROK;
              POJAZDY.SAMDELTY:=SAM.SAMDELTY;
              POJAZDY.SIL:=SAM.SIL;
              POJAZDY.ST:=SAM.ST;
              POJAZDY.VIN:=SAM.VIN;
              POJAZDY.WERSJA:=SAM.WERSJA;

              POJAZDY.LAD:=SAM.LAD;
              POJAZDY.DK:=SAM.DK;
              POJAZDY.ND:=SAM.ND;
              POJAZDY.PRZEPL:=SAM.PRZEPL;
              POJAZDY.DOPL:=SAM.DOPL;
              POJAZDY.KIEROWCA:=SAM.KIEROWCA;

              _unik:=1;
              POJAZDY.cntx_psh();
              POJAZDY.index('DISP');
              POJAZDY.prefix(REF.FIRMA,POJAZDY.NAZ,POJAZDY.NRREJ,);
              {? POJAZDY.first() & (_add | (~_add & _poj<>$POJAZDY.ref())) || _unik:=0 ?};
              POJAZDY.cntx_pop();
              {? _unik=0
              || _tmp:=' ('+POJAZDY.NREW+')';
                 {? +(POJAZDY.NAZ+_tmp)>60
                 || _ile:=+(POJAZDY.NAZ+_tmp)-60;
                    POJAZDY.NAZ:=(POJAZDY.NAZ-_ile)+_tmp
                 || POJAZDY.NAZ:=POJAZDY.NAZ+_tmp
                 ?}
              ?};

              {? (_add & POJAZDY.add(1)) | (_add=0 & POJAZDY.put(1))
              || _poj:=$POJAZDY.ref();
                 _txt:=SAM.memo_txt(,1,'UWAGI');
                 POJAZDY.memo_set(_txt,'UWAGI');
                 POJAZDY.memo_put(,'UWAGI');
                 EDOKUMP.cntx_psh();
                 _ename:=EDOKUMP.names();
                 {? _ename.first()
                 || {! |?
                        EDOKUMP.use(_ename.NAME);
                        EDOKUMP.index('SAMCHR');
                        EDOKUMP.prefix($SAM.ref());
                        {? EDOKUMP.first()
                        || {! |?
                              EDOKUMP.cntx_psh();
                              EDOKUMP.prefix();
                              EDOKUMP.SAMREF:=$POJAZDY.ref();
                              EDOKUMP.put(1);
                              EDOKUMP.cntx_pop();
                              EDOKUMP.first()
                           !}
                        ?};
                        _ename.next()
                    !}
                 ?};
                 EDOKUMP.cntx_pop();
:: jeżeli środek związany z pojazdem miał dokumenty zmiany właściwości
                 SRDO.cntx_psh();
                 SRDO.index('SRODZAJ');
                 SRDO.prefix(POJAZDY.SRSR,'L');
                 {? SRDO.first()
                 || {! |?
                       {? SRDO.TYP().JORG='T' | SRDO.TYP().OSOBA='T'
                       || ZASOB_ZM.cntx_psh();
                          ZASOB_ZM.prefix();
                          exec('zsb_blank','zasoby_wspolne',POJAZDY.SRSR);
                          ZM_ZASOB.ZASOBY:=ZASOB_ZM.ZASOBY;
                          ZASOB_ZM.DATAOD:=SRDO.DO;
                          ZASOB_ZM.OSOBA:=SRDO.OSOBA;
                          ZASOB_ZM.JORG:=SRDO.JORG;
                          ZASOB_ZM.SRDO_Z:=SRDO.ref();
                          {? ZASOB_ZM.add(1) || exec('zam_poprz','zasoby_wspolne') ?};
                          ZASOB_ZM.cntx_pop()
                       ?};
                       SRDO.next()
                    !}
                 ?};
                 SRDO.cntx_pop()
              || _result:=0
              ?};
              {? _sam<>'' & _poj<>''
              || __sampoj.blank();
                 __sampoj.SAM:=_sam;
                 __sampoj.POJ:=_poj;
                 __sampoj.add(1)
              ?}
             ");
VAR_DEL.delete('__idx_poj');
SAM.cntx_pop(); POJAZDY.cntx_pop(); SRSR.cntx_pop(); SRST.cntx_pop(); SRDO.cntx_pop(); SRDT.cntx_pop();

:: zamiana pól SAM na POJAZD

__sam4hn:=tab_tmp(2,
:: 'POLE','TYP','Nazwa w oknie',
   'HN','STRING[16]','Nazwa pola 1',
   'SAM','STRING[16]','Nazwa pola 2'
);

HN.cntx_psh();
HN.prefix();
{? HN.first()
|| {!
   |? {? var_pres('_sam')>100
      || obj_del(_sam)
      ?};
      _sam:=exec('transfer_harmon','umowy_harm',HN.ref(),,1);
      {? _sam.last()
      ||
::       Przyjmuję że ostatnio użyty samochód w harmonogramie będzie stemplować pozycję umowy
         __sam4hn.prefix($HN.ref(),);
         {? __sam4hn.first()=0
         || __sam4hn.blank();
            __sam4hn.HN:=$HN.ref();
            __sam4hn.SAM:=_sam.REF;
            __sam4hn.add(1)
         ?}
      ?};
      HN.next()
   !}
?};
HN.cntx_pop();
__sam4hn.prefix();

__sampoj.prefix();
{? __sampoj.first()
|| _tab:=spli_str('OFE.OFK.ZK_N.ZK_P.REJO.WYDRUKIL.ZD_NAG.ZKW_N.ZLP.SAMK.UP','.');
   _len:=obj_len(_tab);
   {! _i.._len
   |! _msk:=($(_tab[_i]+'.names()'))();
      _msk.clear();
      {? _msk.first()
      || {!
         |? {| ($(_tab[_i]))()
            |! cntx_psh();
               use(_msk.NAME);
               {? _tab[_i]='SAMK'
               || SAMK.index('D2')
               ?};
               prefix();
               {? first()
               || {!
                  |? {? _tab[_i]='WYDRUKIL'
                     || {? POJAZDOD=null() & SAM_OD<>null()
                        || __sampoj.clear();
                           __sampoj.prefix($SAM_OD,);
                           {? __sampoj.first() & (@.POJAZDY.prefix(); @.POJAZDY.seek(__sampoj.POJ))
                           || POJAZDOD:=@.POJAZDY.ref();
                              put(1)
                           ?}
                        ?};
                        {? POJAZDDO=null() & SAM_DO<>null()
                        || __sampoj.clear();
                           __sampoj.prefix($SAM_DO,);
                           {? __sampoj.first() & (@.POJAZDY.prefix(); @.POJAZDY.seek(__sampoj.POJ))
                           || POJAZDDO:=@.POJAZDY.ref();
                              put(1)
                           ?}
                        ?}
                     |? _tab[_i]='UP'
                     || {? H<>null() & H().TYP=1 & POJAZDY=null()
                        || SAM.cntx_psh();
                           SAM.index('ZLP');
::                         Jest harmonogram, pobieram zapamiętane dla harmonogramu samochody                           __zlbr4hn.prefix($UP.H,);
                           __sam4hn.prefix($H,);
                           {? __sam4hn.first()
                           ||
                              __sampoj.clear();
                              __sampoj.prefix(__sam4hn.SAM,);
                              {? __sampoj.first() & (@.POJAZDY.prefix(); @.POJAZDY.seek(__sampoj.POJ))
                              || POJAZDY:=@.POJAZDY.ref();
                                 put(1)
                              ?}
                           ?};
                           SAM.cntx_pop()
                        ?}
                     |? POJAZD=null() & SAM<>null()
                     || __sampoj.clear();
                        __sampoj.prefix($SAM,);
                        {? __sampoj.first() & (@.POJAZDY.prefix(); @.POJAZDY.seek(__sampoj.POJ))
                        || POJAZD:=@.POJAZDY.ref();
                           put(1)
                        ?}
                     ?};
                     next()
                  !}
               ?};
               cntx_pop()
            |};
            _msk.next()
         !}
      ?};
      obj_del(_msk)
   !};
   obj_del(_tab)
?};
exec('tra2SAMN','upgradexpertis');
VAR_DEL.delete('__sampoj');
{? _result
|| __UPG.msg('Przeniesiono samochody do nowej kartoteki pojazdów.')
|| __UPG.msg('Nie udało się przenieść samochodów do nowej kartoteki pojazdów.')
?};
_result


\sam2poj_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: sam2poj - opis formuły
::  ORG: \sam2poj_desc/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Przenoszenie samochodów do nowej kartoteki pojazdów.'


\m_kh_sv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Uaktualnienie pól tabeli M_KH_SV
::  ORG: \m_kh_sv/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_stat:=obj_new('popr','niepopr','analiza');
{! _lp:=1 .. obj_len(_stat)
|! _stat[_lp]:=0
!};
params_set('stat',_stat);

M_KH_SV.cntx_psh();
M_KH_SV.prefix();
_stat.analiza:=M_KH_SV.size();
_wyn:=M_KH_SV.for_each(
         "_stat:=params_get().stat;
         {? M_KH_SV.SV_KH='' || M_KH_SV.SV_KH:='N';{? M_KH_SV.put(1) || _stat.popr+=1 || _stat.niepopr+=1 ?} ?}",1);
M_KH_SV.cntx_pop();
{? _wyn=1
|| __UPG.msg('Uzupełniono pole SV_KH w tabeli stawek VAT kontrahenta. Poprawiono %1 z %2 analizowanych rekordów.'
   [$_stat.popr,$_stat.analiza]);
   {? _stat.niepopr>0
   ||
      __UPG.msg('Nie udało uzupełnić pola dla %1 rekordów.'[$_stat.niepopr]);
      _wyn:=-1
   ?}
|| __UPG.msg('Wystąpił błąd podczas uzupełniania pola SV_KH w tabeli stawek VAT kontrahenta.');
   _wyn:=0
?};
_wyn


\m_kh_sv_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Uaktualnienie pól tabeli M_KH_SV - opis
::  ORG: \m_kh_sv_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola w tabeli stawek VAT kontrahenta. Pole (M_KH_SV.SV_KH) domyślną wartością N'


\m_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Uzupełnia pole M.OO
::   WE:
::   WY:
::  ORG: \m_fill/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
M.cntx_psh();
M.prefix();
M.for_each("
   _put:=0;
   __lrec+=1;
   {? M.OO='' || _put:=1; M.OO:='N' ?};
   {? M.SP_WYM='' || _put:=1; M.SP_WYM:='N' ?};
   {? _put || {? M.put() || __lmod+=1 ?} ?}
");
M.cntx_pop();
__UPG.msg('Zaktualizowano zapisy w kartotece materiałowej.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\m_fill_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Uzupełnia pole M.OO - opis
::   WE:
::   WY:
::  ORG: \m_fill_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie M.OO'


\plusREZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Aktualizacja znacznika REZ.PLUS
::  ORG: \plusREZ/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_msk:=REZ.names();
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
REZ.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? REZ.use(_msk.NAME);
      REZ.prefix();
      REZ.for_each("__lrec+=1;
                    _put:=0;
                    {? REZ.PLUS='' || REZ.PLUS:='N'; _put:=1 ?};
                    {? REZ.RP_REZ='' || REZ.RP_REZ:='N'; _put:=1 ?};
                    {? _put & REZ.put(1) || __lmod+=1 ?}",1);
      _msk.next()
   !}
?};
REZ.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano zapisy dla rezerwacji.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\plusREZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Aktualizacja znacznika REZ.PLUS - opis
::  ORG: \plusREZ_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika zwiększenia ilości dla rezerwacji'


\edi_i_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Uzupełnia pole EDI_I.TYP
::   WE:
::   WY:
::  ORG: \edi_i_fill/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_msk:=EDI_I.names();
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
EDI_I.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? EDI_I.use(_msk.NAME);
      EDI_I.prefix();
      EDI_I.for_each("
         _put:=0;
         __lrec+=1;
         {? EDI_I.TYP='' || _put:=1; EDI_I.TYP:='L' ?};
         {? _put || {? EDI_I.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
EDI_I.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano zapisy dla dokumentów magazynowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\edi_i_fill_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Uzupełnia pole EDI_I.TYP - opis
::   WE:
::   WY:
::  ORG: \edi_i_fill_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie EDI_I.TYP'


\actOperMob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Uzupełnia pole EANN.ZLEC
::  ORG: \actOperMob/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_msk:=EANN.names();
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
EANN.cntx_psh();
EANP.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? EANN.use(_msk.NAME);
      EANP.use('eanp'+(_msk.NAME+3));
      EANN.prefix();
      EANN.for_each("
         _put:=0;
         __lrec+=1;
         {? EANN.ZLEC='' || _put:=1; EANN.ZLEC:='N' ?};
         {? EANN.TYP='R' & EANN.IS_R='' & EANN.IS_M=''
         || _put:=1; EANN.IS_R:='T'; EANN.IS_M:='N'
         || {? EANN.IS_R='' || _put:=1; EANN.IS_R:='N' ?};
            {? EANN.IS_M='' || _put:=1; EANN.IS_M:='N' ?}
         ?};
         {? EANN.ST_R='' || _put:=1; EANN.ST_R:='N' ?};
         {? EANN.ST_M='' || _put:=1; EANN.ST_M:='N' ?};
         {? _put
         || {? EANN.put()
            || __lmod+=1;
               EANP.index('EANN');
               EANP.prefix(EANN.ref());
               {? EANP.first()
               || {!
                  |? {? EANP.IS_RM=''
                     || EANP.IS_RM:={? EANP.EANN().TYP='R' || 'R' || 'N' ?};
                        EANP.put(1)
                     ?};
                     EANP.next()
                  !}
               ?}
            ?}
         ?}
      ");
      _msk.next()
   !}
?};
EANN.cntx_pop();
EANP.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano zapisy dla operacji mobilnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\actOperMob_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Uzupełnia pole EANN.ZLEC - opis
::  ORG: \actOperMob_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie znaczników operacji na urządzeniach mobilnych'


\actZlecFakt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Uzupełnia pole FAKZ.KOREKTA
::  ORG: \actZlecFakt/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_msk:=FAKZ.names();
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
FAKZ.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? FAKZ.use(_msk.NAME);
      FAKZ.prefix();
      FAKZ.for_each("
         _put:=0;
         __lrec+=1;
         {? FAKZ.KOREKTA='' || _put:=1; FAKZ.KOREKTA:='N' ?};
         {? _put || {? FAKZ.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
FAKZ.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano zapisy dla zleceń fakturowania.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\actZlecFakt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Uzupełnia pole FAKZ.KOREKTA - opis
::  ORG: \actZlecFakt_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie znaczników korekta dla zleceń fakturowania'


\updateFAKSO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje znacznik MODIFY dla informacji dodatkowych
::  ORG: \updateFAKSO/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_msk:=FAKSO.names();
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
FAKSO.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? FAKSO.use(_msk.NAME);
      FAKSO.prefix();
      FAKSO.for_each("
         _put:=0;
         __lrec+=1;
         {? FAKSO.MODIFY='' || _put:=1; FAKSO.MODIFY:='N' ?};
         {? _put || {? FAKSO.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
FAKSO.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano zapisy i definicje dla informacji dodatkowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\updateFAKSO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje znacznik MODIFY dla informacji dodatkowych - opis
::  ORG: \updateFAKSO_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji informacji dodatkowych.'


\KHODB2position
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Uzupełnia zapisy KH i ODB dla pozycji FAKS, ND, ZK_N, ZD_NAG
::  ORG: \KHODB2position/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ND.names();

ND.cntx_psh();
DK.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ND.use(_msk.NAME);
      DK.use('dokma'+(_msk.NAME+3));
      DK.prefix();
      DK.for_each("
         _put:=0;
         __lrec+=1;
         {? DK.KH=null() & DK.N().KH<>null() || _put:=1; DK.KH:=DK.N().KH ?};
         {? DK.KH_ODB=null() & DK.N().KH_ODB<>null() || _put:=1; DK.KH_ODB:=DK.N().KH_ODB ?};
         {? _put || {? DK.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
ND.cntx_pop();
DK.cntx_pop();

__UPG.msg('Zaktualizowano zapisy dla pozycji dokumentów magazynowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
_msk:=FAKS.names();
FAKS.cntx_psh();
FAP.cntx_psh();
_msk.clear();
{? _msk.first()
|| VAR_DEL.delete('__mskh');
   {!
   |? __mskh:=(_msk.NAME+2)='hs';
      FAKS.use(_msk.NAME);
      FAP.use('fakpo'+(_msk.NAME+3));
      FAP.prefix();
      FAP.for_each("
         _put:=0;
         __lrec+=1;
         {? FAP.KH=null() & FAP.FAKS().KH<>null() || _put:=1; FAP.KH:=FAP.FAKS().KH ?};
         {? FAP.KH_ODB=null() & FAP.FAKS().KH_ODB<>null() || _put:=1; FAP.KH_ODB:=FAP.FAKS().KH_ODB ?};
         {? FAP.SP_WYM='' || _put:=1; FAP.SP_WYM:='N' ?};
         {? __mskh
         || {? FAP.MX=''
            || FAP.MX:=FAP.M().KTM;
               FAP.NX:=FAP.M().N;
               _put:=1
            ?}
         ?};
         {? _put || {? FAP.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !};
   VAR_DEL.delete('__mskh')
?};
FAKS.cntx_pop();
FAP.cntx_pop();

__UPG.msg('Zaktualizowano zapisy dla pozycji dokumentów sprzedaży i zakupów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
_msk:=ZK_N.names();
ZK_N.cntx_psh();
ZK_P.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ZK_N.use(_msk.NAME);
      ZK_P.use('zkpoz'+(_msk.NAME+3));
      ZK_P.prefix();
      ZK_P.for_each("
         _put:=0;
         __lrec+=1;
         {? ZK_P.KH=null() & ZK_P.N().KH<>null() || _put:=1; ZK_P.KH:=ZK_P.N().KH ?};
         {? ZK_P.KH_ODB=null() & ZK_P.N().ODB<>null() || _put:=1; ZK_P.KH_ODB:=ZK_P.N().ODB ?};
         {? _put || {? ZK_P.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
ZK_N.cntx_pop();
ZK_P.cntx_pop();

__UPG.msg('Zaktualizowano zapisy dla pozycji zamówień sprzedaży.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
_msk:=ZD_NAG.names();
ZD_NAG.cntx_psh();
ZD_POZ.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ZD_NAG.use(_msk.NAME);
      ZD_POZ.use('zdpoz'+(_msk.NAME+3));
      ZD_POZ.prefix();
      ZD_POZ.for_each("
         _put:=0;
         __lrec+=1;
         {? ZD_POZ.KH=null() & ZD_POZ.ZD_NAG().KH<>null() || _put:=1; ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH ?};
         {? ZD_POZ.KH_MSC=null() & ZD_POZ.ZD_NAG().KH_MSC<>null() || _put:=1; ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC ?};
         {? _put || {? ZD_POZ.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
ZD_NAG.cntx_pop();
ZD_POZ.cntx_pop();

__UPG.msg('Zaktualizowano zapisy dla pozycji zamówień dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
OFE.cntx_psh(); OFE.prefix();
OFP.cntx_psh(); OFP.prefix();
M.cntx_psh(); M.prefix();

OFP.for_each("
   __lrec+=1;
   _put:=0;
   {? OFP.KH_N=null()   || OFP.KH_N:=OFP.OFE().KH;  _put:=1 ?};
   {? OFP.KH_ODB=null() || OFP.KH_ODB:=OFP.OFE().ODB; _put:=1 ?};
   {? OFP.JM=null()     || OFP.JM:=OFP.M().J;   _put:=1 ?};
   {? _put              || {? OFP.put(1) || __lmod+=1 ?} ?}
");

OFE.cntx_pop();
OFP.cntx_pop();
M.cntx_pop();

__UPG.msg('Przeniesienie wartości do pozycji oferty w celu podglądu danych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

VAR_DEL.delete('__lrec','__lmod');
_res


\KHODB2position_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Uzupełnia zapisy KH i ODB dla pozycji FAKS, ND, ZK_N, ZD_NAG - opis
::  ORG: \KHODB2position_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów dla pozycji:\n'
'   dokumentów magazynowych,\n'
'   dokumentów sprzedaży,\n'
'   dokumentów zakupu,\n'
'   zamówień sprzedaży,\n'
'   zamówień dostaw.'


\updateMDOST
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje pole MKODK dla tabeli MDOST
::  ORG: \updateMDOST/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
MKODK.cntx_psh();
MKODK.prefix();
MKODK.for_each("
   _put:=0;
   __lrec+=1;
   {? MKODK.M<>null() & MKODK.KH<>null() & exec('act_mdostMkodk','kody_kresk',MKODK.KH,MKODK.M,MKODK.ref())=2
   || __lmod+=1
   ?}
");
MKODK.cntx_pop();
__UPG.msg('Zaktualizowano definicje dla dostawców materiałów o oznaczenia kodów zewnętrznych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\updateMDOST_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Aktualizuje znacznik NDVAT dla typów dokumentów sprzedaży - opis
::  ORG: \updateMDOST_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji dostawców dla materiałów o oznaczenia kodów zewnętrznych.'


\plat_zkn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Aktualizuje płatności na zamówieniach dostaw
::   WE:
::   WY:
::  ORG: \plat_zkn/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('czytaj','#stalesys',,INFO,'NAROD');
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ZK_N.names();
ZK_N.cntx_psh();
ZK_P.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? ZK_N.use(_msk.NAME);
      ZK_N.use((5+ZK_N.name())+(_msk.NAME+3));
      ZK_N.prefix();
      ZK_N.for_each("
         _put:=0;
         __lrec+=1;

         {? ZK_N.PL
         ||
            exec('plat_sel','faktury_plat',ZK_N);
            {? FZ.FORMAPLA=''
            ||
               _put:=1;
               FZ.FORMAPLA:=ZK_N.PL().KOD;
               FZ.PL:=ZK_N.PL;
               FZ.TERMPLAT:=ZK_N.DP+ZK_N.LD;
               ZK_N.TZ:=FZ.TERMPLAT;
               exec('plat_one','faktury_plat',ZK_N.ref());
               {? ZK_N.put() || __lmod+=1 ?};
               exec('plat_przel','faktury_plat',ZK_N.ref())

            ?}
         ?}
      ");
      _msk.next()
   !}
?};
ZK_N.cntx_pop();
ZK_P.cntx_pop();

__UPG.msg('Zaktualizowano płatności dla zamówień sprzedaży.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\plat_zkn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Aktualizuje płatności na zamówieniach dostaw - opis
::   WE:
::   WY:
::  ORG: \plat_zkn_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja płatności na zamówieniach sprzedaży'


\faks_odb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Aktualizuje danych odbiorcy do wydruku na dokumentach sprzedaży
::   WE:
::   WY:
::  ORG: \faks_odb/upgrade_1802.fml
::       \faks_kh_kraj/upgrade_1822.fml
::       \FAKS/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('czytaj','#stalesys',,INFO,'NAROD');
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKS.names();
FAKS.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("
         _put:=0;
         __lrec+=1;
         {? FAKS.KH_ODB
            & FAKS.O_NAZ='' & FAKS.O_NIP='' & FAKS.O_UL='' & FAKS.O_MIASTO='' & FAKS.O_POCZ='' & FAKS.O_KRAJ=''
         || FAKS.O_NAZ:=FAKS.KH_ODB().NAZ;
            FAKS.O_MIASTO:=KH_ODB.MIASTO;
            FAKS.O_UL:=KH_ODB.UL;
            FAKS.O_POCZ:=KH_ODB.KPOCZ+' '+KH_ODB.POCZ;
            FAKS.O_NIP:=KH_ODB.NIP;
            FAKS.O_KRAJ:=FAKS.KH_ODB().KRAJ;
            _put:=1
         ?};
         {? FAKS.KH & FAKS.KH_KRAJ='' || FAKS.KH_KRAJ:=FAKS.KH().KRAJ; _put:=1 ?};
         {? FAKS.SP_WYM='' || FAKS.SP_WYM:='N'; _put:=1 ?};
         {? FAKS.INTRAKC=''
         || FAKS.INTRAKC:={? FAKS.KZ='' & (FAKS.IST_TYP='' | FAKS.AKC='N') || 'N' || 'T' ?};
            _put:=1
         ?};
         {? FAKS.STATKSEF='' || _put:=1 ?};
         {? FAKS.ZAK2='' || FAKS.ZAK2:='N'; _put:=1 ?};
         _kpocz:=FAKS.KH().KPOCZ; {? _kpocz<>'' & FAKS.KPOCZ='' || _put:=1; FAKS.KPOCZ:=_kpocz ?};
         _kpocz:=FAKS.KH_ODB().KPOCZ; {? _kpocz<>'' & FAKS.O_KPOCZ='' || _put:=1; FAKS.O_KPOCZ:=_kpocz ?};
::       FAKS.EDOKUMEN i przy okacji FAKS.DRUKUJ ustawiane w triggerze
         {? FAKS.EDOKUMEN='' || _put:=1 ?};
         {? FAKS.KSEF_ERR='' || FAKS.KSEF_ERR:='N'; _put:=1 ?};
         {? FAKS.GV_WEW='' || FAKS.GV_WEW:='N'; _put:=1 ?};
         {? _put || {? FAKS.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();

__UPG.msg('Zaktualizowano kraj kontrahenta, dane odbiorcy do wydruku dla dokumentów sprzedaży.');
__UPG.msg('Zaktualizowano znaczniki zaksięgowania dla faktur fiskalnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\faks_odb_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Aktualizuje danych odbiorcy do wydruku na dokumentach sprzedaży - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kraju kontrahenta, danych odbiorcy do wydruku na dokumentach sprzedaży.'


\del140
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Usuwa parametr 140 systemu
::  ORG: \del140/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_ustaw:=exec('get_par','#parametr',140)='T';
{? exec('del','#parametr',,140)
|| __UPG.msg('Usunięto parametr systemu 140.')
|| _res:=-1;
   __UPG.msg('Nie usunięto parametru systemu 140.')
?};
{? var_pres('_ustaw')=type_of(0) & _ustaw
|| M_ATR.cntx_psh();
   M_ATR.index('TYP');
   M_ATR.prefix('B',);
   {? M_ATR.first()
   || _lrec:=M_ATR.size();
      {!
      |? M_ATR.ADD:=2;
         M_ATR.put(1);
         M_ATR.next()
      !}
   ?};
   M_ATR.cntx_pop();
   __UPG.msg('Zaktualizowano definicje wzorców atrybutów indeksu.');
   __UPG.msg('Przetworzono: %1 zapisów.'[$_lrec])
?};
_res


\del140_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Usuwa parametr 140 systemu - opis
::  ORG: \del140_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie parametru: 140 - Logistyka i produkcja: indeks dla materiałów i usług tworzony wg słowników.'


\eancAKT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Ustawienie znacznika aktywności urządzeń mobilnych
::  ORG: \eancAKT/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_lrec:=0;
_lmod:=0;
EANC.cntx_psh();
EANC.index('IDADD');
EANC.prefix();
{? EANC.first()
|| _lrec:=EANC.size();
   {!
   |? {? EANC.AKT='T' & EANC.IDTERM=''
      || EANC.AKT:='N';
         {? EANC.put(1) || _lmod+=1 ?}
      ?};
      EANC.next()
   !}
?};
EANC.cntx_pop();
__UPG.msg('Zaktualizowano znacznik aktywności urządzeń mobilnych.');
__UPG.msg('Przetworzono: %1 zapisów. Zaktualizowano %2.'[$_lrec,$_lmod]);
_res


\eancAKT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Ustawienie znacznika aktywności urządzeń mobilnych - opis
::  ORG: \eancAKT_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika aktywności urządzeń mobilnych.'


\actualMG
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizuje znacznik MG.IL
::  ORG: \actualMG/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
MG.cntx_psh();
MG.prefix();
_res:=MG.for_each("
   _put:=0;
   __lrec+=1;
   {? MG.IL='' || _put:=1; MG.IL:='N' ?};
   {? MG.COS='' || _put:=1; MG.COS:='N' ?};
   {? _put || {? MG.put() || __lmod+=1 ?} ?}
");
MG.cntx_pop();
__UPG.msg('Zaktualizowano znacznik Magazyn ilościowy dla definicji magazynu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\actualMG_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizuje znacznik MG.IL - opis
::  ORG: \actualMG_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika Magazyn ilościowy dla definicji magazynu.'


\StanPROD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizacja stanów w produkcji
::  ORG: \StanPROD/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_buf:=tab_tmp(2,'MAT','STRING[16]','','MAG','STRING[16]','');
_stoddz:=ST.ODDZ_KOD;
_stodd2:=ST.ODDZ;
ODDZ.cntx_psh();
SM.cntx_psh();
ZL.cntx_psh();
ODDZ.index('KOD');
{? ODDZ.first()
|| {!
   |? _lrec:=0;
      {? form(ODDZ.KOD)<>''
      || ST.ODDZ_KOD:=ODDZ.KOD;
         ST.ODDZ:=ODDZ.KOD;
         SM.use('stm__'+ODDZ.KOD+'zb');
         SM.index('SM');
         SM.prefix();
         ZL.index('SMPROD');
         ZL.prefix('O',ODDZ.KOD,'Z','P','Z');
         {? ZL.first()
         || {!
            |? {? ZL.ILPDOK>0 & ZL.KTM<>null() & ZL.MG<>null() & (_buf.clear(); ~_buf.find_key($ZL.KTM,$ZL.MG))
               || _lrec+=1;
                  _buf.blank();
                  _buf.MAT:=$ZL.KTM;
                  _buf.MAG:=$ZL.MG;
                  _buf.add(1);
                  exec('obl_stan','magazyn_stan',ZL.KTM,1,ZL.MG)
               ?};
               ZL.next()
            !}
         ?};
         ZL.prefix('O',ODDZ.KOD,'Z','P','P');
         {? ZL.first()
         || {!
            |? {? ZL.ILPDOK>0 & ZL.KTM<>null() & ZL.MG<>null() & (_buf.clear(); ~_buf.find_key($ZL.KTM,$ZL.MG))
               || _lrec+=1;
                  _buf.blank();
                  _buf.MAT:=$ZL.KTM;
                  _buf.MAG:=$ZL.MG;
                  _buf.add(1);
                  exec('obl_stan','magazyn_stan',ZL.KTM,1,ZL.MG)
               ?};
               ZL.next()
            !}
         ?};
         __UPG.msg('Zaktualizowano Stan w produkcji dla magazynów oddziału %1.'@[ODDZ.KOD]);
         __UPG.msg('Przetworzono: %1 zapisów.'[$_lrec])
      ?};
      _res & ODDZ.next()
   !}
?};
ODDZ.cntx_pop();
SM.cntx_pop();
ZL.cntx_pop();
obj_del(_buf);
ST.ODDZ_KOD:=_stoddz;
ST.ODDZ:=_stodd2;
_res


\StanPROD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Aktualizacja stanów w produkcji - opis
::  ORG: \StanPROD_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola Stan w produkcji dla stanów magazynowych'


\pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Uzupełnienie pól w tabeli POZF
::  ORG: \pozf/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=POZF.names();
{? _tab.first()
|| {!
   |? {? (8+_tab.NAME+4)<>'____'
      || POZF.use(_tab.NAME); POZF.prefix();
         DOK.use('doku'+(8+_tab.NAME+4)); DOK.prefix();
         echo('Przetwarzanie tabeli POZF maska %1 ...'@[_tab.NAME]);
         POZF.for_each("{? POZF.ODD=null || POZF.put(1) ?}")
      ?};
      _tab.next()
   !}
?};
echo();
__UPG.msg('Uzupelniono dane pozycji faktur dokumentu księgowego.');
1


\pozf_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Uzupełnienie pól w tabeli POZF
::----------------------------------------------------------------------------------------------------------------------
'Uzpełnienie danych pozycji faktur dokumentu księgowego'


\RS_zl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.28]
:: OPIS: Modyfikacja wzorca dla słownika zleceń
::  ORG: \RS_zl/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
RS.cntx_psh(); RS.index('TAB_POLE'); RS.prefix();
{? RS.find_key('ZL','ZL','UNRZL','UNRZL')
|| RS.KOD:='ID_KSG';
   RS.put()
?};
RS.cntx_pop();
__UPG.msg('Zaktualizowano wzorzec dla słownika zleceń.');
1


\RS_zl_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.28]
:: OPIS: Modyfikacja wzorca dla słownika zleceń - opis
::----------------------------------------------------------------------------------------------------------------------
'Modyfikacja wzorca dla słownika zleceń.'


\trn_rs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Transfer słowników uzytkownika o wzorcu 'fiks', 'Przychody/koszty'
::  ORG: \trn_rs/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh(); SLU.prefix();
SLU.for_each("{? SLU.WZ='fiks' | SLU.WZ='Przychody/koszty' || SLU.WZ:='prosty'; SLU.put() ?}");
SLU.cntx_pop();
RS.cntx_psh(); RS.index('TAB'); RS.prefix('PODAT',);
{? RS.first() || RS.del() ?};
RS.cntx_pop();
__UPG.msg('Zmodyfikowano wzorce słowników użytkownika fiks i przychody\\koszty.');
1


\trn_rs_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Transfer słowników uzytkownika o wzorcu 'fiks', 'Przychody/koszty' - opis
::----------------------------------------------------------------------------------------------------------------------
'Modyfikacja wzorców słowników użytkownika fiks i przychody\\koszty'


\upgrade_event
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.28]
:: OPIS: Aktualizacja zdarzeń
::  ORG: \upgrade_event/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fill_tab','#b_event');
__UPG.msg('Zaktualizowano zdarzenia.');
1


\upgrade_event_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.28]
:: OPIS: Aktualizacja zdarzeń - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zdarzeń dla modelera'


\alg_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Uaktualnienie pól tabeli związanych ze sprawozdaniami
::  ORG: \alg_par/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
ALG_PAR.prefix();
ALG_PAR.for_each("
   _put:=0;
   {? ALG_PAR.WYL='' || ALG_PAR.WYL:='N'; _put:=1 ?};
   {? ALG_PAR.MIEJSCE=''
   || ALG_PAR.MIEJSCE:={? ALG_PAR.ROK().PLAN_GR='T' || 'G' || 'F' ?};
      _put:=1
   ?};
   {? _put || ALG_PAR.put() ?}
");
GR_SIK.index('UID');
GR_SIK.prefix();
GR_SIK.for_each("
   {? GR_SIK.UID=''
   || _uid:=GR_SIK.SKROT;
      _nr:=1;
      GR_SIK.cntx_psh();
      {!
      |? {? GR_SIK.find_key(_uid)
         || _uid:=(8+_uid)+(('00'+$_nr)+2);
            _nr+=1;
            1
         ?}
      !};
      GR_SIK.cntx_pop();
      GR_SIK.UID:=_uid;
      GR_SIK.put()
   ?}
");
DEFW.index('UID');
DEFW.prefix();
DEFW.for_each("
   {? DEFW.UID=''
   || DEFW.UID:=exec('set_defw_uid','sprfin');
      DEFW.put()
   ?}
");
__UPG.msg('Zaktualizowano definicje sprawozdań.');
1


\alg_par_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Uaktualnienie pól tabeli ALG_PAR - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie definicji sprawozdań'


\skid_mbn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Uaktualnienie pól tabeli SKID_MBN
::  ORG: \skid_mbn/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBN.prefix();
SKID_MBN.for_each("
   {? SKID_MBN.KONTRPOZ=''
   || SKID_MBN.KONTRPOZ:={? SKID_MBN.KONTROLA='T' || 'F' || 'N' ?};
      exec('k_insmbn','konsolidacja');
      SKID_MBN.put()
   ?}
");
__UPG.msg('Zaktualizowano modele danych.');
1


\skid_mbn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Uaktualnienie pól tabeli SKID_MBN - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie modeli danych'


\con_rodf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Dodanie funkcji dla controllingu
::  ORG: \con_rodf/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
exec('add_rodf1','upgradexpertis',34,'Aktualizacja danych z wykonań','Aktualizacja wartości ze sprawozdań finansowych',3);
__UPG.msg('Dodano funkcję aktualizacji modelu ze sprawozdań finansowych.');
1


\add_rodf1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Dolaczanie rodzajow funkcji - wewnetrzna
::   WE: _a - numer rodzaju funkcji
::       _b - nazwa typu
::       _c - opis
::       _d - numer typu
::  ORG: \add_rodf1/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
CON_RODF.index('UNIK2'); CON_RODF.prefix(_a);
{? CON_RODF.first()
|| CON_RODF.TYP:=_b;
   CON_RODF.OPIS:=_c;
   CON_RODF.NUMER:=_d;
   CON_RODF.put(1)
|| CON_RODF.NUM_TYP:=_a;
   CON_RODF.TYP:=_b;
   CON_RODF.OPIS:=_c;
   CON_RODF.NUMER:=_d;
   CON_RODF.add(1)
?};
CON_RODF.GRUPA:={? CON_RODF.NUM_TYP=12 | CON_RODF.NUM_TYP=16 | CON_RODF.NUM_TYP=27 | CON_RODF.NUM_TYP=28 |
                   CON_RODF.NUM_TYP=11 | CON_RODF.NUM_TYP=13 | CON_RODF.NUM_TYP=14 |
                   CON_RODF.NUM_TYP=2 | CON_RODF.NUM_TYP=5 | CON_RODF.NUM_TYP=4 | CON_RODF.NUM_TYP=23
                || 0
                || 1
                ?};
CON_RODF.put()


\con_rodf_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Dodanie funkcji dla controllingu - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowej funkcji aktualizacyjnej dla controllingu'


\rs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Naprawa formuły dla wzorca SKID_RBK
::  ORG: \rs/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
RS.cntx_psh();
RS.index('TAB_POLE'); RS.prefix();
{? RS.find_key('SKID_RBK','SKID_RBK','KOD','KOD') & RS.TR<>'exec(\'wz_rbk\',\'slo_slu\')'
|| RS.TR:='exec(\'wz_rbk\',\'slo_slu\')';
   {? RS.put()
   || __UPG.msg('Poprawiono formułę na treść dla wzorca słowników opartych o tabelę SKID_RBK.');
      _ok:=1
   ?}
?};
RS.cntx_pop();
{? ~_ok
|| __UPG.msg('Formułę na treść dla wzorca słowników opartych o tabelę SKID_RBK nie wymagała naprawy.')
?};
1


\rs_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Naprawa formuły dla wzorca SKID_RBK - opis
::----------------------------------------------------------------------------------------------------------------------
'Zmiana formuły na treść dla wzorca słowników opartych o tabelę SKID_RBK'


\formuly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Dodanie formuł na firmę wyłączeniową algorytmów wierszy sprawozadnia
::  ORG: \formuly/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
FORMULA.index('FORMULA4');
FORMULA.prefix();
_add:="
   {? FORMULA.find_key('w',_a,)
   || FORMULA.NAZWA:=_b;
      FORMULA.FORMULA:=_c;
      FORMULA.put()
   || FORMULA.RODZAJ:='w';
      FORMULA.SKROT:=_a;
      FORMULA.NAZWA:=_b;
      FORMULA.FORMULA:=_c;
      FORMULA.add(1)
   ?}
";
_add('FIRMA','Firma wyłączeniowa z konta z firmą w analityce',$"exec('kon_firma','konsolidacja',_a)");
_add('^FIRMA','Firma wyłączeniowa z wyróżnika z firmą',$"exec('wyr_firma','konsolidacja')");
_add('KH','Firma wyłączeniowa z konta z kontrahentem w analityce',$"exec('konto_firma_pow','kontrahent',_a)");
_add('^KH','Firma wyłączeniowa z wyróżnika z kontrahentem',$"exec('wyr_firma_pow','kontrahent')");
__UPG.msg('Dodano/uaktualniono formuły na firmę wyłączeniową dla algorytmów wierszy sprawozdań');
1


\formuly_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Dodanie formuł na firmę wyłączeniową algorytmów wierszy sprawozadnia - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie/uaktualnienie formuł na firmę wyłączeniową dla algorytmów wierszy sprawozdań'


\kh_nasz_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Uzupełnienie pola KH_DOD.NASZ_KOD
::  ORG: \kh_nasz_kod/upgrade_1742.fml
::       \zatory/upgrade_2014_07.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__lmop');
__lrec:=__lmod:=__lmop:=0;
KH_DOD.index('KH');
KH.for_each("__lrec+=1; _puk:=0;
   {? 'msd'*(-KH.WIELKOSC)>0 & KH.WIELKOSC<>''
   || KH.WIELKOSC:={? -KH.WIELKOSC='m'
                   || 'mały'
                   |? -KH.WIELKOSC='s'
                   || 'średni'
                   |? -KH.WIELKOSC='d'
                   || 'duży'
                   ?};
      _puk:=1
   ?};
   NIPY.cntx_psh();
   NIPY.index('DEFAULT');
   NIPY.prefix(KH.ref(),'T');
   {? ~NIPY.first()
   || NIPY.prefix(KH.ref());
      {? NIPY.first() & NIPY.size()=1
      || {? KH.NIP_UE<>NIPY.NIP
         || KH.NIP_UE:=NIPY.NIP;
            _puk:=1
         ?}
      || {? KH.NIP_UE<>''
         || KH.NIP_UE:='';
            _puk:=1
         ?}
      ?}
   ?};
   {? _puk & KH.put(1) || __lmod+=1 ?};
   NIPY.cntx_pop();
   {? KH.NASZ_KOD<>''
   || KH_DOD.prefix(KH.ref());
      {? KH_DOD.first()
      || _put:=0;
         {!
         |? KH_DOD.NASZ_KOD:=KH.NASZ_KOD;
            {? KH_DOD.put() & ~_put || _put:=1; __lmop+=1 ?};
            KH_DOD.next()
         !}
      ?}
   ?}
");
__UPG.msg('Uzupełniono pole nasz kod oraz NIP UE u kontrahenta');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmop]);
__UPG.msg('Zmieniono wartości wielkości kontrahenta');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__lmop');
_res


\kh_nasz_kod_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Uzupełnienie pola KH_DOD.NASZ_KOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola nasz kod u kontrahenta (KH_DOD.NASZ_KOD=KH.NASZ_KOD)'


\sch_xd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Uaktualnienie pól tabeli SCH_XD
::  ORG: \sch_xd/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
SCH_XD.cntx_psh(); DK.cntx_psh(); FAP.cntx_psh();
_name:=SCH_XD.names();
{? _name.first()
|| {! |?
      SCH_XD.use(_name.NAME); SCH_XD.prefix();
      SCH_XD.for_each("{? SCH_XD.AUT_GEN=''
                       || SCH_XD.AUT_GEN:='N'; SCH_XD.put()
                       ?};
                       {? SCH_XD.DK<>null & SCH_XD.ND=null
                       || DK.use(ref_name(SCH_XD.DK)); DK.prefix();
                          SCH_XD.ND:=SCH_XD.DK().N; SCH_XD.put()
                       ?};
                       {? SCH_XD.FAP<>null & SCH_XD.FAKS=null
                       || FAP.use(ref_name(SCH_XD.FAP)); FAP.prefix();
                          SCH_XD.FAKS:=SCH_XD.FAP().FAKS; SCH_XD.put()
                       ?}
                      ");
      _name.next()
   !}
?};
SCH_XD.cntx_pop(); DK.cntx_pop(); FAP.cntx_pop();
__UPG.msg('Zaktualizowano podziały controllingowe dla dokumentów sprzedaży, zakupu i magazynowych.')


\sch_xd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Uaktualnienie pól tabeli SCH_XD - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie pól podziałów controllingowych dla logistyki'


\okr_obsg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Uzupełnienie tabeli OKR_OBSG zapisami z OKR_OBSZ - ale tyko dotyczącymi okresów obrachunkowych FK/FST
::  ORG: \okr_obsg/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
OKR_OBSZ.cntx_psh(); OKR_OBSG.cntx_psh();
OKR_OBSZ.prefix(); OKR_OBSG.prefix();
OKR_OBSZ.for_each("{? OKR_OBSZ.OKRO_F<>null
                   || OKR_OBSG.index('B_DOMAIN');
                      OKR_OBSG.prefix(OKR_OBSZ.OKRO_F().ROK().FIRMA,OKR_OBSZ.B_DOMAIN,OKR_OBSZ.OKRO_F);
                      {? ~OKR_OBSG.first()
                      || OKR_OBSG.B_DOMAIN:=OKR_OBSZ.B_DOMAIN;
                         OKR_OBSG.FIRMA:=OKR_OBSZ.OKRO_F().ROK().FIRMA;
                         OKR_OBSG.OKRO_F:=OKR_OBSZ.OKRO_F;
                         OKR_OBSG.add()
                      ?}
                   ?}
                  ");
OKR_OBSZ.cntx_pop(); OKR_OBSG.cntx_pop();
__UPG.msg('Zaktualizowano globalną kartotekę okresów dla obszarów.');
1


\okr_obsg_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Uzupełnienie tabeli OKR_OBSG - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja globalnej kartoteki okresów dla obszarów.\nUzupełnienie tabeli OKR_OBSG.'


\rmk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Uzupełnienie pola RMK.TYP
::  ORG: \rmk/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_names:=RMK.names();
{? _names.first()
|| {!
   |? RMK.use(_names.NAME);
      RMK.prefix();
      RMK.for_each("
         _put:=0;
         {? RMK.TYP='' || RMK.TYP:='RMK'; _put:=1 ?};
         {? RMK.S='' || RMK.S:='M'; _put:=1 ?};
         {? _put || RMK.put() ?}
      ");
      _names.next()
   !}
?};
__UPG.msg('Uzupełniono pole typu rozliczenia międzyokresowego');
1


\rmk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Uzupełnienie pola RMK.TYP - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola typu rozliczenie międzyokresowego'


\con_rodf_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.02]
:: OPIS: Dodanie wszystkich funkcji dla controllingu
::  ORG: \con_rodf_all/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
CON_RODF.cntx_psh();
exec('add_rodf','!ctr_pdm_patd');
CON_RODF.cntx_pop();
__UPG.msg('Dodano wszystkie funkcje aktualizacyjne dla controllingu.');
1


\con_rodf_all_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.02]
:: OPIS: Dodanie wszystkich funkcji dla controllingu - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie wszystkich funkcji aktualizacyjnych dla controllingu'


\ROK_F
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnia zawartość pola ZK w tabeli ROK_F
::  ORG: \ROK_F/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
ROK_F.prefix();
__prec:=0;
_result:=ROK_F.for_each("{? ROK_F.ZK='' || ROK_F.ZK:='N'; ROK_F.put(); __prec+=1 ?}",1);
__UPG.msg('Zaktualizowano lata obrachunkowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$ROK_F.size(),$__prec]);
VAR_DEL.delete('__prec');
ROK_F.cntx_pop();
_result


\ROK_F_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Opis formuły uzupełniającej zawartość pola ZK w tabeli ROK_F
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w latach obrachunkowych.'


\zus_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Uzupełnianie słownika ~Płatności ZUS i typów przelewów
::  ORG: \zus_one/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,FINFO);
SKID_RBK.cntx_psh(); SLU.cntx_psh(); SLO.cntx_psh(); BANORG.cntx_psh(); B.cntx_psh();
SKID_RBK.index('TAB'); SKID_RBK.prefix();
SLU.index('WZORZEC'); SLU.prefix('prosty','~Płatności ZUS');
{? SLU.first()
|| SLO.index('SL'); SLO.prefix(SLU.ref());

   KRAJE.cntx_psh();
   KRAJE.index('KRAJE');
   KRAJE.prefix('PL');
   {? KRAJE.first() || _kraj_ref:=KRAJE.ref() || _kraj_ref:=null ?};
   KRAJE.cntx_pop();

   BANORG.index('NDX');
   BANORG.prefix('60000','NBP ZUS',);

   _org:=_bank:=null;

   {? BANORG.first()
   || _org:=BANORG.ref()
   || BANORG.blank();
      BANORG.KOD:='60000';
      BANORG.KODZ:='';
      BANORG.AKTYWNY:='T';
      BANORG.NAZ:='NBP ZUS';
      BANORG.NRCB:='';
      {? BANORG.add() || _org:=BANORG.ref() ?}
   ?};
   {? _org
   || B.index('BANK');
      B.prefix('NBP ZUS SKŁADKI','60000002');
      {? B.first()
      || _bank:=B.ref()
      || B.blank();
         B.BANORG:=_org;
         B.KOD:='';
         B.KODISO:=_kraj_ref;
         B.NB:='NBP ZUS SKŁADKI';
         B.NUMER:='60000002';
         B.STATUS:='B';
         {? B.add() || _bank:=B.ref() ?}
      ?}
   ?};

   {? _kraj_ref & ~SLO.find_key('ZUS')
   || SLO.blank();
      SLO.KOD:='ZUS';
      SLO.TR:='ZUS e-składka';
      {? SLO.add()
      || SKID_RBK.prefix(REF.FIRMA,'ZUS','ZUS',0,SLO.ref());
         {? ~SKID_RBK.first()
         || SKID_RBK.prefix();
            SKID_RBK.blank(1);
            SKID_RBK.TAB:='ZUS';
            SKID_RBK.INNWAL:='N';
            SKID_RBK.RS:='N';
            SKID_RBK.RD:='N';
            SKID_RBK.FIRMA:=REF.FIRMA;
            SKID_RBK.SLU:=SLU.ref();
            SKID_RBK.SLO:=SLO.ref();
            SKID_RBK.N:='';
            SKID_RBK.BANK:=_bank;
            SKID_RBK.KRAJ:=_kraj_ref;
            SKID_RBK.WAL:=FINFO.NAROD;
            SKID_RBK.add()
         ?}
      ?}
   |? _kraj_ref & SLO.find_key('ZUS')
   || SKID_RBK.prefix(REF.FIRMA,'ZUS','ZUS',0,SLO.ref());
      {? ~SKID_RBK.first()
      || SKID_RBK.prefix();
         SKID_RBK.blank(1);
         SKID_RBK.TAB:='ZUS';
         SKID_RBK.INNWAL:='N';
         SKID_RBK.RS:='N';
         SKID_RBK.RD:='N';
         SKID_RBK.FIRMA:=REF.FIRMA;
         SKID_RBK.SLU:=SLU.ref();
         SKID_RBK.SLO:=SLO.ref();
         SKID_RBK.N:='';
         SKID_RBK.BANK:=_bank;
         SKID_RBK.KRAJ:=_kraj_ref;
         SKID_RBK.WAL:=FINFO.NAROD;
         SKID_RBK.add()
      ?}
   ?}
?};
SKID_RBK.cntx_pop(); SLU.cntx_pop(); SLO.cntx_pop(); BANORG.cntx_pop(); B.cntx_pop();
__UPG.msg('Zaktualizowano listę rachunków bankowych do płatności ZUS.');
TZL.cntx_psh();
TZL.index('KOD'); TZL.prefix();
TZL.KOD:='PPL: ZUS'; TZL.OPIS:='Przelewy z płac dla ZUS';
{? ~TZL.find_key(TZL.KOD,) || TZL.add(1) ?};
TZL.cntx_pop();
__UPG.msg('Zaktualizowano słownik typów przelewów bankowych.');
1


\zus_one_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS:  Uzupełnianie słownika ~Płatności ZUS i typów przelewów - opis
::----------------------------------------------------------------------------------------------------------------------
' Uzupełnianie słownika ~Płatności ZUS i typów przelewów'


\CDTAB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia tabelę CDTAB
::  ORG: \CDTAB/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
CDTAB.cntx_psh();
CDTAB.index('CDTAB');
CDTAB.prefix();
_ok:=0;
{? ~CDTAB.find_key('KH',)
|| CDTAB.blank();
   CDTAB.AKRONIM:='KH';
   {? CDTAB.add() || _ok+=1 ?}
|| _ok+=1
?};
{? ~CDTAB.find_key('SRSR',)
|| CDTAB.blank();
   CDTAB.AKRONIM:='SRSR';
   {? CDTAB.add() || _ok+=1 ?}
|| _ok+=1
?};
CDTAB.cntx_pop();
{? _ok=2 || _result:=1 || _result:=0 ?};
{? _result
|| __UPG.msg('Zaktualizowano listę tabel z obsługą cech dodatkowych.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji listy tabel z obsługą cech dodatkowych.')
?};
_result


\CDTAB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia tabelę CDTAB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy tabel z obsługą cech dodatkowych.'


\cit25
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.22]
:: OPIS: ER/WRT/XP/12.51/1804/0003 CIT (25) wiersze 103 i 104
::       ER/WRT/XP/12.51/1803/0038 Błędny opis na sprawozdaniu finansowym i deklaracji CIT-8/O wersja 25
::  ORG: \cit25/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
GR_SIK.cntx_psh(); DEFW.cntx_psh();
GR_SIK.index('SYSSKROT'); GR_SIK.prefix(REF.FIRMA,'FKS');
{? GR_SIK.find_key('CIT-8/O_25') & GR_SIK.AKC<>'T'
|| DEFW.index('GRUPA');
   DEFW.prefix(GR_SIK.ref(),'25');
   {? DEFW.first
   || DEFW.NAZWA:='Dotacje otrzymane z budżetu państwa - wolne od podatku na podst.art.17ust.1pkt47';
      DEFW.put()
   ?}
?};
GR_SIK.cntx_pop(); DEFW.cntx_pop();
exec('fiks_30a','upgradexpertis',103,'exec(\'poz_edek_plus\',\'xml\',103)');
exec('fiks_30a','upgradexpertis',104,'exec(\'poz_edek_plus\',\'xml\',104)');
__UPG.msg('Zaktualizowano sprawozdania CIT.');
1


\cit25_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.22]
:: OPIS: Poprawki spisowe do CIT (25)
::----------------------------------------------------------------------------------------------------------------------
'Formuła naprawcza do CIT (25)'


\fiks_30a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.22]
:: OPIS: Formula pomocnicza do cit25
::   WE: _a - nr pozycji deklaracji
::       _b - tresc formuly
::  ORG: \fiks_30a/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','CIT8','CIT8',25);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('P_'+$_a,)
      || ISTDEFS.REGULY:=_b;
         ISTDEFS.put()
      ?}
   ?}
?};
__UPG.msg('Zaktualizowano sprawozdanie CIT.');
1


\sp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Aktualizacja danych związanych z podzielymi płatnościami (Split Payment)
::  ORG: \sp/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
AktFun:=obj_new('akt','msg','is');
AktFun.akt:=AktFun.is:=0;
AktFun.msg:="
   {? AktFun.akt
   || AktFun.is:=1;
      __UPG.msg('Zaktualizowano pole dla '+_a+'.');
      AktFun.akt:=0
   ?}
";
KS.prefix();
KS.for_each("
   {? KS.SP='' || KS.SP:='N'; AktFun.akt:=1; KS.put() ?}
");
AktFun.msg('kont księgowych');
DOK_REJ.prefix();
DOK_REJ.for_each("
   {? DOK_REJ.SP_PRZEL='' || DOK_REJ.SP_PRZEL:='N'; AktFun.akt:=1; DOK_REJ.put() ?};
   {? DOK_REJ.FORM_SYM='exec(\\'sym_dok\\',\\'skid\\')'
   || DOK_REJ.FORM_SYM:='exec(\\'sym_dok\\',\\'fks_auto\\')'; AktFun.akt:=1; DOK_REJ.put()
   ?}
");
AktFun.msg('rodzajów dokumentów');
OP.cntx_psh();
ZAP_OP.cntx_psh();
_op:=OP.names();
{? _op.first()
|| {!
   |? echo('Aktualizacja rozrachunków: %1'@[_op.NAME]);
      OP.use(_op.NAME);
      OP.prefix();
      OP.for_each("
         {? OP.SP='' || OP.SP:='N'; AktFun.akt:=1; OP.put() ?}
      ");
      _op.next()
   !}
?};
AktFun.msg('rozrachunków');
_zo:=ZAP_OP.names();
{? _zo.first()
|| {!
   |? echo('Aktualizacja zapisów rozrachunków: %1'@[_zo.NAME]);
      OP.use('operac'+(_zo.NAME+2));
      ZAP_OP.use(_zo.NAME);
      ZAP_OP.prefix();
      ZAP_OP.for_each("
         {? ZAP_OP.SP_TYP='' || ZAP_OP.SP_TYP:='NN'; AktFun.akt:=1; ZAP_OP.put() ?}
      ");
      _zo.next()
   !}
?};
AktFun.msg('zapisów rozrachunków');
echo();
ZAP_OP.cntx_pop();
OP.cntx_pop();
_poz:=POZ.names();
{? _poz.first()
|| POZ.cntx_psh();
   DOK.cntx_psh();
   {!
   |? echo('Aktualizacja pozycji dokumentów księgowych: %1'@[_poz.NAME]);
      POZ.use(_poz.NAME); POZ.prefix();
      DOK.use('doku'+(_poz.NAME+4)); DOK.prefix();
      DOK.for_each("DOK.put()");
      POZ.for_each("{? POZ.SP_KS='' || POZ.SP_KS:='NN'; AktFun.akt:=1 ?}; POZ.put()");
      _poz.next()
   !};
   DOK.cntx_pop();
   POZ.cntx_pop()
?};
AktFun.msg('pozycji dokumentów księgowych');
echo();
{? AktFun.is=0
|| __UPG.msg('Aktualizacja nie była wymagana.')
?};
VAR_DEL.delete('AktFun');
1


\sp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Aktualizacja danych związanych z podzielymi płatnościami (Split Payment) - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych na potrzeby podzielnej płatności'


\KH_DOD_SP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pole KH_DOD.SP (split-payment)
::  ORG: \KH_DOD_SP/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_formula:="_put:=0;
           {? KH_DOD.SP='' || _put:=1; KH_DOD.SP:='N' ?};
           {? KH_DOD.BL_ACT='Rejestruj dokument w obiegu' || _put:=1; KH_DOD.BL_ACT:='Rejestruj fakturę w obiegu' ?};
           {? KH_DOD.KSEFKH='' || KH_DOD.KSEFKH:='N'; _put:=1 ?};
           {? KH_DOD.KSEFLI='' || KH_DOD.KSEFLI:='N'; _put:=1 ?};
           {? _put || KH_DOD.put() ?}";
KH_DOD.prefix();
_result:=KH_DOD.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pole w kartotece danych kontrahentów.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowego pola w kartotece danych kontrahentów.')
?};
_result


\KH_DOD_SP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pola w tabeli KH_DOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki danych kontrahentów.'


\PB_SP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pole PB.SP (split-payment)
::  ORG: \PB_SP/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_formula:="{? PB.SP='' || PB.SP:='N'; PB.put() ?}";
PB.cntx_psh();
_maska:=PB.names();
_result:=1;
{? _maska.first()
|| {! |?
      PB.use(_maska.NAME); PB.prefix();
      echo('Przetwarzanie tabeli PB maska %1 ...'@[_maska.NAME]);
      _result:=PB.for_each(_formula);
      _result & _maska.next()
   !}
?};
{? _result
|| __UPG.msg('Zaktualizowano nowe pole w kartotece przelewów elektronicznych.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowego pola w kartotece przelewów elektronicznych.')
?};
PB.cntx_pop();
echo();
_result


\PB_SP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pola w tabeli PB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki przelewów elektronicznych.'


\PW_SP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pole PW.SP (split-payment)
::  ORG: \PW_SP/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_formula:="{? PW.SP='' || PW.SP:='N'; PW.put() ?}";
PW.cntx_psh();
_maska:=PW.names();
_result:=1;
{? _maska.first()
|| {! |?
      PW.use(_maska.NAME);
      PW.prefix();
      echo('Przetwarzanie tabeli PW maska %1 ...'@[_maska.NAME]);
      _result:=PW.for_each(_formula);
      _result & _maska.next()
   !}
?};
{? _result
|| __UPG.msg('Zaktualizowano nowe pole w kartotece pozycji wyciągów bankowych.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowego pola w kartotece pozycji wyciągów bankowych.')
?};
PW.cntx_pop();
echo();
_result


\PW_SP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Uzupełnia pola w tabeli PW - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki pozycji wyciągów bankowych.'


\vat_ue
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1703/0055
::       Deklaracja VATUE w wersji 4 dla osoby fizycznej wymagane dodatkowe pole NIP
::  OLD: \fiks_25/napraw_f.fml
::  ORG: \vat_ue/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
exec('vat_uea','upgradexpertis',4,'POD');
exec('vat_uea','upgradexpertis',4,'PODK');
__UPG.msg('Poprawiono błąd do deklaracji VAT UE.')


\vat_ue_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1703/0055  Deklaracja VATUE w wersji 4 dla osoby fizycznej wymagane dodatkowe pole NIP
::----------------------------------------------------------------------------------------------------------------------
'Poprawka spisowa do deklaracji VAT UE'


\vat_uea
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.30]
:: OPIS: Formula pomocnicza do vat_ue
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_b,_b,_a);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_b,VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('etd:OsobaFizyczna')
      || _ref:=ISTDEFS.ref();
         ISTDEFS.index('DRZEWO');

         ISTDEFS.prefix(ISTDEFS.ISTDEF,#_ref);
         {? ISTDEFS.first()
         || {! |? {? ISTDEFS.OPIS='etd:PESEL'
                  || ISTDEFS.del
                  |? ISTDEFS.OPIS='etd:NIP'
                  || ISTDEFS.REGULY:='STR.gsub(XINFO.NIP,\'-\',\'\')';
                     ISTDEFS.WYM:='T';
                     ISTDEFS.put();
                     ISTDEFS.next()
                  || ISTDEFS.next()
                  ?}
            !}
         ?}
      ?}
   ?}
?};
1


\rp7_os
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Zmiany w tabeli RP7_OS.
::   WE:
::   WY:
::  ORG: \rp7_os/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_stat:=obj_new('JO');
_stat.JO:=0;
params_set('stat',_stat);

P.cntx_psh();
RP7_OS.cntx_psh();
RP7_OS.prefix();
__UPG.msg('Liczba rekordów do przetworzenia: %1' [$RP7_OS.size()]);
RP7_OS.for_each(
   "  {? RP7_OS.P<>null() & RP7_OS.WYDZIAL<>RP7_OS.P().WYDZIAL
      || RP7_OS.WYDZIAL:=RP7_OS.P().WYDZIAL;
         params_get().stat.JO+=RP7_OS.put()
      ?}
   ",1);
RP7_OS.cntx_pop();
P.cntx_pop();
__UPG.msg('Liczba zaaktualizowanych jednostek organizacyjnych: %1' [$_stat.JO]);
1


\rp7_os_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Zmiany w tabeli RP7_OS - opis.
::   WE:
::   WY:
::  ORG: \rp7_os_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Zmiany w tabeli RP7_OS'


\rp7_kw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Zmiany w tabeli RP7_KW.
::   WE:
::   WY:
::  ORG: \rp7_kw/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_stat:=obj_new('T','W');
_stat.T:=_stat.W:=0;
params_set('stat',_stat);
R.cntx_psh();
R.prefix();
RP7_KW.cntx_psh();
RP7_KW.index('ZAPIS');
RP7_KW.prefix();
__UPG.msg('Liczba rekordów do przetworzenia: %1' [$RP7_KW.size()]);
RP7_KW.for_each("
   _stat:=params_get().stat;
   _put:=0;
   {? _poz:='SZNWU'*RP7_KW.T
   || RP7_KW.T:=$_poz;
      _stat.T+=1;
      _put+=1
   ?};
   {? RP7_KW.R<>null() & RP7_KW.W=''
   || RP7_KW.W:=RP7_KW.R().RT;
      _stat.W+=1;
      _put+=1
   ?};
   {? _put
   || RP7_KW.put()
   ?}
",1);
RP7_KW.cntx_pop();
R.cntx_pop();
{? _stat.T
|| __UPG.msg('Zmieniono wartości pola RP7_KW.T. Liczba zmian: %1' [$_stat.T])
|| __UPG.msg('Wartości pola RP7_KW.T nie wymagały zmian.')
?};
{? _stat.W
|| __UPG.msg('Zmieniono wartości pola RP7_KW.W. Liczba zmian: %1' [$_stat.T])
|| __UPG.msg('Wartości pola RP7_KW.W nie wymagały zmian.')
?};
1


\rp7_kw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Zmiany w tabeli RP7_KW - opis.
::   WE:
::   WY:
::  ORG: \rp7_kw_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Zmiany funkcjonalne w tabeli wyjaśnień składników na zaświadczeniu ZUS Rp-7'


\kom_rp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Potrącenia inne - nie występujące w KP (pojazdy, SIM).
::   WE:
::   WY:
::  ORG: \kom_rp/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
exec('kom_rp','dane_startowe');
__UPG.msg('Uzupełnienie słownika potrąceń komorniczych.');
1


\kom_rp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Potrącenia inne - nie występujące w KP (pojazdy, SIM) - opis.
::   WE:
::   WY:
::  ORG: \kom_rp_desc/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Potrącenia inne - nie występujące w KP (pojazdy, SIM)'


\slo_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Utworzenie nowych słowników kadrowych.
::   WE:
::   WY:
::  ORG: \slo_typ/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_err:=0;
{! _lp:=1 .. 6
|! _symbol:='PROJWSK'+$_lp;
   {? exec('slo_typ','ext_slo',_symbol)=null()
   || _err+=1;
      __UPG.msg('%1 - utworzenie typu słownika kadrowego nie powiodło się.' [_symbol])
   || __UPG.msg('%1 - typ słownika kadrowego został utworzony lub istniał.' [_symbol])
   ?}
!};
{? _err>0
|| -1
|| 1
?}


\slo_typ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Utworzenie nowych słowników kadrowych - opis.
::   WE:
::   WY:
::  ORG: \slo_typ_desc/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Utworzenie nowych słowników kadrowych'


\rp7_blok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Uzupełnienie nowych pól związanych z blokadą zapisów: RP7_KP.BLOKADA, RP7_NB.BLOKADA.
::   WE:
::   WY:
::  ORG: \rp7_blok/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_stat:=obj_new('kp1','kp0','nb1','nb0');
{! _lp:=1 .. obj_len(_stat)
|! _stat[_lp]:=0
!};

params_set('stat',_stat);

RP7_KP.cntx_psh();
RP7_KP.prefix();
RP7_KP.for_each(
   "  _stat:=params_get().stat;
      {? RP7_KP.BLOKADA=''
      || _stat.kp0+=1;
         RP7_KP.BLOKADA:='N';
         RP7_KP.put()
      || _stat.kp1+=1
      ?}
   ",1);
RP7_KP.cntx_pop();
__UPG.msg('Wynagrodzenie [zapisów poprawnych: %1, zapisów poprawionych: %2].' [$_stat.kp1,$_stat.kp0]);

RP7_NB.cntx_psh();
RP7_NB.prefix();
RP7_NB.for_each(
   "  _stat:=params_get().stat;
      {? RP7_NB.BLOKADA=''
      || _stat.nb0+=1;
         RP7_NB.BLOKADA:='N';
         RP7_NB.put()
      || _stat.nb1+=1
      ?}
   ",1);
RP7_NB.cntx_pop();
__UPG.msg('Nieobecności [zapisów poprawnych: %1, zapisów poprawionych: %2].' [$_stat.nb1,$_stat.nb0]);

1


\rp7_blok_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Uzupełnienie nowych pól związanych z blokadą zapisów: RP7_KP.BLOKADA, RP7_NB.BLOKADA - opis
::   WE:
::   WY:
::  ORG: \rp7_blok_desc/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól związanych z blokadą zapisów dotyczących zaświadczenia ZUS Rp-7'


\stalesys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [17.42]
::  MOD: DG [12.51]
:: OPIS: Dodanie pol do KST
::   WE: [_a] [INTEGER] - Wykonanie dla wszystkich firm jeden raz [1 - tak, 0 - nie]
::  ORG: \wem_wek/upgrade_1742.fml
::  ORG: \wnr_zc/upgrade_1742.fml
::  ORG: \ku_l50/upgrade_1802.fml
::  ORG: \stalesys/upgrade_1822.fml
::  ORG: \oddelegowania_KST/upgrade_1842.fml
::  ORG: \kw_wolna_zas/upgrade_1842.fml
::  ORG: \stalesys/upgrade_1842.fml
::  ORG: \KST_ZUS_OSW/upgrade_1902.fml
::  ORG: \aktualizacja_PPK/upgrade_1942.fml
::  ORG: \ku_10_2019/upgrade_1942.fml
::  ORG: \KST_PDGN/upgrade_1942.fml
::  ORG: \kst_kus/upgrade_2014.fml
::  ORG: \wnr_rh/upgrade_2042.fml
::  ORG: \margines_we_wy/upgrade_2042.fml
::  ORG: \kst_pl/upgrade_2137_02.fml
::  ORG: \kst_lim_skzw/upgrade_2137_11.fml
::  ORG: \kst_pl/upgrade_2137_13.fml
::  ORG: \KST_upd/upgrade_2226.fml
::  ORG: \PIT_2022_akt_2226_11/upgrade_2226.fml
::  ORG: \stalesys_PL2023/upgrade_2226_06.fml
::----------------------------------------------------------------------------------------------------------------------
:: UWAGA
:: Formuła aktualizuje stałe systemu, które są wykonywane zarówno raz dla całego systemu, jak i wielekorotnie
:: w każdej firmie z osobna. Sterowanie za pomocą zmiennej _runOnlyOnce
:: * 1 - stałe aktualizowane raz dla całego systemu (domyślnie),
:: * 0 - stałe aktualizowane wielokrotnie dla każdej firmy z osobna

_runOnlyOnce:={? var_pres('_a')=type_of(0) & _a=0 || 0 || 1 ?};

:: Stałe systemu, które są wspólne i powinny być wykonane tylko raz
{? _runOnlyOnce
||

:: ORG: \wem_wek/upgrade_1742.fml
   exec('add_acr','#stalesys','WEM','Wiek emerytalny: mężczyźni','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.WEM','0',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.WEM','65',date(2017,10,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o wieku emerytalnym mężczyzn.')
   ?};
   exec('add_acr','#stalesys','WEK','Wiek emerytalny: kobiety','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.WEK','0',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.WEK','60',date(2017,10,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o wieku emerytalnym kobiet.')
   ?};

:: ORG: \ku_l50/upgrade_1802.fml
   exec('add_acr','#stalesys','KU_L50','Kwota ograniczenia 50% kosztów uzyskania','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.KU_L50','0',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.KU_L50','42764',date(2013,01,1));
   _dodano+=exec('kst_war_add','#stalesys','KST.KU_L50','85528',date(2018,01,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o kwocie ograniczenia 50% kosztów uzyskania.')
   ?};

:: ORG: \stalesys/upgrade_1822.fml
   _dodano:=
      (exec('add_acr','#stalesys','AIP_PASS',,'N','N','T','XINFO','ROD')<>0)+
      (exec('add_acr','#stalesys','AIP_SYS',,'N','N','T','XINFO','ROD')<>0)+
      (exec('add_acr','#stalesys','AIP_URL',,'N','N','T','XINFO','ROD')<>0)+
      (exec('add_acr','#stalesys','AIP_USR',,'N','N','T','XINFO','ROD')<>0)+
      (exec('add_acr','#stalesys','EM_IDO',,'N','N','T','XINFO','ROD')<>0)+
      (exec('add_acr','#stalesys','NA_IDO',,'N','N','T','XINFO','ROD')<>0)+
      (exec('add_acr','#stalesys','IM_IDO',,'N','N','T','XINFO','ROD')<>0);
   {? _dodano
   || __UPG.msg('Dodano definicje stałych związanych z obsługą RODO.')
   ?};

:: ORG: \oddelegowania_KST/upgrade_1842.fml
   exec('add_acr','#stalesys','LKZ','Limit kosztów zakwaterowania','T','T','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.LKZ','500',date())
   || __UPG.msg('Dodano do stałych systemu informacje o limicie kosztów zakwaterowania.')
   ?};
   exec('add_acr','#stalesys','PDBP','Procent diety bez podatku','T','T','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.PDBP','30',date())
   || __UPG.msg('Dodano do stałych systemu informacje o procencie diet bez podatku.')
   ?};

:: ORG: \kw_wolna_zas/upgrade_1842.fml
   exec('add_acr','#stalesys','KWWOLALI','Kwota wolna potrącenia alimentacyjne','T','T','T','KST','PPL');
   {? (exec('kst_war_add','#stalesys','KST.KWWOLALI','0',date(0,0,0))<>0)+
      (exec('kst_war_add','#stalesys','KST.KWWOLALI','500',date(2018,7,1))<>0)
   || __UPG.msg('Dodano do stałych systemu informacje o kwocie wolnej potrącenia alimentacyjnego.')
   ?};
   exec('add_acr','#stalesys','KWWOLPOZ','Kwota wolna potrącenia niealimentacyjne','T','T','T','KST','PPL');
   {? (exec('kst_war_add','#stalesys','KST.KWWOLPOZ','0',date(0,0,0))<>0)+
      (exec('kst_war_add','#stalesys','KST.KWWOLPOZ','825',date(2018,7,1))<>0)
   || __UPG.msg('Dodano do stałych systemu informacje o kwocie wolnej potrącenia niealimentacyjnego.')
   ?};

:: ORG: \stalesys/upgrade_1842.fml
   __UPG.msg('Prognozowane przeciętne miesięczne wynagrodzenie brutto w gospodarce narodowej:');
   _acr:='PRGWYN';
   {? exec('add_acr','#stalesys',_acr,'Prognozowane miesięczne','T','T','T','KST','PPL')
   || __UPG.msg(' * Parametr został dodany do widoku.')
   || __UPG.msg(' * Parametr nie został dodany do widoku (istniał już?).')
   ?};
   _fld:='KST.'+_acr;
   {? exec('kst_war_add','#stalesys',_fld,'0',date(0,0,0))
   || __UPG.msg(' * Zapisano wartość początkową parametru.')
   || __UPG.msg(' * Wartość początkowa parametru nie została zapisana (istniała już?).')
   ?};
   _dt:=date(2018,1,1);
   {? exec('kst_war_add','#stalesys',_fld,'4443',_dt)
   || __UPG.msg(' * Zapisano wartość parametru obowiązującą od %1.' [_dt$1])
   || __UPG.msg(' * Wartość parametru obowiązująca od %1 nie została zapisana (istniała już?).' [_dt$1])
   ?};

:: ORG: \aktualizacja_PPK/upgrade_1942.fml
   exec('add_acr','#stalesys','PPKEMAIL','E-mail do powiadomień','T','T','T','KST_PPK','PPK');
   exec('kst_war_add','#stalesys','KST_PPK.PPKEMAIL','\'S\'',date(2019,7,1));

:: ORG: \akt_1922_05/upgrade_1922.fml
   {? var_pres('ZWPOD',KST)>0 & var_pres('ZWPODW',KST)>0
   || exec('add_acr','#stalesys','ZWPOD','Zwolnienie od podatku - Limit','T','T','T','KST','PPL');
      exec('kst_war_add','#stalesys','KST.ZWPOD','0.00',date(0,0,0));
      exec('kst_war_add','#stalesys','KST.ZWPOD','35636.67',date(2019,8,1));
      exec('kst_war_add','#stalesys','KST.ZWPOD','85528.00',date(2020,1,1));
      exec('add_acr','#stalesys','ZWPODW','Zwolnienie od podatku - Wiek','T','T','T','KST','PPL');
      exec('kst_war_add','#stalesys','KST.ZWPODW','0',date(0,0,0));
      exec('kst_war_add','#stalesys','KST.ZWPODW','26',date(2019,8,1));
      __UPG.msg('Wykonano aktualizację historii stałych systemu dla zwolnienia od podatku (art.21 ust.1 pkt 148).')
   || __UPG.msg('Brak pól w stałych systemu dla zwolnienia od podatku (art.21 ust.1 pkt 148).')
   ?};

:: ORG: \ku_10_2019/upgrade_1942.fml
   {? exec('kst_war_add','#stalesys','KST.KU','250.00',date(2019,10,1)) &
      exec('kst_war_add','#stalesys','KST.KUZ','300.00',date(2019,10,1))
   || __UPG.msg('Dodano wartości kosztów uzyskania podstawowych: 250.00, zwiększonych: 300.00 od 2019.10.01.')
   || __UPG.msg('Nie wykonano ustalenia wartości kosztów uzyskania podstawowych, zwiększonych od 2019.10.01.')
   ?};
   {? exec('kst_war_add','#stalesys','KST.UL','43.76',date(2019,10,1))
   || __UPG.msg('Dodano wartość ulgi miesięcznej: 43.76 od 2019.10.01.')
   || __UPG.msg('Nie wykonano ustalenia wartości ulgi miesięcznej od 2019.10.01.')
   ?};
   {? exec('kst_war_add','#stalesys','KST.UL_ROK','548.30',date(2019,10,1)) &
      exec('kst_war_add','#stalesys','KST.UL_ROK','525.12',date(2020,1,1))
   || __UPG.msg('Dodano wartości ulgi rocznej 548.30 od 2019.10.01 oraz 525.12 od 2020.01.01.')
   || __UPG.msg('Nie wykonano ustalenia wartości ulgi rocznej od 2019.10.01 oraz 2020.01.01.')
   ?};

:: ORG: \kst_kus/upgrade_2014.fml
   _dodano:=0;
   {? exec('add_acr','#stalesys','KUS','Koszty uzyskania przychodu - szczególne','T','T','T','KST','PPL','PKD','PRC')
      |
      exec('upd_acr','#stalesys','KUS','Koszty uzyskania przychodu - szczególne','T','T','T','KST','PPL','PKD','PRC')
   || {? exec('kst_war_add','#stalesys','KST.KUS','111.25',date(0,0,0))
         &
         exec('kst_war_add','#stalesys','KST.KUS','250',date(2019,10,1))
      || _dodano:=1
      ?}
   ?};
   _txt:={? _dodano
         || 'Wykonano aktualizację historii stałych systemu dla KST.KUS - Koszty uzyskania przychodu - szczególne'
         || 'Błąd. Nie udało się dodać/zaktualizować stałej systemu KST.KUS - Koszty uzyskania przychodu - szczególne'
         ?};
   __UPG.msg(_txt);

:: ORG: \kst_pl/upgrade_2137_02.fml
   exec('add_acr','#stalesys','ZWPODE','Zwolnienie od podatku - emeryci','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.ZWPODE','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.ZWPODE','85528',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o zwolnieniu od podatku dla emerytów.')
   ?};
   exec('add_acr','#stalesys','ZWPODD','Zwolnienie od podatku - dla rodzin wielodzietnych','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.ZWPODD','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.ZWPODD','85528',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o zwolnieniu od podatku dla rodzin wielodzietnych.')
   ?};
   exec('add_acr','#stalesys','ZWPODP','Zwolnienie od podatku - powracający z zagranicy','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.ZWPODP','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.ZWPODP','85528',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o zwolnieniu od podatku dla powracających z zagranicy.')
   ?};
   exec('add_acr','#stalesys','ULGA_KS1','Pierwszy próg naliczenia ulgi dla klasy średniej','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.ULGA_KS1','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.ULGA_KS1','5701.00',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o pierwszym progu naliczenia ulgi dla klasy średniej.')
   ?};
   exec('add_acr','#stalesys','ULGA_KS2','Drugi próg naliczenia ulgi dla klasy średniej','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.ULGA_KS2','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.ULGA_KS2','8549.00',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o drugim progu naliczenia ulgi dla klasy średniej.')
   ?};
   exec('add_acr','#stalesys','ULGA_KS3','Trzeci próg naliczenia ulgi dla klasy średniej','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.ULGA_KS3','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.ULGA_KS3','11141.00',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o trzecim progu naliczenia ulgi dla klasy średniej.')
   ?};
   exec('add_acr','#stalesys','LIMPUU','Limit po ustaniu ubezpieczenia','T','T','T','KST_PAR','PKD,PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST_PAR.LIMPUU','0',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST_PAR.LIMPUU','91',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o ilości dni zasiłkowych po ustaniu zatrudnienia.')
   ?};
   {? exec('kst_war_add','#stalesys','KST.UL','425.00',date(2022,1,1)) &
      exec('kst_war_add','#stalesys','KST.UL_ROK','5100.00',date(2022,1,1))
   || __UPG.msg('Zaktualizowano informacje o uldze podatkowej.')
   ?};
   {? exec('kst_war_add','#stalesys','KST.KU_L50','120000.00',date(2022,1,1))
   || __UPG.msg('Zaktualizowano informacje o kwocie ograniczenia 50% kosztów uzyskania.')
   ?};

:: ORG: \kst_lim_skzw/upgrade_2137_11.fml
   exec('add_acr','#stalesys','LIM_SKZW','Limit odliczenia od dochodu składek członkowskich','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.LIM_SKZW','0',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.LIM_SKZW','300.00',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o limicie odliczenia od dochodu składek członkowskich.')
   ?};

:: ORG: \kst_pl/upgrade_2137_13.fml
   exec('add_acr','#stalesys','PZ_LP','Limit przychodów dla przesunięcia zaliczki','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.PZ_LP','0',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.PZ_LP','12800',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o limicie przychodów dla przesunięcia zaliczki.')
   ?};

:: ORG: \KST_upd/upgrade_2226.fml
   exec('add_acr','#stalesys','PW_TWP','Procent wynagrodzenia za terminową wpłatę podatków','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.PW_TWP','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.PW_TWP','0.30',date(2013,1,1));
   _dodano+=exec('kst_war_add','#stalesys','KST.PW_TWP','0.60',date(2022,7,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o procencie wynagrodzenia za terminową wpłatę podatków.')
   ?};

:: ORG: \PIT_2022_akt_2226_11/upgrade_2226.fml
   _dodano:=exec('kst_war_add','#stalesys','KST.LIM_SKZW','500.00',date(2022,7,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o limicie 500 zł odliczenia od dochodu składek członkowskich od lipca 2022.')
   ?};

::  ORG: \stalesys_PL2023/upgrade_2226_06.fml
   _dodano:=exec('upd_acr','#stalesys','UL','Ulga podatkowa 1/12',,,,'KST');
   exec('add_acr','#stalesys','UL2','Ulga podatkowa 1/24','T','T','T','KST','PPL','PKD','PRC');
   _dodano+=exec('kst_war_add','#stalesys','KST.UL2','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.UL2','150.00',date(2023,1,1));
   exec('add_acr','#stalesys','UL3','Ulga podatkowa 1/36','T','T','T','KST','PPL','PKD','PRC');
   _dodano+=exec('kst_war_add','#stalesys','KST.UL3','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.UL3','100.00',date(2023,1,1));
   exec('add_acr','#stalesys','KW_W','Kwota wolna','T','T','T','KST','PPL','PKD','PRC');
   _dodano+=exec('kst_war_add','#stalesys','KST.KW_W','0.00',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.KW_W','30000.00',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Zaktualizowano definicje stałych systemu dotyczace ulgi podatkowej.')
   ?};

:: ORG: \spr_amor/upgrade_rrxx.fml
   exec('add_acr','#stalesys','SPR_AMOR','Wyłączenie kontroli dekretacji amor. w poprzednim okresie',
        'T','T','T','FINFO','FST');
   _dodano:=exec('kst_war_add','#stalesys','FINFO.SPR_AMOR','\'N\'',date(0,0,0));
   {? _dodano
   || __UPG.msg('Dodano parametr umożliwiający wyłączenie kontroli dekretacji amortyzacji w poprzednim okresie.')
   ?}

:: Stałe systemu, które nie są wspólne i powinny być wykonywane wielokrotnie w każdej firmie osobno
|? ~_runOnlyOnce
||

:: ORG: \wnr_zc/upgrade_1742.fml
   exec('add_acr','#stalesys','NR_ZC_AU','Numeracja umów zleceń','T','N','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.NR_ZC_AU','\'B\'',date(0,0,0))
   || __UPG.msg('Dodano informacje o automatycznej numeracji umów zleceń.')
   ?};
   exec('add_acr','#stalesys','NR_ZC_FM','Format numeru umowy zlecenia określony przez formułę','T','N','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.NR_ZC_FM','\'\'',date(0,0,0))
   || __UPG.msg('Dodano informacje w polu: Format numeru umowy zlecenia określony przez formułę.')
   ?};
   exec('add_acr','#stalesys','NR_RH_AU','Numeracja rachunków umów zleceń','T','N','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.NR_RH_AU','\'B\'',date(0,0,0))
   || __UPG.msg('Dodano informacje o automatycznej numeracji rachunków do umów zleceń.')
   ?};
   exec('add_acr','#stalesys','NR_RH_FM','Format numeru rachunku określony przez formułę','T','N','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.NR_RH_FM','\'\'',date(0,0,0))
   || __UPG.msg('Dodano informacje w polu: Format numeru rachunku określony przez formułę.')
   ?};

:: ORG: \KST_ZUS_OSW/upgrade_1902.fml
   exec('add_acr','#stalesys','ZUSOSW','Data złożenia oświadczenia','T','N','T','KST','PPL');

:: ORG: \KST_PDGN/upgrade_1942.fml
   exec('add_acr','#stalesys','PDGN','Procent dodatku za godziny nocne','T','N','T','KST','PPL');
   exec('kst_war_add','#stalesys','KST.PDGN','20.00',date(0,0,0));

:: ORG: \wnr_rh/upgrade_2042.fml
   exec('add_acr','#stalesys','NR_RH_AU','Numeracja rachunków umów zleceń','T','N','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.NR_RH_AU','\'B\'',date(0,0,0))
   || __UPG.msg('Dodano informacje o automatycznej numeracji rachunków do umów zleceń.')
   ?};
   exec('add_acr','#stalesys','NR_RH_FM','Format numeru rachunku określony przez formułę','T','N','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.NR_RH_FM','\'\'',date(0,0,0))
   || __UPG.msg('Dodano informacje w polu: Format numeru rachunku określony przez formułę.')
   ?};

:: ORG: \margines_we_wy/upgrade_2042.fml
   exec('add_acr','#stalesys','R_WEWY_R','Sposób interpretacji marginesów We/Wy','T','N','T','KST','PRC');
   {? exec('kst_war_add','#stalesys','KST.R_WEWY_R','\'S\'',date(0,0,0))
   || __UPG.msg('Dodano informacje o sposobie interpretacji marginesów We/Wy.')
   ?};
   exec('add_acr','#stalesys','R_PWE','Margines wejścia po godzinie zaplanowanej','T','N','T','KST','PRC');
   {? exec('kst_war_add','#stalesys','KST.R_PWE','\'time(0,0,0)\'',date(0,0,0))
   || __UPG.msg('Dodano informacje do stałych systemu odnośnie marginesu wejścia po godzinie zaplanowanej.')
   ?};
   exec('add_acr','#stalesys','R_PWY','Margines wyjścia przed godziną zaplanowaną','T','N','T','KST','PRC');
   {? exec('kst_war_add','#stalesys','KST.R_PWY','\'time(0,0,0)\'',date(0,0,0))
   || __UPG.msg('Dodano informacje do stałych systemu odnośnie marginesu wyjścia przed godziną zaplanowaną.')
   ?};

:: ORG: \rest_acc/upgrade_rrxx.fml
   exec('add_acr','#stalesys','REST_ACC','Przelew zaakceptowany po przywróceniu z archiwum',
        'T','N','T','XINFO','HBN');
   _wyn:=exec('kst_war_add','#stalesys','XINFO.REST_ACC','\'T\'',date(0,0,0));
   {? _wyn
   || __UPG.msg('Dodano informacje o statusie przelewu po przywróceniu z archiwum.')
   ?}

?};

1


\stale_once
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
::  MOD: DG [12.51]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach firmowych.
::   WE:
::   WY:
::  OLD \stalesys/upgradexpertis.fml
::----------------------------------------------------------------------------------------------------------------------
exec('stalesys','upgradexpertis',1)


\stale_once_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [12.51]
:: OPIS: Aktualizacja pól stałych systemu KST - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\stale_many
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
::  MOD: DG [12.51]
:: OPIS: Aktualizacja pól stałych systemu KST - zadanie wykonywane dla każdej firmy osobno.
::   WE:
::   WY:
::  OLD: \stalesys/upgradexpertis.fml
::----------------------------------------------------------------------------------------------------------------------
exec('stalesys','upgradexpertis',0)


\stale_many_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [12.51]
:: OPIS: Aktualizacja pól stałych systemu KST - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\par_pokr_graf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.02]
:: OPIS: Dodanie rubryk płacowych związanych z grafikami czasu pracy.
::   WE:
::   WY:
::  ORG: \add_rub_graf/upgrade_1802.fml
::  ORG: \PAR_POKR/upgrade_1842.fml
::----------------------------------------------------------------------------------------------------------------------
:: Uwaga: dodanie odpowiednich rubryk i atrybutów przeniesiono do pliku transfer.fml.
::
exec('__RUB','object');
_tab:=obj_new(6);
_tab[1]:=7051;
_tab[2]:=7041;
_tab[3]:=7055;
_tab[4]:=7053;
_tab[5]:=7006;
_tab[6]:=7059;
PAR_POKR.index('PAR_POKR');
PAR_POKR.prefix();
_ile:=0;
_firma:=exec('ref_firma','ustawienia');
{! _ind:=1 .. obj_len(_tab)
|! {? PAR_POKR.find_key(_firma,_tab[_ind])
   || {? PAR_POKR.DZ_12<>'T' | PAR_POKR.PTZO<>'N'
      || PAR_POKR.DZ_12:='T';
         PAR_POKR.PTZO:='N';
         {? PAR_POKR.put()
         || _ile+=1;
            __UPG.msg('%1 - premia okresowa została zaktualizowana.' [$_tab[_ind]])
         || __UPG.msg('%1 - premii okresowej nie udało się zaktualizować.' [$_tab[_ind]])
         ?}
      || _ile+=1;
         __UPG.msg('%1 - premia okresowa już istnieje.' [$_tab[_ind]])
      ?}

   || PAR_POKR.blank();
      PAR_POKR.R:=__RUB.ref(_tab[_ind]);
      PAR_POKR.RP:='S';
      PAR_POKR.RZ:='T';
      PAR_POKR.PTZO:='T';
      PAR_POKR.FIRMA:=_firma;
      PAR_POKR.DZ_12:='T';
      PAR_POKR.PTZO:='N';
      {? PAR_POKR.add(1)
      || _ile+=1;
         __UPG.msg('%1 - premia okresowa została dodana.' [$_tab[_ind]])
      || __UPG.msg('%1 - premii okresowej nie udało się dodać.' [$_tab[_ind]])
      ?}
   ?}
!};
{? _ile=obj_len(_tab)
|| 1
|| -1
?}


\par_pokr_graf_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.02]
:: OPIS: Dodanie rubryk płacowych związanych z grafikami czasu pracy.
::  ORG: \add_rub_graf_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Dodanie premii okresowych związanych z grafikami czasu pracy'


\zz_kryt_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Zwraca nazwę pliku zawierającego informacje o strukturze drzewa tabeli ZZ_KRYT.
::   WE:
::   WY: nazwa pliku
::  ORG: \zz_kryt_str/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'zz_kryt_str.txt'


\zz_kryt_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Spłaszcza" strukturę kryteriów umożliwiając aktualizację danych tabeli ZZ_KRYT w indeksie UNIQUE.
::   WE:
::   WY:
::  ORG: \zz_kryt_exp/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_name:=exec('zz_kryt_str','upgradexpertis');
: zawartość pliku jest tworzona tylko raz
{? fexists(_name,1)=0
|| _handle:=fopen(_name,'w',1,0);
   {? _handle<=0
   || __UPG.msg('Błąd dostępu do pliku %1'[_name]);
      return(0)
   ?};
   ZZ_KRYT.cntx_psh();
   ZZ_KRYT.index('ZZ_KRYT');
   ZZ_KRYT.prefix();
   _loop:=ZZ_KRYT.first();
   {!
   |? _loop
   |! {? ZZ_KRYT.ZZ_KRYT<>0
      || fwrite(_handle,'%1|%2|%3'[$ZZ_KRYT.ref(),$ZZ_KRYT.ZZ_KRYT,$ZZ_KRYT.NUMER])
      ?};
      _loop:=ZZ_KRYT.next()
   !};
   ZZ_KRYT.cntx_pop();
   fclose(_handle)
?};

: wyłącz kontrolę podczas zapisu danych
_files:=exec('chk_off_files','phr_dane');
_arg:=exec('off_arg','phr_dane','_chk','1','_putb','1','_puta','~~');
_off:=exec('fml_off','phr_dane',_files,_arg);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_mk_flat:="
   _err:=0;
   ZZ_KRYT.cntx_psh();
   ZZ_KRYT.index('ZZ_KRYT');
   ZZ_KRYT.prefix(_a);
   {!
   |? _err=0 & ZZ_KRYT.first()
   |! _err+=_c(#ZZ_KRYT.ref(),_b,_c)
   !};
   ZZ_KRYT.cntx_pop();
   {? ZZ_KRYT.ZZ_KRYT<>0
   || ZZ_KRYT.ZZ_KRYT:=0;
      ZZ_KRYT.NUMER:=_b+#ZZ_KRYT.ref();
      ZZ_KRYT.cntx_psh();
      ZZ_KRYT.clear();
      {? ~ZZ_KRYT.put(1)
      || _err+=1
      ?};
      ZZ_KRYT.cntx_pop()
   ?};
   _err
";

: "spłaszczenie" struktury
: (wszystko albo nic)
do();
_err:=0;
ZZ_KRYT.cntx_psh();
ZZ_KRYT.index('NAZWA');
ZZ_KRYT.prefix();
_base:=ZZ_KRYT.size();
_loop:=ZZ_KRYT.first();
{!
|? _loop & do_state()=1
|! _err+=_mk_flat(#ZZ_KRYT.ref(),_base,_mk_flat);
   {? _err<>0
   || undo()
   ?};
   _loop:=ZZ_KRYT.next()
!};
ZZ_KRYT.cntx_pop();
end();

: jeśli "spłaszczenie" się nie powiodło,
: to plik śladowy powinien być usunięty
{? _err<>0
|| ferase(_name,1);
   __UPG.msg('Eksport struktury kryteriów nie powiódł się.')
|| __UPG.msg('Eksport struktury zakończył się sukcesem.')
?};

: przywróć kontrolę zapisu danych
exec('chk_on','phr_dane',_off);
_err=0


\zz_kryt_exp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Spłaszcza" strukturę kryteriów umożliwiając aktualizację danych tabeli ZZ_KRYT w indeksie UNIQUE - opis.
::   WE:
::   WY: Opis zadania
::  ORG: \zz_kryt_exp_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Przygotowanie danych tabeli ZZ_KRYT do aktualizacji'


\move_doks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przenosi dokumenty kadrowe (tabela ZZ_DOK) ze zbioru zz_dok do zbioru docelowego.
::   WE:
::   WY:
::  ORG: \move_doks/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_arg:=exec('off_arg','phr_dane','_chk','1','_putb','1','_puta','~~');
_off:=exec('fml_off','phr_dane',_files,_arg);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_dst:='zz_do'+($exec('ref_firma','ustawienia')+1);
exec('move_doks','phr_dane','zz_dok',_dst,1);
__UPG.msg('Zamieniono odwołania do zbioru zz_dok na %1'[_dst]);

exec('chk_on','phr_dane',_off);
1


\move_doks_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przenosi dokumenty kadrowe (tabela ZZ_DOK) ze zbioru zz_dok do zbioru docelowego - opis.
::   WE:
::   WY:
::  ORG: \move_doks_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Przeniesienie dokumentów kadrowych (tabela ZZ_DOK) ze zbioru zz_dok do zbioru docelowego'


\zz_kryt_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Odtwarza" strukturę kryteriów po aktualizacji danych tabeli ZZ_KRYT w indeksie UNIQUE.
::   WE:
::   WY:
::  ORG: \zz_kryt_imp/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_name:=exec('zz_kryt_str','upgradexpertis');
: wymagane istnienie pliku
{? fexists(_name,1)=0
|| __UPG.msg('Nie znaleziono pliku %1'[_name]);
   return(0)
?};
_handle:=fopen(_name,'r',1,0);
: wymagany dostęp do pliku
{? _handle<=0
|| __UPG.msg('Błąd dostępu do pliku %1'[_name]);
   return(0)
?};

: wyłącz kontrolę podczas zapisu danych
_files:=exec('chk_off_files','phr_dane');
_arg:=exec('off_arg','phr_dane','_chk','1','_putb','1','_puta','~~');
_off:=exec('fml_off','phr_dane',_files,_arg);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

: odtwórz strukturę drzewa
: (wszystko albo nic)
do();
_err:=0;
ZZ_KRYT.cntx_psh();
ZZ_KRYT.index('NAZWA');
{!
|? _err=0 & (_line:=fread(_handle))<>'\n'
|! _buf:=spli_str(_line,'|');
   ZZ_KRYT.prefix();
   {? ZZ_KRYT.seek(_buf[1])
   || ZZ_KRYT.ZZ_KRYT:=#_buf[2];
      ZZ_KRYT.NUMER:=#_buf[3];
      {? ~ZZ_KRYT.put(1)
      || _err+=1;
         undo()
      ?}
   ?};
   &_buf
!};
ZZ_KRYT.cntx_pop();
end();

: usuń plik śladowy tylko wtedy, gdy odbudowa struktury zakończyła się sukcesem
fclose(_handle);
{? _err=0
|| ferase(_name,1);
   __UPG.msg('Import struktury zakończył się sukcesem.')
|| __UPG.msg('Import struktury kryteriów nie powiódł się.')
?};

: przywróć kontrolę zapisu danych
exec('chk_on','phr_dane',_off);
_err=0


\zz_kryt_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Odtwarza" strukturę kryteriów po aktualizacji danych tabeli ZZ_KRYT w indeksie UNIQUE - opis.
::   WE:
::   WY: Opis zadania
::  ORG: \zz_kryt_imp_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Odtworzenie struktury drzewa w tabeli ZZ_KRYT po aktualizacji'


\create_doks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Aktualizuje dane pod kątem spójości zapisow w tabeli ZZ_DOK.
::   WE:
::   WY:
::  ORG: \create_doks/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

exec('create_doks','phr_dane',1);
__UPG.msg('Zaktualizowano dane tabel zawierających wskazanie na wiersz tabeli ZZ_DOK');

exec('chk_on','phr_dane',_off);
1


\create_doks_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Aktualizuje dane pod kątem spójości zapisow w tabeli ZZ_DOK - opis.
::   WE:
::   WY:
::  ORG: \create_doks_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych pod kątem spójności zapisów w tabeli ZZ_DOK'


\firma_p_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Uzupełnia zawartość kolumny FIRMA tabeli (formuła biblioteczna).
::   WE:  _a  [TABLE]     - Alias tabeli.
::       [_b] [REFERENCE] - Referencja wiersza tabeli FIRMA [domyślnie: bieżąca].
::       [_c] [STRING]    - Akronim kolumny [domyślnie: 'FIRMA'].
::   WY:
::  ORG: \firma_p_akt/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_TAB:=_a;
_ref:={? var_pres('_b')=type_of(null()) & _b<>null() & ref_tab(_b)=FIRMA || _b || exec('ref_firma','ustawienia') ?};
_col:={? var_pres('_c')=type_of('') & _c<>'' || _c || 'FIRMA' ?};

progress(,'Uzupełnienie kolumny %1 w tabeli %2.'@ [_col,2-!_TAB],FUN.TYT,1);

_iok:=_ibad:=0;
_TAB.cntx_psh();
_TAB.trig_off('*','*');
_MASK:=_TAB.names();
_loop:=_MASK.first();
{!
|? _loop
|! _TAB.use(_MASK.NAME);
   {? _TAB.f_active()
   || _TAB.f_clear()
   || _TAB.clear()
   ?};
   _TAB.f_set(,,'%1.%2 is null' [2-!_TAB,_col]);
   _loop:=_TAB.f_first();
   {!
   |? _loop
   |! ($('_a.%1:=_b' [_col]))(_TAB,_ref);
      {? _TAB.put()
      || _iok+=1
      || _ibad+=1
      ?};
      _loop:=_TAB.f_next()
   !};
   _TAB.f_clear();
   _loop:=_MASK.next()
!};
_TAB.trig_on('*','*');
_TAB.cntx_pop();
__UPG.msg('Tabela: %1 (%2) | Zapisów poprawionych: %3 | Zapisów, których nie udało się poprawić: %4 |'
   [2-!_TAB,_TAB.comment(),$_iok,$_ibad]
);

prgs_clr();
{? _iok=_ibad
|| 1
|| -1
?}


\firma_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Uzupełnienie wartości kolumny FIRMA w wybranych tabelach.
::   WE:
::   WY:
::  ORG: \firma_p/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
:: Zrezygnowano z mechanizmu reloading_fml na rzecz wyłączania wyzwalaczy (w firma_p_akt).
:: 0 -> 2
:: 1 -> 3
_stat:=obj_new(3);
{! _lp:=1 .. obj_len(_stat)
|! _stat[_lp]:=0
!};

_firma:=exec('ref_firma','ustawienia');
_stat[2+exec('firma_p_akt','upgradexpertis',ZZ_HIST,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',ZF_SKL,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',ZF_DEF,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',ZO_PROG,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',ZA_INST,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',ZA_ZEST,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',SZB_OKR,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',SZK_PROG,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',STN_GR,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',SZK_PLAN,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',SZK_DAT,_firma)]+=1;
_stat[2+exec('firma_p_akt','upgradexpertis',SZK_ZAP,_firma)]+=1;

{? _stat[2]
:: Jeżeli wystąpił choć jeden błąd, to zgłaszany jest błąd.
|| 0
:: Jeżeli wystąpiło choć jedno ostrzeżenie, to zgłaszane jest ostrzeżenie.
|? _stat[1]
|| -1
|| 1
?}


\firma_p_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Uzupełnienie wartości kolumny FIRMA w wybranych tabelach - opis.
::   WE:
::   WY:
::  ORG: \firma_p_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wartości kolumny FIRMA w wybranych tabelach'


\szk_tren
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Uzupełnienie informacji związanych z trenerami.
::   WE:
::   WY:
::  ORG: \szk_tren/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_nok:=0;
_nerr:=0;
_nleft:=0;
_state:=no_msg(1);

SZK_TREN.cntx_psh();
SZK_TREN.index('ZZ_DOK');
SZK_TREN.prefix();
_loop:=SZK_TREN.first();
{!
|? _loop
|! {? SZK_TREN.put(1)
   || _nok+=1
   || _nerr+=1
   ?};
   _loop:=SZK_TREN.next()
!};
SZK_TREN.cntx_pop();

__UPG.msg('Tabela: %1 (%2) | Zapisów poprawionych: %3 | Zapisów, których nie udało się poprawić: %4 |'
   ['SZK_TREN',SZK_TREN.comment(),$_nok,$_nerr]
);

SZK_TREN.cntx_psh();
SZK_TREN.index('ZZ_OSOBA');

_nok:=0;
_nerr:=0;
_nleft:=0;
SZK_OFE.cntx_psh();
SZK_OFE.index('ZZ_DOK');
SZK_OFE.prefix();
_loop:=SZK_OFE.first();
{!
|? _loop
|! {? SZK_OFE.SZK_TREN=null & SZK_OFE.TRENER<>null
   || {? SZK_TREN.find_key(SZK_OFE.TRENER)
      || SZK_OFE.SZK_TREN:=SZK_TREN.ref();
         {? SZK_OFE.put(1)
         || _nok+=1
         || _nerr+=1
         ?}
      || _nerr+=1
      ?}
   || _nleft+=1
   ?};
   _loop:=SZK_OFE.next()
!};
SZK_OFE.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów poprawionych: %3 '
   '| Zapisów, których nie udało się poprawić: %4 '
   '| Zapisów niewymagających modyfikacji: %5'
   ['SZK_OFE',SZK_OFE.comment(),$_nok,$_nerr,$_nleft]
);

_nok:=0;
_nerr:=0;
_nleft:=0;
SZK_OPIS.cntx_psh();
SZK_OPIS.index('ZZ_DOK');
SZK_OPIS.prefix();
_loop:=SZK_OPIS.first();
{!
|? _loop
|! {? SZK_OPIS.SZK_TREN=null & SZK_OPIS.TRENER<>null
   || {? SZK_TREN.find_key(SZK_OPIS.TRENER)
      || SZK_OPIS.SZK_TREN:=SZK_TREN.ref();
         {? SZK_OPIS.put(1)
         || _nok+=1
         || _nerr+=1
         ?}
      || _nerr+=1
      ?}
   || _nleft+=1
   ?};
   _loop:=SZK_OPIS.next()
!};
SZK_OPIS.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów poprawionych: %3 '
   '| Zapisów, których nie udało się poprawić: %4 '
   '| Zapisów niewymagających modyfikacji: %5'
   ['SZK_OPIS',SZK_OPIS.comment(),$_nok,$_nerr,$_nleft]
);

SZK_TREN.cntx_pop();

exec('chk_on','phr_dane',_off);
no_msg(_state);
1


\szk_tren_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Uzupełnienie informacji związanych z trenerami.
::   WE:
::   WY:
::  ORG: \szk_tren_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie informacji związanych z trenerami'


\zo_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Uzupełnienie danych w tabeli ZO_TEST.
::   WE:
::   WY:
::  ORG: \zo_test/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_nok:=0;
_nerr:=0;
_nleft:=0;
_state:=no_msg(1);

ZO_TEST.cntx_psh();
ZO_TEST.index('ZZ_DOK');
ZO_TEST.prefix();
_loop:=ZO_TEST.first();
{!
|? _loop
|! {? ZO_TEST.KOMPLET='' | ZO_TEST.P_FORM=''
   || {? ZO_TEST.put(1)
      || _nok+=1
      || _nerr+=1
      ?}
   || _nleft+=1
   ?};
   _loop:=ZO_TEST.next()
!};
ZO_TEST.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów poprawionych: %3 '
   '| Zapisów, których nie udało się poprawić: %4 '
   '| Zapisów niewymagających modyfikacji: %5'
   ['ZO_TEST',ZO_TEST.comment(),$_nok,$_nerr,$_nleft]
);

exec('chk_on','phr_dane',_off);
no_msg(_state);
1


\zo_test_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Uzupełnienie danych w tabeli ZO_TEST.
::   WE:
::   WY:
::  ORG: \zo_test_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie informacji w ocenach całkowitych'


\zo_cel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego.
::   WE:
::   WY:
::  ORG: \zo_cel/upgrade_1802.fml
::  ORG: \zo_cel/upgrade_1842.fml
::----------------------------------------------------------------------------------------------------------------------
ZO_CEL.cntx_psh();
ZO_CEL.prefix();
_loop:=ZO_CEL.first();
{? _loop
|| _lrec:=ZO_CEL.size();
   _lmod:=0;
   _firma:=$exec('ref_firma','ustawienia');
   FUN.prg_start(ZO_CEL.size(),'Przetwarzanie tabeli %1'@['ZO_CEL'],,,1);
   {!
   |? _loop
   |! ZZ_DOK.use('zz_do'+(_firma+1));
      _tab:=sql('
         select ZO_PROC.REFERENCE as REF, ZO_PROC.OKRES_OD as OD, ZO_PROC.OKRES_DO as DO
         from ZO_PROC
         where ZO_PROC.ZO_PROG=\':_a\' and ZO_PROC.OKRES_DO<TO_DATE(\':_b\')',
         $ZO_CEL.ZO_PROG,$ZO_CEL.OKRES_DO
      );
      _put:=0;
::    \zo_cel/upgrade_1802.fml
      {? ZO_CEL.Z='?'
      || ZO_CEL.Z:='X';
         _put+=1
      ?};
::    \zo_cel/upgrade_1842.fml
      {? ZO_CEL.PROC=''
      || {? ZO_CEL.ZO_PROC
         || ZO_CEL.ZO_PROC();
            {? ZO_PROC.prev()
            || ZO_CEL.PROC:=$ZO_PROC.ref();
               _put+=1
            || ZO_CEL.PROC:=$ZO_PROC.ref();
               _put+=1
            ?}
         || {? _tab.last()
            || ZO_CEL.PROC:=_tab.REF;
               _put+=1
            ?}
         ?}
      ?};
      {? _put
      || _lmod+=ZO_CEL.put()
      ?};
      &_tab;
      FUN.prg_next();
      _loop:=ZO_CEL.next()
   !};
   FUN.prg_stop();
   __UPG.msg('Zapisów przetworzonych: %1. Zapisów zmodyfikowanych: %2.'[$_lrec,$_lmod])
|| __UPG.msg('Brak danych do aktualizacji.')
?};
ZO_CEL.cntx_pop();
1


\zo_cel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego - opis.
::   WE:
::   WY:
::  ORG: \zo_cel_desc/upgrade_1802.fml
::  ORG: \zo_cel/upgrade_1842.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola PROC tabeli ZO_CEL. Informacja o sesji ocen, w której cel został wyznaczony.\n'
'Zamiana znaku specjalnego "?" na "X" w znaczniku realizacji celu (ZO_CEL.Z).\n'
'Powodem zamiany są zmiany technologiczne - wprowadzenie możliwości korzystania ze znaków specjalnych '
'w polach szybkiego filtra.'


\zo_celk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego.
::   WE:
::   WY:
::  ORG: \zo_celk/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
:: Zrezygnowano z mechanizmu reloading_fml na rzecz wyłączania wyzwalaczy.
_ret:=0;

ZO_CELK.cntx_psh();
ZO_CELK.trig_off('*','*');
ZO_CELK.index('UNIQUE');
ZO_CELK.prefix();
{? ZO_CELK.first()
|| __UPG.msg('Wszystkich zapisów: %1.' [$ZO_CELK.size()]);
   _p1:=_p0:=_ok:=0;
   {!
   |? {? ZO_CELK.Z='?'
      || ZO_CELK.Z:='X';
         {? ZO_CELK.put()
         || _p1+=1
         || _p0+=1
         ?}
      || _ok+=1
      ?};
      ZO_CELK.next()
   !};
   __UPG.msg('Zapisów nie wymagających zmiany: %1.' [$_ok]);
   __UPG.msg('Zapisów wymagających zmiany: %1, w tym poprawionych: %2 / niepoprawionych: %3.' [$(_p1+_p0),$_p1,$_p0]);
   _ret:={? _p0 || -1 || 1 ?}

|| __UPG.msg('Brak zapisów do poprawienia.');
   _ret:=1
?};
ZO_CELK.trig_on('*','*');
ZO_CELK.cntx_pop();

_ret


\zo_celk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego - opis.
::   WE:
::   WY:
::  ORG: \zo_celk_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Zamiana znaku specjalnego "?" na "X" w znaczniku realizacji kompetencji celu (ZO_CELK.Z).\n'
'Powodem zamiany są zmiany technologiczne - wprowadzenie możliwości korzystania ze znaków specjalnych '
'w polach szybkiego filtra.'


\szk_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego.
::   WE:
::   WY:
::  ORG: \szk_prac/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
:: Zrezygnowano z mechanizmu reloading_fml na rzecz wyłączania wyzwalaczy.
_ret:=0;

P.cntx_psh();
P.prefix();
SZK_PRAC.cntx_psh();
SZK_PRAC.trig_off('*','*');
SZK_PRAC.index('PRAC_SZK');
SZK_PRAC.prefix();
{? SZK_PRAC.first()
|| __UPG.msg('Wszystkich zapisów: %1.' [$SZK_PRAC.size()]);
   _p1:=_p0:=_ok:=0;
   {!
   |? _put:=0;
      {? SZK_PRAC.EMAIL_PO='?'
      || SZK_PRAC.EMAIL_PO:='X';
         _put+=1
      ?};
::    Dodatkowo uzupełnijmy pola, które chyba nie były uzupełniane w Xpertisie ...
      {? SZK_PRAC.P
      || SZK_PRAC.P();
         {? SZK_PRAC.WYDZIAL<>P.WYDZIAL
         || SZK_PRAC.WYDZIAL:=P.WYDZIAL;
            _put+=1
         ?};
         {? SZK_PRAC.KK<>P.KK
         || SZK_PRAC.KK:=P.KK;
            _put+=1
         ?};
         {? SZK_PRAC.STN<>P.ST
         || SZK_PRAC.STN:=P.ST;
            _put+=1
         ?}
      ?};
      {? _put
      || {? SZK_PRAC.put()
         || _p1+=1
         || _p0+=1
         ?}
      || _ok+=1
      ?};
      SZK_PRAC.next()
   !};
   __UPG.msg('Zapisów nie wymagających zmiany: %1.' [$_ok]);
   __UPG.msg('Zapisów wymagających zmiany: %1, w tym poprawionych: %2 / niepoprawionych: %3.' [$(_p1+_p0),$_p1,$_p0]);
   _ret:={? _p0 || -1 || 1 ?}

|| __UPG.msg('Brak zapisów do poprawienia.');
   _ret:=1
?};
SZK_PRAC.trig_on('*','*');
SZK_PRAC.cntx_pop();
P.cntx_pop();

_ret


\szk_prac_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego - opis.
::   WE:
::   WY:
::  ORG: \szk_prac_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Zamiana znaku specjalnego "?" na "X" w potwierdzeniu uczestnictwa w szkoleniu (SZK_PRAC.EMAIL_PO).\n'
'Powodem zamiany są zmiany technologiczne - wprowadzenie możliwości korzystania ze znaków specjalnych '
'w polach szybkiego filtra.'


\slo_kod_wer_wyn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego.
::   WE:
::   WY:
::  ORG: \slo_kod_wer_wyn/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

_kod:='WER_WYN';
{? (_typ:=exec('slo_typ','ext_slo',_kod))<>null()
|| SLO_KOD.cntx_psh();
   SLO_KOD.index('KOD');
   SLO_KOD.prefix(_typ,'?');
   {? SLO_KOD.first()
   || SLO_KOD.prefix();
      SLO_KOD.KOD:='X';
      {? SLO_KOD.put()
      || __UPG.msg('Pozycję słownika udało się poprawić.')
      || __UPG.msg('Pozycji słownika nie udało się poprawić.');
         _ret:=-1
      ?}
   || __UPG.msg('Pozycja słownika nie wymaga poprawienia.')
   ?};
   SLO_KOD.cntx_pop();
   _ret
|| __UPG.msg('Nie udało się utworzyć słownika [%1].' [_kod])
?};

_ret


\slo_kod_wer_wyn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Rezygnacja ze znaku specjalnego - opis.
::   WE:
::   WY:
::  ORG: \slo_kod_wer_wyn_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Zamiana znaku specjalnego "?" na "X" w wyniku weryfikacji zapotrzebowania na szkolenia.\n'
'Powodem zamiany są zmiany technologiczne - wprowadzenie możliwości korzystania ze znaków specjalnych '
'w polach szybkiego filtra.'


\zo_osoba_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Usunięcie zapisów rodzaju O z tabeli ZO_OSOBA, dla których nie znaleziono rekordów w tabeli ZO_TEST.
::   WE:
::   WY:
::  ORG: \zo_osoba_del/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_ndel:=0;
_nerr:=0;
_nleft:=0;
_state:=no_msg(1);

ZO_TEST.cntx_psh();
ZO_TEST.index('UNIQUE');
ZO_OSOBA.cntx_psh();
ZO_OSOBA.index('ZO_OSOBA');
ZO_OSOBA.prefix('O');
_loop:=ZO_OSOBA.first();
{!
|? _loop
|! {? ZO_TEST.find_key(ZO_OSOBA.ref())
   || _loop:=ZO_OSOBA.next();
      _nleft+=1
   || {? ZO_OSOBA.del()
      || _loop:=1;
         _ndel+=1
      || _loop:=ZO_OSOBA.next();
         _nerr+=1
      ?}
   ?}
!};
ZO_OSOBA.cntx_pop();
ZO_TEST.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów usuniętych: %3 '
   '| Zapisów, których nie udało się usunąć: %4 '
   '| Zapisów pozostawionych: %5'
   ['ZO_TEST',ZO_TEST.comment(),$_ndel,$_nerr,$_nleft]
);

exec('chk_on','phr_dane',_off);
no_msg(_state);
1


\zo_osoba_del_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Usunięcie zapisów rodzaju O z tabeli ZO_OSOBA, dla których nie znaleziono rekordów w tabeli ZO_TEST.
::   WE:
::   WY:
::  ORG: \zo_osoba_del_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie nadmiarowych zapisów w tabeli ZO_OSOBA'


\stn_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Uzupełnienie danych w tabeli STN_GRP.
::   WE:
::   WY:
::  ORG: \stn_grp/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_files:=exec('chk_off_files','phr_dane');
_off:=exec('chk_off','phr_dane',_files);
{? _off.STATUS<>''
|| exec('chk_on','phr_dane',_off);
   __UPG.msg(_off.STATUS);
   return(0)
?};

_nput:=0;
_nerr:=0;
_nleft:=0;
_nadd:=0;
_state:=no_msg(1);

STN_GRP.cntx_psh();
STN_GRP.clear();
STN.cntx_psh();
STN.index('STANONAZ');
STN.prefix();
_loop:=STN.first();
{!
|? _loop
|! {? STN.STN_GR<>null
   || STN_GRP.blank();
      do();
      STN_GRP.STN_GR:=STN.STN_GR;
      STN_GRP.STN:=STN.ref();
      {? STN_GRP.add(1)
      || _nadd+=1;
         STN.STN_GR:=null;
         {? STN.put(1)
         || _nput+=1
         || _nerr+=1;
            undo()
         ?}
      || _nerr+=1;
         undo()
      ?};
      end()
   || _nleft+=1
   ?};
   _loop:=STN.next()
!};
STN.cntx_pop();
STN_GRP.cntx_pop();

__UPG.msg(
   'Tabela: %1 (%2) '
   '| Zapisów poprawionych: %3 '
   '| Zapisów, których nie udało się poprawić: %4 '
   '| Zapisów niewymagających zmiany: %5 '
   '| Zapisów utworzonych: %6'
   ['STN',STN.comment(),$_nput,$_nerr,$_nleft,$_nadd]
);

exec('chk_on','phr_dane',_off);
no_msg(_state);
1


\stn_grp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Uzupełnienie danych w tabeli STN_GRP.
::   WE:
::   WY:
::  ORG: \stn_grp_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie danych w kartotece grup stanowisk'


\updateKAL_BUFF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.02]
:: OPIS: Aktualizuje pola KAL_BUFF.NB i KAL_BUFF.NH ze względu na zmianę obsługi nieobecności w H (z kodem 'BOW')
::  ORG: \updateKAL_BUFF/upgrade_1802.fml
::  ORG: \updateKAL_BUFF/upgrade_1842.fml
::  ORG: \KAL_BUFF/upgrade_2114.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lmod:=0;
_lrec:=0;
P.cntx_psh();
P.index('OSOBA');
P.prefix();
KAL_BUFF.cntx_psh();
KAL_BUFF.index('NIEOBECN');
exec('MASK','object');

:: uzupełnia pola WYL i WYM (1802) i P_DOM (18xx)
H.cntx_psh();
H.prefix();
KAL_BUFF.prefix();
KAL_BUFF.for_each("
   _put:=0;
   {? KAL_BUFF.H & (KAL_BUFF.WYL=0 | KAL_BUFF.WYM=0)
   || KAL_BUFF.WYL:=KAL_BUFF.H().WYL;
      KAL_BUFF.WYM:=H.WYM;
      _put+=1
   ?};
   {? KAL_BUFF.P_DOM=''
   || KAL_BUFF.P_DOM:='T';
      _put+=1
   ?};
   {? KAL_BUFF.GPW='Z'
   || KAL_BUFF.cntx_psh();
      KAL_BUFF.index('OKRPRDT');
      _p_ref:=KAL_BUFF.P;
      _wewy:=exec('get_wewy','prc_wewy',KAL_BUFF.DATA,_p_ref);
      {? _wewy.first()
      || _PRZ:=exec('get_wewy_przerwy','prc_wewy',_p_ref,_wewy.DWE,_wewy.GWE,_wewy.DWY,_wewy.GWY);
         {? _PRZ.first()
         || KAL_BUFF.STATUS:='P';
            _put+=1
         ?};
         &_PRZ
      ?};
      &_wewy;
      KAL_BUFF.cntx_pop()
   ?};
   {? KAL_BUFF.TYP='R' & KAL_BUFF.TYPWS='W'
   || KAL_BUFF.TYPWS:='S';
      _put+=1
   ?};
   {? _put
   || KAL_BUFF.put()
   ?}
");
H.cntx_pop();

:: aktualizuje dawne nieobecności 'BOW'
KAL_BUFF.prefix();
N.cntx_psh();
_ndx:=N.ndx_tmp('',1,'P',,0, 'NB','RN',0, 'OD',,0);
N.index(_ndx);
_tab:=sql(''+"select REFERENCE as REF, P, NB, NH, DATA from KAL_BUFF where KAL_BUFF.NH like '%hist%' order by P, DATA");
{? _tab.first()
|| _result:=0;
   {!
   |? _lrec+=1;
      {? P.seek(_tab.P)
      || _n_nr:=#_tab.NB;
         N.prefix(P.ref(),_n_nr);
         {? N.find_tab(1,'OD',,'<=',_tab.DATA,'DO',,'>=',_tab.DATA) & KAL_BUFF.seek(_tab.REF)
         || KAL_BUFF.NH:=$N.ref();
            _lmod+=KAL_BUFF.put()
         |? _n_nr=2
         || N.prefix(P.ref(),25);
::          jeżeli dla urlopu bezpłatnego nie została znaleziona nieobecnośc z kodem 2 (urlop bezpłatny), to zobacz
::          czy nie ma przypadkiem tej nieobecności na kodzie 25 (urlop bezpłatny udzielony na wniosek pracodawcy)
            {? N.find_tab(1,'OD',,'<=',_tab.DATA,'DO',,'>=',_tab.DATA) & KAL_BUFF.seek(_tab.REF)
            || KAL_BUFF.NH:=$N.ref();
               _lmod+=KAL_BUFF.put()
            ?}
         ?}
      ?};
      _tab.next()
   !};
   _result:={? _lmod>0 || {? _lmod=_lrec || 1 || -1 ?} || 0 ?}
?};
N.cntx_pop();
N.ndx_drop(_ndx);
KAL_BUFF.cntx_pop();
P.cntx_pop();

{? _result
|| __UPG.msg('Zakończono aktualizowanie zapisów w tabeli buforów planowania czasu pracy (KAL_BUFF).');
   {? _lrec
   || __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
      {? _result=-1
      || __UPG.msg('W trakcie aktualizacji nie udało się poprawić wszystkich wymaganych zapisów.');
         __UPG.msg('W polu KAL_BUFF.NH nie powinno być zapisów wskazujących na tabelę H _hist*.');
         __UPG.msg('Należy zweryfikować poprawność rekordów w tabeli KAL_BUFF.')
      ?}
   || __UPG.msg('W trakcie aktualizacji nie znaleziono zapisów z wprowadzoną '+
                'nieobecnością, powiązanych z tabelą H i kodem \'BOW\'.')
   ?}
|| __UPG.msg('Wystąpił błąd podczas aktualizacji zapisów w tabeli buforów planowania czasu pracy.')
?};
_result


\updateKAL_BUFF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.02]
:: OPIS: Aktualizuje pola KAL_BUFF.NB i KAL_BUFF.NH ze względu na zmianę obsługi nieobecności w H (z kodem 'BOW')
::  ORG: \updateKAL_BUFF_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów w tabeli buforów planowania czasu pracy (KAL_BUFF)'


\R_ZMIANY
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [areKc] [18.02]
:: OPIS: Wczytanie definicji pracy zmianowej. Import danych z pliku tekstowego do tabeli.
::  ORG: \R_ZMIANY/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
R_ZMIANY.clear();
{? R_ZMIANY.size()
|| __UPG.msg('Nie uzupełniono tabeli definicji pracy zmianowej.');
   __UPG.msg('Tabela nie jest pusta.')
|| {? exec('import','prc_zmiany')
   || __UPG.msg('Tabela definicji pracy zmianowej została uzupełniona.');
      {? R_ZMIANY.size()=1
      || __UPG.msg('Do tabeli dodano: %1 rekord.' [$R_ZMIANY.size()])
      || __UPG.msg('Do tabeli dodano: %1 rekordy.' [$R_ZMIANY.size()])
      ?};
      _result:=1
   || __UPG.msg('Nie uzupełniono tabeli definicji pracy zmianowej.');
      __UPG.msg('Brak pliku "zmiany.txt" zawierającego definicje pracy zmianowej.')
   ?}
?};
_result


\R_ZMIANY_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [areKc] [18.02]
:: OPIS: Wczytanie definicji pracy zmianowej.
::  ORG: \R_ZMIANY_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słownika definicji pracy zmianowej.'


\zf_rpi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Transfer formatów elementów formularzy ocen pracowniczych.
::   WE:
::   WY:
::  ORG: \zf_rpi/upgrade_1802.fml
::  ORG: \zf_rpi_repair/upgrade_1842.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

:: Zrezygnowano z mechanizmu reloading_fml na rzecz wyłączania wyzwalaczy.

:: \zf_rpi_repair/upgrade_1842.fml  Początek - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
ZF_SKL.cntx_psh();
ZF_SKL.clear();
ZF_SKL.f_set(,'','ZF_SKL.KOD in (\':_a\') and ZF_SKL.DOKUMENT<>\'N\'','ST,SZ,KD');
ZF_SKL.f_each("
   ZF_SKL.DOKUMENT:='N';
   ZF_SKL.put()
",1);
ZF_SKL.f_clear();
ZF_SKL.cntx_pop();

ZF_POZ.cntx_psh();
ZF_POZ.clear();
ZF_POZ.f_set(,'join ZF_SKL','ZF_SKL.KOD in (\':_a\') and (ZF_POZ.DOKUMENT<>\'N\' or ZF_POZ.ZF_RPI is null)','ST,SZ,KD');
ZF_POZ.f_each("
   ZF_POZ.ZF_RPI:=null();
   ZF_POZ.DOKUMENT:='N';
   ZF_POZ.put()
",1);
ZF_POZ.f_clear();
ZF_POZ.cntx_pop();
:: \zf_rpi_repair/upgrade_1842.fml  Koniec - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

_arr:=obj_new(2);
{! _lp:=1 .. 2
|! _arr[_lp]:=obj_new('acr','blank','std')
!};
_arr[1].acr:='SZABLON';
_arr[2].acr:='SZABLON2';
{! _lp:=1 .. 2
|! _arr[_lp].blank:=ZF_RPI.fld_fml(_arr[_lp].acr,'BLANK',"*")
!};
_arr[1].std:=
   '|poc_zfcn01.rpi|poc_zfcw01.rpi|poc_zfdn01.rpi|poc_zfdod01.rpi|poc_zfdp01.rpi|poc_zfmb01.rpi|poc_zfok01.rpi'
   '|poc_zfopk01.rpi|poc_zfopp01.rpi|poc_zfp01.rpi||poc_zfpoo01.rpi|poc_zfps01.rpi|poc_zfrk01.rpi|poc_zfwp01.rpi|';
_arr[2].std:=
   '|poc_zrcn01.rpi|poc_zrcw01.rpi|poc_zrdn01.rpi|poc_zrdod01.rpi|poc_zrdp01.rpi|poc_zrmb01.rpi|poc_zrok01.rpi'
   '|poc_zropk01.rpi|poc_zropp01.rpi|poc_zrp01.rpi|poc_zrpoo01.rpi||poc_zrps01.rpi|poc_zrrk01.rpi|poc_zrwp01.rpi|';

ZF_SKL.cntx_psh();
ZF_SKL.f_clear();
ZF_SKL.index('KOD');
ZF_SKL.prefix(exec('ref_firma','ustawienia'));
{? ZF_SKL.first()
|| _ok:=0;
   ZF_POZ.cntx_psh();
   ZF_POZ.index('FK_SKL');
   ZF_POZ.prefix();

   ZF_RPI.cntx_psh();
   ZF_RPI.trig_off('*','*');
   ZF_RPI.f_clear();
   ZF_RPI.index('NAZWA');
   {!
   |? ZF_RPI.prefix(ZF_SKL.ref());
      {? ZF_RPI.first()
      || {!
         |? _nag:='Składnik: %1 | Szablon: %2 | ' [ZF_RPI.ZF_SKL().KOD,ZF_RPI.NAZWA];
            _put:=0;
            _next:=0;

::          Najpierw sytuacje szczególne (\zf_rpi_repair/upgrade_1842.fml)
            {? ZF_RPI.ZF_SKL().KOD='ST' | ZF_RPI.ZF_SKL().KOD='SZ' | ZF_RPI.ZF_SKL().KOD='KD'
            || __UPG.msg(_nag+'Usunięto zbędny wzorzec wydruku.');
               do();
               ZF_POZ.prefix(ZF_RPI.ZF_SKL);
               {? ZF_POZ.first()
               || {!
                  |? {? ZF_POZ.ZF_RPI=ZF_RPI.ref()
                     || ZF_POZ.ZF_RPI:=null();
                        ZF_POZ.DOKUMENT:='N';
                        ZF_POZ.put()
                     ?};
                     ZF_POZ.next()
                  !}
               ?};
               _next:=ZF_RPI.del(1,1)=2;
               _ret:=end()

            |? ZF_RPI.ZF_SKL().KOD='P'
            || _vala:='poc_zrp01.rpi';
               {? ZF_RPI.SZABLON2<>_vala
               || __UPG.msg(_nag+'Zmieniono wartość pola %1 z [%2] na [%3].' ['SZABLON2',ZF_RPI.SZABLON2,_vala]);
                  ZF_RPI.SZABLON2:=_vala;
                  _put+=1
               ?}

::             A teraz sytuacje standardowe (\zf_rpi/upgrade_1802.fml)
            || {! _lp:=1 .. 2
               |! _fld:='ZF_RPI.'+_arr[_lp].acr;
                  {? ($_fld)()=''
::                Pole nie było wypełnione - przyjmijmy wartość domyślną zwracaną przez formułę na wartość początkową.
                  || _val:=_arr[_lp].blank();
                     ($(_fld+':=_a'))(_val);
                     __UPG.msg(_nag+'Uzupełniono wartość pola %1 [%2].' [_arr[_lp].acr,_val]);
                     _put+=1
                  ?};
                  _valb:=($_fld)();
                  {? 4+_valb<>'poc_'
::                "Stara" nazwa szablonu, jeszcze bez prefisku 'poc_', który musi być doklejony.
                  || _vala:='poc_'+_valb;
                     {? _arr[_lp].std*('|%1|' [_vala])=0
::                   Szablon nie jest standardowy - trzeba dokleić literkę 'q'.
                     || {? _vala+4='.rpi'
                        || _vala:=(_vala-4)+'q.rpi';
                           __UPG.msg(_nag+'Zmieniono wartość pola %1 z [%2] na [%3].' [_arr[_lp].acr,_valb,_vala])
                        || __UPG.msg(_nag+'Nieprawidłowa wartość pola %1 [%2].' [_arr[_lp].acr,_valb])
                        ?}
                     || __UPG.msg(_nag+'Zmieniono wartość pola %1 z [%2] na [%3].' [_arr[_lp].acr,_valb,_vala])
                     ?};
                     ($(_fld+':=_a'))(_vala);
                     _put+=1
                  ?};
                  _val:=($_fld)();
                  {? ~fexists(_val,1)
                  || __UPG.msg(_nag+'Brak dostępu do pliku %1.' [_val])
                  ?}
               !}
            ?};
            {? _put
            || ZF_RPI.put()
            |? ~_next
            || _ok+=1
            ?};
            _ret=1 & (_next | ZF_RPI.next())
         !}
      ?};
      ZF_SKL.next()
   !};
   ZF_RPI.trig_on('*','*');
   ZF_RPI.cntx_pop();
   ZF_POZ.cntx_pop();
   {? _ok
   || __UPG.msg('Liczba szablonów, które nie wymagały zmian: %1.' [$_ok])
   ?}
|| __UPG.msg('Brak elementów formularzy ocen pracowniczych - transfer formatów nie był wymagany.')
?};
ZF_SKL.cntx_pop();

_ret


\zf_rpi_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Transfer formatów elementów formularzy ocen pracowniczych - opis.
::   WE:
::   WY:
::  ORG: \zf_rpi_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Transfer formatów elementów formularzy ocen pracowniczych'


\zf_wid_clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Usunięcie nadmiarowych uprawnień elementów statycznych.
::   WE:
::   WY:
::  ORG: \zf_wid_clean/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
:: Zrezygnowano z mechanizmu reloading_fml. Pomijanie walidacji przy usuwaniu rekordu nie jest potrzebne.
_ret:=0;

_firma:=exec('ref_firma','ustawienia');

_i1b:=_i1a:=0;
_i2b:=_i2a:=0;
_err:=0;

ZF_SKL.cntx_psh();
ZF_SKL.index('KOD');
ZF_SKL.prefix(_firma);
{? ZF_SKL.first()
|| ZF_POZ.cntx_psh();
   ZF_POZ.index('FK_SKL');
   ZF_WID.cntx_psh();
   ZF_WID.index('TYP');
   {!
   |? {? ZF_SKL.STALY='T'
      || do();
         ZF_WID.prefix(ZF_SKL.NP_DOK,ZF_SKL.ZZ_DOK);
         _i1b+=ZF_WID.size();
         {? ZF_WID.first()
         || {!
            |? ZF_WID.del()
            !}
         ?};
         _i1a:=ZF_WID.size();

         ZF_POZ.prefix(ZF_SKL.ref());
         {? ZF_POZ.first()
         || {!
            |? ZF_WID.prefix(ZF_POZ.NP_DOK,ZF_POZ.ZZ_DOK);
               _i2b+=ZF_WID.size();
               {? ZF_WID.first()
               || {!
                  |? ZF_WID.del()
                  !}
               ?};
               _i2a:=ZF_WID.size();
               ZF_POZ.next()
            !}
         ?};
         _err+=~end()
      ?};
      ZF_SKL.next()
   !};
   ZF_WID.cntx_pop();
   ZF_POZ.cntx_pop()
?};
ZF_SKL.cntx_pop();

__UPG.msg('Uprawnienia elementów formularzy: zbędnych / usuniętych: %1 / %2.' [$_i1b,$(_i1b-_i1a)]);
__UPG.msg('Uprawnienia pozycji formularzy: zbędnych / usuniętych: %1 / %2.' [$_i2b,$(_i2b-_i2a)]);
{? _err
|| __UPG.msg('W trakcie przetwarzanie wystąpiły błędy zapisu. Operację należy powtórzyć.')
?};

{? _i1a=_i1b & _i2a=_i2b & ~_err
|| 1
|| -1
?}


\zf_wid_clean_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Usunięcie nadmiarowych uprawnień elementów statycznych - opis.
::   WE:
::   WY:
::  ORG: \zf_wid_clean_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Dla elementów statycznych i związanych z nimi pozycjami formularzy usunięcie zbędnych (nadmiarowych) uprawnień.'


\R_CZYT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja tabeli dostawców czytników.
::  ORG: \R_CZYT/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
R_CZYT.cntx_psh();
R_CZYT.index('R_CZYT');
R_CZYT.prefix();
{? R_CZYT.first()
|| FORMULA.cntx_psh();
   FORMULA.index('FORMULA3');
   FORMULA.prefix('X');
   {? ~FORMULA.size()
   || exec('formula_p','dane_startowe');
      {? FORMULA.size()
      || __UPG.msg('Uzupełniono w słowniku %1 formuły do odczytu danych z czytników RCP.'[$FORMULA.size()])
      || __UPG.msg('Nie udało się uzupełnić słownika z formułami do odczytu danych z czytników RCP.')
      ?}
   ?};
   {? FORMULA.first()
   || _formula:=
         "  _ok:=0;
            {!
            |? {? +R_CZYT.F & FORMULA.FORMULA*R_CZYT.F
               || R_CZYT.FORMULA:=FORMULA.ref();
                  _ok:=R_CZYT.put()
               ?};
               ~_ok & FORMULA.next()
            !};
            _ok
         ";
      {!
      |? {? _formula()
         || __UPG.msg('Dla czytnika %1 udało się dodać formułę: %2.'[R_CZYT.N,R_CZYT.F])
         || _result:=-1;
            __UPG.msg('Dla czytnika %1 nie udało się odnaleźć formuły: %2.'[R_CZYT.N,R_CZYT.F])
         ?};
         R_CZYT.next()
      !}
   || _result:=-1;
      __UPG.msg('Brak definicji formuł odczytu danych.')
   ?};
   FORMULA.cntx_pop()
|| _result:=-1;
   __UPG.msg('Brak danych w tabeli dostawców czytników.')
?};
R_CZYT.cntx_pop();
_result


\R_CZYT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja tabeli dostawców czytników.
::  ORG: \R_CZYT_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualiacja informacji dotyczącej formuł odczytu danych.'


\R_ODN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja statusu wniosku o odbiór czasu wolnego.
::  ORG: \R_ODN/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
R_ODN.cntx_psh();
{? R_ODN.first()
|| _lrec:=R_ODN.size();
   _lmod:=_lnmod:=0;
   {!
   |? {? R_ODN.A='A'
      || R_ODN.A:='T';
         _lmod+=R_ODN.put()
      |? R_ODN.A='T'
      || _lnmod+=1
      ?};
      R_ODN.next()
   !};
   __UPG.msg('Zaktualizowano tabelę z wnioskami o odbiór czasu wolnego.');
   __UPG.msg(
      {? _lrec=1 || 'Przetworzono: %1 zapis.'[$_lrec]
      |? _lrec<5 || 'Przetworzono: %1 zapisy.'[$_lrec]
      || 'Przetworzono: %1 zapisów.'[$_lrec]
      ?}
   );
   __UPG.msg(
      {? _lmod=1 ||'Zmodyfikowano: %1 zapis.'[$_lmod]
      |? _lmod<5 || 'Zmodyfikowano: %1 zapisy.'[$_lmod]
      || 'Zmodyfikowano: %1 zapisów.'[$_lmod]
      ?}
   );
   __UPG.msg(
      {? _lnmod=1 || '%1 zapis nie wymagał modyfikacji.'[$_lnmod]
      |? _lnmod<5 || '%1 zapisy nie wymagały modyfikacji.'[$_lnmod]
      || '%1 zapisów nie wymagało modyfikacji.'[$_lnmod]
      ?}
   );
   {? _lrec=_lmod+_lnmod || _result:=1 ?}
|| __UPG.msg('Brak danych w tabeli wniosków o odbiór czasu wolnego.')
?};
R_ODN.cntx_pop();
_result


\R_ODN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja statusu wniosku o odbiór czasu wolnego.
::  ORG: \R_ODN_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja statusu wniosku o odbiór czasu wolnego.'


\r_opczyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja wartości w polach "Portal" i "Słownik" tabeli R_OPCZYT.
::   WE:
::   WY:
::  ORG: \r_opczyt/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_lrec:=_lmod:=0;
R_OPCZYT.cntx_psh();
R_OPCZYT.index('R_OPCZYS');
R_OPCZYT.prefix();
{? R_OPCZYT.first()
|| {!
   |? {? R_OPCZYT.SYMBOL='<SYSTEM>'
      || R_OPCZYT.PORTAL:=R_OPCZYT.SL:='T'
      || R_OPCZYT.PORTAL:=R_OPCZYT.SL:='N'
      ?};
      {? R_OPCZYT.put(1)
      || _lmod+=1
      ?};
      _lrec+=1;
      R_OPCZYT.next()
   !}
?};
R_OPCZYT.cntx_pop();
{? _lrec=_lmod
|| __UPG.msg('We wszystkich wierszach tabeli źródeł danych uzupełniono wartości pól "Portal" i "Słownik".');
   _result:=1
|? ~_lmod
|| __UPG.msg('W żadnym z wierszy tabeli źródeł danych nie uzupełniono wartości pól "Portal" i "Słownik".')
|| __UPG.msg('W %1 z %2 wierszy tabeli źródeł danych uzupełniono wartości w polach "Portal" i "Słownik".'[_lmod,_lrec])
?};
_result


\r_opczyt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Aktualizacja wartości w polach "Portal" i "Słownik" tabeli R_OPCZYT - opis.
::   WE:
::   WY:
::  ORG: \r_opczyt_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wartości w polach "Portal" i "Słownik" tabeli źródeł danych'


\PL_RES
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_RES
::   WY: -1/0/1
::  TAG: <PRYWATNA>
::  ORG: \PL_RES/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

PL_RES.cntx_psh();
PL_RES.index('SYM');
PL_RES.prefix();
_fml:="
   _put:=0;
   {? PL_RES.LAYERS=''
   || {? PL_RES.PARALLEL='1'
      || PL_RES.LAYERS:='T'
      || PL_RES.LAYERS:='N'
      ?};
      _put:=1
   ?};
   {? _put>0
   || PL_RES.put()
   ?};
   ~~
";
_result:=PL_RES.for_each(_fml,1);
{? _result
|| __UPG.msg('Zaktualizowano zasoby planu operacyjnego.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji zasobów planu operacyjnego.')
?};
PL_RES.cntx_pop();
_result


\PL_RES_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_RES - opis
::  ORG: \PL_RES_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli zasobów planu operacyjnego o znacznik rysowania w wielu warstwach.\n'
'Może się nie udać, tylko jeżeli pole tabeli PL_RES zostało zablokowane przez innego użytkownika'


\ZLBR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełania pola w tabeli ZLBR
::  ORG: \ZLBR/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

ZLBR.cntx_psh();
ZLBR.prefix();
_formula:="
   _put:=0;
   __lrec+=1;
   {? ZLBR.AKT=''
   || {? #ZLBR.DO & ZLBR.DO<date()
      || ZLBR.AKT:='N'
      || ZLBR.AKT:='T'
      ?};
      _put:=1
   ?};
   {? ZLBR.DYNAMIC='' || ZLBR.DYNAMIC:='N'; _put:=1 ?};
   {? _put || {? ZLBR.put(1) || __lmod+=1 ?} ?}
";
_result:=ZLBR.for_each(_formula,1);
ZLBR.cntx_pop();
__UPG.msg('Zaktualizowano brygady robocze.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZLBR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli ZLBR - opis
::  ORG: \ZLBR_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli brygad o znacznik aktywności.'


\PL_OPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_OPER
::  ORG: \PL_OPER/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

PL_OPER.cntx_psh();
PL_OPER.prefix();
_formula:="
   _put:=0;
   __lrec+=1;
   {? PL_OPER.PROBLEM='' || PL_OPER.PROBLEM:='N'; _put:=1 ?};
   {? _put || {? PL_OPER.put(1) || __lmod+=1 ?} ?}
";
_result:=PL_OPER.for_each(_formula,1);
PL_OPER.cntx_pop();
__UPG.msg('Zaktualizowano plan operacyjny.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PL_OPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_OPER - opis
::  ORG: \PL_OPER_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli pozycji planu operacyjnego o znacznik problemu.'


\PX_POZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PX_POZ
::  ORG: \PX_POZ/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

PX_POZ.cntx_psh();
PX_POZ.prefix();
_formula:="
   _put:=0;
   __lrec+=1;
   {? PX_POZ.WHEN='' || PX_POZ.WHEN:=PX_POZ.PX_CUP().SYMBOL; _put:=1 ?};
   {? PX_POZ.LANE=0 || PX_POZ.LANE:=1; _put:=1 ?};
   {? PX_POZ.PROBLEM='' || PX_POZ.PROBLEM:='N'; _put:=1 ?};
   {? _put || {? PX_POZ.put(1) || __lmod+=1 ?} ?}
";
_result:=PX_POZ.for_each(_formula,1);
PX_POZ.cntx_pop();
__UPG.msg('Zaktualizowano plan strategiczny.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_POZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PX_POZ - opis
::  ORG: \PX_POZ_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli pozycji planu strategicznego o znacznik problemu.'


\ZTP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli ZTP
::   WY: -1/0/1
::  TAG: <PRYWATNA>
::  ORG: \ZTP/upgrade_1714.fml
::       \ZTP/upgrade_2042.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

ZTP.cntx_psh();
ZTP.index('TP');
ZTP.prefix();
_fml:="
   _put:=0;
   __lrec+=1;
   {? ZTP.PROJZAKR=''
   || ZTP.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty');
      ZTP.PROJWJO:='N';
      ZTP.PROJZKH:='N';
      _put:=1
   ?};
   {? ZTP.RP_REZ=''
   || ZTP.RP_REZ:='N';
       _put:=1
   ?};
   {? _put>0 || {? ZTP.put(1) || __lmod+=1 ?} ?};
   ~~
";
_result:=ZTP.for_each(_fml,1);
ZTP.cntx_pop();
__UPG.msg('Zaktualizowano typów zleceń.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZTP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli ZTP - opis
::  ORG: \ZTP_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów zleceń o nowe pola związane z obsługą projektów.\n'
'Może się nie udać, tylko jeżeli pole tabeli ZTP zostało zablokowane przez innego użytkownika.'


\PX_WYK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PX_WYK
::  ORG: \PX_WYK/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

PX_WYK.cntx_psh();
PX_WYK.prefix();
_formula:="
   _put:=0;
   __lrec+=1;
   {? PX_WYK.ZAK=''
   || PX_WYK.ZAK:='T';
      _put:=1
   ?};
   {? PX_WYK.DATA0=date(0,0,0)
   || PX_WYK.DATA0:=PX_WYK.DATA;
      _put:=1
   ?};
   {? PX_WYK.CZAS0=time(0,0,0)
   || PX_WYK.CZAS0:=PX_WYK.CZAS;
      _put:=1
   ?};
   {? _put || {? PX_WYK.put(1) || __lmod+=1 ?} ?}
";
_result:=PX_WYK.for_each(_formula,1);
PX_WYK.cntx_pop();
__UPG.msg('Zaktualizowano rejestracje wykonań (plan strategiczny).');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_WYK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.14]
:: OPIS: Uzupełnia pola w tabeli PX_WYK - opis
::  ORG: \PX_WYK_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli wykonań planu strategicznego o znacznik zakończenia oraz datę i godzinę rozpoczęcia rejestracji.'


\B_PROC
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Aktualizacja pól tabeli B_PROC
::   WY: 0/1
::  TAG: <PUBLICZNA>
::  ORG: \B_PROC/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

B_PROC.cntx_psh();
B_PROC.index('AKT');
B_PROC.prefix('N');
{? B_PROC.first()
|| {!
   |? _put:=0;
      __lrec+=1;
      {? B_PROC.MODIFIED=''
      || B_PROC.MODIFIED:='N';
         _put:=1
      ?};
      {? B_PROC.ARCHDEL=''
      || B_PROC.ARCHDEL:='N';
         _put:=1
      ?};
      {? _put>0
      || {? B_PROC.put()<=0
         || __lmod+=1;
            _result:=0
         ?}
      ?};
      B_PROC.next()
   !}
?};
B_PROC.cntx_pop();
__UPG.msg('Zaktualizowano procesy (procesowość).');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\B_PROC_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Aktualizacja pól tabeli B_PROC - opis
::   WY: STRING
::  TAG: <PUBLICZNA>
::  ORG: \B_PROC_desc/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli procesów o pola służące do identyfikacji czy proces jest standardowy czy zmodyfikowany.\n'
'Może się nie udać, tylko jeżeli nie udał się zapis któregoś rekordu tabeli B_PROC.'


\ZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Nawija nowe pola w tabeli ZL
::   WY: -1/0/1
::  TAG: <PRYWATNA>
::  ORG: \ZL/upgrade_1728.fml
::       \ZL/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

VAR_DEL.delete('__lrec','__lmod','__zltab');
__lrec:=__lmod:=0;
__zltab:=~~;
ZL.trig_off('*','*');
ZGH.trig_off('*','*');
ZL.cntx_psh();
ZL.index('SYM');
ZL.prefix();
{? ZL.find_tab('first','ROK_MAX',,'=',0)
||
:: Jeżeli jest chociaż jeden rekord z nieuzupełnionym polem ROK_MAX to tworzona tabelka
   _sql:='select DK.ZL as REF, MAX(DK.AR) as DATA from @DK join ZL using (DK.ZL, ZL.REFERENCE) group by DK.ZL';
   __zltab:=sql(_sql)
?};
_fml:="
   __lrec+=1;
   _put:=0;
   {? ZL.PLAN_PO=''
   || {? exec('zl_utilization_po','upgradexpertis',ZL.ref())>0 || ZL.PLAN_PO:='T' || ZL.PLAN_PO:='N' ?}; _put:=1
   ?};
   {? ZL.PLAN_PX=''
   || {? exec('zl_utilization_px','upgradexpertis',ZL.ref())>0 || ZL.PLAN_PX:='T' || ZL.PLAN_PX:='N' ?}; _put:=1
   ?};
   {? ZL.ID_KSG=0 & ZL.UNRZL<=2147483647 || ZL.ID_KSG:=ZL.UNRZL; _put:=1 ?};
   {? ZL.IL0=0 | ZL.ILPDOK=0 | ZL.ILPWYK=0 | ZL.ILDOK<>0 | ZL.ILWYK<>0 || {? ZL.IL0=0 || ZL.IL0:=ZL.IL ?}; _put:=1 ?};
   {? ZL.RP='' || {? exec('is_main_podzlec','zl_link')>0 || ZL.RP:='T' || ZL.RP:='N' ?}; _put:=1 ?};
   {? ZL.RP_REZ='' || ZL.RP_REZ:='N'; _put:=1 ?};
:: Ustawienie znacznika BRAKI
   {? ZL.BRAKI='' || exec('update_braki','zl_common',ZL.ref(),,0) ?};
:: Aktualiuje ilość wykonaną (pola ILWYK oraz ILWYK_D)
   exec('oblicz_zgh_1zl','zl_wyk',ZL.ref());
:: Pole ZL.IMPROVE
   {? ZL.IMPROVE='' || ZL.IMPROVE:='N'; _put:=1 ?};
   {? ZL.STAT_B='' || {? ZL.BRAKI='T' || ZL.STAT_B:='N' || ZL.STAT_B:='T' ?}; _put:=1 ?};
   {? ZL.ROK_MAX=0
   || {? var_pres('__zltab')>100 & __zltab.find_tab(,'REF',,'=',$ZL.ref()) & __zltab.DATA<>0
      || ZL.ROK_MAX:=__zltab.DATA
      || ZL.ROK_MAX:=ZL.OD~1
      ?};
      _put:=1
   ?};
   {? _put>0 || __lmod+=1; ZL.put() ?};
   ~~
";
_result:=ZL.for_each(_fml,1);
ZL.cntx_pop();
ZGH.trig_on('*','*');
ZL.trig_on('*','*');
__UPG.msg('Zaktualizowano zlecenia.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__zltab');
_result


\ZL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Nawija nowe pola w tabeli ZL - opis
::   WY: STRING
::  TAG: <PRYWATNA>
::  ORG: \ZL_desc/upgrade_1728.fml
::       \ZL_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'1. Aktualizacja tabeli zleceń produkcyjnych o znaczniki występowania w planie produkcji oraz identyfikator księgowy.\n'
'Może się nie udać, tylko jeżeli jakiś rekord tabeli ZL został zablokowany przez innego użytkownika.\n'
'2. Uzupełnienie pól w zleceniach.'


\zl_utilization_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Wykorzystanie zasobow - dla wybranej tabeli
::   WE: _a - ZL.ref
::   WY: 1 - wykorzystany, 0 - brak zapisow
::  ORG: \zl_utilization_po/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=~~;
ZL.cntx_psh();
PL_OPER.cntx_psh();
PL_PART.cntx_psh();

_result:=0;
ZL.clear();
{? ZL.seek(_a)
|| {? ZL.RODZAJ<>'P'
   ||
      PL_PART.index('NRNZL');
      PL_PART.prefix(ZL.UNRZL);
      _result:=PL_PART.first()
   || PL_PART.index('ZL');
      PL_PART.prefix(ZL.ref());
      _result:=PL_PART.first()
   ?}
?};
PL_OPER.cntx_pop();
PL_PART.cntx_pop();
ZL.cntx_pop();
_result


\zl_utilization_px
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [12.10]
:: OPIS: Sprawdza czy zlecenie jest zaplanowane w planie strategicznym (WPP)
::   WE: _a - ZL.ref() - zlecenie
::       [_b] - INTEGER - [0] - sprawdzać tylko w wersji głównej
::                         1 - sprawdzac we wszysktich wersjach
::       [_c] - obj_new(1), w której ustawiany jest zwrotnie znacznik potwierdzenia planu
::   WY: 0 - nie jest zaplanowane
::       1 - jest zaplanowane, są pozycje planu
::       2 - ma być zaplanowane, jest w kolejce ale nie ma jeszcze pozycji planu (plan jest nieprzeliczony)
:: DOST: PUBLIC
::  ORG: \zl_utilization_px/upgrade_1728.fml
::----------------------------------------------------------------------------------------------------------------------
_zl:=_a;

_ver_chk:=0;
{? var_pres('_b')=type_of(0)
|| _ver_chk:=_b
?};

{? var_pres('_c')=type_of(obj_new(1))
|| _confirm:=_c
|| _confirm:=obj_new(1);
   _confirm[1]:='N'
?};

_result:=0;
_mainver:=null();
PX_VER.cntx_psh();
PX_VER.index('SYM');
PX_VER.prefix(1);
{? PX_VER.first()
|| _mainver:=PX_VER.ref();
:: przypisuje wersję główna do zmiennej
   PX_VAR.MAINVER:=PX_VER.ref()
?};
PX_VER.cntx_pop();

_can_continue:=0;

ZL.cntx_psh();
PX_OBJ.cntx_psh();
PX_POZ.cntx_psh();

ZL.clear();
{? ZL.seek(_zl)
||
   PX_OBJ.index('ZL');
   PX_OBJ.prefix(ZL.ref());
   {? PX_OBJ.first()
   ||
::    1. Sprawdzam powiązania pod wzgledem pozycji planu
      {? _ver_chk=0
      ||
         PX_POZ.index('TM_VOBJ');
         PX_POZ.prefix(_mainver,PX_OBJ.ref())
      || PX_POZ.index('PX_OBJ3');
         PX_POZ.prefix(PX_OBJ.ref())
      ?};
      {? PX_POZ.size()>0
      || _result:=1
      ?};
      PX_CONN.cntx_psh();
      {? _ver_chk=0
      || PX_CONN.index('VER');
         PX_CONN.prefix(_mainver,PX_OBJ.ref())
      || PX_CONN.index('PX_OBJ');
         PX_CONN.prefix(PX_OBJ.ref())
      ?};
::    2. Sprawdzam powiązania pod względem pozycji w kolejce
::    (ważne dla nieprzeliczonego planu)
      {? _result=0
      || {? PX_CONN.size()>0
         || _result:=2
         ?}
      ?};
::    3. Utalenie znacznika potwierdzenia planu
      _c_n:=_c_p:=_c_t:=0;
      {? PX_CONN.first()
      || {!
         |? _cc:=PX_CONN.PX_GRP().CONFIRM;
            {? _cc='N' || _c_n+=1
            |? _cc='P' || _c_p+=1
            |? _cc='T' || _c_t+=1
            ?};
            PX_CONN.next()
         !}
      ?};
      {? _c_n=0 & _c_p=0 & _c_t>0 & _confirm[1]='N'
      || _confirm[1]:='T'
      |? _c_p>0 | _c_t>0
      || _confirm[1]:='P'
      || _confirm[1]:='N'
      ?};
      PX_CONN.cntx_pop()
   ?};
:: Jeśli zlecenie złożone to rekurencyjnie sprawdzam też podzlecenia
   {? _result=0
   || _can_continue:=0;
      {? ZL.RODZAJ<>'P' & ZL.UNRZL<>0
      || ZL.cntx_psh();
         ZL.index('NRNZL');
         ZL.prefix(ZL.UNRZL);
         {? ZL.first()
         || {!
            |? _can_continue:=exec('zl_utilization_px','upgradexpertis',ZL.ref(),,_confirm);
               ZL.next() & _can_continue=0
            !}
         ?};
         ZL.cntx_pop()
      ?};
      _result:=_can_continue
   ?}
?};
PX_POZ.cntx_pop();
PX_OBJ.cntx_pop();
ZL.cntx_pop();
_result


\B_PREL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.42]
:: OPIS: Nawija nowe pola w tabeli B_PREL
::   WY: -1/0/1
::  ORG: \B_PREL/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_can_continue:=1;
B_PREL.cntx_psh();
B_PREL.index('CODE');
B_PREL.prefix();
B_TIMER.cntx_psh();
B_TIMER.index('B_PREL');
{? B_PREL.first()
|| {!
   |? _put:=0;
      {? B_PREL.ENABLED=''
      || _put:=1;
         B_TIMER.prefix(B_PREL.ref());
         {? B_TIMER.first()
         || B_PREL.ENABLED:=B_TIMER.ON
         || B_PREL.ENABLED:='T'
         ?}
      ?};
      {? _put>0
      || _can_continue:=B_PREL.put();
         {? _can_continue=0
         || _result:=0;
            __UPG.msg('Nie udany zapis elementu procesu: %1. Proces: %2'@
               [exec('B_PREL','#to_string'),exec('record','#to_string',B_PREL.B_PROC)]
            )
         ?}
      ?};
      B_PREL.next()
   !}
?};
B_PREL.cntx_pop();
B_TIMER.cntx_pop();
{? _result>0
|| __UPG.msg('Zaktualizowano zapisy tabeli B_PREL.')
|| __UPG.msg('Nie udało się zaktualizować wszystkich zapisów tabeli B_PREL.')
?};
_result


\B_PREL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.42]
:: OPIS: Nawija nowe pola w tabeli B_PREL - opis
::   WY: -1/0/1
::  ORG: \B_PREL_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w tabeli B_PREL (elementy procesów)'


\ZLZAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.42]
:: OPIS: Nawija nowe pola w tabeli ZLZAM
::   WY: -1/0/1
::  ORG: \ZLZAM/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_can_continue:=1;
_lrec:=_lmod:=0;
ZLZAM.index('PL_DTTM');
ZLZAM.prefix();
{? ZLZAM.first()
|| {!
   |? _put:=0;
      _lrec+=1;
      {? ZLZAM.SRC=''
      || _put:=1;
         {? 1+ZLZAM.ZAMPOZ='z'
         || ZLZAM.SRC:='Z'
         |? 3+ZLZAM.ZAMPOZ='psp'
         || ZLZAM.SRC:='S'
         |? 5+ZLZAM.ZAMPOZ='plrel'
         || ZLZAM.SRC:='A'
         |? 5+ZLZAM.ZAMPOZ='plprd'
         || ZLZAM.SRC:='P'
         || ZLZAM.SRC:='R'
         ?}
      ?};
      {? _put>0
      || _can_continue:=ZLZAM.put();
         {? _can_continue>0 || _lmod+=1 ?}
      ?};
      ZLZAM.next() & _can_continue>0
   !}
?};
{? _can_continue>0
|| _result:=1;
   __UPG.msg('Zaktualizowano zapisy tabeli ZLZAM.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$_lrec,$_lmod])
|| __UPG.msg('Nie zaktualizowano wszystkich zapisów tabeli ZLZAM.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$_lrec,$_lmod])
?};
_result


\ZLZAM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Nawija nowe pola w tabeli ZLZAM - opis
::   WY: STRING
::  ORG: \ZLZAM_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli powiązań zleceń z elementami na podstawie których wygenerowano zlecenie.\n'
'Może się nie udać, jeżeli nie udał się zapis na rekordzie tabeli ZLZAM (rekord zablokowany, usunięty itp).'


\MGRP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.02]
:: OPIS: Uzupełnia zawartość pól WYPOS, ZWROT, CTRLIL w tabeli MGRP
::  ORG: \MGRP/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
MGRP.cntx_psh();
MGRP.prefix();
__prec:=0;
_result:=MGRP.for_each("_put:=0;
                        {? MGRP.WYPOS=''
                        || MGRP.WYPOS:='N';
                           _put:=1
                        ?};
                        {? MGRP.ZWROT=''
                        || MGRP.ZWROT:='T';
                           _put:=1
                        ?};
                        {? MGRP.CTRLIL=''
                        || MGRP.CTRLIL:='O';
                           _put:=1
                        ?};
                        {? _put
                        || MGRP.put();
                           __prec+=1
                        ?}",1);
__UPG.msg('Zaktualizowano podgrupy materiałowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$MGRP.size(),$__prec]);
VAR_DEL.delete('__prec');
MGRP.cntx_pop();
_result


\MGRP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.02]
:: OPIS: Opis formuły uzupełniającej zawartość zawartość pól WYPOS, ZWROT, CTRLIL w tabeli MGRP
::  ORG: \MGRP_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w podgrupach materiałowych.'


\GROP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w tabeli GROP
::  ORG: \GROP/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
GROP.cntx_psh();
GROP.trig_off('*','*');
GROP.prefix();
_result:=GROP.for_each(
 "_put:=0;
  {? GROP.PROBLEM='' || GROP.PROBLEM:='N'; _put:=1 ?};
  {? GROP.DIR=0 || exec('dir_update','zl_grop'); _put:=1 ?};
  {? GROP.PLAN_PX='' || GROP.PLAN_PX:='N'; _put:=1 ?};
  {? GROP.PLAN_PO='' || GROP.PLAN_PO:={? exec('get_min_start','po_ogr',GROP.ref())>0 || 'T' || 'N' ?}; _put:=1 ?}
  {? GROP.TM_REA=0 || exec('termin_update','zl_grop'); _put:=1 ?};
  {? GROP.PROBLEM='T' & GROP.PROBKLAS='' || GROP.PROBKLAS:=exec('prob_class_suspensing','zl_wkj'); _put:=1 ?};
  {? GROP.NO_START='' || GROP.NO_START:='N'; _put:=1 ?};
  {? GROP.ODDZ='' || GROP.ODDZ:='c'; _put:=1 ?};
  {? GROP.ROK_MAX=0 || GROP.ROK_MAX:=GROP.DATA~1; _put:=1 ?};
  {? GROP.IDEAN='' || GROP.IDEAN:=exec('bl_idean','kody_kresk',GROP); _put:=1 ?};
  {? GROP.LIM='' || GROP.LIM:='N'; _put:=1 ?};
  {? _put || GROP.put() ?};
  exec('actualize_grop','!tpp_gop_dred',GROP.ref())",1);
GROP.trig_on('*','*');
GROP.cntx_pop();

:: Aktualizuje pola ILW, ILPOTW, CZY_WYK, CZY_POTW (put robiony wewnątrz formuły)
{? _result
|| _result:=exec('odtworz_ilw','zl_grop',0)
?};
{? _result || __UPG.msg('Zaktualizowano grupy operacji.') ?};
_result


\GROP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w tabeli GROP
::  ORG: \GROP_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli GROP (grupy operacji).'


\PROD_REJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Aktualizuje zapisy w tabeli do rejestracji wykonań PROD_REJ
::  ORG: \PROD_REJ/upgrade_1802.fml
::       \PROD_REJ/upgrade_2014.fml
::       \PROD_REJ/upgrade_2325.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('load_4rej','zl_grop',0);
{? _result.RESULT
|| __UPG.msg('Zaktualizowano %1 zapisów do rejestracji wykonań produkcji.'[$_result.IL]);
   VAR_DEL.delete('__lrec','__lmod');
   __lrec:=__lmod:=0;

   PROD_REJ.trig_off('*', '*');
   PROD_REJ.cntx_psh();
   PROD_REJ.prefix();
   GROP.cntx_psh();
   GROP.prefix();
   _res:=PROD_REJ.for_each("
      __lrec+=1;
      _put:=0;
      {? PROD_REJ.SRODZ='ZL' & PROD_REJ.GROP<>null()
      || PROD_REJ.GROP();
         exec('fields_4rej','zl_grop');
         _put:=1
      |? PROD_REJ.SRODZ='PL' & PROD_REJ.PL_OGR<>null()
      || exec('fields_4rej','po_wyk');
         _put:=1
      ?};
::    Ustawienie znacznika RP
      {? PROD_REJ.RP=''
      || PROD_REJ.RP:='N';
         {? PROD_REJ.ZGP<>null()
         || {? PROD_REJ.ZGP().DOK<>null()
            || PROD_REJ.RP:='T'
            ?}
         |? PROD_REJ.PL_OPER<>null() & PROD_REJ.PL_OPER().ZGP<>null()
         || {? PROD_REJ.PL_OPER().ZGP().DOK<>null()
            || PROD_REJ.RP:='T'
            ?}
         |? PROD_REJ.PX_POZ<>null() & PROD_REJ.PX_POZ().PX_STAGE<>null()
         || PROD_REJ.RP:=exec('is_rp','px_stage',PROD_REJ.PX_POZ().PX_STAGE)
         ?};
         _put:=1
      ?};
::    Aktualizacja pól PRAC, BRYG, IL_KAP
      {? PROD_REJ.SRODZ='ZL' & PROD_REJ.ZGP<>null()
      || {? exec('zgp_is_kap','zl_kap',ZGP.ref())>0
         || {? ZGP.BRYG='T' || PROD_REJ.BRYG:='T' || PROD_REJ.PRAC:='T' ?}
         ?};
          PROD_REJ.IL_KAP:=exec('prod_rej_il_kap','prod_rej');
         _put:=1
      ?};
      {? PROD_REJ.GKTL=null()
      || {? PROD_REJ.ZGP<>null() & PROD_REJ.ZGP().NRPRZ().GKTL<>null()
         || PROD_REJ.GKTL:=PROD_REJ.ZGP().NRPRZ().GKTL;
            _put:=1
         |? PROD_REJ.PL_OPER<>null() & PROD_REJ.PL_OPER().ZGP<>null() & PROD_REJ.PL_OPER().ZGP().NRPRZ().GKTL<>null()
         || PROD_REJ.GKTL:=PROD_REJ.PL_OPER().ZGP().NRPRZ().GKTL;
            _put:=1
         ?}
      ?};
      {? _put>0
      || {? PROD_REJ.put()
         || __lmod+=1
         ?}
      ?}
   ");
   GROP.cntx_pop();
   PROD_REJ.cntx_pop();
   PROD_REJ.trig_on('*', '*');
   __UPG.msg('Zaktualizowano opisy operacji grupowych w oknie rejestracji produkcji.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
   VAR_DEL.delete('__lrec','__lmod')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji zapisów do rejestracji wykonań produkcji.')
?};
_result.RESULT


\PROD_REJ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Aktualizuje zapisy w tabeli do rejestracji wykonań produkcji PROD_REJ - opis
::  ORG: \PROD_REJ_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów w tabeli do rejestracji wykonań produkcji (PROD_REJ).'


\ZLGD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.02]
:: OPIS: Uzupełnia brakujące pola w tabeli ZLGD
::  ORG: \ZLGD/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=0;

:: Wyłączam triggery
TRIG_OFF.ZLGD:='T';
{? exec('get','#params',500611,2)='T'
|| exec('trig_ZLGD','prod_rej',0)
?};

_formula:="
   _result:=0;
   _can_continue:=1;

   ZLGD.cntx_psh();
   ZLGD.index('ZPARN');
   ZLGD.prefix();
   {? ZLGD.first()
   || {!
      |? _put:=0;
         {? ZLGD.POTW='' || ZLGD.POTW:='N'; _put:=1 ?};
         {? ZLGD.KJ_NEED=''
         || {? ZLGD.ZGP().KJ_BAD<>'N'
            || ZLGD.KJ_NEED:='T'
            || ZLGD.KJ_NEED:='N'
            ?};
            _put:=1
         ?};
         {? ZLGD.GROP<>null() & ZLGD.GROPS=null()
         || GROPS.index('GROP');
            GROPS.prefix(ZLGD.GROP);
            {? GROPS.first()
            || ZLGD.GROPS:=GROPS.ref();
               _put:=1
            ?}
         ?};
         {? ZLGD.STAT_B='' || ZLGD.STAT_B:='N'; _put:=1 ?};
         {? ZLGD.ZGP().DOK<>null() & ZLGD.DK=''
         || _dk:=exec('transfer_zlnd4zlgd','magdok_prod');
            {? _dk<>'' || ZLGD.DK:=_dk; _put:=1 ?}
         ?};
         {? _put>0
         || _can_continue:=ZLGD.put()
         ?};
         ZLGD.next() & _can_continue>0
      !}
   ?};
   ZLGD.cntx_pop();
   {? _can_continue>0
   || _result:=1
   ?};
   _result
";

_msk:=ZLGD.names();
_msk.clear();
{? _msk.first()
|| {!
   |? ZLGD.cntx_psh();
      ZLGD.use(_msk.NAME);
      ZLGD.prefix();
      _result:=exec('for_each_mask','#table',ZLGD,_formula,,,,1,1);
      ZLGD.cntx_pop();
      _result>0 & _msk.next()
   !}
?};
obj_del(_msk);

:: Włączam triggery
TRIG_OFF.ZLGD:='';
{? exec('get','#params',500611,2)='T'
|| exec('trig_ZLGD','prod_rej',1)
?};

{? _result
|| __UPG.msg('Zaktualizowano zapisy w tabeli rejestracji wykonań do zleceń.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji zapisów w tabeli rejestracji wykonań do zleceń.')
?};
_result


\ZLGD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia brakujące pola w tabeli ZLGD
::  ORG: \ZLGD_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów w tabeli rejestracji wykonań do zleceń (ZLGD).'


\PLIKIDOC
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Przepisuje dokumenty z biblioteki PLIB_DOC bezpośrednio do załączników PLIKIDOC,
::       przepisuje załączniki z PLIKIIMG do PLIKIDOC
::  ORG: \PLIKIDOC/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
PLIKIDOC.cntx_psh(); PLIKIIMG.cntx_psh();
PLIKIDOC.prefix(); PLIKIIMG.prefix();
__lrec:=__lmod:=0;
{? _result=1
|| _formula:="
      __lrec+=1;
      {? PLIKIDOC.DOC=null()
      || PLIKIDOC.DOC:=PLIKIDOC.FILE().DOC;
         __lmod+=1;
         PLIKIDOC.put()
      ?}
   ";
   _result:=PLIKIDOC.for_each(_formula,1);
   __UPG.msg('Zaktualizowano zapisy dla załączników (dokumenty).');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};
{? _result=1
|| __lrec:=__lmod:=0;
   _formula:="
      __lrec+=1;
      PLIKIDOC.index('TRS');
      PLIKIDOC.prefix(PLIKIIMG.TAB,PLIKIIMG.MSK,PLIKIIMG.REF,'',null(),PLIKIIMG.FILE().IMAGE);
      {? ~PLIKIDOC.first()
      || PLIKIDOC.blank(1);
         PLIKIDOC.TAB:=PLIKIIMG.TAB;
         PLIKIDOC.REF:=PLIKIIMG.REF;
         PLIKIDOC.MSK:=PLIKIIMG.MSK;
         PLIKIDOC.DOC:=PLIKIIMG.FILE().IMAGE;
         __lmod+=1;
         PLIKIDOC.add()
      ?}
   ";
   _result:=PLIKIIMG.for_each(_formula,1);
   __UPG.msg('Zaktualizowano zapisy dla załączników (obrazy).');
   __UPG.msg('Przetworzono: %1 zapisów, z czego przeniesiono: %2 zapisów.'[$__lrec,$__lmod])
?};
PLIKIDOC.cntx_pop(); PLIKIIMG.cntx_pop();
VAR_DEL.delete('__lrec','__lmod');
_result


\PLIKIDOC_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Przepisuje dokumenty z biblioteki bezpośrednio do załączników PLIKIDOC,
::       przepisuje załączniki z PLIKIIMG do PLIKIDOC - opis
::  ORG: \PLIKIDOC_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Reorganizacja załączników przypisanych do technologii i zleceń.'


\dokumTOPER2ZGP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia dokumentację do pozycji przewodników na podstawie źródłowych operacji
::  ORG: \dokumTOPER2ZGP/upgrade_1802.fml
::       \ZGP/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
exec('declare','tech_doc');
KOMM.init();
_result:=1;
ZGP.trig_off('*','*');
ZGP.cntx_psh();
ZGP.prefix();
{? ZGP.first()
|| {!
   |? __lrec+=1;
      _put:=0;
      {? ZGP.RTOPER<>''
      || DocLib.copy('TOPER',exec('FindAndGet','#table',TOPER,ZGP.RTOPER,,"ref()",null()),'ZGP',ZGP.ref())
      ?};
      {? ZGP.FIX_NORM=''
      || ZGP.FIX_NORM:='N';
         _put:=1
      ?};
      {? ZGP.WEW='N' & ZGP.KOOP_AKC=''
      || {? ZGP.TMSTARTK>0 | ZGP.TMENDK>0
         || ZGP.KOOP_AKC:='T'
         || ZGP.KOOP_AKC:='N'
         ?};
         _put:=1
      ?};
      {? _put || {? ZGP.put(1) || __lmod+=1 ?} ?};
      ZGP.next()
   !}
?};
ZGP.cntx_pop();
ZGP.trig_on('*','*');
__UPG.msg('Uzupełniono dokumentację do pozycji przewodników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
_tab:=KOMM.tab();
{? _tab.first()
|| __UPG.msg('Poniższe komunikaty mogą wynikać z powtórnego uruchomienia funkcji.');
   {!
   |? __UPG.msg(_tab.MESSAGE);
      _tab.next()
   !}
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\dokumTOPER2ZGP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia dokumentację do pozycji przewodników na podstawie źródłowych operacji - opis
::  ORG: \dokumTOPER2ZGP_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie dokumentacji do pozycji przewodników na podstawie źródłowych operacji.'


\ZGP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w pozycjach przewodników
::  ORG: \ZGP/upgrade_1802.fml
::  ORG: \ZGP/upgrade_2325.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZGP.trig_off('*','*');
ZGP.cntx_psh();
TOPER.cntx_psh(); TTOPER.cntx_psh(); TTOUT.cntx_psh();
TOPER.prefix(); TTOPER.prefix(); TTOUT.prefix();
_formula:="
   __lrec+=1;
   _put:=0;
   {? ZGP.DOC_REJ='' || ZGP.DOC_REJ:='P'; _put:=1 ?};
   {? ZGP.OPER_NAZ='' & ZGP.TOPER<>null()
   || {? TOPER.seek(ZGP.TOPER,ref_name(ZGP.TOPER),1)
      || ZGP.OPER_NAZ:={? TOPER.WEW='T' || TOPER.OPER().NA || TOPER.TTOUT().NA ?};
         _put:=1
      ?}
   ?};
   {? ZGP.PROBLEM='T' & ZGP.PROBKLAS='' || ZGP.PROBKLAS:=exec('prob_class_suspensing','zl_wkj'); _put:=1 ?};
   {? ZGP.WYK_FAST='' || ZGP.WYK_FAST:='N'; _put:=1 ?};
   {? ZGP.NO_START='' || ZGP.NO_START:='N'; _put:=1 ?};
   {? ZGP.IDEAN='' || ZGP.IDEAN:=exec('bl_idean','kody_kresk',ZGP); _put:=1 ?};
   {? _put || __lmod+=1; ZGP.put() ?}
";
_result:=ZGP.for_each(_formula,1);
ZGP.cntx_pop();
TOPER.cntx_pop(); TTOPER.cntx_pop(); TTOUT.cntx_pop();
ZGP.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje przewodników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZGP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnia pola w pozycjach przewodników - opis
::  ORG: \ZGP_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w pozycjach przewodników.'


\BRAKI_GD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnienie pól w tabeli BRAKI_GD
::  ORG: \BRAKI_GD/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
BRAKI_GD.cntx_psh(); BRAKI_GD.prefix();
__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   {? BRAKI_GD.MZLGD=''
   || __lmod+=1;
::    Pole BRAKI_GD.MZLGD ustawia trigger przed put
      BRAKI_GD.put()
   ?}
";
_result:=BRAKI_GD.for_each(_formula,1);
__UPG.msg('Zaktualizowano tabelę BRAKI_GD.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
BRAKI_GD.cntx_pop();
_result


\BRAKI_GD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Uzupełnienie pól w tabeli BRAKI_GD - opis
::  ORG: \BRAKI_GD_desc/upgrade_1802.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w tabeli BRAKI_GD.'


\FIRMA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli FIRMA
::  ORG: \FIRMA/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=FIRMA.for_each("{? FIRMA.A='' || FIRMA.A:='T'; FIRMA.put() ?}",1);
{? _result
|| __UPG.msg('Zaktualizowano pole określające aktywność firmy.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji kartoteki firm.')
?};
_result


\FIRMA_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli FIRMA - opis
::  ORG: \FIRMA_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli firm.'


\DOKUM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnienie nowych pól w tabeli DOKUM oraz brakujących wartości DOKUM.DOKUMI
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DOKUM.names();
_msk.clear();
DOKUM.cntx_psh(); DOKUMZ.cntx_psh(); DOKUMZA.cntx_psh();
DOKUM.trig_off('*','*');
{? _msk.first()
|| {!
   |? DOKUM.use(_msk.NAME);
      DOKUM.index('DOKUM');
      DOKUM.prefix();
      {? DOKUM.first()
      || {!
         |? __lrec+=1;
            _put:=0;
            {? DOKUM.KH<>null() & DOKUM.STAT_REJ='' & (5+DOKUM.REFSQL)='faktu' || DOKUM.KH:=null(); _put:=1 ?};
            {? DOKUM.RECEIVED='' || DOKUM.RECEIVED:='N'; _put:=1 ?};
            {? DOKUM.BSEND='' || DOKUM.BSEND:='N'; _put:=1 ?};
            {? DOKUM.STAT_REJ='' || DOKUM.STAT_REJ:='N'; _put:=1 ?};
            _zal:={? (DOKUMZ.index('GUID');DOKUMZ.prefix(DOKUM.ref());DOKUMZ.first()) || 'T' || 'N' ?};
            {? DOKUM.ZAL<>_zal || DOKUM.ZAL:=_zal; _put:=1 ?};
            {? DOKUM.P_POL='' || DOKUM.P_POL:='N'; _put:=1 ?};
            {? DOKUM.P_POT='' || DOKUM.P_POT:='N'; _put:=1 ?};
            {? (8+DOKUM.REFSQL)='pj_nproj'
            || {? DOKUM.PROJEKTY=null() & PROJEKTY.seek(DOKUM.REFSQL) || DOKUM.PROJEKTY:=PROJEKTY.ref(); _put:=1 ?};
               {? DOKUM.TYP='I' || DOKUM.TYP:='D'; _put:=1 ?}
            ?};
            {? DOKUM.KSEFUPOG=''
            || {? DOKUM.BL_GUID<>''
               || DOKUMZ.index('GUID');
                  DOKUMZ.prefix(DOKUM.ref(),DOKUM.BL_GUID);
                  {? DOKUMZ.first()
                  || DOKUM.KSEFUPOG:='T'
                  || {? DOKUM.KSEFUPO='' || DOKUM.KSEFUPOG:='N' || DOKUM.KSEFUPOG:='P' ?}
                  ?}
               || DOKUM.KSEFUPOG:='N'
               ?};
               _put:=1
            ?};
            {? DOKUM.name()='dokum' & DOKUM.DOKUMI=null()
            || DOKUMZ.index('DISP');
               DOKUMZ.prefix(DOKUM.ref());
               _ok:=0;
               {? DOKUMZ.first()
               || {!
                  |? {? DOKUMZ.DOKUM<>null() & DOKUMZ.bl_info('DOKUM','EXTENSION')='pdf'
                     || DOKUM.DOKUMI:=DOKUMZ.DOKUM;
                        _put:=1; _ok:=1
                     ?};
                     _ok=0 & DOKUMZ.next()
                  !}
               ?}
            ?};
            _war:=DOKUM.RODZAJ_K='P' & DOKUM.BL_VISIB='T';
            {? DOKUM.BL_VDZK=''
            || DOKUM.BL_VDZK:={? _war & (3+DOKUM.BL_TYPE='INV' | DOKUM.BL_TYPE='DOC') || 'T' || 'N' ?}; _put:=1
            ?};
            {? DOKUM.BL_VORD=''
            || DOKUM.BL_VORD:={? _war & (DOKUM.BL_TYPE='ORD' | DOKUM.BL_TYPE='DOC') || 'T' || 'N' ?}; _put:=1
            ?};
            {? DOKUM.INFO_END='' || DOKUM.INFO_END:='N'; _put:=1 ?};
            {? DOKUM.BATCH='' || DOKUM.BATCH:='N'; _put:=1 ?};
            {? DOKUM.KSEF2SGN='' || DOKUM.KSEF2SGN:='N'; _put:=1 ?};
            {? _put || __lmod+=1; DOKUM.put() ?};
            DOKUM.next()
         !}
      ?};
      _msk.next()
   !}
?};
DOKUM.trig_on('*','*');
DOKUM.cntx_pop(); DOKUMZ.cntx_pop(); DOKUMZA.cntx_pop();
__UPG.msg('Zaktualizowano załączniki.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\DOKUM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli DOKUM - opis
::  ORG: \DOKUM_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli dokumentów.'


\B_KEYREF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizacja rekordów kluczowych
::  ORG: \B_KEYREF/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
B_KEYREF.cntx_psh();
B_KEYREF.index('UID_REF');

_result:=0;
_can_continue:=1;

B_KEYREF.prefix();
{? B_KEYREF.first()
|| {!
   |?
::    Uwaga, cała funkcja sprowadza się do przeputowania wszystkich rekordów
::    bo nawijanie brakujących pól jest w triggerze
      _can_continue:=B_KEYREF.put();
      B_KEYREF.next() & _can_continue>0
   !}
?};
B_KEYREF.cntx_pop();
{? _can_continue>0
|| _result:=1;
   __UPG.msg('Zaktualizowano rekordy kluczowe.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji rekordów kluczowych.')
?};
_result


\B_KEYREF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizacja rekordów kluczowych - opis
::  ORG: \B_KEYREF_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rekordów kluczowych'


\KH_OSOB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli KH_OSOB
::  ORG: \KH_OSOB/upgrade_1822.fml
::       \kh_osob/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   _put:=0;
   {? KH_OSOB.BL='' || KH_OSOB.BL:='N'; _put:=1 ?};
   {? KH_OSOB.BL_RESET='' || KH_OSOB.BL_RESET:='N'; _put:=1 ?};
   {? KH_OSOB.OS_ZARZ='' || KH_OSOB.OS_ZARZ:='N'; _put:=1 ?};
   {? KH_OSOB.OS_ODP='' || KH_OSOB.OS_ODP:='N'; _put:=1 ?};
   {? KH_OSOB.OS_DEC='' || KH_OSOB.OS_DEC:='N'; _put:=1 ?};
   {? KH_OSOB.REL='' || KH_OSOB.REL:='N'; _put:=1 ?};
   {? KH_OSOB.ARCH='' || KH_OSOB.ARCH:='N'; _put:=1 ?};
   {? _put || KH_OSOB.put() ?}
";
_result:=KH_OSOB.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w kartotece osób kontaktowych kontrahenta.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w kartotece osób kontaktowych kontrahenta.')
?};
_result


\KH_OSOB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Uzupełnia pola w tabeli KH_OSOB - opis
::  ORG: \KH_OSOB_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli osób kontaktowych kontrahenta.'


\FO_DEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizuje pola w tabeli FO_DEF
::  ORG: \FO_DEF/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
FO_DEF.cntx_psh();
FO_DEF.index('TAB');
FO_DEF.prefix();
_can_continue:=1;
_result:=0;
{? FO_DEF.first()
|| {!
   |? {? FO_DEF.WDR=''
      ||
         _put:=1;
         FO_DEF.cntx_psh();
         FO_DEF.index('NR');
         FO_DEF.prefix(FO_DEF.KIND,'N',FO_DEF.NR);
         {? FO_DEF.first()
         || _put:=0
         ?};
         FO_DEF.cntx_pop();

         {? _put>0
         || FO_DEF.WDR:='N';
            _can_continue:=FO_DEF.put()
         ?}
      ?};
      FO_DEF.next() & _can_continue>0
   !}
?};
{? _can_continue>0
|| _result:=1;
   __UPG.msg('Zaktualizowano uniwersalne parametry FO (aplikacyjne).')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji uniwersalnych parametrów FO (aplikacyjnych).')
?};
FO_DEF.cntx_pop();
_result


\FO_DEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizuje pola w tabeli FO_DEF - opis
::  ORG: \FO_DEF_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja mechanizmu uniwersalnych parametrów FO (parametry aplikacyjne)'


\FO_USR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizuje pola w tabeli FO_USR
::  ORG: \FO_USR/upgrade_1822.fml
::       \FO_USR/upgrade_2042.fml
::----------------------------------------------------------------------------------------------------------------------
FO_USR.cntx_psh();
FO_USR.index('TAB');
FO_USR.prefix();
_can_continue:=1;
_result:=0;
{? FO_USR.first()
|| {!
   |? {? FO_USR.WDR=''
      ||
         _put:=1;
         FO_USR.cntx_psh();
         FO_USR.index('USERS');
         FO_USR.prefix(FO_USR.USERS,'N',FO_USR.NR);
         {? FO_USR.first()
         || _put:=0
         ?};
         FO_USR.cntx_pop();

         {? _put>0
         || FO_USR.WDR:='N';
            _can_continue:=FO_USR.put()
         ?}
      ?};
      FO_USR.next() & _can_continue>0
   !}
?};
{? _can_continue>0
|| _result:=exec('upgrade_usr','#params');
   __UPG.msg('Zaktualizowano uniwersalne parametry FO (użytkownika).')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji uniwersalnych parametrów FO (użytkownika).')
?};
FO_USR.cntx_pop();
_result


\FO_USR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Aktualizuje pola w tabeli FO_USR - opis
::  ORG: \FO_USR_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja mechanizmu uniwersalnych parametrów FO (parametry użytkownika)'


\UAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.22]
:: OPIS: Aktualizacja atrybutów technologii
::  ORG: \UAT/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=UAT.for_each("{? UAT.ACT='' || UAT.ACT:='T'; UAT.put() ?}",1);
{? _result
|| __UPG.msg('Zaktualizowano pole określające aktywność atrybutu technologii.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji atrybutów technologii.')
?};
_result


\UAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.22]
:: OPIS: Aktualizacja atrybutów technologii - opis
::  ORG: \UAT_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja atrybutów technologii'


\DATY
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Dodanie pola ROK_T do tabeli DATY
::  ORG: \DATY/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_lrec:=_lmod:=0;
DATY.cntx_psh();
DATY.index('DATA');
DATY.prefix();
_size:=DATY.size();
{? DATY.first()
|| FUN.prg_start(_size,'Przetwarzanie tabeli %1'@['DATY'],,,1);
   {!
   |? _lrec+=1;
      _put:=0;
      {? DATY.ROK<2038
      ||
      {? DATY.ROK_T=0
      || DATY.ROK_T:=(DATY.DATA-DATY.DZIEN_T+4)~1;
         _put:=1
      ?};
      _dt1:=date(DATY.ROK,DATY.MIESIAC,DATY.DZIEN_M)+1;
      _stamp_e:=tm_stamp(_dt1~1,_dt1~2,_dt1~3, 0,0,0);
      {? DATY.STAMP_E<>_stamp_e
      || DATY.STAMP_E:=_stamp_e;
         _put:=1
      ?};
         {? _put || {? DATY.put() || _lmod+=1 ?} ?}
      ?};
      FUN.prg_next();
      DATY.next()
   !};
   FUN.prg_stop()
?};
DATY.cntx_pop();
__UPG.msg('Zaktualizowano tabelę DATY.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
_res


\DATY_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Dodanie pola ROK_T do tabeli DATY - opis
::  ORG: \DATY_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli DATY'


\ZL1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Aktualizacja pola ZL.TREE_TYP
::  ORG: \ZL/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
_raport:=obj_new('P','N','Z','M','F');
_raport.P:=_raport.N:=_raport.Z:=_raport.M:=_raport.F:=0;

ZL.cntx_psh();
ZL.index('UP_START');
ZL.prefix();
_il_rec:=ZL.size();
ZL.prefix(0);
{? ZL.first()
|| FUN.prg_start(_il_rec,'Aktualizacja zleceń'@,,,1);
   {!
   |? FUN.prg_next();
      {? ZL.TREE_TYP=''
      || {? ZL.RODZAJ='P'
         || ZL.TREE_TYP:='P';
            {? ZL.put() || _raport.P+=1 ?}
         || {? ZL.RODZAJ='N'
            || ZL.TREE_TYP:='N';
               {? ZL.put() || _raport.N+=1 ?};
               ZL.cntx_psh();
               ZL.index('NRNZL');
               ZL.prefix(ZL.UNRZL);
               {? ZL.first()
               || {!
                  |? ZL.TREE_TYP:='P';
                     {? ZL.put() || _raport.P+=1 ?};
                     ZL.next()
                  !}
               ?};
               ZL.cntx_pop()
            |? ZL.RODZAJ='Z'
            || ZL.TREE_TYP:='Z';
               {? ZL.put() || _raport.Z+=1 ?};
               _ktm:=ZL.KTM;
               ZL.cntx_psh();
               ZL.index('NRNZL');
               ZL.prefix(ZL.UNRZL);
               {? ZL.first()
               || {!
                  |? {? ZL.KTM=_ktm
                     || ZL.TREE_TYP:='M';
                        {? ZL.put() || _raport.M+=1 ?}
                     || ZL.TREE_TYP:='F';
                        {? ZL.put() || _raport.F+=1 ?}
                     ?};
                     ZL.next()
                  !}
               ?};
               ZL.cntx_pop()
            ?}
         ?}
      ?};
      ZL.next()
   !};
   FUN.prg_stop()
?};
ZL.cntx_pop();

_result:=1;
{? _il_rec>0
|| __UPG.msg('Zaktualizowano zapisy w kartotece zleceń.');
   __UPG.msg('Stan kartoteki zleceń po aktualizacji:');
   _summary:=sql('select distinct TREE_TYP, count(*) as CNT from ZL group by TREE_TYP order by TREE_TYP');
   {? _summary.first()
   || {!
      |? {? _summary.TREE_TYP=''
         || __UPG.msg('- Zlecenia błędne, które nie zostały zaktualizowane: %1'[$_summary.CNT]);
            _zlecenia:=sql('select SYM from ZL where ZL.TREE_TYP=\'\' order by ZL.SYM');
            {? _zlecenia.first()
            || {!
               |? __UPG.msg('   '+_zlecenia.SYM);
                  _zlecenia.next()
               !}
            ?};
            _result:=-1
         |? _summary.TREE_TYP='P'
         || __UPG.msg('- Zlecenia proste: %1'[$_summary.CNT])
         |? _summary.TREE_TYP='N'
         || __UPG.msg('- Zlecenia niezależne (nagłówki): %1'[$_summary.CNT])
         |? _summary.TREE_TYP='Z'
         || __UPG.msg('- Zlecenia złożone z półfabrykatami (nagłówki): %1'[$_summary.CNT])
         |? _summary.TREE_TYP='M'
         || __UPG.msg('- Zlecenia montażowe: %1'[$_summary.CNT])
         |? _summary.TREE_TYP='F'
         || __UPG.msg('- Zlecenia półfabrykatowe: %1'[$_summary.CNT])
         ?};
         _summary.next()
      !}
   ?}
|| __UPG.msg('Brak zapisów do aktualizacji w kartotece zleceń.')
?};
_result


\ZL1_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Aktualizacja pola ZL.TREE_TYP - opis
::  ORG: \ZL_desc/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki zleceń.\nWypełnienie pola ZL.TREE_TYP (Typ elementu w drzewie zlecenia).'


\za_nota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.22]
:: OPIS: Uzupełnienie pola NOTA tabeli ZA_NOTA na podstawie Adnotacji z ZA_NOTA.ZZ_DOK.
::  ORG: \za_nota/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
ZZ_DOK.cntx_psh();
ZZ_DOK.prefix();
ZA_NOTA.cntx_psh();
ZA_NOTA.prefix();
_loop:=ZA_NOTA.first();
_lrec:=0;
_lmod:=0;
{!
|? _loop
|! _lrec+=1;
   _firma:=$ZA_NOTA.ZA_TEST().ZA_ZEST().FIRMA;
   ZZ_DOK.use('zz_do'+(_firma+1));
   {? ZZ_DOK.seek(ZA_NOTA.ZZ_DOK)
   || {? +ZZ_DOK.memo_txt(,1,'NOTA')
      || _txt:=ZZ_DOK.memo_txt(,1,'NOTA');
         ZA_NOTA.memo_set(_txt,'NOTA');
         {? ZA_NOTA.memo_put(,'NOTA')
         || ZZ_DOK.memo_set('','NOTA');
            ZZ_DOK.memo_put(,'NOTA');
            _lmod+=1
         ?}
      ?}
   ?};
   _loop:=ZA_NOTA.next()
!};
ZA_NOTA.cntx_pop();
ZZ_DOK.cntx_pop();

__UPG.msg('Zaktualizowano odpowiedzi opisowe z ankiet na podstawie zapisów z dokumentów kadrowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego przepisano: %2 zapisów.'[$_lrec,$_lmod]);
1


\za_nota_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.22]
:: OPIS: Uzupełnienie pola NOTA tabeli ZA_NOTA na podstawie Adnotacji z ZA_NOTA.ZZ_DOK - opis.
::  ORG: \za_nota_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie odpowiedzi opisowych z ankiet.'


\zo_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Uzupełnia znacznik 'Poprawność' dla formularzy ocen pracowników. Badanie poprawności zdefiniowanych kompetencji
::       dla formularza.
::----------------------------------------------------------------------------------------------------------------------
_lrec:=0;
_lmod:=0;
ZO_FORM.cntx_psh();
ZO_FORM.prefix();
_blad:=0;
_licz:=0;
_forms:=sql('select ZO_FORM.REFERENCE as REF from ZO_FORM where ZO_FORM.VALID=\'\'');
_loop:=_forms.first();
{? _loop
|| ZO_FORM.trig_off('*','*');
   FUN.prg_start(_forms.size(),'Przetwarzanie tabeli %1'@['ZO_FORM'],,,1);
   {!
   |? _loop
   |! _lrec+=1;
      _komps:=sql('
         select
            ZZ_KOMP.REFERENCE as REF,
            ZZ_KOMP.ZZ_KOMP as KOMP
         from
            ZO_KOMP join ZZ_KOMP join ZO_FORM
         where
            ZO_FORM.REFERENCE=\':_a\'
         order by
            2',
         _forms.REF
      );

      _loop:=_komps.first();
      {!
      |? _loop
      |! {? _komps.KOMP=0
         || _ref:=exec('FindAndGet','#table',ZZ_KOMP,_komps.REF,,,null());
            _komps.cntx_psh();
            _komps.prefix(_ref);
            {? _komps.first()
            || _blad:=1;
             _licz+=1
            ?};
            _komps.cntx_pop()
         ?};
         _loop:=_komps.next()
      !};
      {? ZO_FORM.seek(_forms.REF)
      || {? _blad
         || ZO_FORM.VALID:='N'
         || ZO_FORM.VALID:='T'
         ?};
         {? ZO_FORM.put()
         || _lmod+=1
         ?}
      ?};
      &_komps;
      _blad:=0;
      FUN.prg_next();
      _loop:=_forms.next()
   !};
   ZO_FORM.trig_on('*','*');
   FUN.prg_stop();
   __UPG.msg('Sprawdzono poprawność formularzy ocen okresowych pracowników.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów. Znaleziono %3 błędnych formularzy.'
             [$_lrec,$_lmod,$_licz])
|| __UPG.msg('Brak formularzy ocen okresowych do aktualizacji.')
?};
ZO_FORM.cntx_pop();
1


\zo_form_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Uzupełnia znacznik 'Poprawność' dla formularzy ocen pracowników. Badanie poprawności zdefiniowanych kompetencji
::       dla formularza.
::----------------------------------------------------------------------------------------------------------------------
'Sprawdzenie poprawności wygenerowanych formularzy ocen pracowników.'


\matkan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Sprawdzenie występowania w danych nazwiska matki pracownika.
::   WE:
::   WY:
::  ORG: \matkan/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

{? var_pres('MATKAN',OSOBA)<0
|| __UPG.msg('Tabela OSOBA nie zawiera kolumny "Nazwisko matki".')
|| _TAB1:=sql('select count(*) ILE from OSOBA where OSOBA.MATKAN<>\'\'');
   {? _TAB1.first() & _TAB1.ILE>0
   || __UPG.msg('Liczba wierszy w tabeli OSOBA z wypełnionym polem "Nazwisko matki": %1.' [$_TAB1.ILE]);
      __UPG.msg('Dane te (po konsultacji z klientem) powinny być usunięte.');
      _ret:=-1
   || __UPG.msg('Tabela OSOBA nie zawiera danych w kolumnie "Nazwisko matki".')
   ?}
?};

{? var_pres('MATKAN',RD)<0
|| __UPG.msg('Tabela RD nie zawiera kolumny "Nazwisko matki".')
|| _TAB2:=sql('select count(*) ILE from RD where RD.MATKAN<>\'\'');
   {? _TAB2.first() & _TAB2.ILE>0
   || __UPG.msg('Liczba wierszy w tabeli RD z wypełnionym polem "Nazwisko matki": %1.' [$_TAB2.ILE]);
      __UPG.msg('Dane te (po konsultacji z klientem) powinny być usunięte.');
      _ret:=-1
   || __UPG.msg('Tabela RD nie zawiera danych w kolumnie "Nazwisko matki".')
   ?}
?};

_ret


\matkan_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Sprawdzenie występowania w danych nazwiska matki pracownika - opis.
::   WE:
::   WY:
::  ORG: \matkan_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Sprawdzenie występowania w danych nazwiska matki pracownika'


\new_fld_H
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [21.14]
:: OPIS: Uzupełnienie wartości w nowych polach tabeli H.
::  OLD: \oddelegowania/upgrade_1842.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_licz:=1;
_TAB:=H.names();
{? _TAB.first()
|| {!
   |? _licz+={? _TAB.SIZE>0 || _TAB.SIZE ?};
      _TAB.next()
   !}
?};
:: Aktualizacja danych w tabeli z przebiegiem zatrudnienia.
FUN.prg_start(_licz,'Aktualizacja danych w tabeli z przebiegiem zatrudnienia.'@,,,1);
H.trig_off('*','*');
H.cntx_psh();
{? _TAB.first()
|| {!
   |? H.use(_TAB.NAME);
      H.prefix();
      _lrec:=H.size();
      {? _lrec
      || _lmod:=0;
         H.first();
         {!
         |? FUN.prg_next();
            {? H.ODDEL<>'T' & H.ODDEL<>'N'
            || H.ODDEL:=H.KODDEL:='N';
               _lmod+=H.put()
            || _lmod+=1
            ?};
            H.next()
         !};

         __UPG.msg(
            {? H.name()='_his_'
            || 'Tabela z danymi tymczasowymi przebiegu zatrudnienia:'
            || 'Tabela z danymi docelowymi przebiegu zatrudnienia:'
            ?}
         );
         {? _lrec-_lmod
         || {? _result || _result:=0 ?};
            __UPG.msg('- aktualizacja wartości w polach "Oddelegowanie" i "Praca w kilku krajach" nie powiodła się');
            __UPG.msg('- liczba rekordów, których nie udało się poprawić: %1 z %2'[$(_lrec-_lmod),$_lrec]);
            __UPG.msg('- dane należy poprawić ręcznie')
         || __UPG.msg('- aktualizacja wartości w polach "Oddelegowanie" i "Praca w kilku krajach"');
            __UPG.msg('- liczba poprawnych rekordów: %1'[$_lrec])
         ?}
      |? ~_lrec & H.name()='_hist'
      || {? _result=1 || _result:=-1 ?};
         __UPG.msg('Brak danych do aktualizacji w tabeli z przebiegiem zatrudnienia.')
      ?};
      _TAB.next()
   !}
?};
H.cntx_pop();
H.trig_on('*','*');
FUN.prg_stop();
_result


\new_fld_H_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [21.14]
:: OPIS: Uzupełnienie wartości w nowych polach tabeli H - opis.
::  OLD: \oddelegowania_desc/upgrade_1842.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wartości pól w Przebiegu zatrudnienia.'


\rodzaje_oddelegowania
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie tabeli rodzajów pozycji dla oddelegowania do pracy za granicą.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
Cntx.psh(H_ODDR,R);
R.index('RUBKOD');
H_ODDR.clear();

_add:=
   "  H_ODDR.blank();
      H_ODDR.RODZ:=_a;
      H_ODDR.OPIS:=_b;
      H_ODDR.DIETA:=_c;
      H_ODDR.NOC:=_d;
      R.prefix(_e);
      H_ODDR.R:=
         {? R.first()
         || R.ref()
         || null()
         ?};
      H_ODDR.add()
   ";
{? H_ODDR.find_tab(1,'NOC',,'=','T')
|| __UPG.msg('Rodzaj rozliczena noclegu jest już wprowadzony do słownika.')
|| {? _add('Noclegi','Stawka za nocleg podczas oddelegowania do pracy za granicą','N','T',7073)
   || __UPG.msg('Rodzaj rozliczenia o nazwie "Noclegi" został dodany do słownika.')
   || __UPG.msg('Rodzaj rozliczenia o nazwie "Noclegi" nie został dodany do słownika.');
      _result:=0
   ?}
?};

{? H_ODDR.find_tab(1,'DIETA',,'=','T')
|| __UPG.msg('Rodzaj rozliczena diety jest już wprowadzony do słownika.')
|| {? _add('Diety','Kwota diety za dzień pobytu za granicą','T','N',7074)
   || __UPG.msg('Rodzaj rozliczenia o nazwie "Diety" został dodany do słownika.')
   || __UPG.msg('Rodzaj rozliczenia o nazwie "Diety" nie został dodany do słownika.');
      _result:=0
   ?}
?};

Cntx.pop(H_ODDR,R);
_result


\rodzaje_oddelegowania_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie tabeli rodzajów pozycji dla oddelegowania do pracy za granicą - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie tabeli rodzajów pozycji dla oddelegowania do pracy za granicą.'


\kal_wzor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Uzupełnienie nowych pól w tabeli KAL_WZOR
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('KalWzor');
KalWzor:=0;
KAL_WZOR.cntx_psh();
KAL_WZOR.index('KAL_WZOR');
KAL_WZOR.prefix();
KAL_WZOR.for_each("
   {? KAL_WZOR.H_MOD=''
   || KAL_WZOR.H_MOD:='N';
      KalWzor+=KAL_WZOR.put()
   ?}
",1);
{? KalWzor
|| __UPG.msg('Uzupełniono wartości pól dla wzorców kalendarzy.')
|| __UPG.msg('Uzupełnienie wartości pól dla wzorców kalendarzy nie było wymagane.')
?};
KAL_WZOR.cntx_pop();
VAR_DEL.delete('KalWzor');
1


\kal_wzor_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Uzupełnienie nowych pól w tabeli KAL_WZOR - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wartości pól dla wzorców kalendarzy.'


\A_OKRD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: KOD BŁĘDU: ER/WRT/XP/12.51/1809/0010
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
P.cntx_psh();
P.index('OSOBA');
{? P.first()
|| PROGRESS.set(P.size(),'Przetwarzanie danych pracowników w okresach rozliczeniowych...'@);
   A_OKRD.cntx_psh();
   A_OKRD.use('x_okrdi');
   A_OKRD.index('POD');
   {!
   |? A_OKRD.prefix(P.ref());
::    sprawdzamy, czy pierwszy domyślny okres nie zaczyna się przed datą zatrudnienia
      {? A_OKRD.first() & A_OKRD.OD<P.DZA
      || _old_od:=A_OKRD.OD;
         A_OKRD.OD:=P.DZA;
         _result+=A_OKRD.put(1);
         exec('norm_okrd','okres',A_OKRD.P,0);
          {? var_pres('_t_okrp')>100 || obj_del(_t_okrp) ?};
          _t_okrp:=exec('getOkrpFromDate','grafik',P.ref(),A_OKRD.OD);

          {? _t_okrp.first() & _t_okrp.OD<P.DZA
          || A_OKRP.cntx_psh();
             A_OKRP.index('A_OKRDP');
             {? A_OKRP.seek(_t_okrp.NR,_t_okrp.MASK)
             || A_OKRP.OD:=P.DZA;
                _result+=A_OKRP.put(1)
             ?};
             A_OKRP.cntx_pop()
          ?}
      ?};
      PROGRESS.next();
      P.next()
   !};
   PROGRESS.close();
   A_OKRD.cntx_pop()
?};
P.cntx_pop();
{? _result
|| __UPG.msg('Naprawiono przypisania pracowników do pierwszego okresu rozliczeniowego.')
|| __UPG.msg('Naprawienie przypisania pracowników do pierwszego okresu rozliczeniowego nie było wymagane.')
?};
1


\A_OKRD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Naprawa przypisania pracowników do pierwszego okresu rozliczeniowego - opis
::----------------------------------------------------------------------------------------------------------------------
'Naprawa przypisania pracowników do pierwszego okresu rozliczeniowego'


\N_NDOK_do_CU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Przepisanie wartości w tabeli N z pola NDKO do CU dla dla urlopu wychowawczego i urlopów rodzicielskich.
::  ORG: \N_NDOK_do_CU/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lrec:=_lmod:=0;

TRIG_OFF.N:='T';

exec('__RUB','object');
Cntx.psh(R,N);
R.clear();
__RUB.fill();
_rub114:=__RUB.sys_sql(114);
_rub1227:=__RUB.sys_sql(1227);
_kody:=
   '%1%3%2'
   [_rub114,_rub1227,{? _rub114<>'' & _rub1227<>'' || ',' || '' ?}];

{? _kody<>''
|| N.clear();
   {? N.f_active() || N.f_clear() ?};
   N.f_set(,'join R','"N".NDOK<>'''' and R.RN in (:_a)',_kody);
   {? N.f_first()
   || {!
      |? {? #N.NDOK
         || _lrec+=1;
            {? ~N.CU & +N.NDOK=1
            || N.CU:=#N.NDOK;
               N.NDOK:='';
               _lmod+=N.put(1)
            ?}
         ?};
         N.f_next()
      !}
   ?};
   N.f_clear()
?};
Cntx.pop(R,N);

TRIG_OFF.N:='';

{? _lrec
|| {? _lrec-_lmod
   || __UPG.msg('Dla nieobecności o kodach %1 zmodyfikowano %2 z %3 wierszy w tabeli N.'[_kody,$_lmod,$_lrec]);
      _result:=-1
   || __UPG.msg('Dla nieobecności o kodach %1 zmodyfikowano %2 wierszy w tabeli N.'[_kody,$_lrec])
   ?}
|| __UPG.msg('Brak danych do modyfikacji.')
?};
_result


\N_NDOK_do_CU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Przepisanie wartości w tabeli N z pola NDKO do CU dla dla urlopu wychowawczego i urlopów rodzicielskich - opis.
::  ORG: \N_NDOK_do_CU_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola CU z tabeli N wartościami z pola NDOK dla urlopów rodzicielskich i urlopu wychowawczego'


\RD_OKR_zalacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Dodanie nowego typu załącznika kadrowego i jego uzupełnienie tabeli ZALACZ wartościami z tabeli DOKUM.
::  ORG: \RD_OKR_zalacz/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_lrec:=_lmod:=_ldel:=0;
_firma:=exec('ref_firma','ustawienia');
_typ:=exec('slo_naz','ext_slo','ZAL','Wniosek o urlop rodzicielski');

:: [Część kodu związana ze strukturą hierarchiczną załączników osobowych została pominięta. ]
:: [Zaraz po transferze z Xpertisa te tabele i tak są w całości puste.                      ]

:: Przepisanie załączników do wniosków o urlop rodzicielski z tabeli DOKUM do tabeli ZALACZ.
Cntx.psh(RD_OKR,OSOBA,ZALACZ,DOKUM);
RD_OKR.prefix();
{? RD_OKR.first()
|| OSOBA.prefix();
   ZALACZ.index('NAG');
   ZALACZ.prefix();
   DOKUM.index('DOKUM');
   {!
   |? DOKUM.prefix(_firma,$RD_OKR.ref(),);
      _lrec:=DOKUM.size();
      {? DOKUM.first()
      || do();
         {!
         |? ZALACZ.blank();
            ZALACZ.ZAL:=DOKUM.DOKUM;
            ZALACZ.DATA:=DOKUM.DATA;
            ZALACZ.ZAL_NAME:=DOKUM.KR_OP;
            ZALACZ.TYP_ZAL:=_typ;
            ZALACZ.RODZAJ:=2-!RD_OKR;
            ZALACZ.OSOBA:=RD_OKR.OSOBA;
            ZALACZ.P:=RD_OKR.P;
            ZALACZ.NAG:=RD_OKR.uidref();
            {? ZALACZ.add() || _lmod+=1 ?};
            _uid:=DOKUM.uidref(); _rr:=DOKUM.del(); exec('dokumlog_del','zbl_dok',_uid);
            _rr
         !};
         end()
      ?};
      RD_OKR.next()
   !};
   _ldel:=DOKUM.size()
?};
Cntx.pop(RD_OKR,OSOBA,ZALACZ,DOKUM);
{? _lrec
|| {? _lrec-_lmod
   || __UPG.msg('Przeniesiono %1 z %2 załączników do wniosków o urlop rodzicielski z tabeli DOKUM.'[$_lmod,$_lrec]);
      _result:=-1
   || __UPG.msg('Przeniesiono wszystkie załączniki do wniosków o urlop rodzicielski z tabeli DOKUM.')
   ?};
   {? _ldel
   || __UPG.msg('Nie usunięto %1 z %2 załączników do wniosków o urlop rodzicielski z tabeli DOKUM.'[$_ldel,$_lrec]);
      _result:=-1
   ?}
|| __UPG.msg('Brak załączników do przeniesienia.')
?};
_result


\RD_OKR_zalacz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Dodanie nowego typu załącznika kadrowego i jego uzupełnienie tabeli ZALACZ wartościami z tabeli DOKUM - opis.
::  ORG: \RD_OKR_zalacz_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie tabeli ZALACZ wartościami z tabeli DOKUM (Wniosek o urlop rodzicielski)'


\rap_zew
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Aktualizacja definicji raportów zewnętrznych.
::   WE:
::   WY:
::  ORG: \rap_zew/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('reg_rep','rap_zew','PKD','zaswzatr','Zaświadczenie o zatrudnieniu i zarobkach')
|| __UPG.msg('Zaktualizowano definicje raportów zewnętrznych.');
   1
|| __UPG.msg('Aktualizacja raportów zewnętrznych nie powiodła się.');
   0
?}


\rap_zew_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Aktualizacja definicji raportów zewnętrznych - opis.
::   WE:
::   WY: opis zadania
::  ORG: \rap_zew_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji raportów zewnętrznych'


\kst_map
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja mapy stałych systemu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('kst_map','tran_exp');
__UPG.msg('Aktualizacja mapy stałych systemu wykonana.');
1


\kst_map_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja mapy stałych systemu - opis.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja mapy stałych systemu'


\kst_lst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Odtworzenie widoków stałych systemu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_name:='kst_wl.tra';
{? ~fexists(_name,1)
|| __UPG.msg('Nie znaleziono pliku %1.'[_name]);
   return(-1)
?};

_BUF:=tab_tmp(3,
   'B_DOMAIN','STRING[8]','Dziedzina',
   'KST_ZES', 'STRING[8]','Zestaw',
   'NUMER',   'INTEGER',  'Numer',
   'KST_MAP', 'STRING[8]','Stała'
);

: wczytaj konfigurację (format zgodny z dawnymi wersjami)
_BUF.import(_name,,,'|','Mazovia',,
   'B_DOMAIN',,1,,
   'KST_ZES',,2,,
   'KST_MAP',,3,,
   'NUMER',,4,
);

: lista użytych dziedzin
_TMP:=sql('select distinct B_DOMAIN, KST_ZES from :_a',_BUF);

KST_ZES.cntx_psh();
KST_ZES.index('SYMBOL');
KST_ZES.prefix();
KST_MAP.cntx_psh();
KST_MAP.index('SYMBOL');
KST_LST.cntx_psh();
KST_LST.prefix();
KST_LST.erase();
KST_LST.index('UNIQUE');
KST_DOM.cntx_psh();
KST_DOM.prefix();
KST_DOM.erase();
KST_DOM.index('UNIQUE');

_stat:=obj_new('dom','lst');
_stat.dom:=_stat.lst:=0;

: przetwarzaj parami dziedzina, zestaw
_loop:=_TMP.first();
{!
|? _loop
|! _dom:=exec('domain_ref','#b_domain',_TMP.B_DOMAIN);
   {? _dom<>null & KST_ZES.find_key(_TMP.KST_ZES,_TMP.KST_ZES)
   || {? ~KST_DOM.find_key(_dom,KST_ZES.ref())
      || KST_DOM.B_DOMAIN:=_dom;
         KST_DOM.KST_ZES:=KST_ZES.ref();
         _stat.dom+=KST_DOM.add()
      ?};
:     dopisz do widoku dziedziny wybrane stałe
      _BUF.prefix(_TMP.B_DOMAIN,_TMP.KST_ZES);
      _loop:=_BUF.first();
      {! _nr:=1
      |? _loop
      |! KST_MAP.prefix(KST_ZES.ref());
         {? KST_MAP.find_key(_BUF.KST_MAP,_BUF.KST_MAP)
         || KST_LST.KST_DOM:=KST_DOM.ref();
            KST_LST.KST_MAP:=KST_MAP.ref();
            KST_LST.NUMER:=_nr;
            _stat.lst+=KST_LST.add()
         ?};
         _loop:=_BUF.next()
      !}
   ?};
   _loop:=_TMP.next()
!};

: porządki
KST_DOM.cntx_pop();
KST_LST.cntx_pop();
KST_MAP.cntx_pop();
KST_ZES.cntx_pop();
: sukces
__UPG.msg('Dodanych dziedzin zestawów: %1' [$_stat.dom]);
__UPG.msg('Dodanych widoków stałych: %1' [$_stat.lst]);
__UPG.msg('Odtworzenie zakończone.');
1


\kst_lst_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Odtworzenie widoków stałych systemu - opis.
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Odtworzenie widoków stałych systemu'


\control_01
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: kod - ER/WRT/XP/12.41/1410/0017
::       Bledne generowanie widoku danych. Pobieranie danych do arkusza wyswietla wartosci w zlych pozycjach.
::  OLD: \control_01/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
K_N_VIEW.cntx_psh();
K_N_VIEW.index('K_HARM_P'); K_N_VIEW.prefix();
{? K_N_VIEW.first()
|| _ok:=1;
   K_P_VIEW.cntx_psh();
   K_P_VIEW.index('K_P_VIEW');
   K_E_VIEW.cntx_psh();
   K_E_VIEW.index('K_E_VIEW');
   K_HARM_P.cntx_psh();
   K_HARM_P.index('K_N_VIEW');
   K_HARM_P.prefix();
   VAR_DEL.delete('TTEXCEL','TTERR');
   TTERR:=tab_tmp(1,
      'NAZ','STRING[60]','Nazwa zadania',
      'STATUS','STRING[20]','Status',
      'UZU','STRING[40]','Uzupełnia',
      'AKC','STRING[40]','Akceptuje',
      'K_HARM_P','INTEGER',,
      'K_N_VIEW','INTEGER',,
      'FIX','INTEGER',
   );
   _i1:=TTERR.index('?');
   __i2:=TTERR.ndx_tmp('',1,'FIX',,0);
   {!
   |? _napraw:=K_N_VIEW.K_HARM_P=null | K_N_VIEW.K_HARM_P().EXCEL=null;
      _err:=exec('set_kev_lp','control',_napraw);
      {? _err & ~_napraw
      || TTERR.NAZ:=K_N_VIEW.K_HARM_P().NAZ;
         TTERR.STATUS:=exec('set_status','control');
         TTERR.K_HARM_P:=#K_N_VIEW.K_HARM_P;
         TTERR.K_N_VIEW:=#K_N_VIEW.ref();
         TTERR.UZU:=K_HARM_P.KTO_UZU().DANE;
         TTERR.AKC:=K_HARM_P.KTO_AKC().DANE;
         TTERR.FIX:=1;
         TTERR.add()
      ?};
      K_N_VIEW.next()
   !};
   K_HARM_P.cntx_pop();
   K_E_VIEW.cntx_pop();
   K_P_VIEW.cntx_pop()
?};
K_N_VIEW.cntx_pop();
{? _ok & TTERR.first()
|| TTEXCEL:=exec('gen_tab_xlsx','!ctr_hbd_dhar');
   K_N_VIEW.prefix();
   K_HARM_P.prefix();
   {!
   |? {? K_N_VIEW.seek(TTERR.K_N_VIEW,) & K_HARM_P.seek(TTERR.K_HARM_P,)
      || exec('add_tab_xlsx','!ctr_hbd_dhar');
         exec('set_kev_lp','control',1)
      ?};
      TTERR.next()
   !};
   exec('gen_all_xlsx','!ctr_hbd_dhar',0)
?};
VAR_DEL.delete('TTEXCEL','TTERR');
__UPG.msg('Naprawiono błędne arkusze budżetowe (controlling).');
1


\control_01_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: kod - ER/WRT/XP/12.41/1410/0017 - opis
::   WE:
::   WY: opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Naprawa błędnych arkuszy budżetowych (controlling).'


\zaoh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
::  MOD: ARTSLO [19.22]
:: OPIS: Przypomnienie o konieczności konfiguracji załączników akt osobowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_choice:=0;
{? ~exec('lic_or','#b_domain','PKD')
|| __UPG.msg('Wykonanie zadanie nie było konieczne (brak licencji na dziedzinę PKD).');
   1
|? {! |? (_choice:=FUN.choice(
          'Przed przystąpieniem do pracy z załącznikami akt osobowych należy:\n'
          ' - zdefiniować strukturę hierarchiczną prezentacji załączników\n'
          ' - powiązać miejsca prezentacji z typami załączników.\n\n'
          'Oba te zadania można wykonać po uruchomieniu czynności:\n'
          'Ustawienia i parametryzacja -> [zakładka Parametryzacja] ->\n'
          'Kadry -> Parametry działania -> Załączniki akt osobowych\n\n'
          'Czy prace opisane powyżej zostały wykonane?'@
          ,0,'Załączniki akt osobowych'@,'Tak'@,,,'Nie'@))=1
   |! exec('slo_kod','dane_startowe');
      exec('slo_naz','dane_startowe');
      exec('np_run','#b__box','ZWS_PAR_POZA')

   !};
   {? _choice
   || __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      0
   ?}
|| __UPG.msg('Wykonanie zadanie nie było konieczne (brak licencji na dziedzinę PKD).');
   1
?}


\zaoh_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Przypomnienie o konieczności konfiguracji załączników akt osobowych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Konfiguracja załączników akt osobowych'


\rodo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
::  MOD: ARTSLO [19.22]
:: OPIS: Aktualizacja konfiguracji RODO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fun:=obj_new(4);
_fun[1]:='ZWS_PAR_ARDK';
_fun[2]:='ZWS_PAR_ARDD';
_fun[3]:='ZWS_PAR_ARDP';
_fun[4]:='ZWS_IMPORTY_ROD';

{? exec('lic_or','#b_domain','ROD')
|| {? FUN.ask(
         'Aktualizacji wymaga konfiguracja "Zarządzanie danymi osobowymi".\n'
         '\n'
         'Jeżeli klient pracuje na standardowej wersji konfiguracji,\n'
         'to aktualizacja może być wykonana automatycznie.\n'
         'W tym celu po uruchomieniu importu:\n'
         '1. Wybierz opcję: "Nadpisuj istniejące".\n'
         '2. Wskaż katalog merit\\transfer\\rodo2 '
           '(pamiętaj aby wcześniej wkopiować do niego odpowiednie pliki - patrz nota).\n'
         '3. Zapoznaj się z ewentualnymi komunikatami.\n'
         '4. Zamknij okno z podsumowaniem importu.\n'
         '\n'
         'Jeżeli konfiguracja była u klienta modyfikowana, to aktualizację należy\n'
         'przeprowadzić na podstawie danych z pliku transfer\\rodo2\\parametryzacja_zdo.xlsx.\n'
         'Dotyczy to wierszy:\n'
         ' - Wykonanie postanowień umowy o świadczenie usług z podwykonawcą zależnym.\n'
         ' - Realizacja praw majątkowych po zmarłym pracowniku.\n'
         '\n'
         'Czy rozpocząć import?'@
      )
   || exec('import_init','#excel_imex',_fun);
      {? FUN.ask(
              'Czy import został wykonany?\n'
              '\n'
              'Tak - zadanie zostanie oznaczone jako "Wykonane" i nie będzie już można go uruchomić.\n'
              'Nie - zadanie pozostanie na liście zadań i będzie je można wykonać w późniejszym czasie.'@
         )
      || __UPG.msg('Użytkownik %1 wykonał import konfiguracji.' [exec('operatorKod','#users')]);
         1
      || __UPG.msg('Użytkownik %1 pozostawił zadanie do późniejszego wykonania.' [exec('operatorKod','#users')]);
         0
      ?}
   || {? FUN.ask(
              'Czy rezygnujesz (również w przyszłości) z wykonania tego zadania?\n'
              '\n'
              'Tak - zadanie zostanie oznaczone jako "Wykonane" i nie będzie już można go uruchomić.\n'
              'Nie - zadanie pozostanie na liście zadań i będzie je można wykonać w późniejszym czasie.'@
         )
      || __UPG.msg('Użytkownik %1 zdecydował o pominięciu wykonania zadania.' [exec('operatorKod','#users')]);
         1
      || __UPG.msg('Użytkownik %1 pozostawił zadanie do późniejszego wykonania.' [exec('operatorKod','#users')]);
         0
      ?}
   ?}
|| {? exec('import_cfg','rodo',1)
   || __UPG.msg('Automatyczny import konfiguracji zakończył się suckesem.');
      1
   || __UPG.msg('Automatyczny import konfiguracji zakończył się porażką.');
      0
   ?}
?}


\rodo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja konfiguracji RODO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zarządzanie danymi osobowymi - aktualizacja konfiguracji '


\ufz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
::  MOD: ARTSLO [19.22]
:: OPIS: Aktualizacja uprawnień do form współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_choice:=0;
{? exec('lic_or','#b_domain','PKD')
|| {! |? (_choice:=FUN.choice(
                'W związku z przejściem na Merita z Xpertis''a należy zaktualizować "Uprawnienia do form współpracy".\n'
                'Odpowiednia parametryzacja może być wykonana po uruchomieniu:\n'
                'Ustawienia i parametryzacja -> [zakładka Parametryzacja] -> Kadry -> Parametry działania -> '
                'Upraw. do form współpracy.\n'
                '\n'
                'Czy prace opisane powyżej zostały wykonane?'
                ,0,'Upraw. do form współpracy'@,'Tak',,,'Nie'@))=1
      |! exec('np_run','#b__box','ZWS_UPR_RUFZ')
   !};
   {? _choice
   || __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      0
   ?}
|| __UPG.msg('Wykonanie zadanie nie było konieczne (brak licencji na dziedzinę PKD).');
   1
?}


\ufz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja uprawnień do form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień do form współpracy'


\fo_100227
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Ustawienie uniwersalnego parametru aplikacyjnego 100227
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_nr:=100227;
FO_DEF.cntx_psh();
FO_DEF.index('NR');
FO_DEF.prefix('A','N',_nr);
{? FO_DEF.first()
||
   _interface:=exec('interface','#params','A',_nr);
   _interface.save('T')
?};
FO_DEF.cntx_pop();
__UPG.msg('Ustawiono uniwersalny parametr aplikacyjny "100227 Kolejność danych adresowych" na T.');
1


\fo_100227_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Ustawienie uniwersalnego parametru aplikacyjnego 100227 - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Ustawienie uniwersalnego parametru aplikacyjnego "100227 Kolejność danych adresowych" na T (Kod pocztowy, Miasto, Ulica)'


\zasw_ZUS_do_oddel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie słownika rodzajów zaświadczeń ZUS do oddelegowania za granicę.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('zasw_ZUS_do_oddel','dane_startowe');

{? _result
|| __UPG.msg('Uzupełniono słownik rodzajów zaświadczeń o ubezpieczeniu do oddelegowania za granicę.');
   __UPG.msg('Liczba dodanych wierszy: %1.'[$_result])
|| __UPG.msg('Nie uzupełniono słownika rodzajów zaświadczeń o ubezpieczeniu do oddelegowania za granicę.')
?};
1


\zasw_ZUS_do_oddel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie słownika rodzajów zaświadczeń ZUS do oddelegowania za granicę - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słownika rodzajów zaświadczeń ZUS do oddelegowania za granicę.'


\gr_sik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Uzupełnienie pola GR_SIK.TYP_KLIK
::----------------------------------------------------------------------------------------------------------------------
GrSikAkt:=obj_new('ILE','OK');
GrSikAkt.ILE:=0;
GrSikAkt.OK:=0;
GR_SIK.prefix();
GR_SIK.for_each("
   GrSikAkt.ILE+=1;
   {? GR_SIK.TYP_KLIK=''
   || GR_SIK.TYP_KLIK:='NN';
      GrSikAkt.OK+=GR_SIK.put()
   ?}
",1);
__UPG.msg('Uzupełnienie pola sprawozdań finansowych');
__UPG.msg('Liczba przetworzonych zapisów: %1'[$GrSikAkt.ILE]);
__UPG.msg('Liczba poprawionych zapisów: %1'[$GrSikAkt.OK]);
VAR_DEL.delete('GrSikAkt');
1


\gr_sik_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Uzupełnienie pola GR_SIK.TYP_KLIK - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola sprawozdań finansowych'


\p_04
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: ER/WRT/XP/18.22/1808/0002
::       Nieprawidłowe nazwisko w słowniku
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.for_each("
   exec('pop_slo','slo_slu','OSOBA')
",1);
OSOBA.cntx_pop();
__UPG.msg('Uaktualniono dane w słownikach opartych na tabeli OSOBA.');
1


\p_04_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: ER/WRT/XP/18.22/1808/0002
::       Nieprawidłowe nazwisko w słowniku - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie danych w słownikach opartych na tabeli OSOBA.'


\f_03
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Naprawia problem z błędnym adresem do aktualizacji e-deklaracji
::       Spis: Poprawia schemat e-deklaracji dla osób fizycznych
::----------------------------------------------------------------------------------------------------------------------
XINFO.HTTPPATH:='https://www.macrologic.pl/update';
exec('zapisz','#stalesys',1,XINFO,'HTTPPATH');
_a:=18;
_b:='VAT7';
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_b,_b,_a);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_b,VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('etd:OsobaFizyczna')
      || ISTDEFS.OPIS:='tns:OsobaFizyczna';
         ISTDEFS.put
      ?}
   ?}
?};
__UPG.msg(
   'Poprawiono adres i błąd z negatywną weryfikacją deklaracji VAT-7 (18) dla podatnika będącego osobą fizyczną.'
);
1


\f_03_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Naprawia problem z błędnym adresem do aktualizacji e-deklaracji
::       Spis: Poprawia schemat e-deklaracji dla osób fizycznych
::----------------------------------------------------------------------------------------------------------------------
'Ustawia nowy adres do aktualizacji e-deklaracji: https://www.macrologic.pl/update.\n'
'Poprawa błędu z weryfikacja negatywną podatnika będącego osobą fizyczną w Deklaracji VAT-7 (18)'


\odd_dok_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Uzupełnienie pola ODD_DOK tabeli SRDO. Informacja o jednostce księgowej, w której wystawiono dokument.
::----------------------------------------------------------------------------------------------------------------------
exec('maski','srodobj');
SRDO.cntx_psh();
SRDO.index('AUTO');
SRDO.prefix();
{? SRDO.first()
|| {!
   |? {? SRDO.ODD_DOK=null
      || SRST.cntx_psh();
         SRST.index('SROD');
         SRST.prefix();
         {? SRST.find_key(SRDO.SRSR,SRDO.ROK_F,SRDO.OKRO_F)
         || SRDO.ODD_DOK:=SRST.ODD;
            SRDO.put()
         ?};
         SRST.cntx_pop()
      ?};
      SRDO.next()
   !}
?};
SRDO.cntx_pop();
__UPG.msg('Zakończono uzupełnianie informacji o jednostce księgowej w tabeli(SRDO).');
1


\odd_dok_update_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Uzupełnienie pola ODD_DOK tabeli SRDO. Informacja o jednostce księgowej, w której wystawiono dokument - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełniono informację o jednostce księgowej dla dokumentów'


\PX_CUP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli PX_CUP
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   {? PX_CUP.CAL_LVL=0
   || PX_CUP.CAL_LVL:=1;
      PX_CUP.put()
   ?}
";
PX_CUP.prefix();
_result:=PX_CUP.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w pojemnikach planu strategicznego.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w pojemnikach planu strategicznego.')
?};
_result


\PX_CUP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli PX_CUP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pojemników planu strategicznego.'


\convEANU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Przepisanie użytkowników - operatorów urządzeń mobilnych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_lrec:=0;
_ladd:=0;
_kodus:='opemob_';
_lp:=0;

_tryb:=0;
MOBPAR.cntx_psh();
MOBPAR.index('MG');
MOBPAR.prefix(null(),'T','M');
{? MOBPAR.first() || _tryb:=MOBPAR.TAK='T' ?};
MOBPAR.cntx_pop();
{? _tryb
|| EANU.cntx_psh();
   USERS.cntx_psh();
   EANU.index('USER');
   {? EANU.first()
   || {!
      |? _lrec+=1;
         {? EANU.AKT='T'
         || _lp+=1;
            _users:=exec('add_auto','users',EANU.DANE,_kodus+form(_lp,-3,0,'99'));
            {? _users<>null() & (USERS.prefix(); USERS.seek(_users))
            || USERS.MOBIL:='T';
               {? var_pres('IP',EANU)>0
               || USERS.IP:=EANU.IP
               || USERS.IP:=exec('gen_code_id','ekioski')
               ?};
               USERS.PASS:=EANU.PASS;
               USERS.put(1);
               EANU.AKT:='X';
               EANU.put(1);
               _ladd+=1
            ?}
         ?};
         EANU.next()
      !}
   ?};
   __UPG.msg('Utworzono użytkowników wg operatorów operacji mobilnych.'@);
   __UPG.msg('Przetworzono: %1 zapisów, z czego dodano %2 użytkowników mobilnych.'@[$_lrec,$_ladd]);
   EANU.cntx_pop();
   USERS.cntx_pop()
|| __UPG.msg('Zadanie potransferowe pominięte.'@);
   __UPG.msg('Nie jest to transfer z systemu Xpertis lub nie korzystano z operacji mobilnych w trybie ONLINE.'@)
?};
_res


\convEANU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\sch_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
::  MOD: ARTSLO [19.22] [19.42]
:: OPIS: Importuje "personelowe" wnioski w obiegu (w tym dot. PPK)
::  ORG: \sch_imp/upgrade_1842.fml
::  ORG: \akt_woz/upgrade_1902.fml
::  ORG: \sch_imp/upgrade_2137.fml
::----------------------------------------------------------------------------------------------------------------------
_msg:="
{? _a
|| __UPG.msg('Zakończono import wniosku: \"%1\".'@[_b])
|| __UPG.msg('Nie udało się zaimportować wniosku: \"%1\".'@[_b])
?}
";
_result:=1;
_typobieg:=exec('add','typ_ob','Obieg wniosków');

_nazwy:='nazwa_rachunek,nazwa_woz,nazwa_wozda,nazwa_dwwp,nazwa_dwwd,nazwa_drwd,nazwa_wou,nazwa_opif,'
        'nazwa_nadg_w,nazwa_nadg_p,nazwa_dwwd,nazwa_czw,nazwa_odb_p,nazwa_odb_w,nazwa_dowu25,'
        'nazwa_ppsf,nazwa_ppsp,nazwa_dwwd';
_lNazw:=spli_str(_nazwy,',');

{! _ii:=1..obj_len(_lNazw)
|! _nazwa:=exec(_lNazw[_ii],'obiegi');
   {? exec('renameETYPY','sch_imp',_typobieg,_nazwa)<1
   || __UPG.msg('Nie udało się zmienić nazwy wniosku: \"%1\".'@[_nazwa]);
      _result:=-1
   || _msg(exec('imp_wn','sch_imp',_nazwa,1,0),_nazwa)
   ?}
!};
obj_del(_lNazw);

_result


\sch_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Importuje "personelowe" wnioski w obiegu - opis
::----------------------------------------------------------------------------------------------------------------------
'Import typów wniosków w obiegu'


\edi_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Przypomnienie o konieczności aktualizacji komunikatów EDI
::  OLD: \edi_imp/upgradex18xx.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
_fun:=obj_new(1);
_fun[1]:='ZWS_EDI_DEF';
{? FUN.ask(
      'W związku z przejściem na Merita z Xpertis''a należy zaktualizować komunikaty EDI.\n'
      'Jest to wymagane przed importem definicji procesów korzystających z komunikatów EDI.\n'
      '\n'
      'W tym celu po uruchomieniu importu:\n'
      '1. Wybierz opcję: "Nadpisuj istniejące".\n'
      '2. Wskaż katalog merit\\transfer\\ediimp (pamiętaj aby wcześniej wkopiować do niego odpowiednie pliki - patrz nota).\n'
      '3. Zapoznaj się z ewentualnymi komunikatami.\n'
      '4. Zamknij okno z podsumowaniem importu.\n'
      '\n'
      'Czy rozpocząć import?'
   )
|| exec('import_init','#excel_imex',_fun);
   _ask:=FUN.choice('Czy import został wykonany?',,'Tak','Nie','Zostanie wykonany później');
   {? _ask=1 || _res:=1
   |? _ask=2 || _res:=0
   |? _ask=3 || _res:=-1
   ?};
   {? _res=1 || __UPG.msg('Użytkownik %1 wykonał import komunikatów EDI.' [exec('operatorKod','#users')]) ?}
|| _ask:=FUN.choice('Wybierz tryb rezygnacji z importu',,'Nie jest potrzebny','Zostanie wykonany później');
   {? _ask=1 || _res:=1
   |? _ask=2 || _res:=-1
   ?};
   {? _res=1 || __UPG.msg('Użytkownik %1 potwierdził rezygnację z wykonywania importu komunikatów EDI.' [exec('operatorKod','#users')]) ?}
?};
_res


\edi_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Przypomnienie o konieczności aktualizacji komunikatów EDI - opis
::  OLD: \edi_imp_desc/upgradex18xx.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja komunikatów EDI'


\mech_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Przypomnienie o konieczności aktualizacji mechanizmów importu
::  OLD: \edi_imp/upgradex18xx.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
_fun:=obj_new(1);
_fun[1]:='ZWS_IMPORTY_ZWS';
{? FUN.ask(
      'W związku z przejściem na Merita z Xpertis''a należy zaktualizować mechanizmy importu.\n'
      'Jest to wymagane przed importem definicji procesów korzystających z mechanizmów importu.\n'
      '\n'
      'W tym celu po uruchomieniu importu:\n'
      '1. Wybierz opcję: "Nadpisuj istniejące".\n'
      '2. Wskaż katalog merit\\transfer\\mechimp (pamiętaj aby wcześniej wkopiować do niego odpowiednie pliki - patrz nota).\n'
      '3. Zapoznaj się z ewentualnymi komunikatami.\n'
      '4. Zamknij okno z podsumowaniem importu.\n'
      '\n'
      'Czy rozpocząć import?'
   )
|| exec('import_init','#excel_imex');
   _ask:=FUN.choice('Czy import został wykonany?',,'Tak','Nie','Zostanie wykonany później');
   {? _ask=1 || _res:=1
   |? _ask=2 || _res:=0
   |? _ask=3 || _res:=-1
   ?};
   {? _res=1 || __UPG.msg('Użytkownik %1 wykonał import mechanizmów importu.' [exec('operatorKod','#users')]) ?}
|| _ask:=FUN.choice('Wybierz tryb rezygnacji z importu',,'Nie jest potrzebny','Zostanie wykonany później');
   {? _ask=1 || _res:=1
   |? _ask=2 || _res:=-1
   ?};
   {? _res=1 || __UPG.msg('Użytkownik %1 potwierdził rezygnację z wykonywania importu mechanizmów importu.' [exec('operatorKod','#users')]) ?}
?};
_res


\mech_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Przypomnienie o konieczności aktualizacji mechanizmów importu - opis
::   WE:
::   WY:
::  OLD: \edi_imp_desc/upgradex18xx.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja mechanizmów importu'


\WARP100
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Uzupełnienie dodatkowego pola związanego z obsługą amortyzacji jednorazowej do 100 tys.
::   WE:
::   WY:
::  OLD: \WARP100/upgrade_1842.fml
::----------------------------------------------------------------------------------------------------------------------
SRST.cntx_psh(); SRSR.cntx_psh();
exec('maski','srodobj');
SRSR.prefix(); SRST.prefix();
__SRSTOF:=1;
echo('Uzupełnianie środków trwałych - amortyzacja jednorazowa'@);
SRST.for_each("{? SRST.SRSR().AMOR100='T' & SRST.WARP100<>SRSR.WARP100
               || SRST.WARP100:=SRSR.WARP100; SRST.put()
               ?}");
__SRSTOF:=0;
echo();
SRST.cntx_pop(); SRSR.cntx_pop();
__UPG.msg('Uzupełniono dodatkowe pole związane z obsługą amortyzacji jednorazowej.')


\WARP100_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Uzupełnienie dodatkowego pola związanego z obsługą amortyzacji jednorazowej do 100 tys. - opis
::   WE:
::   WY:
::  OLD: \WARP100_desc/upgrade_1842.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie danych związanych z obsługą amortyzacji jednorazowej do 100 tys. zł.'


\zws_par_fspr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Dodanie czynności ZWS_PAR_FSPR do ról z czynnością FKS_KSP_DDEF. Czynność FKS_KSP_DDEF usuwa z roli
::  OLD: \zws_par_fspr/upgrade_18xx.fml
::----------------------------------------------------------------------------------------------------------------------
_zmiana:=0;
_zws:=exec('ref','#b_action','ZWS_PAR_FSPR');
_fks:=exec('ref','#b_action','FKS_KSP_DDEF');
{? _zws & _fks
|| B_ACTROL.cntx_psh();
   B_ACTROL.index('ACTION'); B_ACTROL.prefix(_fks);
   {!
   |? {? B_ACTROL.first()
      || B_ACTROL.cntx_psh();
         B_ACTROL.prefix();
         B_ACTROL.B_ACTION:=_zws;
         _zmiana+=B_ACTROL.put(1);
         B_ACTROL.cntx_pop();
         1
      ?}
   !};
   B_ACTROL.cntx_pop()
?};
_dom:=exec('domain_ref','#b_domain','FKS');
{? _dom & _zmiana
|| _jest:=0;
   B_USRDOM.cntx_psh();
   B_USRDOM.index('S1'); B_USRDOM.prefix(_dom);
   {? B_USRDOM.first()
   || {!
      |? B_USRROL.cntx_psh();
         B_USRROL.index('S1'); B_USRROL.prefix(B_USRDOM.USERS);
         {? B_USRROL.first()
         || {!
            |? B_ACTROL.cntx_psh();
               B_ACTROL.index('B_DOMAIN'); B_ACTROL.prefix(B_USRROL.B_ROLE);
               _jest:=B_ACTROL.find_key(_dom);
               B_ACTROL.cntx_pop();
               _jest=0 & B_USRROL.next()
            !}
         ?};
         B_USRROL.cntx_pop();
         {? _jest
         || B_USRDOM.next()
         || B_USRDOM.del()
         ?}
      !}
   ?};
   B_USRDOM.cntx_pop()
?};
{? _zmiana
|| __UPG.msg('Zamiana czynności %1 na %2 w rolach.'@['FKS_KSP_DDEF','ZWS_PAR_FSPR'])
|| __UPG.msg('Zmiany nie były wymagane.'@)
?};
1


\zws_par_fspr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Dodanie czynności ZWS_PAR_FSPR do ról z czynnością FKS_KSP_DDEF. Czynność FKS_KSP_DDEF usuwa z roli -
::       opis
::  OLD: \zws_par_fspr_desc/upgrade_18xx.fml
::----------------------------------------------------------------------------------------------------------------------
'Zamiana czynności FKS_KSP_DDEF na ZWS_PAR_FSPR w rolach'


\ROKP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Uzupełnienie nowego pola na rok podatkowy dokumentu
::----------------------------------------------------------------------------------------------------------------------
SRDO.cntx_psh();
_maski:=SRDO.names();
{? _maski.first()
|| {! |?
      SRDO.use(_maski.NAME);
      SRDO.prefix();
      {? SRDO.first()
      || SRDO.for_each("{? SRDO.ROKP=0 || SRDO.ROKP:=SRDO.ROK; SRDO.put() ?}",1)
      ?};
      _maski.next()
   !}
?};
SRDO.cntx_pop();
__UPG.msg('Uzupełniono dane dotyczące roku podatkowego dokumentów środków trwałych.')


\ROKP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Uzupełnienie nowego pola na rok podatkowy dokumentu - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie nowego pola: Rok podatkowy dokumentu w tabeli dokumentów środków trwałych.'


\form_plat_pos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Aktualizacja form płatności dla POSa
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Dodanie parametru w formach płatności
RS.cntx_psh();
RS.index('RS');
{? RS.find_key('~Płatności',)
||
   exec('new_par','slo_slu',RS.ref(),4,'Płatność dostępna w POS','TAK_NIE')
?};
RS.cntx_pop();
__UPG.msg('Dodano parametr "Płatność dostępna w POS" dla słowników użytkownika opartych na wzorcu ~Płatności.');

:: Dodanie nowej formy płatności - Karta - płatność kartą

exec('czytaj','#stalesys',,XINFO,'SLP');
_add_karta:=1;

SLO.cntx_psh();
SLO.index('SL');
SLO.prefix(XINFO.SLP);
_loop:=SLO.first() & XINFO.SLP;
{!
|? _loop
|!
   _add_karta:=_add_karta & (SLO.KOD<>'KARTA');
   ZR_SLO.cntx_psh();
   ZR_SLO.index('SLO_NR');
   ZR_SLO.prefix(SLO.ref());
   {? ZR_SLO.find_key(1)
   ||
      {? ZR_SLO.WAR='T'
      ||
         {? ~ZR_SLO.find_key(4)
         ||
::          Płatność dostępna w POS
            ZR_SLO.NR:=4;
            {? ZR_SLO.add()
            ||
               __UPG.msg('Forma płatności %1 ze słownika użytkownika %2 dostępna w punkcie sprzedaży detalicznej.'
                  [SLO.KOD,XINFO.SLP().NAZ])
            ?}
         ?}
      ?}
   ?};
   ZR_SLO.cntx_pop();
   _loop:=SLO.next()
!};
{? XINFO.SLP & _add_karta
||
   SLO.blank();
   SLO.SLU:=XINFO.SLP;
   SLO.KOD:='KARTA';
   SLO.TR:='Płatność kartą';
   {? SLO.add()
   ||
      __UPG.msg('Dodano formę płatności KARTA w słowniku użytkownika %1.'[XINFO.SLP().NAZ]);

      ZR_SLO.cntx_psh();
      ZR_SLO.index('SLO_NR');
      ZR_SLO.prefix(SLO.ref());
      ZR_SLO.blank();
      ZR_SLO.SLO:=SLO.ref();
      {? ~ZR_SLO.find_key(1)
      ||
::       Czy gotówkowa?
         ZR_SLO.NR:=1;
         ZR_SLO.WAR:='N';
         ZR_SLO.add()
      ?};
      {? ~ZR_SLO.find_key(4)
      ||
::       Płatność dostępna w POS
         ZR_SLO.NR:=4;
         ZR_SLO.WAR:='T';
         ZR_SLO.add()
      ?};
      ZR_SLO.cntx_pop()
   ?}
?};
SLO.cntx_pop();
_res


\form_plat_pos_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja form płatności dla POSa'


\RD_slowniki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Uzupełnienie słownika podstaw prawnych i słownika rodzajów nieobecności do wniosków o urlop rodzicielski.
::   WE:
::   WY:
::  ORG: \RD_slowniki/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
:: Uzupełnienie słownika podstaw prawnych do wniosków o urlop rodzicielski.
exec('podstawa_prawna','urlop_rodzicielski');
__UPG.msg('Słownik podstaw prawnych do wniosków o urlop rodzicielski został zaktualizowany.');
:: Uzupełnienie słownika rodzajów nieobecności do wniosków o urlop rodzicielski.
exec('nieobecnosci','urlop_rodzicielski');
__UPG.msg('Słownik rodzajów nieobecności do wniosków o urlop rodzicielski został zaktualizowany.');
1


\RD_slowniki_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.22]
:: OPIS: Uzupełnienie słownika podstaw prawnych i słownika rodzajów nieobecności do wniosków o urlop rodzicielski - opis
::   WE:
::   WY:
::  ORG: \RD_slowniki_desc/upgrade_1822.fml
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słowników podstaw prawnych i rodzajów nieobecności do wniosków o urlop rodzicielski'


\_zdo_01
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: KOD BŁĘDU: ER/WRT/XP/18.42/1810/0011 (kopia: ER/WRT/XP/12.51/1808/0025)
::       Formuła pomocnicza.
::   WE: [_a] [STRING] - Zakres przetwarzania:
::                       'f' - tabele firmowe [domyślnie];
::                       'w' - tabele wspólne.
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_zakres:={? var_pres('_a')=type_of('') & _a='w' || 'w' || 'f' ?};

_warunek:=$(
   "pth_dir(':%1.mdb' [gsub(_a.name(1),'?','ą')])"+
   {? _zakres='f' || '=' || '<>' ?}+
   '\''+gsub(pth_dir(':ąąąąąąąą.mdb'),'\\','\\\\')+'\''
);

_tTABLE:=type_of(OSOBA);

:: Napis konfigurujący zakres przetwarzania. Definicja jest wspólna dla wielu wersji. Oznacza to, że w konkretnej wersji
:: może nie istnieć jakaś z wymienionych tabel, lub nie mieć wszystkich wymienionych pól.
:: Separator: '|'.
:: Budowa jednego elementu: 'T[:P1:O1[:P2:O2[:P3:O3]]]', gdzie:
::    T  - akronim przetwarzanej tabeli;
::    P1 - akronim pola typu _P [domyślnie: 'P'];
::    O1 - akronim pola typu _OSOBA [domyslnie: 'OSOBA'].
::    ..
::    Pn - akronim pola typu _P;
::    On - akronim pola typu _OSOBA.
_arr:=spli_str(
   '|A_OKRP|DEKL_POD:PRAC_ET:OS_PODAT|NBK|OS_VIEW|P_PZ:P:P_OSO:PZ:PZ_OSO|P_WEB_CX'+
   '|PM_DOMP|PM_DYSP|PM_NAG|PM_PREM|PM_RODZ|R_SZYCH|RD_OKR'+
   '|RP_ETOC:DOD_P:DOD_OS|RP_OSET:WER_P:WER_OS|RP_PRET:WER_P:WER_OS|RP_PROC:WER_P:WER_OS'+
   '|RP_ZAP:WER_P:WER_OS:AKC_P:AKC_OS|STRST_P|SZB_WYK|SZK_PSZK'
   '|SZK_ZAP:P:OSOBA:WER_KTO:WER_OS:ZGL_KTO:ZGL_OS|VAT_DEK:P:OS_PODAT|WT_NWU|ZC'+
   '|ZP_DET|ZP_OKR:AKC_P:AKC_OS|ZP_POZ:AKC_P:AKC_OS:RED_P:RED_OS',
   '|'
);

:: Funkcja budująca treść formuły wykonywanej dla każdego przetwarzanego rekordu.
::    _a [STRING] - Akronim tabeli.
::    _b [STRING] - Akronim pola typu _P.
::    _c [STRING] - Akronim pola typu _OSOBA.
_gf:=$(
   "'{? '+_a+'.'+_b+'<>null() & '+_a+'.'+_c+'<>null() & '+_a+'.'+_c+'<>'+_a+'.'+_b+'().OSOBA'+' '+"+
   "'|| '+_a+'.'+_c+':='+_a+'.'+_b+'().OSOBA; '+"+
   "{? _d || '_put+=1' || 'params_get().stat.put+='+_a+'.put(1)' ?}+' '+"+
   "'?}; '"
);

:: Zmienna ma charakter informacyjny. Zawiera akronimy pól (w układzie 'T.P'), które nie występują w bieżącej wersji.
_brak:='';

_stat:=obj_new('put');
params_set('stat',_stat);

OSOBA.cntx_psh();
OSOBA.prefix();
P.cntx_psh();
P.prefix();

_step:=0;
_size:=obj_len(_arr);
{! _la:=1 .. _size
|! _tab:=spli_str(_arr[_la],':');
   _step+=1;
   _proc:=(100*_step/_size)$2;
   progress(_proc,'Przetwarzanie danych ('+form(_proc,6,2)+'%) ',FUN.TYT);
   {? var_pres(_tab[1])=_tTABLE
   || _TAB:=($_tab[1])();
      {? _warunek(_TAB)
      || _lt:=obj_len(_tab);
         _fml:='';
         {? _lt=1
         || _vp:=var_pres('P',_TAB)=26;
            _vo:=var_pres('OSOBA',_TAB)=26;
            {? _vp & _vo
            || _fml:=_gf(_tab[1],'P','OSOBA',0)-2
            || {? ~_vp
               || _brak+=_tab[1]+'.P, '
               ?};
               {? ~_vo
               || _brak+=_tab[1]+'.OSOBA, '
               ?}
            ?}
         || {! _lp:=2 // 2 .. _lt
            |! _vp:=var_pres(_tab[_lp],_TAB)=26;
               _vo:=var_pres(_tab[_lp+1],_TAB)=26;
               {? _vp & _vo
               || _fml+=_gf(_tab[1],_tab[_lp],_tab[_lp+1],_lt>3)
               || {? ~_vp
                  || _brak+=_tab[1]+'.'+_tab[_lp]+', '
                  ?};
                  {? ~_vo
                  || _brak+=_tab[1]+'.'+_tab[_lp+1]+', '
                  ?}
               ?}
            !};
            {? _lt>3
            || _fml:='_put:=0; '+_fml+'{? _put || params_get().stat.put+='+_tab[1]+'.put(1) ?}'
            || _fml:=_fml-2
            ?}
         ?};
         {? _fml<>''
         || _TAB.cntx_psh();
            _NAMES:=_TAB.names();
            _ln:=_NAMES.first();
            _stat.put:=0;
            {!
            |? _ln
            |! _TAB.use(_NAMES.NAME);
               _TAB.prefix();
               _TAB.for_each($_fml);
               _ln:=_NAMES.next()
            !};
            {? _stat.put
            || __UPG.msg('%1: liczba poprawionych zapisów: %2.' [_tab[1],$_stat.put])
            || __UPG.msg('%1: brak błędnych zapisów.' [_tab[1]])
            ?};
            obj_del(_NAMES);
            _TAB.cntx_pop()
         ?}
      ?};
      obj_del(_TAB)
   ?};
   obj_del(_tab)
!};
prgs_clr();

{? _brak<>''
|| __UPG.msg('Zapisy nieobsłużone: %1 .' [_brak-2])
?};

P.cntx_pop();
OSOBA.cntx_pop();

1


\zdo_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach wspólnych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('_zdo_01','upgradexpertis','w')


\zdo_w_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach wspólnych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Naprawa powiązań danych zanonimizowanych w tabelach wspólnych'


\zdo_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach firmowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('_zdo_01','upgradexpertis','f')


\zdo_f_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach firmowych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Naprawa powiązań danych zanonimizowanych w tabelach firmowych'


\f_06
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: ER/WRT/XP/12.51/1811/0007
::       JPK_FA dla walut
::----------------------------------------------------------------------------------------------------------------------
exec('f_06','napraw_f');
__UPG.msg('Zmodyfikowana została struktura schematu plików JPK_FA.');
1


\f_06_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: ER/WRT/XP/12.51/1811/0007
::       JPK_FA dla walut  - opis
::----------------------------------------------------------------------------------------------------------------------
'JPK_FA dla walut'


\KART_DOD_ZUS_RIA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.02]
:: OPIS: Dodanie defnicji kartoteki dodatkowej do przechowywania informacji o przekazanym raporcie ZUS RIA.
::  ORG: \KART_DOD_ZUS_RIA/upgrade_1902.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_symbol:='ZUS_RIA';

KART_DEF.cntx_psh();
KART_DEF.clear();
KART_DEF.index('SYMBOL');
{? KART_DEF.find_key(_symbol,_symbol)
|| __UPG.msg('Kartoteka dodatkowa "Informacja o przekazaniu raportu ZUS RIA" już istnieje.');
   _result:=1
|| KART_DEF.blank();
   KART_DEF.SYMBOL:=_symbol;
   KART_DEF.NAZWA:='Informacja o przekazaniu raportu ZUS RIA';
   KART_DEF.WER:='KTOD';
   KART_DEF.OPIS:='Informacje dodatkowe';
   KART_DEF.OD:='Data przekazania';
   KART_DEF.ODW:=0;
   KART_DEF.KT:='Rodzaj informacji';
   KART_DEF.KTW:=0;
   KART_DEF.CZYWART:='';
   KART_DEF.memo_set(
      '\'P\'|\'Informacja została przygotowana\'\n'+
      '\'W\'|\'Informacja została wysłana\'\n','F3');

   {? KART_DEF.add(1)
   || KART_DEF.memo_put(,'F3');
      __UPG.msg('Kartoteka dodatkowa "Informacja o przekazaniu raportu ZUS RIA" została dodana.');
      _result:=1
   || __UPG.msg('Dodanie kartoteki dodatkowej "Informacja o przekazaniu raportu ZUS RIA" zakończone niepowodzeniem.')
   ?}
?};
KART_DEF.cntx_pop();
_result


\KART_DOD_ZUS_RIA_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.02]
:: OPIS: Dodanie defnicji kartoteki dodatkowej do przechowywania informacji o przekazanym raporcie ZUS RIA - opis.
::  ORG: \KART_DOD_ZUS_RIA_desc/upgrade_1902.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dodanie defnicji kartoteki dodatkowej do przechowywania informacji o przekazanym raporcie ZUS RIA'


\stanowiska
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.02]
:: OPIS: Uzupełnienie wartości w nowych polach do stanowiska pracy.
::  ORG: \stanowiska/upgrade_1902.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
STN.cntx_psh();
STN.trig_off('*','*');
STN.prefix();
{? STN.first()
|| {!
   |? _put:=0;
      {? STN.N=''
      || STN.N:='N';
         _put:=1
      ?};
      {? STN.G=''
      || STN.G:='N';
         _put:=1
      ?};
      {? _put & ~STN.put()
      || _result:=-1
      ?};
      STN.next()
   !}
?};
STN.trig_on('*','*');
STN.cntx_pop();
{? _result=-1
|| __UPG.msg('Nie udało się uzupełnić wartości w nowych polach przy wszystkich stanowiskach pracy.')
|| __UPG.msg('Udało się uzupełnić wartości w nowych polach przy wszystkich stanowiskach pracy.')
?};
_result


\stanowiska_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.02]
:: OPIS: Uzupełnienie wartości w nowych polach do stanowiska pracy. - opis.
::  ORG: \stanowiska_desc/upgrade_1902.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wartości w nowych polach do stanowiska pracy'


\czysc_osoba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
::  MOD: ARTSLO [19.22]
:: OPIS: Formuła usuwa informacje związane z dowodem osobistym i narodowością oraz
::       związane z imieniem i nazwiskiem matki i ojca.
::  ORG: \dowod_osobisty/upgrade_1902.fml
::       \imie_matki_ojca/upgrade_1902.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_PAR:=tab_tmp(1,
   'DOWOD','INTEGER','Seria i numer'@,
   'ORG_DOW','INTEGER','Organ wydający'@,
   'DWY_DOW','INTEGER','Data wydania'@,
   'DWA_DOW','INTEGER','Data ważności'@,
   'NAROD','INTEGER','Narodowość'@,
   'OJCIEC','INTEGER','Imię ojca'@,
   'OJCIECN','INTEGER','Nazwisko ojca'@,
   'MATKA','INTEGER','Imię matki'@,
   'MATKAN','INTEGER','Nazwisko matki'@
);

_we:=_PAR.mk_edit('Informacje do usunięcia'@);
_PAR.win_esep(_we,'Dowód osobisty i narodowość'@);
_PAR.win_efld(_we,,'DOWOD',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
_PAR.win_efld(_we,,'ORG_DOW',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
_PAR.win_efld(_we,,'DWY_DOW',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
_PAR.win_efld(_we,,'DWA_DOW',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
_PAR.win_efld(_we,,'NAROD',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
_PAR.win_esep(_we,'Imiona, nazwiska ojca i matki'@);
_PAR.win_efld(_we,,'OJCIEC',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
_PAR.win_efld(_we,,'OJCIECN',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
_PAR.win_efld(_we,,'MATKA',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
_PAR.win_efld(_we,,'MATKAN',,,,,,,,,'check-box','check_label="%1"' ['Tak, usuń informację'@],"1","0");
exec('ok_esc','#window',_PAR,_we);
_PAR.win_edit(_we);

{? _PAR.edit("
      _PAR:=cur_tab(1,1)
   ")
|| {? _PAR.DOWOD+_PAR.ORG_DOW+_PAR.DWY_DOW+_PAR.DWA_DOW+_PAR.NAROD+_PAR.OJCIEC+_PAR.MATKA+_PAR.MATKAN+_PAR.OJCIECN=0
   || {? FUN.ask(
         'Nie wybrano żadnej informacji do usunięcia.\n'
         'Czy rezygnujesz (również w przyszłości) z wykonania tego zadania?\n'
         '\n'
         'Tak - zadanie zostanie oznaczone jako "Wykonane" i nie będzie można już go uruchomić.\n'
         'Nie - zadanie pozostanie na liście zadań i będzie je można wykonać w późniejszym czasie.'@
         )
      || __UPG.msg('Użytkownik %1 zdecydował o pominięciu wykonania zadania.' [exec('operatorKod','#users')]);
         1
      || __UPG.msg('Użytkownik %1 pozostawił zadanie do późniejszego wykonania.' [exec('operatorKod','#users')]);
         0
      ?}
   || __UPG.msg('Użytkownik %1 zatwierdził usuwanie informacji.' [exec('operatorKod','#users')]);
      _dozmiany:=0;
      _zmienionych:=0;
      OSOBA.cntx_psh();
      OSOBA.index('IDADD');
      OSOBA.prefix();
      {? OSOBA.first()
      || _zeruj:=$(
            "_d0:=_a; "+
            "_put:=0; "+
            {? _PAR.DOWOD || "{? OSOBA.DOWOD<>'' || OSOBA.DOWOD:=''; _put+=1 ?}; " || "" ?}+
            {? _PAR.ORG_DOW || "{? OSOBA.ORG_DOW<>'' || OSOBA.ORG_DOW:=''; _put+=1 ?}; " || "" ?}+
            {? _PAR.DWY_DOW || "{? OSOBA.DWY_DOW<>_d0 || OSOBA.DWY_DOW:=_d0; _put+=1 ?}; " || "" ?}+
            {? _PAR.DWA_DOW || "{? OSOBA.DWA_DOW<>_d0 || OSOBA.DWA_DOW:=_d0; _put+=1 ?}; " || "" ?}+
            {? _PAR.NAROD || "{? OSOBA.NAROD<>'' || OSOBA.NAROD:=''; _put+=1 ?}; " || "" ?}+
            {? _PAR.OJCIEC || "{? OSOBA.OJCIEC<>'' || OSOBA.OJCIEC:=''; _put+=1 ?}; " || "" ?}+
            {? _PAR.OJCIECN || "{? OSOBA.OJCIECN<>'' || OSOBA.OJCIECN:=''; _put+=1 ?}; " || "" ?}+
            {? _PAR.MATKA || "{? OSOBA.MATKA<>'' || OSOBA.MATKA:=''; _put+=1 ?}; " || "" ?}+
            {? _PAR.MATKAN || "{? OSOBA.MATKAN<>'' || OSOBA.MATKAN:=''; _put+=1 ?}; " || "" ?}+
            "{? _put || OSOBA.put() || -1 ?}");
         _d0:=date(0,0,0);
         _size:=OSOBA.size();
         _step:=0;
         {!
         |? _step+=1;
            _proc:=100*_step/_size;
            progress(_proc,'Usuwanie informacji'@,'Przetwarzanie danych');
            _ret:=_zeruj(_d0);
            {? _ret>=0
            || _dozmiany+=1;
               {? _ret=1
               || _zmienionych+=1
               ?}
            ?};
            OSOBA.next()
         !};
         prgs_clr();
         __UPG.msg('Wszystkich zapisów: %1' [$_size]);
         __UPG.msg('Zapisów do zmiany: %1' [$_dozmiany]);
         __UPG.msg('Zapisów zmienionych: %1' [$_zmienionych])
      ?};
      OSOBA.cntx_pop();
      _dozmiany=_zmienionych
   ?}
|| __UPG.msg('Użytkownik %1 zrezygnował z wykonania procedury. Zadanie pozostanie do późniejszego wykonania.'
   [exec('operatorKod','#users')]);
   0
?}


\czysc_osoba_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Formuła usuwa informacje związane z dowodem osobistym oraz z imionami i nazwiskiem ojca i matki - opis.
::  ORG: \dowod_osobisty_desc/upgrade_1902.fml
::       \imie_matki_ojca/upgrade_1902.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Usuwanie informacji związanych z dowodem osobistym i narodowością'


\usp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Zmiany w obsłudze ustania stosunku pracy.
::  ORG: \usp/upgrade_1902.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FUN.info(
   'W przebiegu zatrudnienia dodane zostały pola związane z obsługą ustania stosunku pracy.\n'
   'Za chwilę zostanie uruchomiony mechanizm, który na podstawie dotychczasowego "Trybu" (zwolnienia)\n'
   'pozwala uzupełnić wartości nowych pól. Na ekranie pojawią się wybrane informacje o wszystkich umowach, dla\n'
   'których uzupełniono "Tryb" ich rozwiązania. Dzięki akcji Popraw możliwe jest ustalenie wartości nowych pól.\n'
   'UWAGA: akcja Popraw, jest akcją grupową. Oznacza to, że możemy zaznaczyć wszystkie umowy z tym samym trybem\n'
   'rozwiązania, wybrać akcję Popraw, uzupełnić nowe pola, a po akceptacji wartości (klawisz F2 lub przycisk OK)\n'
   'zmiany zostaną wprowadzone we wszystkich zaznaczonych umowach.'
);
:: Włączony "brudnopis"?
_p180:=-PAR_SKID.get(180)='t';

:: Formuły pomocnicze, traktowane jako kawałki kodu do sklejenia z innymi kawałkami kodu.
_efld_opt:="
   _tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
   {? var_pres('_b')=type_of(H_UM)
   || _TAB:=_b
   |? _tryb='1'
   || _TAB:=cur_tab(1,1)
   || return(0)
   ?};
   {? var_pres('_c')=type_of('')
   || _we:=_c
   |? _tryb='1'
   || _we:=cur_win(1,1)
   || return(0)
   ?};
   {? var_pres('_d')=type_of('')
   || _fld:=_d
   |? _tryb='1'
   || _fld:=cur_afld()
   || _fld:=''
   ?};

   {? _fld='' | _fld='USPPPRAK'
   || _TAB.efld_opt(_we,'enable=%1,mark=%1' [$(H_UM.USPPPRAK().KOD='550')],H_UM,'USPPPRAT')
   ?};

   1
";

_edit:="
   _TAB.edit(""
      {? (_chk:=__CHK.record2(H_UM,'USPKOD','Przyczyna rozwiązania'@,'USPPPRAK','Podstawa prawna (kod)'@))<>''
      || return(_chk)
      |? H_UM.USPPPRAK().KOD='550' & (_chk:=__CHK.record2(H_UM,'USPPPRAT','Podstawa prawna (inna)'@))<>''
      || return(_chk)
      || {? H_UM.USPPPRAK().KOD<>'550'
         || H_UM.USPPPRAT:=''
         ?};
         ''
      ?}""
   )
";

_hum2tab:="
   _TAB.USPKOD:=H_UM.USPKOD().KOD;
   _TAB.USPPPRAK:=H_UM.USPPPRAK().KOD;
   _TAB.USPPPRAT:=H_UM.USPPPRAT;
   _TAB.USPSTR:=H_UM.USPSTR().KOD;
";

_hum2flda:="
   _flda.USPKOD:=H_UM.USPKOD;
   _flda.USPPPRAK:=H_UM.USPPPRAK;
   _flda.USPPPRAT:=H_UM.USPPPRAT;
   _flda.USPSTR:=H_UM.USPSTR;
";

_flda2hum:="
   H_UM.USPKOD:=_flda.USPKOD;
   H_UM.USPPPRAK:=_flda.USPPPRAK;
   H_UM.USPPPRAT:=_flda.USPPPRAT;
   H_UM.USPSTR:=_flda.USPSTR;
";

:: Formuła odpowiedzialna za aktualizację "czystopisu" (jeżeli pracujemy z brudnopisem) oraz tabeli P, jeżeli
:: poprawiona została ostatnia umowa (ostatni rekord H_UM) dla danego pracownika.
_update:=$(
   {? _p180
   || "  {? 1+H_UM.IDMOD<>'D'
         || _idlnk:=H_UM.IDLNK;
            H_UM.cntx_psh();
            H_UM.use('h_um');
            H_UM.prefix();
            {? H_UM.seek(_idlnk)
            || "+_flda2hum+"
               H_UM.put()
            || undo()
            ?};
            H_UM.cntx_pop()
         ?};
      "
   || ""
   ?}+"
      _uid:=H_UM.uidref();
      H_UM.cntx_psh();
      H_UM.prefix(H_UM.P);
      {? H_UM.last() & _uid=H_UM.uidref() & H_UM.P().USPKOD<>H_UM.USPKOD
      || P.USPKOD:=H_UM.USPKOD;
         P.put()
      ?};
      H_UM.cntx_pop();
   "
);


:: Początek
P.cntx_psh();
P.prefix();
H_UM.cntx_psh();
{? _p180
|| H_UM.use('h_u_')
|| H_UM.use('h_um')
?};
H_UM.index('OD');
H_UM.prefix();
H.cntx_psh();
{? _p180
|| H.use('_his_')
|| H.use('_hist')
?};
H.index('HISTUM');
H.prefix();
UD_SKL.cntx_psh();
UD_SKL.prefix();
STN.cntx_psh();
STN.prefix();
CP.cntx_psh();
CP.prefix();
S_ZUS.cntx_psh();
S_ZUS.prefix();

:: Przygotowanie umów do prezentacji
_TAB:=sql(
   'select '
      'OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL, '
      'P.DZA, P.T, '
      'H_UM.OD, H_UM.DO, RU.K, RU.O, SLO_NAZ.NAZWA as ROU, '
      'space(16) as WYDZIAL, space(80) as STN, space(9) as CP, '
      'USPKOD.KOD as USPKOD, USPPPRAK.KOD as USPPPRAK, H_UM.USPPPRAT, USPSTR.KOD as USPSTR, '
      'H_UM.REFERENCE as H_UM '
   'from '
      'H_UM join P using (H_UM.P,P.REFERENCE) join OSOBA using (P.OSOBA,OSOBA.REFERENCE) '
           'left join RU using (H_UM.RU,RU.REFERENCE) '
           'left join SLO_NAZ using (H_UM.ROU,SLO_NAZ.REFERENCE) '
           'left join S_ZUS as USPKOD using (H_UM.USPKOD,USPKOD.REFERENCE) '
           'left join S_ZUS as USPPPRAK using (H_UM.USPPPRAK,USPPPRAK.REFERENCE) '
           'left join S_ZUS as USPSTR using (H_UM.USPSTR,USPSTR.REFERENCE) '
   'where '
      'P.FIRMA=:_a and '
      'H_UM.ROU is not null '
   'order by '
      '1,2,3',
   exec('ref_firma','ustawienia')
);

:: Formuły obsługi pól
_TAB.fld_fml('T','DISPLAY_FORMAT',P.fld_fml('T','*DISPLAY_FORMAT'));

:: Uzupełnienie pewnych informacji
{? _TAB.first()
|| {!
   |? {? H_UM.seek(_TAB.H_UM)
      || H.prefix(H_UM.ref());
         {? H.last()
         || _TAB.WYDZIAL:=H.WYDZIAL().SYMBOL;
            _TAB.STN:=H.ST().ST;
            _TAB.CP:=H.CP().CP;
            _TAB.put()
         ?}
      ?};
      _TAB.next()
   !}
?};

:: Okno wertowania
_ws:=_TAB.mk_sel('Umowy'@,,,,,,,,'U');
_TAB.win_fld(_ws,,'T',,,-6,,,'Numer teczki'@,,MS.comment(P,'T'));
_TAB.win_fld(_ws,,'NAZWISKO',,,20,,,MS.name(OSOBA,'NAZWISKO'),,MS.comment(OSOBA,'NAZWISKO'));
_TAB.win_fld(_ws,,'PIERWSZE',,,10,,,MS.name(OSOBA,'PIERWSZE'),,MS.comment(OSOBA,'PIERWSZE'));
_TAB.win_fld(_ws,,'PESEL',,,,,,MS.name(OSOBA,'PESEL'),,MS.comment(OSOBA,'PESEL'));
_TAB.win_fld(_ws,,'DZA',,,-10,,,'Pierwsza umowa'@,,'Data zawarcia pierwszej umowy'@);
_TAB.win_fld(_ws,,'OD',,,-10,,,'Umowa od'@,,MS.comment(H,'OD'));
_TAB.win_fld(_ws,,'DO',,,-10,,,'Umowa do'@,,MS.comment(H,'DO'));
_TAB.win_fld(_ws,,'WYDZIAL',,,-16,,,MS.name(P,'WYDZIAL'),,MS.comment(P,'WYDZIAL'));
_TAB.win_fld(_ws,,'STN',,,15,,,MS.name(H,'ST'),,MS.comment(H,'ST'));
_TAB.win_fld(_ws,,'CP',,,-9,,,MS.name(H,'CP'),,MS.comment(H,'CP'));
_TAB.win_fld(_ws,,'ROU',,,20,,,'*'+'Przyczyna rozwiązania'@,,'Dotychczasowa przyczyna rozwiazania umowy'@);
_TAB.win_fld(_ws,,'USPKOD',,,-4,,,'Przyczyna rozwiązania'@,,MS.comment(H_UM,'USPKOD'));
_TAB.win_fld(_ws,,'USPPPRAK',,,-4,,,'Podstawa prawna'@,,MS.comment(H_UM,'USPPPRAK'));
_TAB.win_act(_ws,,'Formuła','Popraw'@@,,'Poprawienie bieżącego zapisu'@,$("
   _TAB:=cur_tab(1,1);
   {? H_UM.seek(_TAB.H_UM)
   || _flda:=params_get().flda;
      {? _TAB.sel_size()
      || do();
         "+_flda2hum+"
         H_UM.put();
         "+_hum2tab+"
         _TAB.put();
         "+_update+"
         end()
      || obj_del(_TAB);
         _a:='*';
         _b:=cur_tab(1,1);
         _c:=_b.win_edit('?');
         "+_efld_opt+";
         {? "+_edit+"
         || {? _TAB.USPKOD<>H_UM.USPKOD().KOD |
               _TAB.USPPPRAK<>H_UM.USPPPRAK().KOD | _TAB.USPPPRAT<>H_UM.USPPPRAT |
               _TAB.USPSTR<>H_UM.USPSTR().KOD
            || do();
               H_UM.put();
               "+_hum2tab+"
               _TAB.put();
               "+_update+"
               end()
            ?}
         ?}
      ?}
   ?}"),,
   1,
   1,$("
      _par:=params_get();
      _TAB:=cur_tab(1,1);
      _we:=_par.we;
      _TAB.win_edit(_we[2]);
      H_UM.blank();
      _ret:="+_edit+";
      {? _ret
      || _flda:=_par.flda;
         "+_hum2flda+"
         ~~
      ?};
      _TAB.win_edit(_we[1]);
      _ret
   "),
   "",
   'P'
);
_TAB.win_btn(_ws,'text=%1' ['Popraw'@],'menu:P');
_TAB.win_act(_ws,,'Szukaj');
_TAB.win_act(_ws,,'Kolejność');
_TAB.win_act(_ws,,'Rekord',,,,"~H_UM.seek(cur_tab(1,1).H_UM)");
_TAB.win_act(_ws,,'Wyświetl',,,,$("
   _a:='*';
   _b:=cur_tab(1,1);
   _c:=_b.win_edit('?');
   "+_efld_opt+";
   _TAB.display()
   ")
);
_TAB.win_sel(_ws);

:: Okna redagowania
::    1 - podstawowe
::    2 - do obsługi akcji grupowej
_we:=obj_new(2);
{! _lp:=1 .. 2
|! _we[_lp]:=_TAB.mk_edit('Umowa'@);
   {? _lp=1
   || _TAB.win_esep(_we[_lp],'Dane osobowe'@);
      _TAB.win_efld(_we[_lp],,'T',,,16,,1,'Numer teczki'@,,MS.comment(P,'T'));
      _TAB.win_efld(_we[_lp],,'NAZWISKO',,,30,,1,MS.name(OSOBA,'NAZWISKO'),,MS.comment(OSOBA,'NAZWISKO'));
      _TAB.win_efld(_we[_lp],,'PIERWSZE',,,30,,1,MS.name(OSOBA,'PIERWSZE'),,MS.comment(OSOBA,'PIERWSZE'));
      _TAB.win_efld(_we[_lp],,'PESEL',,,16,,1,MS.name(OSOBA,'PESEL'),,MS.comment(OSOBA,'PESEL'));
      _TAB.win_esep(_we[_lp],'Umowa'@);
      _TAB.win_efld(_we[_lp],,'OD',,,,,1,'Umowa od'@,,MS.comment(H,'OD'));
      _TAB.win_efld(_we[_lp],,'DO',,,,,1,'Umowa do'@,,MS.comment(H,'DO'));
      _TAB.win_efld(_we[_lp],,'WYDZIAL',,,16,,1,MS.name(P,'WYDZIAL'),,MS.comment(P,'WYDZIAL'));
      _TAB.win_efld(_we[_lp],,'STN',,,30,,1,MS.name(H,'ST'),,MS.comment(H,'ST'));
      _TAB.win_efld(_we[_lp],,'CP',,,16,,1,MS.name(H,'CP'),,MS.comment(H,'CP'));
      _TAB.win_efld(_we[_lp],,'ROU',,,30,,1,'*'+'Przyczyna rozwiązania'@,,'Dotychczasowa przyczyna rozwiazania umowy'@)
   ?};
   _TAB.win_esep(_we[_lp],'Ustanie stosunku pracy'@);
   _TAB.win_efld(_we[_lp],H_UM,'USPKOD','KOD','S_ZUS',,,,'Przyczyna rozwiązania'@,,MS.comment(H_UM,'USPKOD'));
   _TAB.win_efld(_we[_lp],H_UM,'USPKOD','LINIA',,27,,1,,1);
   _TAB.win_efld(_we[_lp],H_UM,'USPPPRAK','KOD','S_ZUS',,,,'Podstawa prawna (kod)'@,,MS.comment(H_UM,'USPPPRAK'));
   _TAB.win_efld(_we[_lp],H_UM,'USPPPRAK','LINIA',,27,,1,,1);
   _TAB.win_efld(_we[_lp],H_UM,'USPPPRAT',,,30,,,'Podstawa prawna (inna)'@,,MS.comment(H_UM,'USPPPRAT'));
   _TAB.win_efld(_we[_lp],H_UM,'USPSTR','KOD','S_ZUS',,,,MS.name(H_UM,'USPSTR'),,MS.comment(H_UM,'USPSTR'));
   _TAB.win_efld(_we[_lp],H_UM,'USPSTR','LINIA',,27,,1,,1);
   exec('ok_esc','#window',_TAB,_we[_lp]);

   _TAB.efld_opt(_we[_lp],'mark=1',H_UM,'USPKOD','KOD');
   _TAB.efld_opt(_we[_lp],'mark=1',H_UM,'USPPPRAK','KOD');
   _efld_opt('*',_TAB,_we[_lp])
!};
_TAB.win_edit(_we[1]);

_usppprak_ae:=H_UM.fld_fml('USPPPRAK','AFTER_EDIT',_efld_opt);
params_set(
   'we',_we,
   'flda',obj_new('USPKOD','USPPPRAK','USPPPRAT','USPSTR')
);

P.trig_off('put','*');
H_UM.trig_off('put','*');
{? _TAB.size()
|| _TAB.select()
?};
H_UM.trig_on('put','*');
P.trig_on('put','*');

H_UM.fld_fml('USPPPRAK','AFTER_EDIT',_usppprak_ae);

S_ZUS.cntx_pop();
CP.cntx_pop();
STN.cntx_pop();
UD_SKL.cntx_pop();
H.cntx_pop();
H_UM.cntx_pop();
P.cntx_pop();

_funInfo:={? ~_TAB.size() || 'Brak przebiegów zatrudnienia spełniających warunki ustania stosunku pracy.\n'@ || '' ?};
{? FUN.ask(
      _funInfo+
      'Czy wykonane zostały wszystkie prace związane ze zmianą sposobu raportowania do ZUS ustania stosunku pracy?\n'
      '\n'
      'Tak - Formuła potransferowa zostanie uznana za zakończoną. Jej ponowne uruchomienie nie będzie możliwe.\n'
      'Nie - Nie wszystkie prace zostały wykonane. Formuła potransferowa będzie mogła być ponownie uruchomiona.'@
   )
|| __UPG.msg('Użytkownik %1 potwierdził wykonanie wszystkich prac.' [exec('operatorKod','#users')]);
   1
|| __UPG.msg('Prace będę jeszcze kontynuowane.');
   0
?}


\usp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Zmiany w obsłudze ustania stosunku pracy - opis.
::  ORG: \usp_desc/upgrade_1902.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiany w obsłudze ustania stosunku pracy'


\edok_atk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Aktualizacja rekordów tabeli EDOK_ATK
::----------------------------------------------------------------------------------------------------------------------
_typy:=obj_new('tab','first','next','add','get');
_typy.tab:=tab_tmp(2,
   'ETYPY','INTEGER','typ',
   'TAT','INTEGER','nr',
   'OPIS','STRING[255]','opis'
);
_typy.add:="
   {? ~.tab.find_key(_a,_b)
   || .tab.ETYPY:=_a;
      .tab.TAT:=_b;
      .tab.OPIS:=_c;
      .tab.add()
   ?}
";
_typy.get:="
   ETYPY.prefix();
   _ok:=ETYPY.seek(.tab.ETYPY,);
   {? _ok
   || TAT.prefix();
      _ok:=TAT.seek(.tab.TAT,)
   ?};
   _ok
";
_typy.first:="
   {? .tab.first()
   || .get()
   ?}
";
_typy.next:="
   {? .tab.next()
   || .get()
   ?}
";
ETYPY.cntx_psh();
ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',2));
{? ETYPY.first()
|| ETYP_ATR.cntx_psh();
   ETYP_ATR.index('KOLZ');
   {!
   |? ETYP_ATR.prefix(ETYPY.ref(),null);
      {? ETYP_ATR.first()
      || {!
         |? {? ETYP_ATR.TAT().TYP='O'
            || _txt1:=_txt:=ETYP_ATR.memo_txt(,1,'OPIS');
               {! |? {? 1+_txt=' ' | 1+_txt='-' || _txt:=1-_txt; 1 ?} !};
               {? _txt1<>_txt | 1
               || ETYP_ATR.memo_set(_txt,'OPIS');
                  ETYP_ATR.memo_put(,'OPIS');
                  ETYP_ATR.put();
                  _typy.add(ETYPY.ref,ETYP_ATR.TAT,_txt)
               ?}
            ?};
            ETYP_ATR.next()
         !}
      ?};
      ETYPY.next()
   !};
   ETYP_ATR.cntx_pop()
?};

{? _typy.first()
|| _tab:=EDOK_ATR.names();
   {? _tab.first()
   || EDOK_ATR.cntx_psh();
      {!
      |? EDOK_ATR.use(_tab.NAME);
         EDOK_ATR.index('TATWARTE');
         {? _typy.first()
         || {!
            |? EDOK_ATR.prefix(ETYPY.ref(),TAT.ref());
               {? EDOK_ATR.first()
               || {!
                  |? {? EDOK_ATR.WAR<>_typy.tab.OPIS
                     || EDOK_ATR.WAR:=_typy.tab.OPIS;
                        EDOK_ATR.put()
                     ?};
                     EDOK_ATR.next()
                  !}
               ?};
               _typy.next()
            !}
         ?};
         _tab.next()
      !};
      EDOK_ATR.cntx_pop()
   ?};
   &_tab
?};
ETYPY.cntx_pop();

_tab:=EDOK_ATR.names();
{? _tab.first()
|| {!
   |? {? 6+_tab.NAME='edokat'
      || EDOK_ATK.use('edoknt'+(_tab.NAME+2));
         EDOK_ATK.index('EDOKUM');
         EDOK_ATK.prefix();
         EDOK_ATR.use(_tab.NAME);
         EDOK_ATR.index('REKKOLED');
         EDOK_ATR.prefix();
         {? EDOK_ATR.first()
         || {!
            |? {? ~EDOK_ATK.find_key(EDOK_ATR.EDOKUM,EDOK_ATR.REKORD)
               || EDOK_ATK.EDOKUM:=EDOK_ATR.EDOKUM;
                  EDOK_ATK.REKORD:=EDOK_ATR.REKORD;
                  EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2');
                  EDOK_ATK.add()
               || EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2');
                  EDOK_ATK.put()
               ?};
               EDOK_ATR.prefix(EDOK_ATR.EDOKUM,null,'',EDOK_ATR.REKORD);
               EDOK_ATR.last();
               EDOK_ATR.prefix();
               EDOK_ATR.next()
            !}
         ?}
      ?};
      _tab.next()
   !}
?};
__UPG.msg('Zaktualizowano dane o zestawach informacji dodatkowych wniosków w obiegu.')


\edok_atk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Aktualizacja rekordów tabeli EDOK_ATK
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych o zestawach informacji dodatkowych wniosków w obiegu'


\etypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Uzupełnienie pola ETYPY.ODD_TYP
::----------------------------------------------------------------------------------------------------------------------
::__UPG.add_task('etypy_zal',,,'OBE');
ETYPY.prefix();
ETYPY.for_each("
   _put:=0;
   {? ETYPY.ODD_TYP='' || ETYPY.ODD_TYP:='B'; _put:=1 ?};
   {? ETYPY.ED_TEMAT='' || ETYPY.ED_TEMAT:='T'; _put:=1 ?};
   {? ETYPY.CZY_ZALP<>'T' || ETYPY.CZY_ZALP:='N'; ETYPY.ETYPZAL:=null(); _put:=1 ?};
   {? ETYPY.DOK_VAT='' || ETYPY.DOK_VAT:='N'; _put:=1 ?};
   {? ETYPY.ZAPOT
   || ETYPY.CZ_ZAPOT:=1
   || ETYPY.CZ_ZAPOT:=0
   ?};
   _put:=1;
   {? _put || ETYPY.put() ?}
",1);
__UPG.msg('Uzupełniono nowe pole w typach dokumnetów w obiegu.');
1


\etypy_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Uzupełnienie pola ETYPY.ODD_TYP - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie nowego pola w typach dokumnetów w obiegu'


\sam_el
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Ustawia znacznik SAM_EL dla tabeli SRSR: ZDP_NAG
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
SRSR.cntx_psh();
_name:=SRSR.names();
{? _name.first()
|| echo('Aktualizacja tabeli środków trwałych...'@);
   {! |?
      SRSR.use(_name.NAME);
      SRSR.prefix();
      {? SRSR.first()
      || SRSR.for_each("{? SRSR.SAM_EL='' || SRSR.SAM_EL:='N'; SRSR.put() ?}",0)
      ?};
      _name.next()
   !};
   echo()
?};
SRSR.cntx_pop();
__UPG.msg('Zaktualizowano znacznik: Czy samochód elektryczny, dla środków trwałych.');
_res


\sam_el_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Ustawia znacznik SAM_EL dla tabeli: SRSR - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie znacznika: Czy samochód elektryczny dla środków trwałych.'


\uzup_finfo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Uzupełnienie podstawowych parametrów środków trwałych, zmiany limitów dotyczących amortyzacji,
::       formuł na numerację
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('init_wer','#stalesys');
exec('stalesys','#stalesys');


FINFO.LIMIT_OS:=150000;
FINFO.LIMIT_OE:=225000;
FINFO.W:=10000;
FINFO.NRI_REDP:='exec(\'redp_standard\',\'fst\')';

exec('zapisz','#stalesys',date(2019,1,1),FINFO,'LIMIT_OS');
exec('zapisz','#stalesys',date(2019,1,1),FINFO,'LIMIT_OE');
exec('zapisz','#stalesys',date(2018,1,1),FINFO,'W');
exec('zapisz','#stalesys',1,FINFO,'NRI_REDP');

__UPG.msg('Uzupełniono podstawowe parametry dla środków trwałych.');
_res


\uzup_finfo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Uzupełnienie podstawowych parametrów środków trwałych, zmiany limitów dotyczących amortyzacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie podstawowych parametrów dla środków trwałych - limity dotyczące amortyzacji, formuły dla numeracji.'


\httpbank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Aktualizacja adresu serwera NBP z danymi banków
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('init_wer','#stalesys');
exec('stalesys','#stalesys');

HINFO.HTTPBANK:='https://ewib.nbp.pl/plewibnra?dokNazwa=';
exec('zapisz','#stalesys',date(),HINFO,'HTTPBANK');

__UPG.msg('Zaktualizowano adres serwera NBP z danymi banków.');
_res


\httpbank_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Aktualizacja adresu serwera NBP z danymi banków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja adresu serwera NBP z danymi banków.'


\par6_plat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [19.02]
:: OPIS: Dodanie nowego parametru w słownikach opartych na wzorcu ~Płatności
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
RS.index('RS'); RS.prefix();
{? RS.find_key('~Płatności')
|| exec('new_par','slo_slu',RS.ref(),6,'Termin płat. liczony od daty?','S_O_W')
?};
__UPG.msg('Dodano parametr 6 w słownikach opartych na wzorcu ~Płatności.');
_res


\par6_plat_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [19.02]
:: OPIS: Dodanie nowego parametru w słownikach opartych na wzorcu ~Płatności - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowego parametru w słownikach opartych na wzorcu ~Płatności.'


\del_ocr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Usuwa parametry związane z OCR-em
::----------------------------------------------------------------------------------------------------------------------
_del:=0;
PARAMS.cntx_psh();
PARAMS.index('UNIQ'); PARAMS.prefix(455);
{? PARAMS.first()
|| {!
   |? {? PARAMS.OPIS='Uruchomić moduł rozpoznawania OCR podczas skanowania zbiorczego?'
      || _del:=1;
         PARAMS.del()
      || PARAMS.next()
      ?}
   !}
?};
PARAMS.cntx_pop();
_wz:='~Płatności';
do();
RS.cntx_psh();
RS.index('RS');
RS.prefix(_wz,);
{? RS.first()
|| ZR_DSLO.cntx_psh();
   ZR_DSLO.index('WZ_NR');
   ZR_DSLO.prefix(RS.ref(),3);
   {? ZR_DSLO.first() & ZR_DSLO.NAZ='Płatność rozpoznawana w OCR'
   || SLU.cntx_psh();
      SLU.index('WZORZEC'); SLU.prefix(_wz,);
      {? SLU.first()
      || SLO.cntx_psh();
         SLO.index('SL');
         ZR_SLO.cntx_psh();
         ZR_SLO.index('SLO_NR');
         ZR_SLO.prefix();
         {!
         |? SLO.prefix(SLU.ref());
            {? SLO.first()
            || {!
               |? {? ZR_SLO.find_key(SLO.ref(),3) || ZR_SLO.del() ?};
                  SLO.next()
               !}
            ?};
            SLU.next()
         !};
         ZR_SLO.cntx_pop();
         SLO.cntx_pop()
      ?};
      SLU.cntx_pop();
      ZR_DSLO.del();
      _del:=1
   ?};
   ZR_DSLO.cntx_pop()
?};
RS.cntx_pop();
_del:=end() & _del;
{? _del
|| __UPG.msg('Usunięto parametry związane z OCR-em.')
|| __UPG.msg('Brak parametrów związanych z OCR-em.')
?};
1


\del_ocr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Usuwa parametry związane z OCR-em - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie parametrów związanych z OCR-em.'


\nbp_kursy_fsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Aktualizacja specyfikacji importu kursów walut z NBP
::----------------------------------------------------------------------------------------------------------------------
exec('spc_imp','homebank','10100000_NBP kursy walut (Internet).fsp',1,1);
exec('spc_imp','homebank','10100000_NBP kursy walut (Proces).fsp',1,1);
__UPG.msg('Zaktualizowano specyfikacje importu kursów walut z NBP.');
1


\nbp_kursy_fsp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Aktualizacja specyfikacji importu kursów walut z NBP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja specyfikacji importu kursów walut z NBP'


\form_plat_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Aktualizacja form płatności - nowy parametr: Typ płatności
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

RS.cntx_psh();
{? RS.find_key('~Płatności')
||
   exec('new_par','slo_slu',RS.ref(),5,'Typ płatności','STRING')
?};
RS.cntx_pop();
__UPG.msg('Dodano parametr "Typ płatności" dla słowników użytkownika opartych na wzorcu ~Płatności.');

SLU.cntx_psh();
SLU.index('WZORZEC');
SLO.cntx_psh();
SLO.index('SL');
SLU.prefix('~Płatności');
{? SLU.first()
||
   {! |?
      SLO.prefix(SLU.ref());
      _loop:=SLO.first();
      {!
      |? _loop
      |!
         _got:=0;
         ZR_SLO.cntx_psh();
         ZR_SLO.index('SLO_NR');
         ZR_SLO.prefix(SLO.ref());
         {? ZR_SLO.find_key(1)
         ||
            {? ZR_SLO.WAR='T'
            ||
               _got:=1
            ?}
         ?};
         {? ~ZR_SLO.find_key(5)
         ||
            ZR_SLO.blank();
            ZR_SLO.SLO:=SLO.ref();
            ZR_SLO.NR:=5;

            {? SLO.KOD='GOTOW' | _got=1
            ||
               ZR_SLO.WAR:='gotówka'
            |? SLO.KOD='KARTA'
            ||
               ZR_SLO.WAR:='karta'
            ||
               ZR_SLO.WAR:='inna'
            ?};

            {? ZR_SLO.add()
            ||
               __UPG.msg('Dodano parametr "Typ płatności" dla formy płatności %1 ze słownika użytkownika %2.'
                  [SLO.KOD,SLU.NAZ])
            ?}
         ?};
         ZR_SLO.cntx_pop();
         _loop:=SLO.next()
      !};
      SLU.next()
   !}
?};
SLO.cntx_pop();
SLU.cntx_pop();
_res


\form_plat_typ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja form płatności - nowy parametr: Typ płatności w POS'


\abstore_field
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Aktualizacja pól kontrahentów
::  OLD: \kh/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

KH.cntx_psh();
KH.index('GLN');
KH.prefix();
{? KH.first()
|| {!
   |? __lrec+=1;
      _put:=0;
      {? KH.POPIS=''
      || KH.POPIS:=exec('kh_popis','kontrahent',KH.P);
         {? KH.POPIS<>'' || _put:=1 ?}
      ?};
      {? KH_OSOB.ARCH=''
      || KH_OSOB.ARCH:='N';
         _put:=1
      ?};
      {? _put || {? KH.put(1) || __lmod+=1 ?} ?};
      KH.next()
   !}
?};
KH.cntx_pop();
__UPG.msg('Zaktualizowano pola dla kontrahentów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

VAR_DEL.delete('__lrec','__lmod');
_res


\abstore_field_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Aktualizacja pola AbStore w tabelach: M KH GRKH
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól kontrahentów'


\parametry_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Usuwa nieużywane w Merit parametry
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_p31:=exec('get_par','#parametr',31);
{? type_of(_p31)>0
|| {? exec('del','#parametr',,31)
   || __UPG.msg('Usunięto parametr systemu 31.')
   || _res:=-1;
      __UPG.msg('Nie usunięto parametru systemu 31.')
   ?}
?};

_p91:=exec('get_par','#parametr',91);
{? type_of(_p91)>0
|| {? exec('del','#parametr',,91)
   || __UPG.msg('Usunięto parametr systemu 91.')
   || _res:=-1;
      __UPG.msg('Nie usunięto parametru systemu 91.')
   ?}
?};

_p100:=exec('get_par','#parametr',100);
{? type_of(_p100)>0
|| {? exec('del','#parametr',,100)
   || __UPG.msg('Usunięto parametr systemu 100.')
   || _res:=-1;
      __UPG.msg('Nie usunięto parametru systemu 100.')
   ?}
?};

_p451:=exec('get_par','#parametr',451);
{? type_of(_p451)>0
|| {? exec('del','#parametr',,451)
   || __UPG.msg('Usunięto parametr systemu 451.')
   || _res:=-1;
      __UPG.msg('Nie usunięto parametru systemu 451.')
   ?}
?};

_p452:=exec('get_par','#parametr',452);
{? type_of(_p452)>0
|| {? exec('del','#parametr',,452)
   || __UPG.msg('Usunięto parametr systemu 452.')
   || _res:=-1;
      __UPG.msg('Nie usunięto parametru systemu 452.')
   ?}
?};
_res


\parametry_del_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Usuwa nieużywane w Merit parametry - opis
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie nieużywanych parametrów (Ustawienia i parametryzacja -> Wspólne -> Ustawienia systemu -> Lista parametrów)'


\umowy_miejskie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Aktualizacja dla umów miejskich
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_result:=1;
UPM.cntx_psh();
UPM.prefix();
_formula:="
   _put:=0;
   __lrec+=1;
   {? UPM.IS_SEG=''
   || UPM.IS_SEG:={? UPM.SEG<>'' || 'T' || 'N' ?};
      _put:=1
   ?};
   {? _put || {? UPM.put(1) || __lmod+=1 ?} ?}
";
_result:=UPM.for_each(_formula,1);
UPM.cntx_pop();
__UPG.msg('Zaktualizowano pozycje umów miejskich - Rodzaj i stosowanie segregacji.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\umowy_miejskie_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Aktualizacja dla umów miejskich
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych umów miejskich'


\UM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli UM
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
STU.cntx_psh();
STU.prefix();
_res:=STU.for_each("__lrec+=1;
                    _old:='''um_st_np'',''um'''; _new:='''um_st_np'',''umowy''';
                    _f:=gsub(STU.F,_old,_new); _fp:=gsub(STU.FP,_old,_new);
                    _old:='''umst_ak2'',''um3'''; _new:='''umst_ak2'',''umowy''';
                    _f:=gsub(_f,_old,_new); _fp:=gsub(_fp,_old,_new);
                    _old:='''um_st_ap'',''um'''; _new:='''um_st_ap'',''umowy''';
                    _f:=gsub(_f,_old,_new); _fp:=gsub(_fp,_old,_new);
                    _old:='''um_be_ee'',''um'''; _new:='''um_be_ee'',''umowy''';
                    _f:=gsub(_f,_old,_new); _fp:=gsub(_fp,_old,_new);
                    _old:='''um_st_ee'',''um'''; _new:='''um_st_ee'',''umowy''';
                    _f:=gsub(_f,_old,_new); _fp:=gsub(_fp,_old,_new);
                    _old:='''um_be_zz'',''um'''; _new:='''um_be_zz'',''umowy''';
                    _f:=gsub(_f,_old,_new); _fp:=gsub(_fp,_old,_new);
                    {? _f<>STU.F | _fp<>STU.FP
                    || STU.F:=_f;
                       STU.FP:=_fp;
                       {? STU.put(1) || __lmod+=1 ?}
                    ?}
                 ");
STU.cntx_pop();
__UPG.msg('Zaktualizowano formuły statusów umów cyklicznych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
__lrec:=__lmod:=0;
UM.cntx_psh();
UM.prefix();
_res:=UM.for_each("__lrec+=1;
                   {? UM.STAT_REJ=''
                   || {? UM.STU().RODZ='' & UM.STU().A='N'
                      || UM.STAT_REJ:='X'
                      |? UM.STU().RODZ='E'
                      || UM.STAT_REJ:='E'
                      |? UM.STU().RODZ='N'
                      || UM.STAT_REJ:='Z'
                      || UM.STAT_REJ:='T'
                      ?};
                      {? UM.put(1) || __lmod+=1 ?}
                   ?}
                 ");
UM.cntx_pop();
__UPG.msg('Zaktualizowano umowy cykliczne.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\UM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli UM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja umów cyklicznych'


\POS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli POS.NR - opis
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
POS.cntx_psh();
POS.prefix();
_res:=POS.for_each("__lrec+=1;
                   _posnr:=exec('wz_pos_nr','podz_admin',POS.NR);
                   {? _posnr<>POS.NR
                   || POS.NR:=_posnr;
                      {? POS.put(1) || __lmod+=1 ?}
                   ?}
                 ");
POS.cntx_pop();
__UPG.msg('Zaktualizowano numery posesji.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\POS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli POS.NR - opis
::----------------------------------------------------------------------------------------------------------------------
'Zmiana formatowania numeru posesji'


\ZLP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli ZLP
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ZLP.names();
ZLP.cntx_psh();
ZLE.cntx_psh();
ZLP.trig_off('*','*');
_msk.clear();
{? _msk.first()
|| {!
   |? ZLP.use(_msk.NAME);
      ZLE.use('zlecen_'+(1+(3-_msk.NAME)));
      ZLP.prefix();
      _res:=ZLP.for_each(" __lrec+=1;
                          _put:=0;
                          {? ZLP.STAT_REJ=''
                           || _czum:=exec('FindAndGet','#table',ZLE,ZLP.ZLE,,\"CZUM\",'');
                              {? _czum='N'
                             || ZLP.STAT_REJ:='Z';
                                ZLP.SAMO:='X'
                              || ZLP.STAT_REJ:='N'
                              ?};
                             _put+=1
                          ?};
                          {? ZLP.SP=''
                          || {? ZLP.DZP<>date(0,0,0) | ZLP.DP<>date(0,0,0) || ZLP.SP:='T' || ZLP.SP:='N' ?};
                             _put+=1
                          ?};
                          {? ZLP.NOMAIL=''
                          || ZLP.NOMAIL:='T';
                             _put+=1
                          ?};
                          {? ZLP.GPZO=''
                          || ZLP.GPZO:='N';
                             _put+=1
                          ?};
                          {? ZLP.SPLAN=''
                          || ZLP.SPLAN:='N';
                             _put+=1
                          ?};
                          {? ZLP.TKPO=''
                          || ZLP.TKPO:=exec('typ_kpo','odpady');
                             _put:=1
                          ?};
                          {? ZLP.SENT_KPO=''
                          || ZLP.SENT_KPO:='N';
                             _put:=1
                          ?};
                          {? ZLP.KODK=''
                          || ZLP.KODK:=$ZLP.tm_stamp();
                             _put:=1
                          ?};
                          {? ZLP.TRN=''
                          || ZLP.TRN:='T';
                             _put:=1
                          ?};
                          {? _put
                          || exec('stanPlan','umowy_zlecenia');
                             {? ZLP.put(1) || __lmod+=1 ?}
                          ?}
                        ");
      _msk.next()
   !}
?};
obj_del(_msk);
ZLP.trig_on('*','*');
ZLP.cntx_pop();
ZLE.cntx_pop();
__UPG.msg('Zaktualizowano zgłoszenia jednorazowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\ZLP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli ZLP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zgłoszeń jednorazowych'


\HN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Wypełnia pola w tabeli HN
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
HN.cntx_psh();
HN.prefix();
_res:=HN.for_each("
   __lrec+=1;
   _put:=0;
   {? HN.AKT=''
   || HN.AKT:='T';
      _put:=1
   ?};
   {? HN.D1=''
   || HN.D1:='N';
      _put:=1
   ?};
   {? HN.D2=''
   || HN.D2:='N';
      _put:=1
   ?};
   {? HN.D3=''
   || HN.D3:='N';
      _put:=1
   ?};
   {? HN.D4=''
   || HN.D4:='N';
      _put:=1
   ?};
   {? HN.D5=''
   || HN.D5:='N';
      _put:=1
   ?};
   {? HN.D6=''
   || HN.D6:='N';
      _put:=1
   ?};
   {? HN.D7=''
   || HN.D7:='N';
      _put:=1
   ?};
   {? HN.HOP_DIR=0
   || HN.HOP_DIR:=1;
      _put:=1
   ?};
   {? HN.HOP_MODE=''
   || HN.HOP_MODE:='O';
      _put:=1
   ?};
   {? HN.STAT_REJ=''
   || HN.STAT_REJ:='T';
      _put:=1
   ?};
   {? _put>0
   || {? HN.put()
      || __lmod+=1
      ?}
   ?}
");
HN.cntx_pop();
__UPG.msg('Zaktualizowano nagłówki harmonogramów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\HN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Wypełnia pola w tabeli HN - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja nagłówków harmonogramów (umowy cykliczne).'


\HGEN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Wypełnia pola w tabeli HGEN
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
HGEN.cntx_psh();
HGEN.prefix();
_res:=HGEN.for_each("
   __lrec+=1;
   _put:=0;
   {? HGEN.ST_OK=''
   || HGEN.ST_OK:=HGEN.ST;
      _put:=1
   ?};
   {? _put>0
   || {? HGEN.put()
      || __lmod+=1
      ?}
   ?}
");
HGEN.cntx_pop();
__UPG.msg('Zaktualizowano okresy generacji zgłoszeń.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\HGEN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Wypełnia pola w tabeli HGEN - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja okresów generacji zgłoszeń'


\HARMON
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Wypełnia pola w tabeli HARMON
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
HARMON.cntx_psh();
HARMON.prefix();
_res:=HARMON.for_each("
   __lrec+=1;
   _put:=0;
   {? HARMON.STAT_REJ=''
   || HARMON.STAT_REJ:='Z';
      _put:=1
   ?};
   {? _put>0
   || {? HARMON.put()
      || __lmod+=1
      ?}
   ?}
");
HARMON.cntx_pop();
__UPG.msg('Zaktualizowano pozycje harmonogramów umów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\HARMON_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Wypełnia pola w tabeli HARMON - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pozycji harmonogramów (umowy cykliczne).'


\UP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Wypełnia pola w tabeli UP
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__zlbr4hn');
__lrec:=__lmod:=0;

__zlbr4hn:=tab_tmp(2,
:: 'POLE','TYP','Nazwa w oknie',
   'HN','STRING[16]','Nazwa pola 1',
   'ZLBR','STRING[16]','Nazwa pola 2'
);

HN.cntx_psh();
HN.prefix();
{? HN.first()
|| {!
   |? {? var_pres('_zlbr')>100
      || obj_del(_zlbr)
      ?};
      _zlbr:=exec('transfer_harmon','umowy_harm',HN.ref(),,2);
      {? _zlbr.last()
      ||
::       Przyjmuję że ostatnio użyta brygada w harmonogramie będzie stemplować pozycję umowy
         __zlbr4hn.prefix($HN.ref(),);
         {? __zlbr4hn.first()=0
         || __zlbr4hn.blank();
            __zlbr4hn.HN:=$HN.ref();
            __zlbr4hn.ZLBR:=_zlbr.REF;
            __zlbr4hn.add()
         ?}
      ?};
      HN.next()
   !}
?};
HN.cntx_pop();

__zlbr4hn.prefix();
ZLBR.cntx_psh();
ZLBR.index('ODDZ');
UP.cntx_psh();
UP.prefix();
_res:=UP.for_each("
   __lrec+=1;
   _put:=0;
   {? UP.ZLBR=null()
   ||
      {? UP.H<>null()
      ||
::       Jest harmonogram, pobieram zapamiętane dla harmonogramu brygady
         __zlbr4hn.prefix($UP.H,);
         {? __zlbr4hn.first()
         || UP.ZLBR:=exec('FindAndGet','#table',ZLBR,__zlbr4hn.ZLBR,,,null());
            _put:=1
         ?}
      ?}
   ?};
   {? _put>0
   || {? UP.put()
      || __lmod+=1
      ?}
   ?}
");
UP.cntx_pop();
ZLBR.cntx_pop();
__UPG.msg('Zaktualizowano pozycje umów cyklicznych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__zlbr4hn');
_res


\UP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Wypełnia pola w tabeli UP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pozycji umów cyklicznych'


\FPACZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Wypełnia pola tabeli FPACZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
FPACZ.cntx_psh();
FPACZ.index('ROK');
FPACZ.prefix();
_res:=FPACZ.for_each("
   __lrec+=1;
   _put:=0;
   {? FPACZ.STAT_REJ='' || FPACZ.STAT_REJ:='T'; _put:=1 ?};
   {? FPACZ.ZAL='' || FPACZ.ZAL:='N'; _put:=1 ?};
   {? FPACZ.PAR_PL='' || FPACZ.PAR_PL:='N'; _put:=1 ?};
   {? FPACZ.RAB_NAL='' || FPACZ.RAB_NAL:='P'; _put:=1 ?};
   {? FPACZ.STAT_GEN='' || FPACZ.STAT_GEN:=exec('stat_gen','umowy_faktury',0); _put:=1 ?};
   {? _put || {? FPACZ.put() || __lmod+=1 ?} ?}
");
FPACZ.cntx_pop();
__UPG.msg('Zaktualizowano paczki faktur.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\FPACZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Wypełnia pola tabeli FPACZ - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja paczek faktur'


\FAPOW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Wypełnia pola tabeli FAPOW
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
FAPOW.cntx_psh();
FAPOW.index('FAKS');
FAPOW.prefix();
_res:=FAPOW.for_each("
   __lrec+=1;
   _put:=0;
   {? FAPOW.Z_ZLP=''
   ||
      FAPOW.Z_ZLP:='N';
      _put:=1
   ?};
   {? _put
   ||
      {? FAPOW.put() || __lmod+=1 ?}
   ?}
");
FAPOW.cntx_pop();
__UPG.msg('Zaktualizowano zaliczki.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\FAPOW_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Wypełnia pola tabeli FAPOW - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zaliczek'


\formula_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Uaktualnienie treści formuł sprawozdań finansowych
::  OLD: \formula_spr/upgradexpertis.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Ile');
Ile:=0;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4'); FORMULA.prefix();
_fml:="
   {? FORMULA.find_key('#',_a,) & FORMULA.FORMULA<>_c
   || FORMULA.FORMULA:=_c;
      FORMULA.put();
      Ile+=1;
      __UPG.msg('Uaktualniono treść formuły: %1 sprawozdania finansowego.'[_a])
   ?}
";
exec('add_alg','sprfin',_fml);
FORMULA.cntx_pop();
{? Ile=0
|| __UPG.msg('Uaktualnienie treść formuł sprawozdań finansowych nie było wymagane.')
?};
VAR_DEL.delete('Ile');
1


\formula_spr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Uaktualnienie treści formuł sprawozdań finansowych - opis
::  OLD: \formula_spr_desc/upgradexpertis.fml
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie treści formuł sprawozdań finansowych'


\OKR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Uzupełnia znacznik zamknięcia roku OKR.ZAM
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
OKR.cntx_psh();
OKR.prefix();
OKR.for_each("__lrec+=1;
              {? (+form(OKR.ZAM))=3
              || OKR.ZAM+='N';
                 {? OKR.put(1) || __lmod+=1 ?}
              ?}
             ");
OKR.cntx_pop();
__UPG.msg('Zaktualizowano znacznik zamknięcia okresów dla umów cyklicznych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

_dom_lsp:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','LSP',,,1);
_dom_lum:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','LUM',,,1);
_add:=exec('FindInSet','#table','OKR_OBSZ','OBS_ROK',_dom_lum,REF.FIRMA)=null();

__lrec:=__lmod:=0;
{? _add
|| OKR_OBSZ.cntx_psh();
   OKR_OBSZ.index('OBS_ROK');
   OKR_OBSZ.prefix(REF.FIRMA,_dom_lsp);
   {? OKR_OBSZ.first()
   || {!
      |? __lrec+=1;
         OKR_OBSZ.cntx_psh();
         OKR_OBSZ.prefix();
         OKR_OBSZ.B_DOMAIN:=_dom_lum;
         {? OKR_OBSZ.add(1) || __lmod+=1 ?};
         OKR_OBSZ.cntx_pop();
         OKR_OBSZ.next()
      !}
   ?};
   OKR_OBSZ.cntx_pop();
   __UPG.msg('Dodano dla okresów obszar umów cyklicznych.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};

VAR_DEL.delete('__lrec','__lmod');
_res


\OKR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Uzupełnia znacznik zamknięcia roku OKR.ZAM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zamknięcia okresów obrachunkowych dla umów cyklicznych'


\REM_KAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Utworzenie tabeli kategorii zasobów remontowych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

VAR_DEL.delete('__lrec','__ladd');
__lrec:=__ladd:=0;

PROJKZAS.cntx_psh();
REM_KAT.cntx_psh();
REM_KAT.index('SYMBOL');

PROJKZAS.index('NAZ');
PROJKZAS.prefix('S');
{? PROJKZAS.first()
|| {!
   |? __lrec+=1;
      REM_KAT.prefix(PROJKZAS.NAZWA,);
      {? ~REM_KAT.first()
      || REM_KAT.blank();
         REM_KAT.SYMBOL:=PROJKZAS.NAZWA;
         {? REM_KAT.add() || __ladd+=1 ?}
      ?};
      PROJKZAS.next()
   !};
   __UPG.msg('Utworzono/zaktulizowano słownik kategorii zasobów remontowych.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego dołączono: %2 zapisów.'[$__lrec,$__ladd])
|| __UPG.msg('Nie utworzono zapisów w słowniku kategorii zasobów remontowych (brak zapisów źródłowych).')
?};

REM_KAT.cntx_pop();
PROJKZAS.cntx_pop();
_res


\REM_KAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Utworzenie tabeli kategorii zasobów remontowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Utworzenie tabeli kategorii remontowych.\n'
'Przenoszone są zapisy z programu Xpertis Remonty (tabela PROJKZAS) dotyczące wyłącznie:\n'
'- zasobów wewnętrznych,\n'
'- zasobów zewnętrznych,\n'
'- stanowisk produkcyjnych.'


\REM_ZAS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Utworzenie tabeli zasobów remontowych
::       UWAGA: skutek uboczny działania formuły - utworzenie tabeli globalnej __PROJ2REM
::  ORG: \REM_ZAS/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');


VAR_DEL.delete('__PROJ2REM');

REM_ZAS.cntx_psh();
SRSR.cntx_psh();
REM_ZAS.prefix();
{? REM_ZAS.first()
|| __lrec:=__lmod:=0;
   {!
   |? __lrec+=1;
      {? REM_ZAS.SRSR<>null() & REM_ZAS.NF=''& SRSR.seek($REM_ZAS.SRSR,ref_name($REM_ZAS.SRSR))
      || REM_ZAS.NF:=REM_ZAS.SRSR().NF;
         {? REM_ZAS.put() || __lmod+=1 ?}
      ?};
      REM_ZAS.next()
   !};
   {? var_pres('__UPG')>100
   || __UPG.msg('Zasoby z programu Xpertis Remonty zostały już wcześniej przeniesione.');
      __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
   ?};
   REM_ZAS.cntx_pop();
   SRSR.cntx_pop();
   VAR_DEL.delete('__lrec','__lmod');
   return(_res)
?};
__lrec:=__lmod:=0;

exec('init_ztp','tre');

:: tabela mapująca zasoby
__PROJ2REM:=tab_tmp(2
   ,'PROJZAS','STRING[16]','$PROJZAS.ref()'
   ,'REM_ZAS','STRING[16]','$REM_ZAS.ref()'
);

:: formuła zwracajaca zasób - parametr $PROJZAS.ref()
_rem_zas:="
   _res:=null();
   __PROJ2REM.prefix(_a,);
   {? __PROJ2REM.first()
   || _res:=exec('FindAndGet','#table',REM_ZAS,__PROJ2REM.REM_ZAS)
   ?};
   _res
";

:: formuła zwracająca ref kategorii zasobu
_rem_kat:="
   _res:=null();
   REM_KAT.cntx_psh();
   REM_KAT.index('SYMBOL');
   REM_KAT.prefix(_a,);
   {? REM_KAT.first() || _res:=REM_KAT.ref() ?};
   REM_KAT.cntx_pop();
   _res
";

PROJZAS.cntx_psh();
PROJZAS.index('NAZWA');

SRSR.use('srsrr'+REF.FIRMA().SYMBOL);
SRSR.index('NRI');

POJAZDY.cntx_psh();
POJAZDY.index('FIRSRSR');

:: Sprzęty wewnętrzne
PROJZAS.prefix('SW');
{? PROJZAS.first()
|| {!
   |? __lrec+=1;
      REM_ZAS.blank();
      REM_ZAS.UD_SKL:=PROJZAS.UD_SKL;
      REM_ZAS.ODDZ:='c';
      REM_ZAS.TREE:=0;
      REM_ZAS.SYMBOL:=PROJZAS.ID_SROD().NRI;
      REM_ZAS.NAZWA:=PROJZAS.ID_SROD().NST;
      REM_ZAS.WLASNY:='T';
      REM_ZAS.KH:=null();
      REM_ZAS.M:=null();
      REM_ZAS.DK_C:=null();
      SRSR.prefix('T',ID_SROD.NRI,);
      {? SRSR.first()
      || REM_ZAS.SRSR:=SRSR.ref();
         {? SRSR.POJAZD='T'
         || POJAZDY.prefix(REF.FIRMA,SRSR.ref());
            {? POJAZDY.first()
            || REM_ZAS.POJAZDY:=POJAZDY.ref()
            ?}
         ?}
      || REM_ZAS.SRSR:=null();
         REM_ZAS.POJAZDY:=null()
      ?};
      REM_ZAS.TWRKPLC:=null();
      REM_ZAS.TWRKZBR:=null();
      REM_ZAS.TYP:=
         {? REM_ZAS.POJAZDY<>null()
         || exec('rem_zas_typ_ref','zasoby',exec('rem_zas_typ_val_pojazdy','zasoby'))
         |? REM_ZAS.SRSR<>null()
         || exec('rem_zas_typ_ref','zasoby',exec('rem_zas_typ_val_srsr','zasoby'))
         || exec('rem_zas_typ_ref','zasoby',exec('rem_zas_typ_val_null','zasoby'))
         ?};
      REM_ZAS.KAT:=_rem_kat(PROJZAS.PROJKZAS().NAZWA);
      REM_ZAS.PRZ_MODE:='N';
      REM_ZAS.PRZ_JM:=null();
      REM_ZAS.PRZ_DOKL:=0;
      REM_ZAS.PRZ:=0;
      REM_ZAS.RDKC:='';
      {? REM_ZAS.add(1)
      || __lmod+=1;
         __PROJ2REM.PROJZAS:=$PROJZAS.ref();
         __PROJ2REM.REM_ZAS:=$REM_ZAS.ref();
         __PROJ2REM.add()
      ?};
      PROJZAS.next()
   !}
?};

:: Sprzęty zewnętrzne
PROJZAS.prefix('SZ');
{? PROJZAS.first()
|| {!
   |? __lrec+=1;
      REM_ZAS.blank();
      REM_ZAS.UD_SKL:=PROJZAS.UD_SKL;
      REM_ZAS.ODDZ:='c';
      REM_ZAS.TREE:=0;
      REM_ZAS.SYMBOL:=PROJZAS.PROJSZ().NAZ;
      REM_ZAS.NAZWA:=PROJZAS.PROJSZ().OPIS;
      REM_ZAS.WLASNY:='N';
      REM_ZAS.KH:=PROJZAS.PROJSZ().KH;
      REM_ZAS.M:=null();
      REM_ZAS.DK_C:=null();
      REM_ZAS.SRSR:=null();
      REM_ZAS.POJAZDY:=null();
      REM_ZAS.TWRKPLC:=null();
      REM_ZAS.TWRKZBR:=null();
      REM_ZAS.TYP:=exec('rem_zas_typ_ref','zasoby',exec('rem_zas_typ_val_null','zasoby'));
      REM_ZAS.KAT:=_rem_kat(PROJZAS.PROJKZAS().NAZWA);
      REM_ZAS.PRZ_MODE:='N';
      REM_ZAS.PRZ_JM:=null();
      REM_ZAS.PRZ_DOKL:=0;
      REM_ZAS.PRZ:=0;
      REM_ZAS.RDKC:='';
      {? REM_ZAS.add(1)
      || __lmod+=1;
         __PROJ2REM.PROJZAS:=$PROJZAS.ref();
         __PROJ2REM.REM_ZAS:=$REM_ZAS.ref();
         __PROJ2REM.add()
      ?};
      PROJZAS.next()
   !}
?};

:: Stanowiska produkcyjne
PROJZAS.prefix('SS');
{? PROJZAS.first()
|| {!
   |? __lrec+=1;
      REM_ZAS.blank();
      REM_ZAS.UD_SKL:=PROJZAS.UD_SKL;
      REM_ZAS.ODDZ:='c';
      REM_ZAS.TREE:=0;
      REM_ZAS.SYMBOL:=PROJZAS.TWRKPLC().KOD;
      REM_ZAS.NAZWA:=PROJZAS.TWRKPLC().NA;
      REM_ZAS.WLASNY:='T';
      REM_ZAS.KH:=null();
      REM_ZAS.M:=null();
      REM_ZAS.DK_C:=null();
      REM_ZAS.SRSR:=null();
      REM_ZAS.POJAZDY:=null();
      REM_ZAS.TWRKPLC:=PROJZAS.TWRKPLC;
      REM_ZAS.TWRKZBR:=null();
      REM_ZAS.TYP:=
         {? REM_ZAS.TWRKPLC<>null()
         || exec('rem_zas_typ_ref','zasoby',exec('rem_zas_typ_val_twrkplc','zasoby'))
         || exec('rem_zas_typ_ref','zasoby',exec('rem_zas_typ_val_null','zasoby'))
         ?};
      REM_ZAS.KAT:=_rem_kat(PROJZAS.PROJKZAS().NAZWA);
      REM_ZAS.PRZ_MODE:='N';
      REM_ZAS.PRZ_JM:=null();
      REM_ZAS.PRZ_DOKL:=0;
      REM_ZAS.PRZ:=0;
      REM_ZAS.RDKC:='';
      {? REM_ZAS.add(1)
      || __lmod+=1;
         __PROJ2REM.PROJZAS:=$PROJZAS.ref();
         __PROJ2REM.REM_ZAS:=$REM_ZAS.ref();
         __PROJ2REM.add()
      ?};
      PROJZAS.next()
   !}
?};

:: Załączniki
_ZSB_DOK:=tab_tmp(2,'REF','STRING[16]',,'PROJZAS','STRING[16]',);
_filename:='zsb_dok.tra';
{? fexists(_filename,1)
|| _ZSB_DOK.import(_filename,,1,%1+';','UTF-8',,
      'REF',,1,,
      'PROJZAS',,2,
   );
   {? _ZSB_DOK.first()
   || {!
      |? DOKUM.index('DOKUM');
         DOKUM.prefix(REF.FIRMA,_ZSB_DOK.REF,);
         {? DOKUM.first()
         || {!
            |? REM_ZASA.prefix();
               REM_ZASA.blank();
               REM_ZASA.REM_ZAS:=_rem_zas(_ZSB_DOK.PROJZAS);
               REM_ZASA.NAZWA:=DOKUM.memo_txt(,1,'OPIS');
               REM_ZASA.ATA:=DOKUM.DOKUM;
               REM_ZASA.add();
               DOKUM.next()
            !}
         ?};
         _ZSB_DOK.next()
      !}
   ?}
|| FUN.emsg('Brak pliku: %1 - załączniki do zasobów nie zostaną przeniesione.'@[_filename])
?};

PROJZAS.prefix();
{? var_pres('__UPG')>100
|| __UPG.msg('Utworzono tabelę zasobów remontowych.');
   __UPG.msg(
      'Przetworzono: %1 zapisów źródłowych (PROJZAS) z ogółem %2, na podstawie których dodano: %3 zapisów (REM_ZAS).'
      [$__lrec,$PROJZAS.size(),$__lmod]
   )
?};

POJAZDY.cntx_pop();
SRSR.cntx_pop();
PROJZAS.cntx_pop();
REM_ZAS.cntx_pop();

VAR_DEL.delete('__lrec','__lmod');
_res


\REM_ZAS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Utworzenie tabeli zasobów remontowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Utworzenie tabeli zasobów remontowych.\n'
'Przenoszone są zapisy z programu Xpertis Remonty (tabela PROJZAS) dotyczące wyłącznie:\n'
'- zasobów wewnętrznych,\n'
'- zasobów zewnętrznych,\n'
'- stanowisk produkcyjnych.'


\tra2SAMN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Przenosi dane z pliku samn.tra do tabeli SAMN
::----------------------------------------------------------------------------------------------------------------------
FIRMA.cntx_psh();
_kod:={? REF.WFIRM || REF.FIRMA().SYMBOL || '' ?};
FIRMA.cntx_pop();

_fname:='samn'+_kod+'.tra';
_gname:='samnk'+_kod+'.tra';
_sep:=';';

_samnk:=tab_tmp(1,'SAMN','STRING[16]','','SAMK','STRING[16]','');
{? fexists(_gname,1)
|| _gile:=fopen(_gname,'r',1,1,1);
   {? _gile.is_open
   || {! |? (_war:=fread(_gile))<>'\n'
      |! _wsk:=_war*_sep; _buf:=(_wsk-1)+_war; _war:=_wsk-_war;
         _samn:=_buf;
         _wsk:=_war*_sep; _buf:=(_wsk-1)+_war; _war:=_wsk-_war;
         _samk:=_buf;
         _samnk.blank();
         _samnk.SAMN:=_samn;
         _samnk.SAMK:=_samk;
         _samnk.add(1)
      !};
      fclose(_gile)
   ?}
?};
{? fexists(_fname,1) & var_pres('__sampoj')>1
|| _file:=fopen(_fname,'r',1,1,1);
   {? _file.is_open
   || {! |? (_war:=fread(_file))<>'\n'
      |! _ii:=0;
         SAMN.prefix();
         SAMN.blank();
         {!
         |? _ii+=1;
            _wsk:=_war*_sep; _buf:=(_wsk-1)+_war; _war:=_wsk-_war;
            SAMN[_ii+1]:={? _ii=1  || exec('FindAndGet','#table',SAM,_buf,,,null())
                         |? _ii=2  || exec('str2date','#convert',_buf)
                         |? _ii=3
                          | _ii=17 || {? _buf='' || null() || exec('FindAndGet','#table',SPAL,_buf,,,null()) ?}
                         |? _ii=11
                          | _ii=15
                          | _ii=25
                          | _ii=29
                          | _ii=33 || _buf
                         || #_buf
                         ?};
            {? _ii=1
            || __sampoj.clear();
               __sampoj.prefix(_buf,);
               {? __sampoj.first() & __sampoj.POJ<>''
               || SAMN[1]:=exec('FindAndGet','#table',POJAZDY,__sampoj.POJ,,,null())
               ?}
            ?};
            _ii<33
         !};
         _wsk:=_war*_sep; _buf:=(_wsk-1)+_war; _war:=_wsk-_war;
         {? SAMN.add(1) & _buf<>'' & (_samnk.clear(); _samnk.prefix(_buf,); _samnk.first())
         || {!
            |? {? _samnk.SAMK<>''
               || SAMK.prefix();
                  {? SAMK.seek(_samnk.SAMK)
                  || SAMK.SAMN:=SAMN.ref();
                     SAMK.put(1)
                  ?}
               ?};
               _samnk.next()
            !}
         ?}
      !};
      fclose(_file)
   ?}
?};
obj_del(_samnk);
~~


\N
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Aktualizacja kartoteki nieobecności.
::   WE:
::   WY: zgodna ze specyfikacją (patrz nagłówek pliku)
::  OLD: \N/upgrade_1922.fml
::  OLD: \N/upgrade_2114.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_rep:=exec('createReport','#upgrade');
_n_refs:=tab_tmp(1,'REF','INTEGER',);
N.trig_off('*','*');
N.cntx_psh();
N.index('NIEOPRAC');
N.prefix();
_rep.setAll(N.size());
_loop:=N.first();
{!
|? _loop
|! _rep.incCnt();
   {? N.UW=''
   || N.UW:='D';
      {? N.put()
      || _rep.incMod()
      || _rep.incErr();
         _ret:=-1
      ?}
   || _rep.incUch()
   ?};
   {? N.KOR='T' & N.RODZAJ='R'
   || _n_kor:=#N.ref();
      N.cntx_psh();
      N.index('NIEOTREE');
      N.prefix(_n_kor);
      {? ~N.first()
      || _n_refs.REF:=_n_kor;
         _n_refs.add()
      ?};
      N.cntx_pop()
   ?};
   _loop:=N.next()
!};
N.cntx_pop();
N.cntx_psh();
{? _n_refs.first()
|| {!
   |? N.clear();
      {? N.seek(_n_refs.REF)
      || N.KOR:='N';
         N.RODZAJ:='Z';
         N.put()
      ?};
      _n_refs.next()
   !}
?};
N.cntx_pop();
N.trig_on('*','*');

: raport wykonania
_rep.write();
&_n_refs;
_ret


\N_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Aktualizacja kartoteki nieobecności - opis.
::   WE:
::   WY: zgodna ze specyfikacją (patrz nagłówek pliku)
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki nieobecności'


\OS_N
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Aktualizacja informacji o nieobecnościach zleceniobiorców.
::   WE:
::   WY: zgodna ze specyfikacją (patrz nagłówek pliku)
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_rep:=exec('createReport','#upgrade');

OS_N.trig_off('*','*');
OS_N.cntx_psh();
OS_N.index('OND');
OS_N.prefix();
_rep.setAll(OS_N.size());
_loop:=OS_N.first();
{!
|? _loop
|! _rep.incCnt();
   {? OS_N.UW=''
   || OS_N.UW:='D';
      {? OS_N.put()
      || _rep.incMod()
      || _rep.incErr();
         _ret:=-1
      ?}
   || _rep.incUch()
   ?};
   _loop:=OS_N.next()
!};
OS_N.cntx_pop();
OS_N.trig_on('*','*');

: raport wykonania
_rep.write();

_ret


\OS_N_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Aktualizacja informacji o nieobecnościach zleceniobiorców - opis.
::   WE:
::   WY: zgodna ze specyfikacją (patrz nagłówek pliku)
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja informacji o nieobecnościach zleceniobiorców'


\zws_par_pstn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Formuła dodaje czynność ZWS_PAR_PSTN do odpowiednich ról.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'PKD_EZK_RPZA','PKD_ZAT_PZAT','PKD_DOS_PRSW','PKD_ZAT_ZRDZ','PKD_ZAT_ZRDK','PKD_ZAT_ZRPT','PKD_ZAT_ZRCP'";
_uid:='ZWS_PAR_PSTN';

_ROLE:=sql(
   'select distinct B_ROLE.NAME '
   'from B_ACTROL join B_ACTION using (B_ACTROL.B_ACTION,B_ACTION.REFERENCE) '
                 'join B_ROLE using (B_ACTROL.B_ROLE,B_ROLE.REFERENCE) '
   'where B_ACTROL.FIRMA=:_a and B_ACTION.UID in (:_b) '
   'order by 1',
   exec('ref_firma','ustawienia'),_cr
);

{? type_of(_ROLE)=type_of(~~)
|| __UPG.msg('Przygotowanie listy ról, do których należy dodać czynność %1 nie powiodło się.' [_uid]);
   return(0)
?};

{? _ROLE.first()
|| _ret:=1;
   {!
   |? {? exec('add_actrol_one','#b_role',_ROLE.NAME,_uid)
      || __UPG.msg('Czynność przypisano do roli "%1".' [_ROLE.NAME])
      || __UPG.msg('Przypisanie czynności do roli "%1" nie powiodło się.' [_ROLE.NAME]);
         _ret:=-1
      ?};
      _ROLE.next()
   !};
   _ret
|| __UPG.msg('W systemie brak ról, z uprawnieniami do czynności referencyjnych.');
   1
?}


\zws_par_pstn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Formuła dodaje czynność ZWS_PAR_PSTN do odpowiednich ról - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_PSTN - Lista wszystkich stanowisk" do odpowiednich ról'


\zmiana_R
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [19.22]
:: OPIS: Zmiana położenia rubryki płacowej 970 "Podst. skł. zas.mac."
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');
__RUB.fill();
_rub:=__RUB.sys_rub(56111);
R.cntx_psh();
R.index('RUBKOD');
R.prefix();
{? _rub.first() & R.find_key(_rub.RN)
|| _lp:=R.LP
|| _lp:=-1
?};
_rub_m:=__RUB.sys_rub(56121);
{? _lp>0 & _rub_m.first() & R.find_key(_rub_m.RN)
|| R.LP:=_lp;
   {? R.put()
   || __UPG.msg('Zmieniono kolejność obliczania składnika płacowego nr 970 "Podst. skł. zas.mac."')
   ?}
::   Zamiast tutaj naprawiać kolejność, naprawimy poniżej już dla wszystkich rubryk
::   exec('lp_put_a','!zws_par_pskl')
?};
:: Naprawiamy kolejność wszystkich rubryk, dlatego ustawiamy się na ostatniej rubryce i wywołujemy aktualizację lp
R.index('RUBLP');
R.prefix();
{? R.last()
|| exec('lp_put_a','!zws_par_pskl')
?};
R.cntx_pop();
_lp


\zmiana_R_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [19.22]
:: OPIS: Zmiana położenia rubryki płacowej 970 "Podst. skł. zas.mac." - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiana w kolejności obliczeń składnika płacowego nr 970  "Podst. skł. zas.mac." '


\KAL_OPIS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Uzupełnia pola w tabeli KAL_OPIS
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   _put:=0;
   {? KAL_OPIS.STATUS='' || KAL_OPIS.STATUS:='A'; _put:=1 ?};
   {? _put || KAL_OPIS.put() ?}
";
_result:=KAL_OPIS.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w tabeli wzorców kalendarzy.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w tabeli wzorców kalendarzy.')
?};
_result


\KAL_OPIS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Uzupełnia pola w tabeli KAL_OPIS - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli wzorców kalendarzy.'


\SZK_WZO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Aktualizacja wzorców dla szkoleń
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

SZK_WZO.cntx_psh();
SZK_WZO.prefix();
SZK_WZO.for_each("
   __lrec+=1;
   {? SZK_WZO.TRYB=''
   || SZK_WZO.TRYB:='T';
      {? SZK_WZO.put() || __lmod+=1 ?}
   ?}
");
SZK_WZO.cntx_pop();

__UPG.msg('Zaktualizowano pola w tabeli wzorców dla szkoleń.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\SZK_WZO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Aktualizacja wzorców dla szkoleń - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wzorców dla szkoleń (uzupełnienie pola określającego sposób liczenia punktów na egzaminie po szkoleniu'


\ZA_INST
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
::  MOD: ARTSLO [19.22]
:: OPIS: Aktualizacja statusów dla badań opinii
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
:: najpierw upewnijmy się, że jeśli słownik dla badań istnieje ma poprawny kod
SLO_KOD.cntx_psh();
SLO_KOD.index('NAZWA');
SLO_KOD.prefix();
{? (_typ:=exec('slo_typ','ext_slo','ZA_INSTS'))<>null
|| {? SLO_KOD.find_key(_typ,'aktywne') & SLO_KOD.KOD<>'A'
   || SLO_KOD.KOD:='A';
      SLO_KOD.put()
   ?};
   {? SLO_KOD.find_key(_typ,'zakończone') & SLO_KOD.KOD<>'Z'
   || SLO_KOD.KOD:='Z';
      SLO_KOD.put()
   ?}
?};
:: sprawdźmy bo może wogóle nie ma i trzeba założyć
SLO_KOD.index('KOD');
SLO_KOD.prefix();
{? _typ<>null & ~(SLO_KOD.find_key(_typ,'A'))
|| SLO_KOD.blank(1);
   SLO_KOD.SLO_TYP:=_typ;
   SLO_KOD.KOD:='A';
   SLO_KOD.NAZWA:='aktywne';
   SLO_KOD.SYSTEM:='N';
   SLO_KOD.add()
?};
SLO_KOD.cntx_pop();

_przed:=TRIG_OFF.ZA_INST;
TRIG_OFF.ZA_INST:='CHK|PUTB|PUTA';
ZA_INST.cntx_psh();
ZA_INST.prefix();
ZA_INST.for_each("
   __lrec+=1;
   {? ZA_INST.STATUS=null()
   || ZA_INST.STATUS:=exec('kod','ext_slo','ZA_INSTS','A');
      {? ZA_INST.put() || __lmod+=1 ?}
   ?}
");
ZA_INST.cntx_pop();
TRIG_OFF.ZA_INST:=_przed;

__UPG.msg('Zaktualizowano pola w tabeli Jednorazowe badanie opinii.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\ZA_INST_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Aktualizacja statusów dla badań opinii - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja statusów dla badań opinii (uzupełnienie niewymaganych w Xpertisie pól ze statusem badania)'


\jpk_url
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Aktualizacja lokalizacji schematów JPK
::----------------------------------------------------------------------------------------------------------------------
exec('jpk_url1','upgrade_1902','FA','JPK_FA(1)_v1-0',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_FA%281%29_v1-0.xsd/c048cae9-264e-512f-51fe-bcea7fdf9de0'
);
exec('jpk_url1','upgrade_1902','KR','JPK_KR(1)_v1-0',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_KR%281%29_v1-0.xsd/30ee4a20-f2f5-ba7d-6c90-8500facddd60'
);
exec('jpk_url1','upgrade_1902','MAG','JPK_MAG(1)_v1-0',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_MAG%281%29_v1-0.xsd/425b566f-23a7-6b37-876f-6e9592200f1d'
);
exec('jpk_url1','upgrade_1902','VAT','JPK_VAT(3)_v1-1',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_VAT%283%29_v1-1.xsd/ab0741d5-fa6d-9596-b089-6778ea5df160'
);
exec('jpk_url1','upgrade_1902','WB','JPK_WB(1)_v1-0',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_WB%281%29_v1-0.xsd/194ae091-1f50-9f46-cd56-ce5208653ba2'
);
__UPG.msg('Zaktualizowano lokalizacje schematów JPK.');
1


\jpk_url_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Aktualizacja lokalizacji schematów JPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja lokalizacji schematów JPK'


\jpk_url1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Zmiana lokalizacji pliku JPL
::   WE: _a - rodzaj pliku
::       _b - nazwa pliku
::       _c - nowa lokalizacja
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J',_a,_b,);
{? ISTDEF.first()
|| ISTDEF.XMLNS:=_c;
   ISTDEF.put()
?};
ISTDEF.cntx_pop()


\fix_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Naprawa definicji sprawozdań na potrzeby e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
_zmiany:=0;
_sql:=sql('select REFERENCE as REF from GR_SIK where E_KOD<>\'\'');
{? _sql.first()
|| _wyn:=obj_new('add','FIRMA','SYSTEM','GR_SIK','DEFW','GR_KOL');
   _wyn.add:="
      _txt:='';
      _txt+='firma: '+.FIRMA;
      _txt+=', sprawozdanie: '+.GR_SIK;
      {? .DEFW<>'' || _txt+=', wiersz: '+.DEFW ?};
      {? .GR_KOL<>'' || _txt+=', kolumna: '+.GR_KOL ?};
      _txt+=' - '+_a;
      __UPG.msg(_txt)
   ";
   GR_SIK.cntx_psh();
   GR_SIK.prefix();
   DEFW.cntx_psh();
   GR_KOL.cntx_psh();
   GR_KOL.index('LP');
   DEFA.cntx_psh();
   DEFA.index('LP');
   {!
   |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
      || _gr_sik:=GR_SIK.ref();
         _wyn.FIRMA:=GR_SIK.FIRMA().SYMBOL;
         _wyn.SYSTEM:=GR_SIK.SYSTEM;
         _wyn.GR_SIK:=GR_SIK.SKROT;
         _zmiana:=0;
         {? GR_SIK.E_KOD='PrzeplywyPosr'
         || DEFW.index('E_KOD');
            DEFW.prefix(GR_SIK.ref(),'A_III',);
            {? DEFW.first()
            || DEFA.prefix(DEFW.ref());
               {? DEFA.first()
               || {!
                  |? {? DEFA.ARGUMENT().E_KOD='A_II' & DEFA.WSP=-1
                     || _wyn.GR_KOL:=$DEFA.KOL().LP;
                        _wyn.DEFW:=DEFA.WYR().KOD;
                        DEFA.WSP:=1; DEFA.put(1);
                        _wyn.add('Zmieniono współczynnik w algorytmie numer %1.'[$DEFA.LP]);
                        _zmiana:=1
                     ?};
                     DEFA.next()
                  !}
               ?}
            ?}
         |? GR_SIK.E_KOD='RZiSPor'
         || DEFW.index('E_KOD');
            DEFW.prefix(GR_SIK.ref());
            {? DEFW.find_key('F_III_1',)
            || _wyn.GR_KOL:='';
               {? DEFW.find_key('G_IV')
               || _wyn.DEFW:=DEFW.KOD;
                  {? DEFW.KOD='G_IV' || DEFW.KOD:='F_IV' ?};
                  DEFW.E_KOD:='F_IV';
                  DEFW.put(1);
                  _wyn.add('Zmieniono e-Kod %1 wiersza na %2.'[{? _wyn.DEFW<>DEFW.KOD || 'oraz kod' || '' ?},DEFW.E_KOD]);
                  _zmiana:=1
               ?};
               {? DEFW.find_key('G_II_J')
               || _wyn.DEFW:=DEFW.KOD;
                  {? DEFW.KOD='G_II_J' || DEFW.KOD:='G_II_1' ?};
                  DEFW.E_KOD:='G_II_1';
                  DEFW.put(1);
                  _wyn.add('Zmieniono e-Kod %1 wiersza na %2.'[{? _wyn.DEFW<>DEFW.KOD || 'oraz kod' || '' ?},DEFW.E_KOD]);
                  _zmiana:=1
               ?}
            ?};
            {? DEFW.find_key('B_V',) & DEFW.ALGORYTM<>'F'
            || _wyn.GR_KOL:='';
               _wyn.DEFW:=DEFW.KOD;
               _ref:=DEFW.ref();
               DEFA.prefix(DEFW.ref());
               {? DEFA.first()
               || {!
                  |? {? DEFA.ARGUMENT().E_KOD='B_V_1'
                     || DEFA.del()
                     || DEFA.next()
                     ?}
                  !}
               ?};
               {? ~DEFA.first() & DEFW.seek(_ref)
               || DEFW.ALGORYTM:='F';
                  DEFW.put(1);
                  _wyn.add('Zmieniono typ algorytmu wiersza z F na S.');
                  _zmiana:=1
               ?}
            ?};
            {? DEFW.find_key('C',) & DEFW.ALGORYTM='S'
            || _wyn.DEFW:=DEFW.KOD;
               DEFA.prefix(DEFW.ref());
               {? DEFA.first()
               || {!
                  |? {? DEFA.ARGUMENT().E_KOD='B' & DEFA.WSP=1
                     || _wyn.GR_KOL:=$DEFA.KOL().LP;
                        DEFA.WSP:=-1;
                        DEFA.put(1);
                        _wyn.add('Zmieniono współczynnik algorytmu.');
                        _zmiana:=1
                     ?};
                     DEFA.next()
                  !}
               ?}
            ?}
         |? 18+GR_SIK.E_KOD='ZestZmianWKapitale'
         || DEFW.index('ALGORYTM');
            DEFW.prefix(GR_SIK.ref(),'S');
            {? DEFW.first()
            || {!
               |? _wyn.DEFW:=DEFW.KOD;
                  DEFW.cntx_psh();
                  DEFA.prefix(DEFW.ref());
                  {? DEFA.first()
                  || {!
                     |? _wyr:=DEFA.WYR;
                        _kol:=DEFA.KOL;
                        _wyn.GR_KOL:=$DEFA.KOL().LP;
                        {? 6+DEFA.ARGUMENT().GRUPA().E_KOD='Bilans'
                        || {? DEFA.WYR().NAZWA*'na początek okresu' & DEFA.KOL().E_KOD='KwotaA' & DEFA.ARG_KOL().E_KOD='KwotaA'
                           || GR_KOL.cntx_psh();
                              GR_KOL.index('E_KOD');
                              GR_KOL.prefix(DEFA.ARGUMENT().GRUPA,'KwotaB',);
                              {? GR_KOL.first()
                              || _zmiana:=1;
                                 _wyn.add('Zmieniono kolumnę algorytmu.');
                                 DEFA.ARG_KOL:=GR_KOL.ref();
                                 DEFA.put(1)
                              ?};
                              GR_KOL.cntx_pop();
                              DEFA.next()
                           |? DEFA.KOL().E_KOD='KwotaB1'
                           || _zmiana:=1;
                              _wyn.add('Usunieto algorytm.');
                              _del:=DEFA.del();
                              DEFA.cntx_psh();
                              DEFA.index('LP');
                              DEFA.prefix(_wyr,_kol);
                              {? DEFA.first()
                              || _lp:=0;
                                 {!
                                 |? DEFA.LP:=(_lp+=1);
                                    DEFA.put(1);
                                    DEFA.next()
                                 !}
                              ?};
                              DEFA.cntx_pop();
                              _del
                           ?}
                        || DEFA.next()
                        ?}
                     !}
                  ?};
                  DEFW.cntx_pop();
                  DEFW.next()
               !}
            ?}
         ?};
         {? _zmiana & GR_SIK.AKC<>'N'
         || _wyn.DEFW:=_wyn.GR_KOL:='';
            _wyn.add('Wycofano akceptację sprawozdania.');
            GR_SIK.AKC:='N';
            GR_SIK.put(1)
         ?};
         GR_KOL.prefix(_gr_sik);
         {? GR_KOL.first()
         || _wyn.DEFW:='';
            {!
            |? _wyn.GR_KOL:=$GR_KOL.LP;
               _zm:=0;
               _z:=GR_KOL.NAZWA;
               {? 5+GR_KOL.E_KOD='Kwota' & GR_KOL.E_KOD=GR_KOL.NAZWA
               || {? 10+GR_SIK.E_KOD='Informacja'
                  || {? GR_KOL.E_KOD='KwotaA'
                     || GR_KOL.NAZWA:='Wartość łączna';
                        GR_KOL.put(1);
                        _zm:=1
                     |? GR_KOL.E_KOD='KwotaB'
                     || GR_KOL.NAZWA:='Z zysków kapitałowych';
                        GR_KOL.put(1);
                        _zm:=1
                     |? GR_KOL.E_KOD='KwotaC'
                     || GR_KOL.NAZWA:='Z innych źródeł przychodów';
                        GR_KOL.put(1);
                        _zm:=1
                     ?}
                  || {? GR_KOL.E_KOD='KwotaA'
                     || GR_KOL.NAZWA:='Kwota na dzień kończący bieżący rok obrotowy';
                        GR_KOL.put(1);
                        _zm:=1
                     |? GR_KOL.E_KOD='KwotaB'
                     || GR_KOL.NAZWA:='Kwota na dzień kończący poprzedni rok obrotowy';
                        GR_KOL.put(1);
                        _zm:=1
                     |? GR_KOL.E_KOD='KwotaB1'
                     || GR_KOL.NAZWA:='Przekształcone dane porównawcze za poprzedni rok obrotowy';
                        GR_KOL.put(1);
                        _zm:=1
                     ?}
                  ?}
               ?};
               {? _zm
               || _wyn.add('Zmieniono nazwę kolumy z: %1 na: %2.'[_z,GR_KOL.NAZWA]);
                  _zmiana:=1
               ?};
               GR_KOL.next()
            !}
         ?};
         _zmiany+=_zmiana
      ?};
      _sql.next()
   !};
   DEFA.cntx_pop();
   GR_KOL.cntx_pop();
   DEFW.cntx_pop();
   GR_SIK.cntx_pop()
?};
{? _zmiany=0
|| __UPG.msg('Definicje sprawozdań są poprawne.')
?};
_set:=exec('create_set','#array','INTEGER');
_zmiany:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF','JednostkaMalaW');
{? ISTDEF.first()
|| ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS');
   {!
   |? _zmiana:=0;
      ISTDEFS.prefix(ISTDEF.ref(),'jma:G_IV',);
      {? ISTDEFS.first()
      || ISTDEFS.cntx_psh();
         ISTDEFS.prefix();
         ISTDEFS.OPIS:='jma:F_IV'; ISTDEFS.put(1);
         _zmiana:=1;
         ISTDEFS.cntx_pop()
      ?};
      ISTDEFS.prefix(ISTDEF.ref(),'jma:G_II_J',);
      {? ISTDEFS.first()
      || ISTDEFS.cntx_psh();
         ISTDEFS.prefix();
         ISTDEFS.OPIS:='jma:G_II_1'; ISTDEFS.put(1);
         _zmiana:=1;
         ISTDEFS.cntx_pop()
      ?};
      {? _zmiana & _set.addIfNotExists(ISTDEF.ref())
      || __UPG.msg('Zmodyfikowano schemat: %1.'[ISTDEF.VER]);
         _zmiany:=1
      ?};
      ISTDEF.next()
   !};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
&_sql;
_sql:=sql(
   'select ISTDEFS.REFERENCE as REF '+
   'from ISTDEFS join ISTDEF '+
   'where ISTDEF.RODZAJ=\'SF\' and ISTDEFS.TYPFLD=\'K\' and ISTDEFS.OPIS like \'%KwotaA\''
);
ISTDEFS.cntx_psh();
ISTDEFS.prefix();
{? _sql.first()
|| {!
   |? {? ISTDEFS.seek(BIT.sqlint(_sql.REF),)
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEFS.ISTDEF,ISTDEFS.TREE,);
         {? ISTDEFS.next() & ISTDEFS.TYPFLD='' & ISTDEFS.OPIS+6='KwotaB'
         || ISTDEFS.TYPFLD:='K';
            ISTDEFS.put(1);
            {? _set.addIfNotExists(ISTDEFS.ISTDEF)
            || __UPG.msg('Zmodyfikowano schemat: %1.'[ISTDEFS.ISTDEF().VER])
            ?};
            _zmiany:=1
         ?};
         ISTDEFS.cntx_pop()
      ?};
      _sql.next()
   !}
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF',);
ISTDEFS.cntx_psh();
ISTDEFS.index('OPIS');
{? ISTDEF.first()
|| {!
   |? ISTDEFS.prefix(ISTDEF.ref(),'tns:P_5B',);
      {? ISTDEFS.first() & ISTDEFS.REGULY=$"'false'"
      || ISTDEFS.REGULY:=$"'true'";
         ISTDEFS.put(1);
         {? _set.addIfNotExists(ISTDEFS.ISTDEF)
         || __UPG.msg('Zmodyfikowano schemat: %1.'[ISTDEFS.ISTDEF().VER])
         ?};
         _zmiany:=1
      ?};
      ISTDEF.next()
   !}
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
{? _zmiany=0
|| __UPG.msg('Schematy e-Sprawozdań są poprawne.')
?};
1


\fix_espr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Naprawa definicji sprawozdań na potrzeby e-Sprawozdań - opis
::----------------------------------------------------------------------------------------------------------------------
'Naprawa definicji używanych w e-Sprawozdaniach (sprawozdania i schematy)'


\upd_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Aktualizacja schematów e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
_zmiany:=0;
VAR_DEL.delete('LastLp');
LastLp:="
   _lp:=ISTDEFS.LP;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
   {? ISTDEFS.last()
   || _lp:=LastLp()
   ?};
   ISTDEFS.cntx_pop();
   _lp
";
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF',);
ISTDEFS.cntx_psh();
{? ISTDEF.first()
|| {!
   |? ISTDEFS.index('TYPPOLA'); ISTDEFS.prefix(ISTDEF.ref(),'SP');
      {? ISTDEFS.first()
      || {!
         |? ISTDEFS.cntx_psh();
            ISTDEFS.index('DRZEWO');
            ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
            {? ISTDEFS.first()
            || {!
               |? _zmiana:=0;
                  ISTDEFS.cntx_psh();
                  ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
                  {? ISTDEFS.first()
                  || {!
                     |? {? ISTDEFS.OPIS='dtsf:Kwota'
                        || ISTDEFS.cntx_psh();
                           ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
                           {? ISTDEFS.last() & ISTDEFS.OPIS='dtsf:RP'
                           || ISTDEFS.REGULY:=$"exec('okres','jpk_sf','P'); exec('podat_rp','jpk_sf')";
                              ISTDEFS.put();
                              _zmiana:=1
                           ?};
                           ISTDEFS.cntx_pop()
                        |? (ISTDEFS.OPIS='dtsf:PozycjaUzytkownika' | ISTDEFS.OPIS='dtsf:Pozostale') & ISTDEFS.REGULY='0'
                        || {? ISTDEFS.OPIS='dtsf:PozycjaUzytkownika'
                           || ISTDEFS.FORMULA:=$"exec('esprp_first','jpk_sf',_a,'N')";
                              ISTDEFS.FORM_XML:=$"exec('esprp_next','jpk_sf')";
                              ISTDEFS.PARSER:=$"exec('p_esprp','jpk_sf',_a)"
                           || ISTDEFS.FORMULA:=$"exec('esprp_first','jpk_sf',_a,'T')";
                              ISTDEFS.FORM_XML:=$"exec('esprp_next','jpk_sf')";
                              ISTDEFS.PARSER:=$"exec('p_esprp','jpk_sf',_a,'Pozostałe')"
                           ?};
                           ISTDEFS.REGULY:='';
                           ISTDEFS.put();
                           _zmiana:=1;
                           _lp:=LastLp();
                           _kw:=-3;
                           ISTDEFS.cntx_psh();
                           ISTDEFS.index('LP'); ISTDEFS.prefix(ISTDEF.ref());
                           {? ISTDEFS.next()
                           || {!
                              |? {? ISTDEFS.OPIS='dtsf:NazwaPozycji'
                                 || ISTDEFS.REGULY:=$"exec('esprp_nazwa','jpk_sf')";
                                    ISTDEFS.PARSER:=$"exec('p_esprp_naz','jpk_sf',_a,_b)";
                                    ISTDEFS.put()
                                 |? ISTDEFS.OPIS='dtsf:Kwota'
                                 || ISTDEFS.REGULY:='';
                                    ISTDEFS.PARSER:='';
                                    ISTDEFS.put()
                                 |? ISTDEFS.OPIS='dtsf:KwotaA' | ISTDEFS.OPIS='dtsf:KwotaB' | ISTDEFS.OPIS='dtsf:KwotaC'
                                 || {? ISTDEFS.OPIS='dtsf:KwotaA' || _kw+=3 ?};
                                    _nr:=(%(ISTDEFS.OPIS+1)-64)+_kw;
                                    ISTDEFS.REGULY:=$"exec('esprp_value','jpk_sf'"+{? _kw || ',\'P\'' || '' ?}+')';
                                    ISTDEFS.PARSER:=$"exec('p_v_pod','jpk_sf',_a,_b,"+$_nr+')';
                                    ISTDEFS.put()
                                 |? ISTDEFS.OPIS='dtsf:Art' | ISTDEFS.OPIS='dtsf:Ust' | ISTDEFS.OPIS='dtsf:Pkt' | ISTDEFS.OPIS='dtsf:Lit'
                                 || ISTDEFS.REGULY:='E_SPR_P.'+(~-(ISTDEFS.OPIS+3));
                                    ISTDEFS.PARSER:=$"exec('p_esprp_pp','jpk_sf',_a,_b)";
                                    ISTDEFS.put()
                                 |? ISTDEFS.OPIS='dtsf:Podpozycja'
                                 || ISTDEFS.REGULY:=$"exec('esprp_pod','jpk_sf')";
                                    ISTDEFS.PARSER:=$"exec('p_esprp_pod','jpk_sf',_a,_b)";
                                    ISTDEFS.put()
                                 |? ISTDEFS.OPIS='dtsf:RP'
                                 || ISTDEFS.REGULY:=$"exec('podat_rp','jpk_sf')";
                                    ISTDEFS.put()
                                 ?};
                                 ISTDEFS.LP<_lp & ISTDEFS.next()
                              !}
                           ?};
                           ISTDEFS.cntx_pop()
                        ?};
                        ISTDEFS.next()
                     !}
                  ?};
                  ISTDEFS.cntx_pop();
                  ISTDEFS.next()
               !}
            ?};
            ISTDEFS.cntx_pop();
            ISTDEFS.next()
         !}
      ?};
      {? _zmiana
      || _zmiany:=1;
         __UPG.msg('Zmodyfikowano schemat: %1.'[ISTDEF.VER])
      ?};
      ISTDEF.next()
   !}
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
VAR_DEL.delete('LastLp');
{? _zmiany=0
|| __UPG.msg('Schematy e-Sprawozdań są poprawne. Nie wymagały zmian.'@)
?};
1


\upd_espr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Aktualizacja schematów e-Sprawozdań - opis
::  OLD: \upd_espr_desc/upgrade_1902.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematów e-Sprawozdań (uszczegółowienia informacji podatkowych)'


\fix_xsd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Zmiana nazw plików xsd używanych do walidacji e-deklaracji i plików JPK
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Zmiana');
Zmiana:=0;
_rename:="
   _from:=_a;
   _to:=19+(_from-45)+(_from+45);
   {? _from<>_to & fexists(_from,1)
   || {? fcopy(_from,_to,1,1,1)
      || ferase(_from,1);
         __UPG.msg('Zmieniono nazwę pliku z: %1 na: %2.'@[_from,_to]);
         Zmiana:=1
      ?}
   ?}
";
_rename('v_CIT-8_O(10)_Z_v1-0E_12958e729da3e860f55814dd8091fc965c6c6ceb.xsd');
_rename('v_CIT-8_O(11)_Z_v1-0E_330e8ded1be58c5c20427762701cc665498a3e98.xsd');
_rename('v_CIT-8_O(12)_Z_v1-0E_e041acc049570ff9261dd2ab1f4d4219caf53520.xsd');
_rename('v_CIT-8_O(13)_Z_v1-0E_6b744bb26029548b753013e529e9fca80fbe37d2.xsd');
_rename('v_CIT-8_O(14)_Z_v1-0E_d553db10723c5aa8e6799824490df702cad8cb46.xsd');
_rename('v_CIT-8_O(8)_Z_v3-0E_d478c212c39350be537bcbf608da60faa7dff960.xsd');
_rename('v_CIT-8_O(9)_Z_v1-0E_fb0c95ed09459a4d58254dea60e70219fbd7bdb5.xsd');
_rename('v_CIT-ST_A(5)_Z_v2-0E_6ffb8e245247e8a2fcde5bfaf9e290a217135298.xsd');
_rename('v_CIT-ST_A(5)_Z_v4-0E_f0fd287f74e98038d7ee6bbdd62fa92c9d24c7f1.xsd');
_rename('v_CIT_8SP(1)_Z_v1-0E_65b627b36aa045c3f0ec4eab83c83ab8f22d181f.xsd');
_rename('v_CIT_8SP(2)_Z_v1-0E_1991340f597ef868dab715e01f8c660e69abb1c0.xsd');
_rename('v_CIT_MIT(1)_Z_v1-0E_eca4b9cebc931512c761b6933f80e052329d6571.xsd');
_rename('v_CIT_MIT(2)_Z_v1-0E_9a21947a8ee0193b35f33bd916da9a8b77b183b0.xsd');
_rename('v_ElementarneTypyDanych_v3-0E_74b9fd4efa3b9293af9a6a8c544fd90162bec623.xsd');
_rename('v_ElementarneTypyDanych_v4-0E_6d47c45907f05c92e4aba075087fd4fdd7195fa0.xsd');
_rename('v_ElementarneTypyDanych_v5-0E_64d5a1d2df51484a29940936ac06fe002932078f.xsd');
_rename('v_JednostkaInnaStrukturyDanychSprFin_v1-0_d1ffa52f3a0773f43d34e086e04b43c9a3bc1cfa.xsd');
_rename('v_JednostkaInnaWTysiacach(1)_v1-0_89d7d7538627ca325b8baf9d539297b86c6f61fd.xsd');
_rename('v_JednostkaInnaWZlotych(1)_v1-0_cade3ffc85e7103aac89af7a64d6f879106847a2.xsd');
_rename('v_JednostkaMalaStrukturyDanychSprFin_v1-0_278b07d7ac8b99677e9dee0426d0d8a8beacb57e.xsd');
_rename('v_JednostkaMalaWTysiacach(1)_v1-0_55ef15b865d15bd126e22eb4ef7090338d4b8cab.xsd');
_rename('v_JednostkaMalaWZlotych(1)_v1-0_52310d7e6560d34d8d5dec54695732ee007be3e3.xsd');
_rename('v_KodyCechKrajow_v3-0E_097abda8d7b68fe8e78965817b129a9c50b9e471.xsd');
_rename('v_KodyKrajowUE_v2-0E_2bd412d9a0bac66bf362338e107824159a3985ea.xsd');
_rename('v_KodyUrzedowSkarbowych_v3-0E_b2a09044e323e2a27b017a82c155f0d555303b81.xsd');
_rename('v_KodyUrzedowSkarbowych_v4-0E_eacdd80247933d94db150c27d85f67c5345b4816.xsd');
_rename('v_KodyUrzedowSkarbowych_v5-0E_3fcbbbc88d14f9c53d4363cdf54096fa3e646d53.xsd');
_rename('v_SSE-R_A(2)_Z_v2-0E_738dd6a2ab7e9fd7649de3d8e6db2c42bd10b215.xsd');
_rename('v_SSE-R_A(3)_Z_v2-0E_235c282cb53de948e577be811ec3b91452392db5.xsd');
_rename('v_SSE-R_A(3)_Z_v3-0E_96a99d82f9a8e3af33d9456d12763fac612a3f7b.xsd');
_rename('v_StrukturyDanychSprFin_v1-0_85032dda228324d24bca930ab8bdda784c7d7c21.xsd');
_rename('v_StrukturyDanych_v3-0E_11b1ae2ae7de1f9cf19a3ac8726b35145704f1bc.xsd');
_rename('v_StrukturyDanych_v4-0E_29db0c7898fdeb5ddb3bb4b3e19d07ff3daf2705.xsd');
_rename('v_StrukturyDanych_v5-0E_4616ef3e134d599988a1bee7f43a43ef4482427a.xsd');
{? Zmiana=0
|| __UPG.msg('Zmiana nazw plików *.xsd nie była wymagana.'@)
?};
VAR_DEL.delete('Zmiana');
1


\fix_xsd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Zmiana nazw plików xsd używanych do walidacji e-deklaracji i plików JPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Zmiana nazw plików xsd używanych do walidacji e-deklaracji i plików JPK'


\espr_o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Uaktualnienie zawartości pola E_SPR_O.FIRMA
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Dane');
Dane:=obj_new('ILE','UPD');
Dane.ILE:=0;
Dane.UPD:=0;
E_SPR_O.cntx_psh();
E_SPR_O.prefix();
E_SPR.cntx_psh();
E_SPR.prefix();
E_SPR_O.for_each("
   Dane.ILE+=1;
   {? E_SPR_O.FIRMA=null
   || E_SPR_O.FIRMA:=E_SPR_O.E_SPR().FIRMA;
      {? E_SPR_O.put()
      || Dane.UPD+=1
      ?}
   ?}
");
E_SPR.cntx_pop();
E_SPR_O.cntx_pop();
{? Dane.UPD
|| __UPG.msg('Uzupełniono zawartości pola E_SPR_O.FIRMA.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$Dane.ILE,$Dane.UPD])
|| __UPG.msg('Uzupełnienie zawartości pola E_SPR_O.FIRMA było zbędne.')
?};
VAR_DEL.delete('Dane');
1


\espr_o_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Uaktualnienie zawartości pola E_SPR_O.FIRMA - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie zawartości pola E_SPR_O.FIRMA'


\fks_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Aktualizacja danych Finansowych.
::----------------------------------------------------------------------------------------------------------------------
{? exec('lic_or','#b_domain','FKS')
|| {? FUN.ask(
         'Zalecana aktualizacja danych Finansowych dotyczących sprawozdań, e-deklaracji i jpk.\n'
         '\n'
         'Jeżeli klient pracuje na standardowej wersji konfiguracji,\n'
         'to aktualizacja może być wykonana automatycznie.\n'
         'W tym celu należy wykonać następujące kroki:\n'
         '1. Uruchom w Mericie obszar "Ustawienia i parametryzacja".\n'
         '2. W zakładce "Parametryzacja" ustaw się na pozycji "Finanse".\n'
         '3. Wybierz akcję "Importuj" (nie pomyl z "Importuj wszystkie").\n'
         '4. Wybierz opcję: "Nadpisuj istniejące".\n'
         '5. Wskaż katalog merit\\transfer\\fksimp '
           '(pamiętaj aby wcześniej wkopiować do niego odpowiednie pliki - patrz nota).\n'
         '6. Zapoznaj się z ewentualnymi komunikatami.\n'
         '7. Zamknij okno z podsumowaniem importu.\n'
         '\n'
         'Jeżeli konfiguracja była u klienta modyfikowana, to aktualizację należy\n'
         'przeprowadzić na podstawie danych z folderu transfer\\fksimp\n'
         '\n'
         'Czy prace opisane powyżej zostały wykonane?'
      )
   || __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      0
   ?}
|| __UPG.msg('Aktualizacja niepotrzebna. Brak licencjonowania dziedziny FKS.');
   1
?}


\fks_import_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Aktualizacja konfiguracji RODO.
::----------------------------------------------------------------------------------------------------------------------
'Finanse i księgowość - aktualizacja konfiguracji '


\KPO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Aktualizacja kart przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_formula:="
   KPO.index('NR');
   KPO.prefix();
   KPO.for_each(\"
      __lrec+=1;
      _put:=0;
      {? KPO.WYS=null() & KPO.MP<>''
      || WYS.prefix(KPO.ODDZ,KPO.MP,);
         {? WYS.first() || KPO.WYS:=WYS.ref(); _put:=1 ?}
      ?};
      {? (KPO.R='W' | KPO.R='G') & KPO.KH_2=null() & KPO.KH<>null()
      || KPO.KH_2:=KPO.KH;
         KPO.KH:=null();
         _put:=1
      ?};
      {? KPO.TYP=''
      || {? KPO.R<>'G'
         || KPO.TYP:=exec('typ_kpo','odpady');
            _put:=1
         || KPO.TYP:=exec('typ_kpok_tra','odpady');
            _put:=1
         ?}
      ?};
      {? KPO.KORW=''
      || KPO.KORW:='K';
         _put:=1
      ?};
      {? KPO.OB=''
      || KPO.OB:='T';
         _put:=1
      ?};
      {? KPO.STATUS=''
      || {? KPO.BDO_ID=''
         || KPO.STATUS:='N'
         || KPO.STATUS:='T'
         ?};
         _put:=1
      ?};
      {? KPO.KEO_GEN=''
      || KPO.KEO_GEN:='N';
         _put:=1
      ?};
      {? KPO.KEO_REC=''
      || KPO.KEO_REC:='N';
         _put:=1
      ?};
      {? _put || {? KPO.put() || __lmod+=1 ?} ?}
   \")
";

WYS.cntx_psh();
_ndx:=WYS.ndx_tmp(,,'ODDZ',,,'NAZ',,);
WYS.index(_ndx);
KPO.cntx_psh();
exec('for_each_mask','#table',KPO,_formula);
KPO.cntx_pop();
WYS.ndx_drop(_ndx);
WYS.cntx_pop();

__UPG.msg('Zaktualizowano pola w tabel kart przekazania odpadów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\KPO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Aktualizacja kart przekazania odpadów - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola KPO.WYS w kartach przekazania odpadów'


\KPO_manual
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Zadanie ręczne do aktualizacji kart przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
_res:=-1;

_tab:=sql('
   select
      KPO.REFERENCE as KPO_REF,
      KPO.ODDZ,
      KPO.AR,
      KPO.AM,
      KPO.SYM,
      KPO.MP,
      WYS.REFERENCE as WYS_REF,
      WYS.KOD as WYS_KOD,
      WYS.NAZ as WYS_NAZ
   from @KPO left join WYS
');

{? _tab.size()=0
|| FUN.info('Brak kart odpadów, zadanie zostaje zakończone.'@);
   _res:=1

|| FUN.info(
      'Procedura służy do uzupełnienia powiązań kart odpadów ze składowiskami.\n\n'
      'W prawym oknie należy wskazać składowisko, w lewym oknie zaznaczyć karty i wykonać funkcję \'Powiąż\'.\n'
      'Po wywołaniu funkcji \'Zakończ\' dane są zapisywane a okno zostaje zamknięte.\n'
      'Uzupełnienie danych można wykonywać częściowo, wtedy nie należy kończyć zadania.'@
   );

   _wer:=_tab.mk_sel('Karty przekazania odpadów'@,'P',,'kpo_wer',,,,,'U');
   _tab.win_fld(_wer,,'ODDZ',,,-3,,,'Oddział'@);
   _tab.win_fld(_wer,,'AR',,,-4,,,'Rok'@);
   _tab.win_fld(_wer,,'AM',,,-4,,,'Miesiąc'@);
   _tab.win_fld(_wer,,'SYM',,,,,,'Symbol'@);
   _tab.win_fld(_wer,,'MP',,,50,,,'Miejsce przeznaczenia'@);
   _tab.win_fld(_wer,,'WYS_KOD',,,-3,,,'Kod składowiska'@);
   _tab.win_fld(_wer,,'WYS_NAZ',,,,,,'Nazwa składowiska'@);
   _formula:="
      _tab:=cur_tab(1,1);
      _tab.WYS_REF:=$WYS.ref();
      _tab.WYS_KOD:=WYS.KOD;
      _tab.WYS_NAZ:=WYS.NAZ;
      _tab.put();
      ~~
   ";
   _tab.win_act(_wer,,'Formuła','Powiąż'@@,,'Powiąż kartę ze składowiskiem'@,_formula,,1,1,,,'P');
   _btn:=_tab.win_btn(_wer,'text='+'Powiąż'@,'menu:P');
   _tab.btn_opt(_btn,'default=1,tooltip='+'Powiąż kartę ze składowiskiem'@);
   _formula:="
      _tab:=cur_tab(1,1);
      {? _tab.first()
      || {!
         |? WYS.prefix();
            {? WYS.seek(_tab.WYS_REF)
            || exec('FindAndGet','#table',KPO,_tab.KPO_REF,,\"@.KPO.WYS:=@.WYS.ref(); @.KPO.put()\",~~)
            ?};
            _tab.next()
         !}
      ?};
      sel_exit()
   ";
   _tab.win_act(_wer,,'Formuła','Zatwierdź'@@,,,_formula,,,,,,'Z');
   _tab.win_btn(_wer,'panel=bottom,align=end,text='+'Zatwierdź'@,'menu:Z');
   _tab.win_act(_wer,,'Kolejność');
   _tab.win_btn(_wer,'panel=bottom,align=end,text='+'Anuluj'@,'key:Esc');
   _before:="WYS.first(); grp_disp(WYS,'WER')";
   _grp:=_tab.grp_make('Uzupełnienie danych na kartach przekazania odpadów'@,_before,'kpo_grp',,,,,'normal');
   _tab.grp_sel(_grp,,_wer,,,,,,,,,,'maximized_with_title');
   _tab.grp_splt(_grp,,'vertical','wys');
   _tab.grp_sel(_grp,WYS,'WER',,,,,,,,,,'maximized_with_title');
   _tab.win_sel(_grp);
   {? _tab.select()
   || {? FUN.ask('Czy zakończyć zadanie (wszystkie dane zostały uzupełnione)?'@)
      || _res:=1
      ?}
   ?}
?};

_res


\KPO_manual_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Zadanie ręczne do aktualizacji kart przekazania odpadów - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie ręczne pola KPO.WYS w kartach przekazania odpadów.\n'
'Uzupełnienie słownika składowisk.'


\UPM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli UM
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
UPM.cntx_psh();
UPM.prefix();
_res:=UPM.for_each("__lrec+=1;
                   {? UPM.BIE=''
                   || UPM.BIE:='T';
                      {? UPM.put(1) || __lmod+=1 ?}
                   ?}
                 ");
UPM.cntx_pop();
__UPG.msg('Zaktualizowano umowy miejskie.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\UPM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli UM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja umów miejskich'


\PERM_UPD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Uaktualnienie uprawnień do danych użytkowników dla bieżącej firmy
::----------------------------------------------------------------------------------------------------------------------
exec('perm_upd','tran_upr');
__UPG.msg('Zaktualizowano uprawnienia do danych użytkowników.')


\PERM_UPD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Uaktualnienie uprawnień do danych użytkowników dla bieżącej firmy - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień do danych użytkowników'


\ud_upr_i
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: Import uprawnień do elementów schematów danych
::----------------------------------------------------------------------------------------------------------------------
_choice:=0;
{! |? (_choice:=FUN.choice(
                'Czy wykonać import uprawnień do elementów schematów danych na podstawie pliku ud_upr_i.tra?\n'
                '\n'
                'Uwaga!\n'
                'Przed rozpoczęciem importu należy:\n'
                '1. Uzupełnić konfigurację ochrony elementów schematów danych.\n'
                '   W tym celu:\n'
                '   1.1. Uruchom w Mericie obszar "Ustawienia i parametryzacja".\n'
                '   1.2. W zakładce "Parametryzacja" ustaw się na pozycji "Wspólne".\n'
                '   1.3. Rozwiń gałąź i wybierz element "Schematy danych".\n'
                '   1.4. W otwartym oknie w zakładce "Definicja" ustaw odpowiednio znacznik "Ochrona".\n'
                '        Możesz to zrobić ustawiając się na wybranym typie w oknie Typy i wybierając akcję Popraw.\n'
                '        Znacznik "Ochrona" powinien być włączony tylko dla typów dla których faktycznie realizowane jest\n'
                '        ograniczenie dostępu, ponieważ znacząco wpływa to na czas wykonywania procedury importu.\n'
                '2. Upewnić się, że w katalogu work\\all\\imex znajduje się plik ud_upr_i.tra\n'
                '(patrz rozdział "Przygotowanie do transferu" noty transferowej)'@
                ,0,'Schematy danych'@,'Tak',,,'Nie'@))=1
|! exec('np_run','#b__box','ZWS_PAR_RSCH')
!};
{? _choice
|| {? FUN.ask(
      'Czy wykonano działania przygotowawcze i rozpocząć import?\n'
      '\n'
      'Tak - import zostanie rozpoczęty; operacja może być długotrwała\n'
      'Nie - zadanie pozostanie na liście zadań i będzie je można wykonać w późniejszym czasie.'@
      )
   || exec('import','ud_upr_i',1);
      __UPG.msg('Użytkownik %1 zdecydował o imporcie uprawnień do elementów schematów danych '
                'na podstawie pliku ud_upr_i.tra' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 pozostawił zadanie do późniejszego wykonania.' [exec('operatorKod','#users')]);
      0
   ?}
|| {? FUN.ask(
         'Czy rezygnujesz (również w przyszłości) z wykonania tego zadania?\n'
         '\n'
         'Tak - zadanie zostanie oznaczone jako "Wykonane" i nie będzie już można go uruchomić.\n'
         'Nie - zadanie pozostanie na liście zadań i będzie je można wykonać w późniejszym czasie.'@
      )
   || __UPG.msg('Użytkownik %1 zdecydował o pominięciu wykonania zadania.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 pozostawił zadanie do późniejszego wykonania.' [exec('operatorKod','#users')]);
      0
   ?}
?}


\ud_upr_i_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [ARTSLO] [19.22]
:: OPIS: Import uprawnień do elementów schematów danych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Import uprawnień do elementów schematów danych'


\UPMPOJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli UPMPOJ
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
UPMPOJ.cntx_psh();
UPM.cntx_psh();
UPMPOJ.prefix();
_res:=UPM.for_each("__lrec+=1; _put:=0;
                   {? (6+UPMPOJ.UP_SRC)='odpady'
                   || UPMPOJ.KODODP:=exec('FindAndGet','#table',ODP,UPMPOJ.UP_SRC,,,null());
                      UPMPOJ.UP_SRC:='';
                      _put:=1
                   ?};
                   {? UPMPOJ.UP_SRC='' & UPMPOJ.UPM<>null()
                   || UPMPOJ.UP_SRC:=$UPMPOJ.UPM;
                      _put:=1
                   ?};
                   {? ~((';KS'*UPMPOJ.TYPPOJ)>1)
                   || UPMPOJ.TYPPOJ:={? UPMPOJ.HN<>null() || 'K' || 'S' ?};
                      _put:=1
                   ?};
                   {? _put & UPMPOJ.put(1) || __lmod+=1 ?}
                 ");
UPMPOJ.cntx_pop();
UPM.cntx_pop();
__UPG.msg('Zaktualizowano usługi dla pozycji umów miejskich.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\UPMPOJ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Wypełnia pola w tabeli UPMPOJ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja usług dla pozycji umów miejskich'


\pba_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Formuła naprawcza dla ankiet sprzed aktualizacji (aktualizacja 12.51_19)
::----------------------------------------------------------------------------------------------------------------------
{? exec('upgrade_pba','phr_dane')
|| __UPG.msg('Zaktualizowano moduł badań ankietowych.')
|| __UPG.msg('Nie zaktualizowano modułu badań ankietowych.')
?};
1


\pba_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Formuła naprawcza dla ankiet sprzed aktualizacji (aktualizacja 12.51_19)
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja modułu badań ankietowych'


\istdefs_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Formuła aktualizująca struktury e-deklaracji.
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
ISTDEFS.cntx_psh();
ISTDEFS.index('LP'); ISTDEFS.prefix();
{? ISTDEFS.first()
|| {!
   |? {? ISTDEFS.REGULY<>''
      || _reg:=exec('transfer','xml',ISTDEFS.REGULY);
         {? ISTDEFS.REGULY<>_reg
         || ISTDEFS.REGULY:=_reg;
            ISTDEFS.put();
            _ok:=1
         ?}
      ?};
      ISTDEFS.next()
   !}
?};
ISTDEFS.cntx_pop();
{? _ok
|| __UPG.msg('Zaktualizowano struktury e-deklaracji.')
|| __UPG.msg('Aktualizacja struktur e-deklaracji nie jest wymagana.')
?};
1


\istdefs_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Formuła aktualizująca struktury e-deklaracji. - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja struktur e-deklaracji'


\akc_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Akceptacja schematów dokumentów oraz vat.
::----------------------------------------------------------------------------------------------------------------------
{? exec('lic_or','#b_domain','FKS')
|| _choice:=0;
   {! |? (_choice:=FUN.choice(
               'Formuła potransferowa zmieniła formuły schematów dokumentów'
               ' (formuły automatycznej rejestracji dokumentów księgowych)'
               ' oraz schematy VAT. Schematy w których występują nieistniejące formuły lub pliki zostały odakceptowane.\n'
               'Należy:\n'
               '1. Przejść do:\n'
               'Struktury księgowe → Kartoteki → [Schematy dokumentów], [Schematy VAT]\n'
               '2. Zaakceptować schematy dokumentów (formuły automatycznej rejestracji dokumentów księgowych) oraz schematy VAT.\n'
               'Podczas akceptacji schematów sprawdzane jest istnienie formuły w pliku dla konstrukcji exec(\'formuła\',\'plik\').\n\n'
               'Czy prace opisane powyżej zostały wykonane?'
               ,0,'Struktury księgowe'@,'Tak',,,'Nie'@))=1
      |! exec('fl_decl','dekret_aut');
         ROK_F.cntx_psh(); ROK_F.index('NAZWA'); ROK_F.prefix(REF.S_FIRMA);
         ROK_F.win_edit('POP1');
         ROK_F.seek(SSTALE.AR);
         exec('k_roczne','!zws_par_fask');
         ROK_F.cntx_pop()
   !};
   {? _choice
   || __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      0
   ?}
|| __UPG.msg('Działanie niepotrzebne. Brak licencjonowania dziedziny FKS.');
   1
?}


\akc_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Akceptacja schematów dokumentów oraz vat - opis
::----------------------------------------------------------------------------------------------------------------------
'Akceptacja schematów dokumentów (formuły automatycznej rejestracji dokumentów księgowych) oraz schematy VAT.'


\nazwy_typy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Zmiana nazw typów dla faktur, wniosków i delegacji
::----------------------------------------------------------------------------------------------------------------------
{? exec('lic_or','#b_domain','OBG') | exec('lic_or','#b_domain','OBE')
|| _choice:=0;
   {! |? _ready:=0;
         ETYPY.cntx_psh();
         _ndx:=ETYPY.ndx_tmp(,1,'NAZWA',,);
         ETYPY.index(_ndx); ETYPY.prefix('*');
         {? ~ETYPY.first() || _ready:=1 ?};
         ETYPY.ndx_drop(_ndx);
         ETYPY.cntx_pop();
         (_choice:={? ~_ready
                   || FUN.choice(
                      'Podczas transferu do nazw typów faktur w obiegu, wniosków i delegacji '
                      'dopisywane są na początku znaki \'*\' i cyfry. '
                      'Spowodowane to jest koniecznością zachowania unikalności nazw typów w systemie Merit. '
                      'Po transferze można ręcznie poprawić nazwy tych typów na inne.\n'
                      'Czy prace opisane powyżej zostały wykonane?'
                      ,0,'Typy &wniosków i delegacji'@,'Typy &faktur','Tak',,'Nie'@)
                   || FUN.choice(
                      'Brak typów faktur w obiegu, wniosków lub delegacji z dopisanym znakiem \'*\'\n'
                      'na początku nazwy.\n\n'
                      'Czy oznaczyć zadanie jako wykonane?'
                      ,0,'Typy &wniosków i delegacji'@,'Typy &faktur','Tak',,'Nie'@)
                   ?});
          _choice=1 | _choice=2
      |! {? _choice=1
         || exec('main','!zws_par_wtyp')
         |? _choice=2
         || exec('main','!zws_par_otyp')
         ?}
   !};
   {? _choice
   || __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      0
   ?}
|| __UPG.msg('Działanie niepotrzebne. Brak licencjonowania dziedziny FKS.');
   1
?}


\nazwy_typy_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Zmiana nazw typów dla faktur, wniosków i delegacji- opis
::----------------------------------------------------------------------------------------------------------------------
'Zmiana nazw typów dla faktur, wniosków i delegacji.'


\fks_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Sprawdzenie poprawności formuł dla schematów dziedzinowych
::       oraz formuł na nadawanie symboli dokumentom księgowym.
::----------------------------------------------------------------------------------------------------------------------
{? exec('lic_or','#b_domain','FKS')
|| _choice:=0;
   {! |? (_choice:=FUN.choice(
               'Należy sprawdzić poprawność poniższych danych:\n'
               '1. Formuły dla schematów dziedzinowych (formuły automatycznej dekretacji z innych systemów).\n'
               'Struktury księgowe -> Kartoteki → [Schematy dziedzinowe]\n'
               '2. Formuły na nadawanie symboli dokumentom księgowym.\n'
               'Struktury księgowe -> Kartoteki → [Rejestry] -> Rodzaje dokumentów.\n\n'
               'Czy prace opisane powyżej zostały wykonane?'
               ,0,'Struktury księgowe'@,'Tak',,,'Nie'@))=1
      |! exec('fl_decl','dekret_aut');
         ROK_F.cntx_psh(); ROK_F.index('NAZWA'); ROK_F.prefix(REF.S_FIRMA);
         ROK_F.win_edit('POP1');
         ROK_F.seek(SSTALE.AR);
         exec('k_roczne','!zws_par_fask');
         ROK_F.cntx_pop()
   !};
   {? _choice
   || __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      0
   ?}
|| __UPG.msg('Działanie niepotrzebne. Brak licencjonowania dziedziny FKS.');
   1
?}


\fks_form_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Sprawdzenie poprawności formuł dla schematów dziedzinowych
::       oraz formuł na nadawanie symboli dokumentom księgowym. - opis
::----------------------------------------------------------------------------------------------------------------------
'Sprawdzenie poprawności formuł dla schematów dziedzinowych oraz formuł na nadawanie symboli dokumentom księgowym'


\fo_xpertis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Zadanie manualne - import wartości FO z xpertis
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
{? FUN.ask('Czy importować wartości formuł z poprzedniego systemu? (plik \'%1\')'@['fo_def_xpertis.tra'])
|| _result:=exec('import_xpertis','#params','fo_def_xpertis.tra',0);
   {? _result>0
   || __UPG.msg('Użytkownik %1 wykonał import pliku: %2.'[exec('operatorKod','#users'),'fo_def_xpertis.tra'])
   |? _result=0
   || __UPG.msg('Wystąpił problem podczas importu pliku: %1.'['fo_def_xpertis.tra'])
   ?}
?};
{? _result<>0
||
   {? FUN.ask('Czy importować wartości stałych z poprzedniego systemu? (plik \'%1\')'@['fo_info_xpertis.tra'])
   || _result:=exec('import_xpertis','#params','fo_info_xpertis.tra',0);
      {? _result>0
      || __UPG.msg('Użytkownik %1 wykonał import pliku: %2.' [exec('operatorKod','#users'),'fo_info_xpertis.tra'])
      |? _result=0
      || __UPG.msg('Wystąpił problem podczas importu pliku: %1.'['fo_info_xpertis.tra'])
      ?}
   ?}
?};
{? _result<>0
||
   {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['fo_xpertis'])
   || _result:=1
   || _result:=-1
   ?}
?};
_result


\fo_xpertis_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Zadanie manualne - import wartości FO z xpertis - opis
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
'Import wartości parametrów FO.\n'
'Z pliku \'fo_def_xpertis.tra\' nanoszone są wartości formuł z poprzedniego systemu, '
'z pliku \'fo_info_xpertis.tra\' nanoszone są wartości przechowywane poprzednio w stałych systemu.\n'
'Naniesione wartości nadpiszą bezwarunkowo ustawione dotychczas wartości.'


\h2n_rap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: Raporty nieobecności po przenosinach z przebiegu zatrunienia.
::       Oryginalnie obecne były w \h2n/transfer.fml
::----------------------------------------------------------------------------------------------------------------------
:: przebieg
_RS:=sql('
   select
      P.T,
      OSOBA.NAZWISKO,
      OSOBA.PIERWSZE,
      OSOBA.PESEL,
      UD_SKL.SYMBOL,
      STN.ST,
      H.OD,
      H.DO,
      H_ZM.KZ,
      H_ZM.R
   from
      H left join
      P using (H.P,P.REFERENCE) left join
      UD_SKL using (P.WYDZIAL,UD_SKL.REFERENCE) left join
      STN using (P.ST,STN.REFERENCE) left join
      OSOBA using (P.OSOBA,OSOBA.REFERENCE) left join
      H_ZM using (H.KZ,H_ZM.REFERENCE)
   where
      H_ZM.KZ<>\'Z\' and P.FIRMA=:_a
   order by
      OSOBA.NAZWISKO,
      OSOBA.PIERWSZE,
      OSOBA.PESEL',
   exec('ref_firma','ustawienia'));
:: nieobecności
_RS2:=sql('
   select
      P.T,
      OSOBA.NAZWISKO,
      OSOBA.PIERWSZE,
      OSOBA.PESEL,
      UD_SKL.SYMBOL,
      STN.ST,
      N.OD,
      N.DO,
      N.NK,
      N.NR,
      N.NG,
      R.RN,
      R.RT,
      N.REFERENCE
   from
      N left join
      P using (N.P,P.REFERENCE) left join
      UD_SKL using (P.WYDZIAL,UD_SKL.REFERENCE) left join
      STN using (P.ST,STN.REFERENCE) left join
      OSOBA using (P.OSOBA,OSOBA.REFERENCE) left join
      R using (N.NB,R.REFERENCE)
   where
      N.LT=\'TRANSFER\' and P.FIRMA=:_a
   order by
      OSOBA.NAZWISKO,
      OSOBA.PIERWSZE,
      OSOBA.PESEL',
   exec('ref_firma','ustawienia'));

{? _RS.size() | _RS2.size()
|| {? _RS.first()
   || FUN.emsg('Znaleziono zapisy w przebiegu zatrudnienia o kodzie innym niż \'Z\, których z nieustalonego\n'
               'powodu nie udało się automatycznie rozpisać na nieobecności. Należy przejrzeć listę, która\n'
               'zostanie wyświetlona i zbadać przyczynę.'@);
      _wnd:=_RS.mk_sel('Przebieg zatrudnienia'@,'T',0,,,,,,'U');
      _RS.win_fld(_wnd,,'T',,,9,,,'Nr teczki'@);
      _RS.win_fld(_wnd,,'NAZWISKO',,,20,,,'Nazwisko'@);
      _RS.win_fld(_wnd,,'PIERWSZE',,,20,,,'Imię'@);
      _RS.win_fld(_wnd,,'SYMBOL',,,16,,,'Jednostka org.'@);
      _RS.win_fld(_wnd,,'ST',,,20,,,'Stanowisko'@);
      _RS.win_fld(_wnd,,'OD',,,,,,'Od dnia'@);
      _RS.win_fld(_wnd,,'DO',,,,,,'Do dnia'@);
      _RS.win_fld(_wnd,,'KZ',,,1,,,'Kod'@);
      _RS.win_fld(_wnd,,'R',,,20,,,'Opis'@);
      _RS.win_sel(_wnd);
      _RS.select()
   ?};
   obj_del(_RS);

   {? _RS2.first()
   || {? FUN.ask('Znaleziono nieobecności przeniesione z przebiegu zatrudnienia, które dotyczą minionych okresów.\n'
                 '(Znacznik listy płac ustalony na "TRANSFER")\n'
                 'Czy wyświetlić ich listę?'@)
      || _wnd:=_RS2.mk_sel('Nieobecności'@,'T',0,,,,,,'U');
         _RS2.win_fld(_wnd,,'T',,,9,,,'Nr teczki'@);
         _RS2.win_fld(_wnd,,'NAZWISKO',,,20,,,'Nazwisko'@);
         _RS2.win_fld(_wnd,,'PIERWSZE',,,20,,,'Imię'@);
         _RS2.win_fld(_wnd,,'SYMBOL',,,16,,,'Jednostka org.'@);
         _RS2.win_fld(_wnd,,'ST',,,20,,,'Stanowisko'@);
         _RS2.win_fld(_wnd,,'OD',,,,,,'Od dnia'@);
         _RS2.win_fld(_wnd,,'DO',,,,,,'Do dnia'@);
         _RS2.win_fld(_wnd,,'NK',,,3,,,'Kal.'@);
         _RS2.win_fld(_wnd,,'NR',,,3,,,'Rob.'@);
         _RS2.win_fld(_wnd,,'NG',,,6,2,,'Godz.'@);
         _RS2.win_fld(_wnd,,'RN',,,5,,,'Kod'@);
         _RS2.win_fld(_wnd,,'RT',,,20,,,'Opis'@);
         _RS2.win_sel(_wnd);
         _RS2.select()
      ?}
   ?};
   obj_del(_RS2);

   {? FUN.ask(
         'Czy zakończono pracę?\n'
         '\n'
         'Tak - zadanie zostanie oznaczone jako "Wykonane" i nie będzie już można go uruchomić.\n'
         'Nie - zadanie pozostanie na liście zadań i będzie je można wykonać w późniejszym czasie.'@
      )
   || __UPG.msg('Użytkownik %1 zakończył prace nad zadaniem.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 pozostawił zadanie do późniejszego wykonania.' [exec('operatorKod','#users')]);
      0
   ?}
|| __UPG.msg('Po transferze nie wykryto zapisów wymagających obsługi ręcznej.');
   1
?}


\h2n_rap_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: Raporty nieobecności po przenosinach z przebiegu zatrunienia. - opis
::----------------------------------------------------------------------------------------------------------------------
'Raporty: Częściowo przeniesione nieobecności, zapisy w przebiegu zatrudnienia o kodzie innym niż \'Z\'.'


\form_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Walidacja danych po transferze.
::----------------------------------------------------------------------------------------------------------------------
_choice:=0;
{! |? (_choice:=FUN.choice(
            'Zadanie służące walidacji danych po transferze. Za pomocą przycisku "Walidacja danych" można:\n'
            '1. Uruchomić zestaw formuł walidacyjnych, które w razie stwierdzenia problemów wyświetlają ich listę.\n'
            '2. W przypadku środków trwałych uruchomić mechanizm manualnej weryfikacji i edycji danych wartościowych, '
            'który porównuje dane wyeksportowane z Xpertis Środki trwałe, z tymi które znajdują się po transferze '
            'w systemie Merit. Uwaga: mechanizm pozwala na modyfikację '
            'danych wartościowych także w zamkniętych okresach.\n\n'
            'Czy chcesz potwierdzić wykonanie zadania?'
            ,0,'Walidacja danych'@,'Tak',,,'Nie'@))=1
   |! exec('start_wal','tran_vla')
!};
{? _choice
|| __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   1
|| __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
   0
?}


\form_wal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Uzupełnienie słownika wydziałów. - opis
::----------------------------------------------------------------------------------------------------------------------
'Walidacja danych po transferze (opcjonalne)'


\obywatel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Aktualizacja słownika OBYWATEL z kodami państw zgodnymi z ISO 3166-1 (kod alfa2).
::  OLD: \obywatel/upgrade_1922.fml
::----------------------------------------------------------------------------------------------------------------------
__UPG.msg('Aktualizacja słownika OBYWATEL');

_stat:=obj_new('put','add','bz');
{! _lp:=1 .. obj_len(_stat)
|! _stat[_lp]:=obj_new('fail','ok');
   _stat[_lp].fail:=_stat[_lp].ok:=0
!};
params_set('stat',_stat);

SLO_TYP.cntx_psh();
SLO_TYP.index('SYMBOL');
SLO_TYP.prefix();

SLO_KOD.cntx_psh();
SLO_KOD.index('KOD');
SLO_KOD.prefix();
_add:="
   _stat:=params_get().stat;
   SLO_KOD.prefix(_a,_b,_b);
   {? SLO_KOD.first()
   || {? SLO_KOD.NAZWA<>_c
      || SLO_KOD.NAZWA:=_c;
         _stat.put[1+SLO_KOD.put()]+=1
      || _stat.bz.ok+=1
      ?}
   || SLO_KOD.blank(1);
      SLO_KOD.SLO_TYP:=_a;
      SLO_KOD.KOD:=_b;
      SLO_KOD.NAZWA:=_c;
      SLO_KOD.SYSTEM:='T';
      _stat.add[1+SLO_KOD.add()]+=1
   ?}
";

_typ:=exec('slo_typ','ext_slo','OBYWATEL');

SLO_KOD.prefix(_typ);
_add(_typ,'AL','albańskie');
_add(_typ,'AT','austriackie');
_add(_typ,'BE','belgijskie');
_add(_typ,'BY','białoruskie');
_add(_typ,'BG','bułgarskie');
_add(_typ,'HR','chorwackie');
_add(_typ,'CY','cypryjskie');
_add(_typ,'CZ','czeskie');
_add(_typ,'DK','duńskie');
_add(_typ,'EE','estońskie');
_add(_typ,'FI','fińskie');
_add(_typ,'FR','francuskie');
_add(_typ,'GR','greckie');
_add(_typ,'GE','gruzińskie');
_add(_typ,'ES','hiszpańskie');
_add(_typ,'NL','holenderskie');
_add(_typ,'IE','irlandzkie');
_add(_typ,'IS','islandzkie');
_add(_typ,'LI','liechtensteińskie');
_add(_typ,'LT','litewskie');
_add(_typ,'LU','luksemburskie');
_add(_typ,'LV','łotewskie');
_add(_typ,'MK','macedońskie');
_add(_typ,'MT','maltańskie');
_add(_typ,'MD','mołdawskie');
_add(_typ,'MC','monakijskie');
_add(_typ,'DE','niemieckie');
_add(_typ,'NO','norweskie');
_add(_typ,'PL','polskie');
_add(_typ,'PT','portugalskie');
_add(_typ,'RU','rosyjskie');
_add(_typ,'RO','rumuńskie');
_add(_typ,'RS','serbskie');
_add(_typ,'SK','słowackie');
_add(_typ,'SI','słoweńskie');
_add(_typ,'CH','szwajcarskie');
_add(_typ,'SE','szwedzkie');
_add(_typ,'TR','tureckie');
_add(_typ,'UA','ukraińskie');
_add(_typ,'HU','węgierskie');
_add(_typ,'GB','brytyjskie');
_add(_typ,'IT','włoskie');
SLO_KOD.cntx_pop();
SLO_TYP.cntx_pop();

__UPG.msg('Liczba pozycji niewymagających aktualizacji: '+$_stat.bz.ok);
__UPG.msg('Liczba pozycji wymagających aktualizacji: '+$(_stat.put.fail+_stat.put.ok));
__UPG.msg('Liczba pozycji zaktualizowanych: '+$(_stat.put.ok));
__UPG.msg('Liczba brakujących pozycji: '+$(_stat.add.fail+_stat.add.ok));
__UPG.msg('Liczba dodanych pozycji: '+$(_stat.add.ok));

_stat.put.fail=0 & _stat.add.fail=0


\obywatel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Aktualizacja słownika OBYWATEL z kodami państw zgodnymi z ISO 3166-1 (kod alfa2) - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słownika OBYWATEL z kodami państw zgodnymi z ISO 3166-1 (kod alfa2)'

:Sign Version 2.0 jowisz:1045 2023/12/12 09:45:21 40c6854418dd0044fe4346cb58047348e38e2bba28095ca2b8f37e39c927091f60df18497eb3e5271a926a4fa9040e11570440558334d376e0f09cc010d735a0257d22e3f4bd82ff272aaa6a2818b5c6a990796925afdd3ab2826ee7cca5b00ed169fa30bdab6e34da0146f974d8828b8344e990a5e9bcd2a32cabd9731487d4

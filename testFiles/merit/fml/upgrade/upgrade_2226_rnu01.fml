:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2226_rnu01.fml
:: Utworzony: 31.05.2023
:: Autor: IS
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.37 na 22.26
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Główna formuła aktualizacji 22.26_RNU01
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2023,6,2),time(0,0,0));
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('rubryki_placowe',,,'PPL',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
:: Zadania manualne
__UPG.add_task('formuly_placowe','N','N','PPL',,,,1,'T');
~~


\rubryki_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie rubryk i atrybutów płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

:: Aktualizacja i dodanie nowych składników płacowych. ----------------------------------------------------------------
:: 7215 Urlop opiekuńczy
:: 7216 Nieob. siła wyższa
:: 7226 Siła wyższa dni
:: 7227 Siła wyższa godziny
:: 7228 Wynagr. siła wyższa
:: 7229 Kor. siła wyższa
{? +exec('import','rubryki','g',
      '7215,7216,7226,7227,7228,7229',
      1,0)
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);
{? _result=-1 || return(_result) ?};

:: Aktualizacja atrybutów rubryk. -------------------------------------------------------------------------------------

:: Dodanie atrybutów dla pracy zadalnej
exec('atrplaimex_decl','rubatr');
_atr:=obj_new(@.CLASS.AtrPlaImEx);
_atr.add_attr('g','1902,1318,1974,1975,1904,455,118,119',1);

:: Sposób wypłacania - przez zakład
exec('add_use','rubatr',161,7216);
:: sposób potrącania w godzinach roboczych
exec('add_use','rubatr',152,7215,7216);
:: Nieobecności na część dnia
exec('add_use','rubatr',199,7216);
:: Nieobecności limitowane dyscypliną pracy
exec('add_use','rubatr',1931,7216,7215);
:: Ikony w okienku wertowania - pozostałe
exec('add_use','rubatr',1923,7216,7215);
:: Miesięczny podział kosztów wg godzin - Nieobecności
exec('add_use','rubatr',100112,7216);
:: Kod świadczenia - Płatnik - 350 - Inne
exec('add_use','rubatr',14350,7216);
:: RSA - Warunkowo pomijane w zapytaniach sql
exec('add_use','rubatr',19421,7215,7216);
:: Składniki do brutto o kodzie większym niż 500
exec('add_use','rubatr',48,7228,7229);
:: RSA - Warunkowo pobierane w zapytaniach sql (ER/WRT/XP/23.25/2309/0061)
exec('add_use','rubatr',19422,7215);

RA_DEF.cntx_psh();
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
_dtUst:=date(2023,4,26);
{? RA_DEF.find_key(1296)
|| exec('use_add','rubatr',RA_DEF.ref(),__RUB.ref(32),_dtUst,'N');
   exec('use_add','rubatr',RA_DEF.ref(),__RUB.ref(33),_dtUst,'N')
?};
{? RA_DEF.find_key(1291)
|| exec('use_add','rubatr',RA_DEF.ref(),__RUB.ref(32),_dtUst,'T');
   exec('use_add','rubatr',RA_DEF.ref(),__RUB.ref(33),_dtUst,'T')
?};
RA_DEF.cntx_pop();

__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\rubryki_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie rubryk i atrybutów płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk i atrybutów płacowych związanych z nowymi rodzajami urlopów:\n'
' * 7215 Urlop opiekuńczy\n'
' * 7216 Nieob. siła wyższa\n'
' * 7226 Siła wyższa dni\n'
' * 7227 Siła wyższa godziny\n'
' * 7228 Wynagr. siła wyższa\n'
' * 7229 Kor. siła wyższa'


\formuly_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie formuł płacowych
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='436,438,7215,7216,7226,7227,7229';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie formuł płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie i aktualizacja formuł płacowych rubryk:\n'
' * 436 Wynagr. za urlop\n'
' * 438 Średnia urlopowa\n'
' * 7215 Urlop opiekuńczy\n'
' * 7216 Nieob. siła wyższa\n'
' * 7226 Siła wyższa dni\n'
' * 7227 Siła wyższa godziny\n'
' * 7229 Kor. siła wyższa'


\formuly_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=436 & ',F,'*',%1,'[_tp]
:: ==================
:: Wynagr. za urlop
|| "
{? FUNKCJE.L_SYS(113) | FUNKCJE.L(24) | FUNKCJE.L_SYS(1318)
|| exec('urlop','!ppl_pll_nals')
|| FUNKCJE.LK(442,,0);
   FUNKCJE.LK(443,,0);
   FUNKCJE.LK(444,,0);
   FUNKCJE.LK_SYS(4551,,0);
   0
?}"
|? _rn=438 & ',U,'*',%1,'[_tp]
:: ==================
:: Średnia urlopowa
|| "
{? FUNKCJE.L_SYS(113) | FUNKCJE.L(24) | FUNKCJE.L_SYS(1318)
|| exec('urlop','!ppl_pll_nals')
|| FUNKCJE.LK(442,,0);
   FUNKCJE.LK(443,,0);
   FUNKCJE.LK(444,,0);
   FUNKCJE.LK_SYS(4551,,0);
   0
?}
"
|? _rn=7215
:: ==================
:: Urlop opiekuńczy
|| "
FUNKCJE.NR(7215)"
|? _rn=7216
:: ==================
:: Nieob. siła wyższa
|| "
FUNKCJE.NG(7216)"
|? _rn=7226
:: ==================
:: Siła wyższa dni
|| "
_rub:=__RUB.sys_kod(1318,O.D);
{? _rub>0 & exec('chkDsTyp','!ppl_pll_nals',_rub)<>'G'
|| FUNKCJE.NR(_rub)
?}"
|? _rn=7227
:: ==================
:: Siła wyższa godziny
|| "
_rub:=__RUB.sys_kod(1318,O.D);
{? _rub>0 & exec('chkDsTyp','!ppl_pll_nals',_rub)='G'
|| FUNKCJE.NG(_rub)
?}"
|? _rn=7229
:: ==================
:: Kor. siła wyższa
|| "
DoList.c[13]$2"

|| ""
?}

:Sign Version 2.0 jowisz:1045 2023/09/26 10:24:39 aeca205687b806050172753458ad4f9c5c01034607aedd4c1b59aba6ecfa40f5c2508fe5100936327e90ac2390445beebafbca92d73dcc6b2b9659ef78b9197e168caba4f19b1a240be27e656eafbdcce97261cd54ca8a429b68f9f4824874700d79cb995573ea6fc57fb8de4613700d11c6cb8a359c1b32cf6c83af197206a9

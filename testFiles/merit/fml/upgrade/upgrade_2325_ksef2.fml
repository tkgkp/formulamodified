:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2325_KSEF2.fml
:: Utworzony: 28.07.2023
:: Autor: [AWI]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 23.25 na 23.25_KSEF2
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::
::            Programowe wyłączenie zadania transferowego.
::             W plikach upgrade_rrxx.fml korzystamy z metody
::               __UPG.off_task(<task>,[<operator>],[<value>])
::                 - dodaje do listy zadanie,
::                   które ma zostać wyłączone wg warunku porównania wg operatora porównania i wartości, gdzie:
::                <task> - symbol zadania do wyłączenia;
::                [<operator>] - operator porównania - domyślnie operator '<'; dostępne opcje:
::                                   '<' - wartość mniejsza;
::                                   '<=' - wartość mniejsza lub równa;
::                                   '=' - wartość równa;
::                [<value>] - wersja systemu lub aktualizacji do porównania z wersją zadania
::                            - domyślnie aktualna wersja aktualizacji (wg pliku).
::            W pliku upgrade.fml NALEŻY umieścić wywołanie poniższej metody na końcu formuły \init
::               (po wywołaniach __UPG.add_head()):
::              __UPG.act_off() - wyłączenie wszystkich zadań wskazanych do wyłączenia.
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AWI] [23.25_KSEF2]
:: OPIS: Główna formuła upgrade-ów RR.xx
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2023,8,21),time(0,0,0));

:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('report',,,'ZWS');
__UPG.add_task('par_skid',,,'ZWS');

:: Zadania wykonywane dla każdej firmy osobno
:: __UPG.add_task('EDI',,'N','ZWS',1,,,1);

:: Zadania manualne wykonywane raz dla całego systemu

:: Zadania manualne wykonywane dla każdej firmy osobno

~~


\EDI
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AWI] [23.25_KSEF2]
:: OPIS: Aktualizuje definicje komunikatów EDI
::----------------------------------------------------------------------------------------------------------------------
:: transfer naprawczy ISTDEFV w wersji RR.xx, w emisyjnej do usunięcia
ISTDEFV.cntx_psh();
ISTDEFV.prefix();
ISTDEFV.for_each("{? ISTDEFV.ARCHIWUM='' || ISTDEFV.ARCHIWUM:='N'; ISTDEFV.put() ?}");
ISTDEFV.cntx_pop();
::
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
:: upgrade_edi_er_wrt_xp_2226_2207_0022.zip
:: upgrade_edi_23xx.zip
_fname:='upgrade_edi_%1.zip'['2325_ksef2'];
_tmp_dir:=fmk_tmp_dir(0);
_tmp_pth:=_tmp_dir.get_path();
_sep:=exec('sep','#file',2);
_zip_name:=_tmp_pth+_sep+'zipfile.zip';
{? ~fcopy(_fname,_zip_name,1,0)
|| __UPG.msg('Nie powiodło się skopiowanie pliku %1.'[_fname]);
   return(0)
?};
{? ~funpack('zip',_zip_name,_tmp_pth)
|| __UPG.msg('Nie powiodło się rozpakowanie archiwum %1.'[_zipname]);
   return(0)
?};
_Files:=fdir(_tmp_pth,1,0);
_loop:=_Files.first();
{!
|? _loop
|!
   {? _Files.TYPE='f' & (_Files.NAME+4)='.dem' & (_Files.NAME+9)<>'_form.dem' & (_Files.NAME+8)<>'_xsd.dem'
   ||
      __lrec+=1;
      _msg:=exec('edi_imp_core','edi_def',_tmp_pth,_Files.NAME,0);
      {? _msg='' | _msg*'Nadpisano strukturę komunikatu:' || __lmod+=1 ?}
   ?};
   _loop:=_Files.next()
!};
__UPG.msg('Zaktualizowano definicje komunikatów EDI.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\EDI_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AWI] [23.25_KSEF2]
:: OPIS: Aktualizuje definicje komunikatów EDI - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizuje definicje komunikatów EDI'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [RR.xx]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
{? _result
|| __UPG.msg('Zaktualizowano wydruki i zestawienia.')
|| __UPG.msg('Wystąpił błąd kompilacji wydruków.');
   _result:=-1
?};
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [RR.xx]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\par_skid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25_KSEF2]
:: OPIS: Aktualizacja listy parametrów
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');
PAR_SKID.set(485,1,1,'','T',
            'Obieg dokumentów: Ponowne odczytanie komunikatu EDI przy dekretacji faktury w obiegu z KSeF?',
            '''T'',''N''');
PAR_SKID.set(486,1,1,'','N',
            'Obieg dokumentów: Pozycje VAT oraz pozycje faktury na podstawie pliku z Businesslink (dekretacja z KSeF)?',
            '''T'',''N''');
__UPG.msg('Zaktualizowano listę parametrów');
1


\par_skid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25_KSEF2]
:: OPIS: Aktualizacja listy parametrów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy parametrów.'


:Sign Version 2.0 jowisz:1045 2023/11/23 11:05:40 1584f3bc9657553cc14192b3ca4d0544addbaba950a4d1049d7dae847ccfe545b3a3094017565b677f6e1ae6a90b468a62b7f286d252c2aff7b403d86063162fb2da7b75dba2477591a58505866e6a9eb51e17bba727d8336f63f416f7426b77bde0953dbb976b6447783d2f013dc2659ec03cdf219552a6442341cfec9f3d08

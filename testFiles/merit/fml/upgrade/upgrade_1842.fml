:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_1842.fml
:: Utworzony: 25.05.2018
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 18.22 na 18.42
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - realizacja zakwalifikowana jako wykonana
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Główna formuła upgrade-ów 18.42
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR');
__UPG.add_task('stalesys_ini',,,'ZWS',2,,,,'T');
__UPG.add_task('zo_cel',,,'POC');
__UPG.add_task('zf_rpi_repair',,,'ZWS');
__UPG.add_task('s_zus',,,'PKD');
__UPG.add_task('rubatr',,,'PPL');
__UPG.add_task('color',,,'ZWS');
__UPG.add_task('edeklaracje',,,'ZWS');
__UPG.add_task('zo_form',,,'POC');
__UPG.add_task('oddelegowania',,,'PKD');
__UPG.add_task('oddelegowania_KST',,,'ZWS');
__UPG.add_task('del_funpar',,,'ZWS');
__UPG.add_task('funpar',,,'ZWS');
__UPG.add_task('tar_upr',,,'ZWS');
__UPG.add_task('skid_par',,,'PPL');
__UPG.add_task('f_02',,,'FKS');
__UPG.add_task('f_03',,,'ZWS');
__UPG.add_task('kw_wolna_zas',,,'PPL');
__UPG.add_task('wyp_sl_kom',,,'PPL');
__UPG.add_task('dek_nag',,,'FKS');
__UPG.add_task('dek_poz',,,'FKS');
__UPG.add_task('kal_wzor',,,'ZWS');
__UPG.add_task('stalesys',,,'PPL');
__UPG.add_task('form_plat_pos',,,'ZWS');
__UPG.add_task('p_04',,,'FKS');
__UPG.add_task('report',,,'ZWS');
__UPG.add_task('MOB_upr',,,'ZWS');
__UPG.add_task('B_USRDOM',,,'ZWS');
__UPG.add_task('kor_url_rodzicielski',,,'PPL');
__UPG.add_task('bi_problems',,,'ZPR');
__UPG.add_task('gr_sik',,,'FKS');
__UPG.add_task('areatitle',,,'ZWS');
__UPG.add_task('zasw_ZUS_do_oddel',,,'ZWS');
__UPG.add_task('oddelegowania_skl_plac',,,'PPL');
__UPG.add_task('sch_imp',,,'OBE');
__UPG.add_task('zws_par_fspr',,,'ZWS');
__UPG.add_task('ROKP',,,'FST');
__UPG.add_task('zdo_w',,,'ROD');
:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('tar_perm_upd',,'N','ZWS',,,,1);
__UPG.add_task('updateKAL_BUFF',,'N','PRC',,,,1);
__UPG.add_task('PL_RES',,'N','ZWS',,,,1);
__UPG.add_task('PX_CUP',,'N','TPP',,,,1);
__UPG.add_task('PX_POZ',,'N','TPP',,,,1);
__UPG.add_task('PX_STAGE',,'N','TPP',,,,1);
__UPG.add_task('convEANU',,'N','LMG',,,,1);
__UPG.add_task('PAR_POKR',,'N','PPL',,,,1);
__UPG.add_task('rodzaje_oddelegowania',,'N','PKD',,,,1);
__UPG.add_task('DATY',,'N','ZWS',,,,1);
__UPG.add_task('f_zatra',,'N','PKD',,,,1);
__UPG.add_task('norm_upr',,'N','ZWS',,,,1);
__UPG.add_task('fo_100227',,'N','ZWS',,,,1);
__UPG.add_task('A_OKRD',,'N','PRC',,,,1);
__UPG.add_task('AFORM',,'N','TTE',,,,1);
__UPG.add_task('WARP100',,'N','FST',,,,1);
__UPG.add_task('param38',,'N','LSP',,,,1);
__UPG.add_task('MOB_deactProc',,'N','ZPR',,,,1);
__UPG.add_task('OP_SP',,'N','FKS',,,,1);
__UPG.add_task('odd_dok_update',,'N','FST',,,,1);
__UPG.add_task('zdo_f',,'N','ROD',,,,1);
__UPG.add_task('tabkrst',,'N','FST',,,,1,'T');
__UPG.add_task('plugins',,'N','ZWS',,,,1,'T');
:: Zadania manualne
__UPG.add_task('rodo','N','T','ZWS',1,'N',,1);
__UPG.add_task('listopad_12','N','N','PRC',,,,1,'T');
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.PKD.PKW.PPL.TPP.TTE.WYP.ZWS.FST.FKS.KON.OBE.OBG','.');
_dom:=spli_str('ZWS.LSP.LZK.PKD.PPL.TPP.WYP.ZPR.LMO.LMG.LSS','.');
_len:=obj_len(_dom);

FUN.prg_start(_len+2,'Aktualizacja czynności.'@,,,1);

:: Uzupełnienie domen (zawsze)
FUN.prg_next();
exec('add_domain','#b_action');
__UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
FUN.prg_next();
exec('fill_tab','#b_type',0);
__UPG.msg('Zaktualizowano typy danych.');

{! _i.._len
|! FUN.prg_next();
   _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
   {? _domain<>null()
   || exec('import_needed_dom','#b_design',_domain,0);
      __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
   ?}
!};
FUN.prg_stop();
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\zo_cel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Uzupełnia pole PROC z tabeli ZO_CEL. Informacja o sesji ocen, w której cel został wyznaczony.
::----------------------------------------------------------------------------------------------------------------------
ZO_CEL.cntx_psh();
ZO_CEL.prefix();
_loop:=ZO_CEL.first();
{? _loop
|| _lrec:=0;
   _lmod:=0;
   _firma:=$exec('ref_firma','ustawienia');
   FUN.prg_start(ZO_CEL.size(),'Przetwarzanie tabeli %1'@['ZO_CEL'],,,1);
   {!
   |? _loop
   |! ZZ_DOK.use('zz_do'+(_firma+1));
      _lrec+=1;
      _tab:=sql('
         select ZO_PROC.REFERENCE as REF, ZO_PROC.OKRES_OD as OD, ZO_PROC.OKRES_DO as DO
         from ZO_PROC
         where ZO_PROC.ZO_PROG=\':_a\' and ZO_PROC.OKRES_DO<TO_DATE(\':_b\')',
         $ZO_CEL.ZO_PROG,$ZO_CEL.OKRES_DO
      );
      {? ZO_CEL.PROC=''
      || {? ZO_CEL.ZO_PROC
         || ZO_CEL.ZO_PROC();
            {? ZO_PROC.prev()
            || ZO_CEL.PROC:=$ZO_PROC.ref();
               ZO_CEL.put();
               _lmod+=1
            || ZO_CEL.PROC:=$ZO_PROC.ref();
               ZO_CEL.put();
               _lmod+=1
            ?}
         || {? _tab.last()
            || ZO_CEL.PROC:=_tab.REF;
               ZO_CEL.put();
               _lmod+=1
            ?}
         ?}
      ?};
      &_tab;
      FUN.prg_next();
      _loop:=ZO_CEL.next()
   !};
   FUN.prg_stop();
   __UPG.msg('Uzupełniono informację o sesji ocen dla wyznaczonych celów.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod])
|| __UPG.msg('Brak danych do aktualizacji.')
?};
ZO_CEL.cntx_pop();
1


\zo_cel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Uzupełnia pole PROC z tabeli ZO_CEL. Informacja o sesji ocen, w której cel został wyznaczony.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola PROC tabeli ZO_CEL. Informacja o sesji ocen, w której cel został wyznaczony.'


\updateKAL_BUFF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Aktualizuje pola KAL_BUFF.P_DOM
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.index('OSOBA');
P.prefix();
KAL_BUFF.cntx_psh();
KAL_BUFF.index('NIEOBECN');
KAL_BUFF.prefix();
KAL_BUFF.for_each("
   {? KAL_BUFF.P_DOM=''
   || KAL_BUFF.P_DOM:='T';
      KAL_BUFF.put()
   ?}
",1);
KAL_BUFF.cntx_pop();
P.cntx_pop();
__UPG.msg('Zakończono aktualizowanie zapisów w tabeli buforów planowania czasu pracy (KAL_BUFF).');
1


\updateKAL_BUFF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Aktualizuje pola KAL_BUFF.P_DOM
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów w tabeli buforów planowania czasu pracy (KAL_BUFF)'


\zf_rpi_repair
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Uzupełnia i ewentualnie poprawia na właściwy szablon pole SZABLON2 dla elementów formularza oceny pracownika
::       (ZF_SKL) i usuwa błędy w istniejących formularzach oceny pracowniczej związane z szablonami (ZF_POZ).
::----------------------------------------------------------------------------------------------------------------------
__lrec:=0;
__lmod:=0;

ZF_SKL.clear();
ZF_SKL.f_set(,'','ZF_SKL.KOD=\':_a\' or ZF_SKL.KOD=\':_b\' or ZF_SKL.KOD=\':_c\'','ST','SZ','KD');
ZF_SKL.f_each("
   ZF_SKL.DOKUMENT:='N';
   ZF_SKL.put()
",1);
ZF_SKL.f_clear(2);

ZF_POZ.clear();
ZF_POZ.f_set(,'join ZF_SKL','ZF_SKL.KOD=\':_a\' or ZF_SKL.KOD=\':_b\' or ZF_SKL.KOD=\':_c\'','ST','SZ','KD');
ZF_POZ.f_each("
   ZF_POZ.ZF_RPI:=null();
   ZF_POZ.DOKUMENT:='N';
   ZF_POZ.put()
",1);
ZF_POZ.f_clear(2);

ZF_POZ.cntx_psh();
ZF_POZ.index('FK_SKL');
ZF_POZ.prefix();

ZF_RPI.clear();
ZF_RPI.for_each("
   __lrec+=1;
   {? ZF_RPI.SZABLON2:=exec('dom_rpi','phr_dane',2); ZF_RPI.SZABLON2<>''
      || ZF_RPI.put();
         __lmod+=1
   ?};
   {? ZF_RPI.ZF_SKL().KOD='ST' | ZF_RPI.ZF_SKL().KOD='SZ' | ZF_RPI.ZF_SKL().KOD='KD'
   || do();
      ZF_POZ.prefix(ZF_RPI.ZF_SKL);
      {? ZF_POZ.first()
      || {!
         |? {? ZF_POZ.ZF_RPI=ZF_RPI.ref()
            || ZF_POZ.ZF_RPI:=null();
               ZF_POZ.DOKUMENT:='N';
               ZF_POZ.put()
            ?};
            ZF_POZ.next()
         !}
      ?};
      ZF_RPI.del();
      end();
      __lmod+=1
   |? ZF_RPI.ZF_SKL().KOD='P'
   || ZF_RPI.SZABLON2:='poc_zrp01.rpi';
      ZF_RPI.put()
   ?}
",1);

ZF_POZ.cntx_pop();

__UPG.msg('Poprawiono szablony elementów formularza oceny pracowniczej - wydruk papierowy formularza.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
&__lrec;
&__lmod;
1


\zf_rpi_repair_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Uzupełnia i ewentualnie poprawia na właściwy szablon pole SZABLON2 dla elementów formularza oceny pracownika.
::----------------------------------------------------------------------------------------------------------------------
'Korekta pola SZABLON2 tabli ZF_RPI. Formuła poprawia szablony do wydruku formularza papierowego oceny pracownika'


\PL_RES
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_RES
::   WY: -1/0/1
::  TAG: <PRYWATNA>
::  ORG: \PL_RES/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

PL_RES.cntx_psh();
PL_RES.index('SYM');
PL_RES.prefix();
_fml:="
   _put:=0;
   {? PL_RES.LAYERS=''
   || {? PL_RES.PARALLEL='1'
      || PL_RES.LAYERS:='T'
      || PL_RES.LAYERS:='N'
      ?};
      _put:=1
   ?};
   {? _put>0
   || PL_RES.put()
   ?};
   ~~
";
_result:=PL_RES.for_each(_fml,1);
{? _result
|| __UPG.msg('Zaktualizowano zasoby planu operacyjnego.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji zasobów planu operacyjnego.')
?};
PL_RES.cntx_pop();
_result


\PL_RES_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Uzupełnia pola w tabeli PL_RES - opis
::  ORG: \PL_RES_desc/upgrade_1714.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli zasobów planu operacyjnego o znacznik rysowania w wielu warstwach.\n'
'Może się nie udać, tylko jeżeli pole tabeli PL_RES zostało zablokowane przez innego użytkownika'


\PX_CUP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli PX_CUP
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   {? PX_CUP.CAL_LVL=0
   || PX_CUP.CAL_LVL:=1;
      PX_CUP.put()
   ?}
";
PX_CUP.prefix();
_result:=PX_CUP.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w pojemnikach planu strategicznego.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w pojemnikach planu strategicznego.')
?};
_result


\PX_CUP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli PX_CUP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pojemników planu strategicznego.'


\PX_POZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli PX_POZ
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   _put:=0;
   {? PX_POZ.WHEN=''
   || PX_POZ.WHEN:=PX_POZ.PX_CUP().SYMBOL;
      _put:=1
   ?};
   {? PX_POZ.LANE=0
   || PX_POZ.LANE:=1;
      _put:=1
   ?};
   {? _put>0
   || PX_POZ.put()
   ?}
";
PX_POZ.cntx_psh();
PX_CUP.cntx_psh();
PX_POZ.prefix();
_result:=PX_POZ.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano nowe pola w pozycjach planu strategicznego.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w pozycjach planu strategicznego.')
?};
PX_POZ.cntx_pop();
PX_CUP.cntx_pop();
_result


\PX_POZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli PX_POZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pozycji planu strategicznego.'


\PX_STAGE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli PX_STAGE
::----------------------------------------------------------------------------------------------------------------------
TOPER.cntx_psh();
PX_STAGE.cntx_psh();
_formula:="
   _put:=0;
   {? TOPER.PX_STAGE<>null()
   || TOPER.PX_STAGE();
      {? TOPER.TTM<>0 & PX_STAGE.TTM=0
      || PX_STAGE.TTM:=TOPER.TTM;
         _put:=1
      ?};
      {? TOPER.NKO<>0 & PX_STAGE.NKO=0
      || PX_STAGE.NKO:=TOPER.NKO;
         _put:=1
      ?};
      {? _put>0
      || PX_STAGE.put()
      ?}
   ?};
   ~~
";
TOPER.prefix();
_result:=TOPER.for_each(_formula,1);

{? _result>0
||
   _formula:="
      _put:=0;
      {? ZGP.PX_STAGE<>null()
      || ZGP.PX_STAGE();
         {? ZGP.TTM<>0 & PX_STAGE.TTM=0
         || PX_STAGE.TTM:=ZGP.TTM;
            _put:=1
         ?};
         {? ZGP.NKO<>0 & PX_STAGE.NKO=0
         || PX_STAGE.NKO:=ZGP.NKO;
            _put:=1
         ?};
         {? _put>0
         || PX_STAGE.put()
         ?}
      ?};
      ~~
   ";
   ZGP.prefix();
   _result:=ZGP.for_each(_formula,1)
?};

{? _result
|| __UPG.msg('Zaktualizowano nowe pola w etapach planu strategicznego.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji nowych pól w etapach planu strategicznego.')
?};
TOPER.cntx_pop();
PX_STAGE.cntx_pop();
_result


\PX_STAGE_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli PX_STAGE - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja etapów planu strategicznego.'


\bi_problems
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pole o problemach w tabelach BI_PROC i BI_MSG
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh();
BI_MSG.cntx_psh();
BI_PROC.cntx_psh();
B_ROLE.cntx_psh();
B_ROLE.index('UNIK');
B_PROC.cntx_psh();
B_PROC.index('AKT');
B_PROC.prefix('N','T');

_result:=0;
_can_continue:=1;

_tab_msg:=tab_tmp(1,
   'MSG','STRING[255]','Nazwa pola 1'
);

:: Tworzenie roli i przydzielenie jej użytkowników oraz czynności
_rola:='Właściciel procesów';
{? B_PROC.first()
|| _buffer:=exec('buffer','#b_usrrol');
   {!
   |? _b_role:=null();
      {? B_PROC.OWNER<>null()
      ||
::       Jeżeli jest właściciel procesu, to zakładam w danej firmie
::       rolę 'Właściciel procesów' jeżeli jeszcze jej nie ma
         B_ROLE.prefix(B_PROC.FIRMA,_rola);
         {? B_ROLE.first()
         || _b_role:=B_ROLE.ref();
            _can_continue:=1
         || B_ROLE.blank();
            B_ROLE.FIRMA:=B_PROC.FIRMA;
            B_ROLE.NAME:=_rola;
            _can_continue:=B_ROLE.add();
            {? _can_continue>0
            || _b_role:=B_ROLE.ref();
               _msg:='Dodano rolę: %1 w firmie: %2'@[_rola,B_PROC.FIRMA().SYMBOL];
               _tab_msg.prefix(_msg,);
               {? _tab_msg.first()=0
               || _tab_msg.MSG:=_msg;
                  _tab_msg.add();
                  __UPG.msg(_msg)
               ?}
            || _msg:='Nie udało się dodać roli: %1 w firmie: %2'@[_rola,B_PROC.FIRMA().SYMBOL];
               _tab_msg.prefix(_msg,);
               {? _tab_msg.first()=0
               || _tab_msg.MSG:=_msg;
                  _tab_msg.add();
                  __UPG.msg(_msg)
               ?}
            ?}
         ?};

::       Roli przydzielam czynności
         {? _can_continue>0 & _b_role<>null()
         || {? var_pres('_uid')>100
            || obj_del(_uid)
            ?};
            _uid:=spli_str('ZPR_PRO_MODE.ZPR_PRO_MODW','.');
            {! _lp:=1 .. obj_len(_uid)
            |! {? exec('add_actrol_one','#b_role',_rola,_uid[_lp],B_PROC.FIRMA)
               || _msg:='Nadano roli: %1 uprawnienia do czynności: %2 w firmie: %3.' [_rola,_uid[_lp],B_PROC.FIRMA().SYMBOL];
                  _tab_msg.prefix(_msg,);
                  {? _tab_msg.first()=0
                  || _tab_msg.MSG:=_msg;
                     _tab_msg.add();
                     __UPG.msg(_msg)
                  ?}
               || _msg:='Nie udało się nadać roli: %1 uprawnień do czynności: %2 w firmie: %3.' [_rola,_uid[_lp],B_PROC.FIRMA().SYMBOL];
                  _tab_msg.prefix(_msg,);
                  {? _tab_msg.first()=0
                  || _tab_msg.MSG:=_msg;
                     _tab_msg.add();
                     __UPG.msg(_msg)
                  ?};
                  _can_continue:=0
               ?}
            !}
         ?};

::       Roli przydzielam użytkownika: właściciela procesu
         {? _can_continue>0 & _b_role<>null()
         || _buffer.FIRMA:=B_PROC.FIRMA;
            _buffer.USERS:=B_PROC.OWNER;
            _buffer.B_ROLE:=_b_role;
            {? exec('add','#b_usrrol',_buffer)<>null()
            || _msg:='Przypisano użytkownika: %1 do roli: %2 w firmie: %3' [B_PROC.OWNER().KOD,_rola,B_PROC.FIRMA().SYMBOL];
               _tab_msg.prefix(_msg,);
               {? _tab_msg.first()=0
               || _tab_msg.MSG:=_msg;
                  _tab_msg.add();
                  __UPG.msg(_msg)
               ?}
            || _msg:='Nie udało się przypisać użytkownika: %1 do roli: %2 w firmie: %3' [B_PROC.OWNER().KOD,_rola,B_PROC.FIRMA().SYMBOL];
               _tab_msg.prefix(_msg,);
               {? _tab_msg.first()=0
               || _tab_msg.MSG:=_msg;
                  _tab_msg.add();
                  __UPG.msg(_msg)
               ?};
               _can_continue:=0
            ?}
         ?}
      ?};
      B_PROC.next() & _can_continue>0
   !}
?};

{? _can_continue>0
||
   _formula:="
      BI_PROC.PROBLEM:='N';
      {? exec('FindInSet','#table','BI_TODO','ERROR','E',BI_PROC.ref())<>null()
      || BI_PROC.PROBLEM:='T'
      |? exec('isProblemsProc','#bi_msg',BI_PROC.ref())>0
      || BI_PROC.PROBLEM:='T'
      ?};
      BI_PROC.put();
      ~~
   ";
   BI_PROC.prefix();
   _can_continue:=BI_PROC.for_each(_formula,1);

   {? _can_continue>0
   ||
      _formula:="
         BI_MSG.PROBLEM:='N';
         {? BI_MSG.STAT_ANS<>__Status.RETURN
         || BI_MSG.PROBLEM:='T'
         ?};
         BI_MSG.TM_ERR:=exec('create','#tm_stamp',BI_MSG.D_ERR,BI_MSG.T_ERR);
         BI_MSG.TM_ANS:=exec('create','#tm_stamp',BI_MSG.D_ANS,BI_MSG.T_ANS);
         BI_MSG.put();
         ~~
      ";
      BI_MSG.prefix();
      _can_continue:=BI_MSG.for_each(_formula,1)
   ?};

   {? _can_continue>0
   || __UPG.msg('Zaktualizowano informację o zgłoszonych problemach w instancjach procesów.')
   || __UPG.msg('Wystąpił błąd podczas aktualizacji informacji o zgłoszonych problemach w instancjach procesów.')
   ?}
?};
{? _can_continue>0
|| _result:=1
?};
B_ROLE.cntx_pop();
B_PROC.cntx_pop();
BI_PROC.cntx_pop();
BI_MSG.cntx_pop();
USERS.cntx_pop();
_result


\bi_problems_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pole o problemach w tabelach BI_PROC i BI_MSG - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja informacji o zgłoszonych problemach w instancjach procesów.'


\s_zus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja słownika kodów ZUS.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
S_ZUS.cntx_psh();
S_ZUS.prefix();
_sb:=S_ZUS.size();
{? exec('s_zus_import','personel',0)
|| _ret:=1;
   _sa:=S_ZUS.size();
   {? _sb=_sa
   || __UPG.msg('Słownik kodów ZUS nie wymagał aktualizacji.')
   || __UPG.msg('Aktualizacja słownika kodów ZUS zakończyła się sukcesem (liczba nowych pozycji: %1).' [$(_sa-_sb)])
   ?}
|| _ret:=-1;
   __UPG.msg('Aktualizacja słownika kodów ZUS nie powiodła się.')
?};
S_ZUS.cntx_pop();
_ret


\s_zus_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja słownika kodów ZUS - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słownika kodów ZUS'


\convEANU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Przepisanie użytkowników - operatorów urządzeń mobilnych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_lrec:=0;
_ladd:=0;
_kodus:='opemob_';
_lp:=0;

_tryb:=0;
MOBPAR.cntx_psh();
MOBPAR.index('MG');
MOBPAR.prefix(null(),'T','M');
{? MOBPAR.first() || _tryb:=MOBPAR.TAK='T' ?};
MOBPAR.cntx_pop();
{? _tryb
|| EANU.cntx_psh();
   USERS.cntx_psh();
   EANU.index('USER');
   {? EANU.first()
   || {!
      |? _lrec+=1;
         {? EANU.AKT='T'
         || _lp+=1;
            _users:=exec('add_auto','users',EANU.DANE,_kodus+form(_lp,-3,0,'99'));
            {? _users<>null() & (USERS.prefix(); USERS.seek(_users))
            || USERS.MOBIL:='T';
               {? var_pres('IP',EANU)>0
               || USERS.IP:=EANU.IP
               || USERS.IP:=exec('gen_code_id','ekioski')
               ?};
               USERS.PASS:=EANU.PASS;
               USERS.put(1);
               EANU.AKT:='X';
               EANU.put(1);
               _ladd+=1
            ?}
         ?};
         EANU.next()
      !}
   ?};
   __UPG.msg('Utworzono użytkowników wg operatorów operacji mobilnych.'@);
   __UPG.msg('Przetworzono: %1 zapisów, z czego dodano %2 użytkowników mobilnych.'@[$_lrec,$_ladd]);
   EANU.cntx_pop();
   USERS.cntx_pop()
|| __UPG.msg('Zadanie potransferowe pominięte.'@);
   __UPG.msg('Nie jest to transfer z systemu Xpertis lub nie korzystano z operacji mobilnych w trybie ONLINE.'@)
?};
_res


\convEANU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.42]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\PAR_POKR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [18.42]
:: OPIS: Aktualizacja parametrów systemowych składników okresowych, Poprawka SPIS ER/WRT/XP/12.51/1803/0004
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new(6);
_tab[1]:=7051; _tab[2]:=7041; _tab[3]:=7055; _tab[4]:=7053; _tab[5]:=7006; _tab[6]:=7059;
PAR_POKR.index('PAR_POKR');
PAR_POKR.prefix(exec('ref_firma','ustawienia'));
{! _ind:=1..6
|! {? PAR_POKR.find_key(_tab[_ind])
   || PAR_POKR.DZ_12:='T';
      PAR_POKR.PTZO:='N';
      {? PAR_POKR.put(1)
      || __UPG.msg('Zaktualizowano składnik okresowy '+$_tab[_ind]+'.')
      || __UPG.msg('Nie zaktualizowano składnika okresowego '+$_tab[_ind]+'.')
      ?}
   || __UPG.msg('Nie zaktualizowano składnika okresowego '+$_tab[_ind]+'.')
   ?}
!};
1


\PAR_POKR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [18.42]
:: OPIS: Aktualizacja parametrów systemowych składników okresowych, Poprawka SPIS ER/WRT/XP/12.51/1803/0004
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametrów systemowych składników okresowych.'


\rubatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.42]
:: OPIS: Uzupełnienie atrybutów rubryk.
::----------------------------------------------------------------------------------------------------------------------
:: Poprawka SPIS ER/WRT/XP/12.51/1803/0016
exec('add_attr','rubatr',43,432,'Składniki do podstawy ekwiwalentu bez uzupełnienia',1,
   'Składniki w kwocie wypłaconej wliczane do podstawy ekwiwalentu bez uzupełniania o nieobecności. '+
   '\nWymagane jest zaznaczenie składnika płacowego jako wliczany do ekwiwalentu.'+
   '\nPobierane są tylko składniki, które są zaznaczone jako wliczane w kwocie wypłaconej.'
);
__UPG.msg('Dodano atrybut 432 - Składniki do podstawy ekwiwalentu bez uzupełnienia.');
1


\rubatr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.42]
:: OPIS: Uzupełnienie atrybutów rubryk - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie atrybutów rubryk'


\edeklaracje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.42]
:: OPIS: Dodanie nowych e-deklaracji
::----------------------------------------------------------------------------------------------------------------------
_dod_ver:="
   VAT_VER.index('VER_OD'); VAT_VER.prefix(_a,_c,_c,_b,_d);
   _jest:=VAT_VER.first();
   {? ~_jest
   || _old:={? _a='FKS' || 'FIKS' |? _a='PPL' || 'KALI' || '' ?};
      VAT_VER.prefix(_old,_c,_c,_b,_d);
      _jest:=VAT_VER.first()
   ?};
   {? _jest
   || _add:=0
   || _add:=1;
      VAT_VER.blank();
      VAT_VER.SYSTEM:=_a;
      VAT_VER.OD:=_b;
      VAT_VER.NAZWA:=_c;
      VAT_VER.NR:=_d
   ?};
   {? _<6 || _f:='' ?};
   {? _<5 || _e:=0 ?};
   _put:=0;
   {? VAT_VER.NRF<>_e || VAT_VER.NRF:=_e; _put+=1 ?};
   {? VAT_VER.WIERSZ<>_f || VAT_VER.WIERSZ:=_f; _put+=1 ?};
   VAT_VER.prefix();
   {? _add || VAT_VER.add(1) |? _put || VAT_VER.put(1) ?}
";
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
{? _dod_ver('PPL',date(2018,1,1),'PIT4R',7,7,',7,8,')
|| __UPG.msg('Dodano 7 wersja e-deklaracji PIT4R obowiązująca od dnia 2018.01.01.');
::import poprawnych struktur e-deklaracji PIT-4R wersja 7
   {? exec('edek_stru','edeklar','PIT4R',7,'pit-4r7_v1.dfg')
   || __UPG.msg('Dodano schemat 7 wersji e-deklaracji PIT4R.')
   || __UPG.msg('Brak aktualizacji, schemat 7 wersji e-deklaracji PIT4R już istnieje.')
   ?}
|| __UPG.msg('Brak aktualizacji, 7 wersja e-deklaracji PIT4R już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
1


\edeklaracje_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.42]
:: OPIS: Dodanie nowych e-deklaracji - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie wersji e-deklaracji'


\zo_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Uzupełnia znacznik 'Poprawność' dla formularzy ocen pracowników. Badanie poprawności zdefiniowanych kompetencji
::       dla formularza.
::----------------------------------------------------------------------------------------------------------------------
_lrec:=0;
_lmod:=0;
ZO_FORM.cntx_psh();
ZO_FORM.prefix();
_blad:=0;
_licz:=0;
_forms:=sql('select ZO_FORM.REFERENCE as REF from ZO_FORM where ZO_FORM.VALID=\'\'');
_loop:=_forms.first();
{? _loop
|| ZO_FORM.trig_off('*','*');
   FUN.prg_start(_forms.size(),'Przetwarzanie tabeli %1'@['ZO_FORM'],,,1);
   {!
   |? _loop
   |! _lrec+=1;
      _komps:=sql('
         select
            ZZ_KOMP.REFERENCE as REF,
            ZZ_KOMP.ZZ_KOMP as KOMP
         from
            ZO_KOMP join ZZ_KOMP join ZO_FORM
         where
            ZO_FORM.REFERENCE=\':_a\'
         order by
            2',
         _forms.REF
      );

      _loop:=_komps.first();
      {!
      |? _loop
      |! {? _komps.KOMP=0
         || _ref:=exec('FindAndGet','#table',ZZ_KOMP,_komps.REF,,,null());
            _komps.cntx_psh();
            _komps.prefix(_ref);
            {? _komps.first()
            || _blad:=1;
             _licz+=1
            ?};
            _komps.cntx_pop()
         ?};
         _loop:=_komps.next()
      !};
      {? ZO_FORM.seek(_forms.REF)
      || {? _blad
         || ZO_FORM.VALID:='N'
         || ZO_FORM.VALID:='T'
         ?};
         {? ZO_FORM.put()
         || _lmod+=1
         ?}
      ?};
      &_komps;
      _blad:=0;
      FUN.prg_next();
      _loop:=_forms.next()
   !};
   ZO_FORM.trig_on('*','*');
   FUN.prg_stop();
   __UPG.msg('Sprawdzono poprawność formularzy ocen okresowych pracowników.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów. Znaleziono %3 błędnych formularzy.'
             [$_lrec,$_lmod,$_licz])
|| __UPG.msg('Brak formularzy ocen okresowych do aktualizacji.')
?};
ZO_FORM.cntx_pop();
1


\zo_form_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Uzupełnia znacznik 'Poprawność' dla formularzy ocen pracowników. Badanie poprawności zdefiniowanych kompetencji
::       dla formularza.
::----------------------------------------------------------------------------------------------------------------------
'Sprawdzenie poprawności wygenerowanych formularzy ocen pracowników.'


\oddelegowania
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie wartości w nowych polach związanych z oddelegowaniami do pracy za granicą.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_licz:=1;
_TAB:=H.names();
{? _TAB.first()
|| {!
   |? _licz+={? _TAB.SIZE>0 || _TAB.SIZE ?};
      _TAB.next()
   !}
?};
:: Aktualizacja danych w tabeli z przebiegiem zatrudnienia.
FUN.prg_start(_licz,'Aktualizacja danych w tabeli z przebiegiem zatrudnienia.'@,,,1);
H.trig_off('*','*');
H.cntx_psh();
{? _TAB.first()
|| {!
   |? H.use(_TAB.NAME);
      H.prefix();
      _lrec:=H.size();
      {? _lrec
      || _lmod:=0;
         H.first();
         {!
         |? FUN.prg_next();
            {? H.ODDEL<>'T' & H.ODDEL<>'N'
            || H.ODDEL:=H.KODDEL:='N';
               _lmod+=H.put()
            || _lmod+=1
            ?};
            H.next()
         !};

         __UPG.msg(
            {? H.name()='_his_'
            || 'Tabela z danymi tymczasowymi przebiegu zatrudnienia:'
            || 'Tabela z danymi docelowymi przebiegu zatrudnienia:'
            ?}
         );
         {? _lrec-_lmod
         || {? _result || _result:=0 ?};
            __UPG.msg('- aktualizacja wartości w polach "Oddelegowanie" i "Praca w kilku krajach" nie powiodła się');
            __UPG.msg('- liczba rekordów, których nie udało się poprawić: %1 z %2'[$(_lrec-_lmod),$_lrec]);
            __UPG.msg('- dane należy poprawić ręcznie')
         || __UPG.msg('- aktualizacja wartości w polach "Oddelegowanie" i "Praca w kilku krajach"');
            __UPG.msg('- liczba poprawnych rekordów: %1'[$_lrec])
         ?}
      |? ~_lrec & H.name()='_hist'
      || {? _result=1 || _result:=-1 ?};
         __UPG.msg('Brak danych do aktualizacji w tabeli z przebiegiem zatrudnienia.')
      ?};
      _TAB.next()
   !}
?};
H.cntx_pop();
H.trig_on('*','*');
FUN.prg_stop();
:: Aktualizacja danych w tabeli pracowników.
{? _result
|| P.trig_off('*','*');
   P.cntx_psh();
   P.prefix();
   {? P.first()
   || _lrec:=P.size();
      _lmod:=0;
      FUN.prg_start(_lrec,'Aktualizacja pola "Oddelegowanie" w tabeli pracowników.'@,,,1);

      {!
      |? FUN.prg_next();
         {? P.ODDEL<>'T' & P.ODDEL<>'N'
         || P.ODDEL:='N';
            _lmod+=P.put(1)
         || _lmod+=1
         ?};
         P.next()
      !};

      {? _lrec-_lmod
      || {? _result || _result:=0 ?};
         __UPG.msg('Aktualizacja pola "Oddelegowanie" w tabeli pracowników nie powiodła się.');
         __UPG.msg('- liczba rekordów, których nie udało się poprawić: %1 z %2'[$(_lrec-_lmod),$_lrec]);
         __UPG.msg('- dane należy poprawić ręcznie')
      || __UPG.msg('%1 %2'
            [  'Aktualizacja pola "Oddelegowanie" w tabeli pracowników wykonana poprawnie.',
               'Liczba poprawnych rekordów: %1.'[$_lrec]
            ]
         )
      ?};

      FUN.prg_stop()
   || __UPG.msg('Brak danych do aktualizacji w tabeli pracowników.');
      _result:=-1
   ?};
   P.cntx_pop();
   P.trig_on('*','*')
?};

_result


\oddelegowania_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie wartości w nowych polach związanych z oddelegowaniami do pracy za granicą - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Oddelegowania do pracy za granicą - aktualizacja wartości pól w Przebiegu zatrudnienia.'


\rodzaje_oddelegowania
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie tabeli rodzajów pozycji dla oddelegowania do pracy za granicą.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
Cntx.psh(H_ODDR,R);
R.index('RUBKOD');
H_ODDR.clear();

_add:=
   "  H_ODDR.blank();
      H_ODDR.RODZ:=_a;
      H_ODDR.OPIS:=_b;
      H_ODDR.DIETA:=_c;
      H_ODDR.NOC:=_d;
      R.prefix(_e);
      H_ODDR.R:=
         {? R.first()
         || R.ref()
         || null()
         ?};
      H_ODDR.add()
   ";
{? H_ODDR.find_tab(1,'NOC',,'=','T')
|| __UPG.msg('Rodzaj rozliczena noclegu jest już wprowadzony do słownika.')
|| {? _add('Noclegi','Stawka za nocleg podczas oddelegowania do pracy za granicą','N','T',7073)
   || __UPG.msg('Rodzaj rozliczenia o nazwie "Noclegi" został dodany do słownika.')
   || __UPG.msg('Rodzaj rozliczenia o nazwie "Noclegi" nie został dodany do słownika.');
      _result:=0
   ?}
?};

{? H_ODDR.find_tab(1,'DIETA',,'=','T')
|| __UPG.msg('Rodzaj rozliczena diety jest już wprowadzony do słownika.')
|| {? _add('Diety','Kwota diety za dzień pobytu za granicą','T','N',7074)
   || __UPG.msg('Rodzaj rozliczenia o nazwie "Diety" został dodany do słownika.')
   || __UPG.msg('Rodzaj rozliczenia o nazwie "Diety" nie został dodany do słownika.');
      _result:=0
   ?}
?};

Cntx.pop(H_ODDR,R);
_result


\rodzaje_oddelegowania_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie tabeli rodzajów pozycji dla oddelegowania do pracy za granicą - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie tabeli rodzajów pozycji dla oddelegowania do pracy za granicą.'


\oddelegowania_skl_plac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Rubryki do wykazywania na liście płac składników związanych z rozliczeniem oddelegowań do pracy za granicą.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=
   {? +exec('import','rubryki','g','7073',1,0)
   || __UPG.msg('Nie dodano rubryki 7073 - Kwota za noclegi.');
      -1
   || __UPG.msg('Dodano rubrykę 7073 - Kwota za noclegi.');
      1
   ?};
_result:=
   {? +exec('import','rubryki','g','7074',1,0)
   || __UPG.msg('Nie dodano rubryki 7074 - Kwota diet.');
      -1
   || __UPG.msg('Dodano rubrykę 7074 - Kwota diet.');
      _result
   ?};

exec('add_attr','rubatr',9,901,'Oddelegowania do pracy za granicą',1,
   'Kwoty obniżające podstawę opodatkowania za dochody uzyskane podczas oddelegowania do pracy za granicą.'
);
__UPG.msg('Dodano atrybut 901 - Oddelegowania do pracy za granicą.');
exec('add_attr','rubatr',901,9011,
  'Kwota za noclegi obniżająca podstawę opodatkowania',1,
  'Kwota za noclegi, obniżająca podstawę opodatkowania za dochody uzyskane podczas oddelegowania do pracy za granicą.'
);
__UPG.msg('Dodano atrybut 9011 - Kwota za noclegi obniżająca podstawę opodatkowania.');
exec('add_attr','rubatr',901,9012,
  'Kwota z diet obniżająca podstawę opodatkowania',1,
  'Kwota z diet, obniżająca podstawę opodatkowania za dochody uzyskane podczas oddelegowania do pracy za granicą.'
);
__UPG.msg('Dodano atrybut 9012 - Kwota z diet obniżająca podstawę opodatkowania.');
exec('add_attr','rubatr',901,9013,
  'Kwota ze zwrotu kosztów przeniesienia służbowego',1,
  'Kwota do ze zwrotu kosztów przeniesienia służbowego do 200% wynagrodzenia należnego w miesiącu przeniesienia, '
  'obniżająca podstawę opodatkowania za dochody uzyskane podczas oddelegowania do pracy za granicą.'
);
__UPG.msg('Dodano atrybut 9013 - Kwota ze zwrotu kosztów przeniesienia służbowego.');


_result:=
   {? +exec('import','rubryki','g','7075',1,0)
   || __UPG.msg('Nie dodano rubryki 7075 - Noclegi - przychód.');
      -1
   || __UPG.msg('Dodano rubrykę 7075 - Noclegi - przychód.');
      _result
   ?};
_result:=
   {? +exec('import','rubryki','g','7076',1,0)
   || __UPG.msg('Nie dodano rubryki 7076 - Noclegi bez podatku.');
      -1
   || __UPG.msg('Dodano rubrykę 7076 - Noclegi bez podatku.');
      exec('add_uses','rubatr',7076,9,901,9011);
      __UPG.msg('Dodano rubrykę 7076 - Noclegi bez podatku do atrybutów 9, 901 i 9011');
      _result
   ?};
_result:=
   {? +exec('import','rubryki','g','7077',1,0)
   || __UPG.msg('Nie dodano rubryki 7077 - Diety bez podatku.');
      -1
   || __UPG.msg('Dodano rubrykę 7077 - Diety bez podatku.');
      exec('add_uses','rubatr',7077,9,901,9012);
      __UPG.msg('Dodano rubrykę 7077 - Diety bez podatku do atrybutów 9, 901 i 9012');
      _result
   ?};
_result:=
   {? +exec('import','rubryki','g','7078',1,0)
   || __UPG.msg('Nie dodano rubryki 7078 - P.służb.bez podatku.');
      -1
   || __UPG.msg('Dodano rubrykę 7078 - P.służb.bez podatku.');
      exec('add_uses','rubatr',7078,9,901,9013);
      __UPG.msg('Dodano rubrykę 7078 - P.służb.bez podatku do atrybutów 9, 901 i 9013');
      _result
   ?};
_result:=
   {? +exec('import','rubryki','g','7080',1,0)
   || __UPG.msg('Nie dodano rubryki 7080 - L.dni oddelegowania.');
      -1
   || __UPG.msg('Dodano rubrykę 7080 - L.dni oddelegowania.');
      _result
   ?};
_result:=
   {? +exec('import','rubryki','g','7081',1,0)
   || __UPG.msg('Nie dodano rubryki 7081 - Przych. opodat. zag.');
      -1
   || __UPG.msg('Dodano rubrykę 7081 - Przych. opodat. zag.');
      _result
   ?};

__UPG.msg('Wymagane jest zaktualizowanie formuł płacowych.');
_result


\oddelegowania_skl_plac_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Rubryki do wykazywania na liście płac składników związanych z rozliczeniem oddelegowań do pracy za granicą.
::       - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych rubryk do rozliczania oddelegowania do pracy za granicą.'


\oddelegowania_KST
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Dodanie pól "Limit kosztów zakwaterowania" i "Procent diety bez podatku" do zmiennej KST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('add_acr','#stalesys','LKZ','Limit kosztów zakwaterowania','T','T','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.LKZ','500',date())
|| __UPG.msg('Do stałych systemu dodano informację o limicie kosztów zakwaterowania.')
|| __UPG.msg('Do stałych systemu nie dodano informacji o limicie kosztów zakwaterowania.')
?};

exec('add_acr','#stalesys','PDBP','Procent diety bez podatku','T','T','T','KST','PPL');
   {? exec('kst_war_add','#stalesys','KST.PDBP','30',date())
|| __UPG.msg('Do stałych systemu dodano informację o procencie diet bez podatku.')
|| __UPG.msg('Do stałych systemu nie dodano informacji o procencie diet bez podatku.')
?};
1


\oddelegowania_KST_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Dodanie pól "Limit kosztów zakwaterowania" i "Procent diety bez podatku" do zmiennej KST - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - oddelegowania do pracy za granicą.'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',0)
|| __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.')
?}


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji.'


\DATY
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Naprawa pola STAMP_E do tabeli DATY
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_lrec:=_lmod:=0;
DATY.cntx_psh();
DATY.index('DATA');
DATY.prefix();
_size:=DATY.size();
{? DATY.first()
|| FUN.prg_start(_size,'Przetwarzanie tabeli %1'@['DATY'],,,1);
   {!
   |? _lrec+=1;

      {? DATY.ROK<2038
      ||
         _dt1:=date(DATY.ROK,DATY.MIESIAC,DATY.DZIEN_M)+1;
         _stamp_e:=tm_stamp(_dt1~1,_dt1~2,_dt1~3, 0,0,0);
         {? DATY.STAMP_E<>_stamp_e
         || DATY.STAMP_E:=_stamp_e;
            {? DATY.put() || _lmod+=1 ?}
         ?}
      ?};
      FUN.prg_next();
      DATY.next()
   !};
   FUN.prg_stop()
?};
DATY.cntx_pop();
__UPG.msg('Zaktualizowano tabelę DATY.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
_res


\DATY_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Naprawa pola STAMP_E do tabeli DATY - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli DATY'


\f_zatra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Aktualizacja czynności dla form współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lrec:=_lmod:=0;
_kody:='P';
_lista:='PKD_EZK_OPOD,PKD_EZK_OROD';

_kod:=spli_str(_kody,',');
_arr:=spli_str(_lista,',');

{! _ii:=1..obj_len(_kod)
|! __UPG.msg('Kody formy współpracy: %1' [_kod[_ii]]);
   _ret:=exec('f_zatra_add','f_zatr','T',_kod[_ii],_lista,1);
   {? _ret<>''
   || __UPG.msg(_ret);
      _result:=-1
   || __UPG.msg('Dodano wszystkie czynności z listy.')
   ?}
!};
_result


\f_zatra_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Aktualizacja czynności dla form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czynności dla form współpracy'


\kw_wolna_zas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.42]
:: OPIS: Dodanie pol do KST kwoty wolne dla potrąceń komorniczych z zasiłków
::----------------------------------------------------------------------------------------------------------------------
exec('add_acr','#stalesys','KWWOLALI','Kwota wolna potrącenia alimentacyjne','T','T','T','KST','PPL');
_dodano:=exec('kst_war_add','#stalesys','KST.KWWOLALI','0',date(0,0,0));
_dodano+=exec('kst_war_add','#stalesys','KST.KWWOLALI','500',date(2018,7,1));
exec('add_acr','#stalesys','KWWOLPOZ','Kwota wolna potrącenia niealimentacyjne','T','T','T','KST','PPL');
_dodano+=exec('kst_war_add','#stalesys','KST.KWWOLPOZ','0',date(0,0,0));
_dodano+=exec('kst_war_add','#stalesys','KST.KWWOLPOZ','825',date(2018,7,1));

{? _dodano
|| __UPG.msg('Dodano informacje o kwotach wolnych dla potrąceń komorniczych z zasiłków.')
?};
1


\kw_wolna_zas_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.42]
:: OPIS: Opis funkcji aktualizacji stałych systemu zwiazanych z kwotami wolne dla potrąceń komorniczych z zasiłków
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - kwoty wolne dla potrąceń komorniczych z zasiłków'


\wyp_sl_kom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.42]
:: OPIS: Aktualizacja słownika potrąceń komorniczych - znaczniki dla alimentów i potrąceń innych
::----------------------------------------------------------------------------------------------------------------------
_dodano:=0;
exec('__RUB','object');
KOM_RP.index('KOM_RP');
KOM_RP.prefix;
{? KOM_RP.first()
|| {!
   |? _dodano+=1;
      {? __RUB.sys_attr(KOM_RP.R,21111)
      || KOM_RP.ALIMENTY:='T'
      || KOM_RP.ALIMENTY:='N'
      ?};
      {? __RUB.sys_attr(KOM_RP.R,21114)
      || KOM_RP.INNE:='T'
      || KOM_RP.INNE:='N'
      ?};
      KOM_RP.put(1);
      KOM_RP.next
   !}
?};
{? _dodano
|| __UPG.msg('Aktualizacja zakończona sukcesem')
?};
1


\wyp_sl_kom_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.42]
:: OPIS: Opis funkcji aktualizującej słownik potrąceń komorniczych - znaczniki dla alimentów i potrąceń innych
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słownika potrąceń komorniczych - znaczniki dla alimentów i potrąceń innych'


\tar_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Przepisanie uprawnień do grup uprawnień do cenników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lrec:=_lmod:=0;

_lcmg_o:=exec('FindInSet','#table','B_ELE','SYMBOL','ZWS_PAR_LCMG');
_lcmg_o:=exec('FindInSet','#table','B_ACTION','B_ELE',_lcmg_o);
_lcmg_n:=exec('FindInSet','#table','B_ELE','SYMBOL','LZK_CEN_LCMG');
_lcmg_n:=exec('FindInSet','#table','B_ACTION','B_ELE',_lcmg_n);
_lcsp_o:=exec('FindInSet','#table','B_ELE','SYMBOL','ZWS_PAR_LCSP');
_lcsp_o:=exec('FindInSet','#table','B_ACTION','B_ELE',_lcsp_o);
_lcsp_n:=exec('FindInSet','#table','B_ELE','SYMBOL','LSP_CEN_LCSP');
_lcsp_n:=exec('FindInSet','#table','B_ACTION','B_ELE',_lcsp_n);

_Tab:=sql('
   select REFERENCE REF from B_ACTROL where B_ACTROL.B_ACTION=:_a or B_ACTROL.B_ACTION=:_b'
   ,_lcmg_o,_lcsp_o);

B_ACTROL.cntx_psh();
B_ACTROL.prefix();
_loop:=_Tab.first();
{!
|? _loop
|!
   _lrec+=1;
   {? B_ACTROL.seek(_Tab.REF)
   ||
      {? B_ACTROL.B_ACTION=_lcmg_o
      ||
         B_ACTROL.cntx_psh();
         B_ACTROL.prefix(_lcmg_n,B_ACTROL.B_ROLE);
         {? ~B_ACTROL.first()
         ||
            B_ACTROL.prefix();
            B_ACTROL.B_ACTION:=_lcmg_n;
            {? B_ACTROL.put() || _lmod+=1 ?}
         ?};
         B_ACTROL.cntx_pop()
      ||
         B_ACTROL.cntx_psh();
         B_ACTROL.prefix(_lcsp_n,B_ACTROL.B_ROLE);
         {? ~B_ACTROL.first()
         ||
            B_ACTROL.prefix();
            B_ACTROL.B_ACTION:=_lcsp_n;
            {? B_ACTROL.put() || _lmod+=1 ?}
         ?};
         B_ACTROL.cntx_pop()
      ?}
   ?};
   _loop:=_Tab.next()
!};
B_ACTROL.cntx_pop();
__UPG.msg('Przepisano uprawnienia do grup uprawnień do cenników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
_result


\tar_upr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Przepisanie uprawnień do grup uprawnień do cenników - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Przepisanie uprawnień do grup uprawnień do cenników'


\tar_perm_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Nadaje uprawnienia do danych cenników
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
USERS.index('USR_KKOD');
USERS.prefix();
{? USERS.first()
|| B_PERM.index('NAME'); B_PERM.prefix();
   B_PERM.find_key('TARS',);
   B_PERM_U.index('USER');
   {!
   |?
      {! _ii:=1..2
      |!
         {? B_PERM.find_key({? _ii=1 || 'TARS' || 'TARD' ?},)
         ||
            B_PERM_U.prefix(REF.FIRMA,USERS.ref(),B_PERM.ref());
            {? B_PERM_U.first()
            || B_PERM_U.ACCESS:='T';
               B_PERM_U.put()
            || TARUP.index('USER');
               TARUP.prefix(USERS.ref());
               _loop:=TARUP.first();
               {!
               |? _loop
               |!
                  _loop:=TARUP.del()
               !}
            ?};
            B_PERM.next()
         ?}
      !};
      USERS.next()
   !}
?};
__UPG.msg('Nadano uprawnienia do danych w zakresie: Uprawnienia do cenników sprzedaży, Uprawnienia do cenników dostawców.');
_result


\tar_perm_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Nadaje uprawnienia do danych cenników - opis
::----------------------------------------------------------------------------------------------------------------------
'Nadaje uprawnienia do danych cenników'


\skid_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.42]
:: OPIS: Aktualizacja listy parametrów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');

:: było już w 17.00
PAR_SKID.set(197,1,0,'','N','Personel: wysyłanie e-maili o zatrudnieniu lub zwolnienu pracownika','''T'',''N''');

:: doszło w 17.14 a nie było w upgrade_1714.fml
PAR_SKID.set(330,1,1,'','N',
   'Personel: Sposób liczenia terminów (m.in. do stażu, urlopu) zgodny z potocznym pojęciem roku i miesiąca? '
   ,'''T'',''N'''
);
PAR_SKID.ae_set(330,"_fld:=~-fld();
                     {? FUN.ask('Czy przeliczyć okresy zatrudnienia według wybranego sposobu liczenia terminów?')
                     || {? var_pres('__KAL')>0 || obj_del(__KAL) ?};
                        exec('__KAL','object');
                        __KAL.set330(_fld='T');
                        STAZ.cntx_psh();
                        STAZ.prefix();
                        STAZ.for_each(\"exec('akt_hist','staz')\");
                        STAZ.cntx_pop();
                        PARAMETR.TRESC:=_fld
                     ?};1");

:: doszło w 17.42 a nie było w upgrade_1742.fml
PAR_SKID.set(258,1,1,'','N','Personel: rachunki z umów cywilnoprawnych wypłacane na listach płac ','''T'',''N''');
PAR_SKID.set(331,5,1,'','?','Personel: Sposób wyboru pliku z podpisem',
   '\'? - pytaj użytkownika\','+
   '\'S - szukaj pliku na serwerze\','+
   '\'L - szukaj pliku na końcówce użytkownika\''
);

:: doszło w 18.02 a nie było w upgrade_1802.fml
PAR_SKID.set(186,2,0,'','14','Personel: koniec okresu kontroli nieaktualnych badań do widoku miesięcznego');
PAR_SKID.set(252,1,1,'','N',
   'Personel: czy weryfikować pracowników z zerowym planowaniem czasu pracy?','''T'',''N'''
);
PAR_SKID.set(254,1,1,'','T',
   'Personel: Automatyczna kwalifikacja danych o godzinach przepracowanych na podstawie WE/WY','''T'',''N'''
);
PAR_SKID.set(255,1,1,'','T',
   'Personel: Automatyczna kwalifikacja zapisów WE/WY dla poprzedniej/następnej doby roboczej','''T'',''N'''
);
PAR_SKID.set(316,1,1,'','N',
   'Personel: wyliczana wartość dopełnienia średniotygodniowego dla okresu rozliczeniowego','''T'',''N'''
);
PAR_SKID.set(317,1,1,'','N',
   'Personel: Delegowanie na wydział - pobieranie oddelegowań z wydziałów podrzędnych','''T'',''N'''
);
PAR_SKID.set(323,1,1,'','T',
   'Personel: Weryfikować tydzień pracy od początku okresu? (N - weryfikacja od daty zatrudnienia)','''T'',''N'''
);
PAR_SKID.set(324,1,1,'','T',
   'Personel: Kontrolować 35 godz. nieprzerwany odpoczynek tygodniowy, w okresie rozpoczynającym się od niedzieli?',
   '''T'',''N'''
);
PAR_SKID.set(325,1,1,'','N',
   'Personel: Kontrolować 35 godz. nieprzerwany odpoczynek tyg., dołączając porzedzającą sobotę?','''T'',''N'''
);
PAR_SKID.set(326,1,1,'','T',
   'Personel: Uwzględniać w weryfikacji (planowanie, grafiki), czas nieobecności jako czas odpoczynku dobowego?',
   '''T'',''N'''
);
PAR_SKID.set(327,1,1,'','T','Personel: Wstępna weryfikacja (planowanie, grafiki) czasu pracy?','''T'',''N''');
PAR_SKID.set(328,1,1,'','T','Personel: Możliwe planowanie z nachodzeniem na wolną niedzielę?','''T'',''N''');
PAR_SKID.set(329,1,1,'','T','Personel: Możliwe planowanie z nachodzeniem na wolne święto?','''T'',''N''');

:: doszło w 18.22 a nie było w upgrade_1822.fml
PAR_SKID.set(320,1,1,'','N',
   'Personel: Uwzględniać w Harmonogramach ustawę o ograniczeniu handlu w niedzielę?','''T'',''N'''
);
PAR_SKID.set(325,1,1,'','N',
   'Personel: Kontrolować 35 godz. nieprzerwany odpoczynek tyg., dołączając poprzedzającą sobotę?','''T'',''N'''
);

:: doszło w 18.42
PAR_SKID.set(259,1,1,'','N',
   'Personel: Czy wyliczać potrącenia komornicze zgodnie z miesiącem podatkowym listy?','''T'',''N'''
);
PAR_SKID.set(261,1,1,'','N','Personel: ewidencja i rozliczanie oddelegowania do pracy za granicą','''T'',''N''');
PAR_SKID.set(353,1,1,'','N','Personel: Planowanie i rozliczanie czasu pracy na obiektach kosztowych','''T'',''N''');

__UPG.msg('Utworzenie listy parametrów wykorzystywanych w dziedzinach: PPL, PKD, PRC.');
1


\skid_par_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [18.42]
:: OPIS: Aktualizacja listy parametrów - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy parametrów'


\f_02
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Naprawia problem z kwotą VAT w przelewie z podzieloną płatnością do faktur z odwrotnym obciążeniem
::----------------------------------------------------------------------------------------------------------------------
exec('f_02','napraw_f');
__UPG.msg('Naprawiono problem z kwotą VAT w przelewie z podzieloną płatnością do faktur z odwrotnym obciążeniem.');
1


\f_02_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Naprawia problem z kwotą VAT w przelewie z podzieloną płatnością do faktur z odwrotnym obciążeniem
::----------------------------------------------------------------------------------------------------------------------
'Naprawia problem z kwotą VAT w przelewie z podzieloną płatnością do faktur z odwrotnym obciążeniem'


\odd_dok_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Uzupełnienie pola ODD_DOK tabeli SRDO. Informacja o jednostce księgowej, w której wystawiono dokument.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('maski','srodobj');
SRDO.cntx_psh();
SRDO.index('AUTO');
SRDO.prefix();
{? SRDO.first()
|| {!
   |? {? SRDO.ODD_DOK=null
      || SRST.cntx_psh();
         SRST.index('SROD');
         SRST.prefix();
         {? SRST.find_key(SRDO.SRSR,SRDO.ROK_F,SRDO.OKRO_F)
         || SRDO.ODD_DOK:=SRST.ODD;
            SRDO.put()
         ?};
         SRST.cntx_pop()
      ?};
      SRDO.next()
   !}
?};
SRDO.cntx_pop();
__UPG.msg('Zakończono uzupełnianie informacji o jednostce księgowej w tabeli(SRDO).');
1


\odd_dok_update_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Uzupełnienie pola ODD_DOK tabeli SRDO. Informacja o jednostce księgowej, w której wystawiono dokument - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełniono informację o jednostce księgowej dla dokumentów'


\f_03
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Naprawia problem z błędnym adresem do aktualizacji e-deklaracji
::       Spis: Poprawia schemat e-deklaracji dla osób fizycznych
::----------------------------------------------------------------------------------------------------------------------
exec('f_03','napraw_f');
exec('f_05','napraw_f');
__UPG.msg(
   'Poprawiono adres i błąd z negatywną weryfikacją deklaracji VAT-7 (18) dla podatnika będącego osobą fizyczną.'
);
1


\f_03_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Naprawia problem z błędnym adresem do aktualizacji e-deklaracji
::       Spis: Poprawia schemat e-deklaracji dla osób fizycznych
::----------------------------------------------------------------------------------------------------------------------
'Ustawia nowy adres do aktualizacji e-deklaracji: https://www.macrologic.pl/update.\n'
'Poprawa błędu z weryfikacja negatywną podatnika będącego osobą fizyczną w Deklaracji VAT-7 (18)'


\dek_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Uaktualnienie pola DEK_NAG.NAZ
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('DekNaz');
DekNaz:=0;
DEK_NAG.prefix();
DEK_NAG.for_each("
   {? DEK_NAG.NAZ=''
   || DEK_NAG.NAZ:=exec('dek_nag_naz','dok_fks_aut_dok','',0);
      DekNaz+=DEK_NAG.put()
   ?}
",1);
{? DekNaz
|| __UPG.msg('Uzupełniono nazwy schematów dziedzinowych.')
|| __UPG.msg('Uzupełnienie nazw schematów dziedzinowych nie było wymagane.')
?};
VAR_DEL.delete('DekNaz');
1


\dek_nag_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Uaktualnienie pola DEK_NAG.NAZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie nazwy schematu dziedzinowego'


\dek_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Wprowadzenie numeracji pozycji schematów dziedzinowych (DEK_POZ.LP)
::----------------------------------------------------------------------------------------------------------------------
DEK_NAG.for_each("
   _lp:=0;
   DEK_POZ.index('DEK_POZ'); DEK_POZ.prefix(DEK_NAG.ref());
   {? DEK_POZ.first()
   || {!
      |? DEK_POZ.LP:=(_lp+=1);
         DEK_POZ.put();
         DEK_POZ.next()
      !}
   ?}
",1);
__UPG.msg('Przenumerowano pozycje schematów dziedzinowych');
1


\dek_poz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Wprowadzenie numeracji pozycji schematów dziedzinowych (DEK_POZ.LP) - opis
::----------------------------------------------------------------------------------------------------------------------
'Wprowadzenie numeracji pozycji schematów dziedzinowych'


\form_plat_pos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Aktualizacja form płatności dla POSa
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Dodanie parametru w formach płatności
RS.cntx_psh();
RS.index('RS');
{? RS.find_key('~Płatności',)
||
   exec('new_par','slo_slu',RS.ref(),4,'Płatność dostępna w POS','TAK_NIE')
?};
RS.cntx_pop();
__UPG.msg('Dodano parametr "Płatność dostępna w POS" dla słowników użytkownika opartych na wzorcu ~Płatności.');

:: Dodanie nowej formy płatności - Karta - płatność kartą

exec('czytaj','#stalesys',,XINFO,'SLP');
_add_karta:=1;

SLO.cntx_psh();
SLO.index('SL');
SLO.prefix(XINFO.SLP);
_loop:=SLO.first() & XINFO.SLP;
{!
|? _loop
|!
   _add_karta:=_add_karta & (SLO.KOD<>'KARTA');
   ZR_SLO.cntx_psh();
   ZR_SLO.index('SLO_NR');
   ZR_SLO.prefix(SLO.ref());
   {? ZR_SLO.find_key(1)
   ||
      {? ZR_SLO.WAR='T'
      ||
         {? ~ZR_SLO.find_key(4)
         ||
::          Płatność dostępna w POS
            ZR_SLO.NR:=4;
            {? ZR_SLO.add()
            ||
               __UPG.msg('Forma płatności %1 ze słownika użytkownika %2 dostępna w punkcie sprzedaży detalicznej.'
                  [SLO.KOD,XINFO.SLP().NAZ])
            ?}
         ?}
      ?}
   ?};
   ZR_SLO.cntx_pop();
   _loop:=SLO.next()
!};
{? XINFO.SLP & _add_karta
||
   SLO.blank();
   SLO.SLU:=XINFO.SLP;
   SLO.KOD:='KARTA';
   SLO.TR:='Płatność kartą';
   {? SLO.add()
   ||
      __UPG.msg('Dodano formę płatności KARTA w słowniku użytkownika %1.'[XINFO.SLP().NAZ]);

      ZR_SLO.cntx_psh();
      ZR_SLO.index('SLO_NR');
      ZR_SLO.prefix(SLO.ref());
      ZR_SLO.blank();
      ZR_SLO.SLO:=SLO.ref();
      {? ~ZR_SLO.find_key(1)
      ||
::       Czy gotówkowa?
         ZR_SLO.NR:=1;
         ZR_SLO.WAR:='N';
         ZR_SLO.add()
      ?};
      {? ~ZR_SLO.find_key(4)
      ||
::       Płatność dostępna w POS
         ZR_SLO.NR:=4;
         ZR_SLO.WAR:='T';
         ZR_SLO.add()
      ?};
      ZR_SLO.cntx_pop()
   ?}
?};
SLO.cntx_pop();
_res


\form_plat_pos_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja form płatności dla POSa'


\p_04
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: ER/WRT/XP/18.22/1808/0002
::       Nieprawidłowe nazwisko w słowniku
::----------------------------------------------------------------------------------------------------------------------
exec('p_04','napraw_p');
__UPG.msg('Uaktualniono dane w słownikach opartych na tabeli OSOBA.');
1


\p_04_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: ER/WRT/XP/18.22/1808/0002
::       Nieprawidłowe nazwisko w słowniku - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie danych w słownikach opartych na tabeli OSOBA.'


\norm_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Przepisanie uprawnień do norm wyposażenia i atrybutów pracowników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lrec:=_lmod:=0;

_wnor_o:=exec('FindInSet','#table','B_ELE','SYMBOL','ZWS_PAR_WNOR');
_wnor_o:=exec('FindInSet','#table','B_ACTION','B_ELE',_wnor_o);
_wnor_n:=exec('FindInSet','#table','B_ELE','SYMBOL','WYP_LWN_WNOR');
_wnor_n:=exec('FindInSet','#table','B_ACTION','B_ELE',_wnor_n);
_wwym_o:=exec('FindInSet','#table','B_ELE','SYMBOL','ZWS_PAR_WWYM');
_wwym_o:=exec('FindInSet','#table','B_ACTION','B_ELE',_wwym_o);
_wwym_n:=exec('FindInSet','#table','B_ELE','SYMBOL','WYP_LWN_WWYM');
_wwym_n:=exec('FindInSet','#table','B_ACTION','B_ELE',_wwym_n);

_Tab:=sql('
   select REFERENCE REF from B_ACTROL where B_ACTROL.B_ACTION=:_a or B_ACTROL.B_ACTION=:_b'
   ,_wnor_o,_wwym_o);

B_ACTROL.cntx_psh();
B_ACTROL.index('ACTION');
B_ACTROL.prefix();
_loop:=_Tab.first();
{!
|? _loop
|!
   _lrec+=1;
   {? B_ACTROL.seek(_Tab.REF)
   ||
      {? B_ACTROL.B_ACTION=_wnor_o
      ||
         {? _wnor_n<>null()
         || B_ACTROL.cntx_psh();
            B_ACTROL.prefix(_wnor_n,B_ACTROL.B_ROLE);
            {? ~B_ACTROL.first()
            ||
               B_ACTROL.prefix();
               B_ACTROL.B_ACTION:=_wnor_n;
               {? B_ACTROL.put() || _lmod+=1 ?}
            ?};
            B_ACTROL.cntx_pop()
         ?}
      ||
         {? _wwym_n<>null()
         || B_ACTROL.cntx_psh();
            B_ACTROL.prefix(_wwym_n,B_ACTROL.B_ROLE);
            {? ~B_ACTROL.first()
            ||
               B_ACTROL.prefix();
               B_ACTROL.B_ACTION:=_wwym_n;
               {? B_ACTROL.put() || _lmod+=1 ?}
            ?};
            B_ACTROL.cntx_pop()
         ?}
      ?}
   ?};
   _loop:=_Tab.next()
!};
B_ACTROL.cntx_pop();
{? _wnor_n<>null() & _wwym_n<>null()
|| __UPG.msg('Przepisano uprawnienia do norm wyposażenia i atrybutów pracowników.')
|| __UPG.msg('Wystąpił błąd podczas przepisywania uprawnień do norm wyposażenia i atrybutów pracowników.');
   _msg:='Nie odnaleziono czynności: ';
   {? _wnor_n=null()
   || _msg+='WYP_LWN_WNOR - Normy wyposażenia';
      {? _wwym_n<>null()
      || _msg+=', WYP_LWN_WWYM - Atrybuty pracowników'
      ?}
   || _msg+='WYP_LWN_WWYM - Atrybuty pracowników'
   ?};
   _msg+='.';
   __UPG.msg(_msg)
?};
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
_result


\norm_upr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Przepisanie uprawnień do norm wyposażenia i atrybutów pracowników - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Przepisanie uprawnień do norm wyposażenia i atrybutów pracowników'


\fo_100227
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Ustawienie uniwersalnego parametru aplikacyjnego 100227
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_nr:=100227;
FO_DEF.cntx_psh();
FO_DEF.index('NR');
FO_DEF.prefix('A','N',_nr);
{? FO_DEF.first()
||
   _interface:=exec('interface','#params','A',_nr);
   _interface.save('T')
?};
FO_DEF.cntx_pop();
__UPG.msg('Ustawiono uniwersalny parametr aplikacyjny "100227 Kolejność danych adresowych" na T.');
1


\fo_100227_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Ustawienie uniwersalnego parametru aplikacyjnego 100227 - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Ustawienie uniwersalnego parametru aplikacyjnego "100227 Kolejność danych adresowych" na T (Kod pocztowy, Miasto, Ulica)'


\kor_url_rodzicielski
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Korekta urlopu rodzicielskiego na macierzyński a podwyższenie zas. macierzyńskiego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Poprawka SPIS ER/WRT/XP/12.51/1803/0037
{? +exec('import','rubryki','g','7071',1,0)
|| __UPG.msg('Nie dodano rubryki 7071 - Kor. podw. zas. mac. i atrybutów z nią związanych.')
|| R.clear();
   exec('__RUB','object');
   __RUB.fill();
   exec('add_uses','rubatr',7071,100,1002,10026,100262);
   __UPG.msg('Dodano rubrykę 7071 - Kor. podw. zas. mac. oraz atrybuty z nią związane.');
   __UPG.msg('Wymagane jest zaktualizowanie formuł płacowych.')
?};
1


\kor_url_rodzicielski_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Korekta urlopu rodzicielskiego na macierzyński a podwyższenie zas. macierzyńskiego - opis.
::       Poprawka SPIS ER/WRT/XP/12.51/1803/0037
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Korekta urlopu rodzicielskiego na macierzyński a podwyższenie zasiłku macierzyńskiego'


\del_funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Usunięcie nieaktywnych czynności z obszaru Ustawienia i parametryzacja.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('usun_czyn','funpar','ZWS_PAR_WNOR');
exec('usun_czyn','funpar','ZWS_PAR_WWYM');
exec('usun_czyn','funpar','ZWS_PAR_LCMG');
exec('usun_czyn','funpar','ZWS_PAR_LCSP');
__UPG.msg('Usunięto nieaktywne czynności z drzewa parametryzacji w obszarze: Ustawienia i parametryzacja.')


\del_funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Usunięcie nieaktywnych czynności z obszaru Ustawienia i parametryzacja - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie nieaktywnych czynności z drzewa parametryzacji w obszarze: Ustawienia i parametryzacja.'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\kal_wzor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Uzupełnienie nowych pól w tabeli KAL_WZOR
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('KalWzor');
KalWzor:=0;
KAL_WZOR.cntx_psh();
KAL_WZOR.index('KAL_WZOR');
KAL_WZOR.prefix();
KAL_WZOR.for_each("
   {? KAL_WZOR.H_MOD=''
   || KAL_WZOR.H_MOD:='N';
      KalWzor+=KAL_WZOR.put()
   ?}
",1);
{? KalWzor
|| __UPG.msg('Uzupełniono wartości pól dla wzorców kalendarzy.')
|| __UPG.msg('Uzupełnienie wartości pól dla wzorców kalendarzy nie było wymagane.')
?};
KAL_WZOR.cntx_pop();
VAR_DEL.delete('KalWzor');
1


\kal_wzor_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Uzupełnienie nowych pól w tabeli KAL_WZOR - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wartości pól dla wzorców kalendarzy.'


\MOB_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Przepisanie uprawnień do czynności mobilnych
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lrec:=_lmod:=0;

_lst:=spli_str('LMG_MOB_DAOP'
'.LMG_MOB_DINW'
'.LMG_MOB_DKOM'
'.LMG_MOB_DKZK'
'.LMG_MOB_DLOK'
'.LMG_MOB_DPRZ'
'.LMG_MOB_DWYD'
'.LMG_MOB_PSTC'
'.LMG_MOB_RETO'
'.LMG_MOM_DADM'
'.LMG_MOM_DINW'
'.LMG_MOM_DKOM'
'.LMG_MOM_DLOI'
'.LMG_MOM_DLOK'
'.LMG_MOM_DLOR'
'.LMG_MOM_DPRZ'
'.LMG_MOM_DSTN'
'.LMG_MOM_DWYD'
'.LMG_MOM_ELOG'
'.LMG_PAL_DPAL'
'.LMG_PAL_PPAL'
,'.');
_len:=obj_len(_lst);

_act:=tab_tmp(1,'ROLD','STRING[16]',''
       ,'RNEW','STRING[16]',''
       ,'OLD','STRING[20]',''
       ,'NEW','STRING[20]','');

_in:='(';
{! _i.._len
|! _act.blank();
   _act.OLD:=_lst[_i];
   _act.NEW:='LMO'+(3-_act.OLD);
   _old:=exec('FindInSet','#table','B_ELE','SYMBOL',_act.OLD);
   _old:=exec('FindInSet','#table','B_ACTION','B_ELE',_old);
   _new:=exec('FindInSet','#table','B_ELE','SYMBOL',_act.NEW);
   _new:=exec('FindInSet','#table','B_ACTION','B_ELE',_new);
   _act.ROLD:=$_old;
   _act.RNEW:=$_new;
   {? _act.RNEW<>'' & _act.ROLD<>''
   || _in+='\''+_act.ROLD+'\',';
      _act.add(1)
   ?}
!};
obj_del(_lst);
_in:=(_in-1)+')';

{? +_in>=18
|| _sql:='select REFERENCE REF from B_ACTROL where B_ACTROL.B_ACTION in '+_in;

   _Tab:=sql(_sql);

   B_ACTROL.cntx_psh();
   B_ACTROL.prefix();
   _loop:=_Tab.first();
   {!
   |? _loop
   |!
      _lrec+=1;
      {? B_ACTROL.seek(_Tab.REF)
      || _act.clear();
         _act.prefix($B_ACTROL.B_ACTION,);
         {? _act.first()
         || B_ACTROL.cntx_psh();
            _ref:=exec('FindAndGet','#table',B_ACTION,_act.RNEW,,,null());
            B_ACTROL.prefix(_ref,B_ACTROL.B_ROLE);
            {? ~B_ACTROL.first()
            || B_ACTROL.prefix();
               B_ACTROL.B_ACTION:=_ref;
               {? B_ACTROL.put() || _lmod+=1 ?}
            ?};
            B_ACTROL.cntx_pop()
         ?}
       ?};
      _loop:=_Tab.next()
   !};
   B_ACTROL.cntx_pop();
   obj_del(_act)
?};

__UPG.msg('Przepisano uprawnienia dla czynności mobilnego magazynu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$_lrec,$_lmod]);
_result


\MOB_upr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Przepisanie uprawnień do czynności mobilnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Przepisanie uprawnień do czynności mobilnego magazynu'


\B_USRDOM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Odtworzenie uprawnień do dziedzin produktowych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('napraw','#b_usrdom',1);
__UPG.msg('Zaaktualizowano uprawnienia do dziedzin produktowych dla użytkowników.');
_res


\B_USRDOM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Odtworzenie uprawnień do dziedzin produktowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Odtworzenie uprawnień do dziedzin produktowych dla użytkowników'


\A_OKRD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: KOD BŁĘDU: ER/WRT/XP/12.51/1809/0010
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
P.cntx_psh();
P.index('OSOBA');
{? P.first()
|| PROGRESS.set(P.size(),'Przetwarzanie danych pracowników w okresach rozliczeniowych...'@);
   A_OKRD.cntx_psh();
   A_OKRD.use('x_okrdi');
   A_OKRD.index('POD');
   {!
   |? A_OKRD.prefix(P.ref());
::    sprawdzamy, czy pierwszy domyślny okres nie zaczyna się przed datą zatrudnienia
      {? A_OKRD.first() & A_OKRD.OD<P.DZA
      || _old_od:=A_OKRD.OD;
         A_OKRD.OD:=P.DZA;
         _result+=A_OKRD.put(1);
         exec('norm_okrd','okres',A_OKRD.P,0);
          {? var_pres('_t_okrp')>100 || obj_del(_t_okrp) ?};
          _t_okrp:=exec('getOkrpFromDate','grafik',P.ref(),A_OKRD.OD);

          {? _t_okrp.first() & _t_okrp.OD<P.DZA
          || A_OKRP.cntx_psh();
             A_OKRP.index('A_OKRDP');
             {? A_OKRP.seek(_t_okrp.NR,_t_okrp.MASK)
             || A_OKRP.OD:=P.DZA;
                _result+=A_OKRP.put(1)
             ?};
             A_OKRP.cntx_pop()
          ?}
      ?};
      PROGRESS.next();
      P.next()
   !};
   PROGRESS.close();
   A_OKRD.cntx_pop()
?};
P.cntx_pop();
{? _result
|| __UPG.msg('Naprawiono przypisania pracowników do pierwszego okresu rozliczeniowego.')
|| __UPG.msg('Naprawienie przypisania pracowników do pierwszego okresu rozliczeniowego nie było wymagane.')
?};
1


\A_OKRD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Naprawa przypisania pracowników do pierwszego okresu rozliczeniowego - opis
::----------------------------------------------------------------------------------------------------------------------
'Naprawa przypisania pracowników do pierwszego okresu rozliczeniowego'


\stalesys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja stałych systemu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__UPG.msg('Prognozowane przeciętne miesięczne wynagrodzenie brutto w gospodarce narodowej:');
_acr:='PRGWYN';

{? exec('add_acr','#stalesys',_acr,'Prognozowane miesięczne','T','T','T','KST','PPL')
|| __UPG.msg(' * Parametr został dodany do widoku.')
|| __UPG.msg(' * Parametr nie został dodany do widoku (istniał już?).')
?};

_fld:='KST.'+_acr;

{? exec('kst_war_add','#stalesys',_fld,'0',date(0,0,0))
|| __UPG.msg(' * Zapisano wartość początkową parametru.')
|| __UPG.msg(' * Wartość początkowa parametru nie została zapisana (istniała już?).')
?};

_dt:=date(2018,1,1);
{? exec('kst_war_add','#stalesys',_fld,'4443',_dt)
|| __UPG.msg(' * Zapisano wartość parametru obowiązującą od %1.' [_dt$1])
|| __UPG.msg(' * Wartość parametru obowiązująca od %1 nie została zapisana (istniała już?).' [_dt$1])
?};

1


\stalesys_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja stałych systemu - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\gr_sik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Uzupełnienie pola GR_SIK.TYP_KLIK
::----------------------------------------------------------------------------------------------------------------------
GrSikAkt:=obj_new('ILE','OK');
GrSikAkt.ILE:=0;
GrSikAkt.OK:=0;
GR_SIK.prefix();
GR_SIK.for_each("
   GrSikAkt.ILE+=1;
   {? GR_SIK.TYP_KLIK=''
   || GR_SIK.TYP_KLIK:='NN';
      GrSikAkt.OK+=GR_SIK.put()
   ?}
",1);
__UPG.msg('Uzupełnienie pola sprawozdań finansowych');
__UPG.msg('Liczba przetworzonych zapisów: %1'[$GrSikAkt.ILE]);
__UPG.msg('Liczba poprawionych zapisów: %1'[$GrSikAkt.OK]);
VAR_DEL.delete('GrSikAkt');
1


\gr_sik_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Uzupełnienie pola GR_SIK.TYP_KLIK - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola sprawozdań finansowych'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\AFORM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia pola w tabeli AFORM
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   {? AFORM.DSP=''
   || AFORM.DSP:='N';
      AFORM.put()
   ?}
";
AFORM.prefix();
_result:=AFORM.for_each(_formula,1);
{? _result
|| __UPG.msg('Zaktualizowano formuły kalkulacji/analiz zleceń.')
|| __UPG.msg('Wystąpił błąd podczas aktualizacji formuł kalkulacji/analiz zleceń.')
?};
_result


\AFORM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Uzupełnia pola w tabeli AFORM - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja formuł kalkulacji/anali zleceń.'


\zasw_ZUS_do_oddel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie słownika rodzajów zaświadczeń ZUS do oddelegowania za granicę.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('zasw_ZUS_do_oddel','dane_startowe');

{? _result
|| __UPG.msg('Uzupełniono słownik rodzajów zaświadczeń o ubezpieczeniu do oddelegowania za granicę.');
   __UPG.msg('Liczba dodanych wierszy: %1.'[$_result])
|| __UPG.msg('Nie uzupełniono słownika rodzajów zaświadczeń o ubezpieczeniu do oddelegowania za granicę.')
?};
1


\zasw_ZUS_do_oddel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.42]
:: OPIS: Uzupełnienie słownika rodzajów zaświadczeń ZUS do oddelegowania za granicę - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słownika rodzajów zaświadczeń ZUS do oddelegowania za granicę.'


\WARP100
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Uzupełnienie dodatkowego pola związanego z obsługą amortyzacji jednorazowej do 100 tys.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SRST.cntx_psh(); SRSR.cntx_psh();
exec('maski','srodobj');
SRSR.prefix(); SRST.prefix();
__SRSTOF:=1;
SRST.for_each("{? SRST.SRSR().AMOR100='T' & SRST.WARP100<>SRSR.WARP100
               || SRST.WARP100:=SRSR.WARP100; SRST.put()
               ?}",1);
__SRSTOF:=0;
SRST.cntx_pop(); SRSR.cntx_pop();
__UPG.msg('Uzupełniono dodatkowe pole związane z obsługą amortyzacji jednorazowej.')


\WARP100_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Uzupełnienie dodatkowego pola związanego z obsługą amortyzacji jednorazowej do 100 tys. - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie danych związanych z obsługą amortyzacji jednorazowej do 100 tys. zł.'


\param38
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Ustawia wartości formuły 300301 wg parametru 38
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_lrec:=_lmod:=0;

exec('set','#params',300301,{? exec('get_par','#parametr',38)='N' || 'T' || 'N' ?});

__UPG.msg('Ustawiono wartość formuły 300301 wg parametur 38.');
_result


\param38_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Ustawia wartości formuły 300301 wg parametru 38 - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Ustawienie wartości formuły 300301 wg parametru 38'


\MOB_deactProc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Deaktywowanie procesów związanych ze starymi czynnościami mobilnymi
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_in:='(\'LMG_MOB_DAOP\',\'LMG_MOB_DINW\',\'LMG_MOB_DKOM\',\'LMG_MOB_DKZK\''
     ',\'LMG_MOB_DLOK\',\'LMG_MOB_DPRZ\',\'LMG_MOB_DWYD\',\'LMG_MOB_RETO\''
     ',\'LMG_MOM_ELOG\')';

__UPG.msg('Kontrola procesów związanych z czynnościami mobilnymi.'@);

:: deaktywacja procesów zawierających dane czynności
_proc:=sql('select distinct B_PREL.B_PROC REF from B_PREL join B_PROC join B_ELE '
           'where B_PROC.FIRMA=:_a and B_PREL.CLASS=\'B_ACTION\' and B_ELE.SYMBOL in '+_in,REF.FIRMA);
_proc.clear();
{? _proc.first()
|| {!
   |? B_PROC.cntx_psh();
      B_PROC.prefix();
      {? B_PROC.seek(_proc.REF,) & B_PROC.ACTIVE='T' & B_PROC.MICRO='N' & B_PROC.ACCEPTED='T'
      || B_PROC.ACTIVE:='N';
         {? B_PROC.put(1)
         || exec('del_b_proc','#b_harm',B_PROC.ref());
            exec('proc_actrol_del','#b_role',B_PROC.ref());
            __UPG.msg('Deaktywowano proces %1 - %2.'@[B_PROC.SYMBOL,B_PROC.NAME])
         ?}
      ?};
      B_PROC.cntx_pop();
      _proc.next()
   !}
?};
obj_del(_proc);
__UPG.msg('Należy zaimportować nowe procesy związane z czynnościami mobilnymi.'@);

:: dodanie dodatkowego statusu
_buffer:=exec('buffer','#bi_stat');
_buffer.SYMBOL:=exec('NO_ACTION','#bi_stat','symbol');
_buffer.NAME:='Czynność nie jest dostępna';
exec('add','#bi_stat',_buffer);
obj_del(_buffer);

:: zgłoszenie błedów do uruchomionych procesów zawierających dane czynności
_todo:=sql('select distinct BI_TODO.REFERENCE REF from BI_TODO '
           'join B_PREL join B_PROC using(B_PREL.B_PROC,B_PROC.REFERENCE) join B_ELE '
           'where B_PROC.FIRMA=:_a and B_ELE.CLASS=\'B_ACTION\' and B_ELE.SYMBOL in '+_in,REF.FIRMA);
_todo.clear();
_lerr:=0;
{? _todo.first()
|| {!
   |? BI_TODO.cntx_psh();
      BI_TODO.prefix();
      {? BI_TODO.seek(_todo.REF,)
       & (exec('statRunTodo','#bi_todo',BI_TODO.BI_STAT,-1) | BI_TODO.BI_STAT=exec('TODO_ERROR','#bi_stat'))
       & exec('todoError','#bi_msg',-2,'Zadanie nie może zostać uruchomione - czynności danego elementu nie jest dostępna')>0
      || _lerr+=1
      ?};
      BI_TODO.cntx_pop();
      _todo.next()
   !}
?};
obj_del(_todo);
{? _lerr>0 || __UPG.msg('Liczba zadań na liście TODO, do których zgłoszono problem %1.'@[$_lerr]) ?};

_result


\MOB_deactProc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Deaktywowanie procesów związanych ze starymi czynnościami mobilnymi - opis
::----------------------------------------------------------------------------------------------------------------------
'Deaktywacja procesów związanych ze starymi czynnościami dotyczącymi urządzeń mobilnych'


\OP_SP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.22]
:: OPIS: Aktualizacja pola OP.SP
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh();
_op:=OP.names();
{? _op.first()
|| {!
   |? echo('Aktualizacja rozrachunków: '+_op.NAME);
      OP.use(_op.NAME);
      OP.prefix();
      OP.for_each(" {? OP.SP='' || OP.SP:='N'; OP.put() ?} ",1);
      _op.next()
   !}
?};
OP.cntx_pop();
1


\OP_SP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.22]
:: OPIS: Aktualizacja pola OP.SP - opis
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/18.22/1809/0025 - SP: brak uzupełnienia pola OP.SP - pozostaje puste po zaksięgowaniu dokumentu'


\sch_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Importuje "personelowe" wnioski w obiegu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:="
{? _a
|| __UPG.msg('Zakończono import wniosku: \"%1\".'@[_b])
|| __UPG.msg('Nie udało się zaimportować wniosku: \"%1\".'@[_b])
?}
";

_nazwa:=exec('nazwa_rachunek','obiegi');
_msg(exec('imp_wn','sch_imp',_nazwa,0,0),_nazwa);
_nazwa:=exec('nazwa_woz','obiegi');
_msg(exec('imp_wn','sch_imp',_nazwa,0,0),_nazwa);
_nazwa:=exec('nazwa_wozda','obiegi');
_msg(exec('imp_wn','sch_imp',_nazwa,0,0),_nazwa)


\sch_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Importuje "personelowe" wnioski w obiegu - opis
::----------------------------------------------------------------------------------------------------------------------
'Import typów wniosków w obiegu'


\rodo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja konfiguracji RODO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('lic_or','#b_domain','ROD')
|| {? FUN.ask(
         'Aktualizacji wymaga konfiguracja "Zarządzanie danymi osobowymi".\n'
         '\n'
         'Jeżeli klient pracuje na standardowej wersji konfiguracji,\n'
         'to aktualizacja może być wykonana automatycznie.\n'
         'W tym celu należy wykonać następujące kroki:\n'
         '1. Uruchom w Mericie obszar "Ustawienia i parametryzacja".\n'
         '2. W zakładce "Parametryzacja" ustaw się na pozycji "Zarządzanie danymi osobowymi".\n'
         '3. Wybierz akcję "Importuj" (nie pomyl z "Importuj wszystkie").\n'
         '4. Wybierz opcję: "Nadpisuj istniejące".\n'
         '5. Wskaż katalog merit\\transfer\\rodo2 '
           '(pamiętaj aby wcześniej wkopiować do niego odpowiednie pliki - patrz nota).\n'
         '6. Zapoznaj się z ewentualnymi komunikatami.\n'
         '7. Zamknij okno z podsumowaniem importu.\n'
         '\n'
         'Jeżeli konfiguracja była u klienta modyfikowana, to aktualizację należy\n'
         'przeprowadzić na podstawie danych z pliku transfer\\rodo2\\parametryzacja_zdo.xlsx.\n'
         'Dotyczy to wierszy:\n'
         ' - Wykonanie postanowień umowy o świadczenie usług z podwykonawcą zależnym.\n'
         ' - Realizacja praw majątkowych po zmarłym pracowniku.\n'
         '\n'
         'Czy prace opisane powyżej zostały wykonane?'
      )
   || __UPG.msg('Użytkownik %1 potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      1
   || __UPG.msg('Użytkownik %1 nie potwierdził wykonania zadania.' [exec('operatorKod','#users')]);
      0
   ?}
|| {? exec('import_cfg','rodo',1)
   || __UPG.msg('Automatyczny import konfiguracji zakończył się suckesem.');
      1
   || __UPG.msg('Automatyczny import konfiguracji zakończył się porażką.');
      0
   ?}
?}


\rodo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Aktualizacja konfiguracji RODO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zarządzanie danymi osobowymi - aktualizacja konfiguracji '


\zws_par_fspr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Dodanie czynności ZWS_PAR_FSPR do ról z czynnością FKS_KSP_DDEF. Czynność FKS_KSP_DDEF usuwa z roli
::----------------------------------------------------------------------------------------------------------------------
_zmiana:=0;
_zws:=exec('ref','#b_action','ZWS_PAR_FSPR');
_fks:=exec('ref','#b_action','FKS_KSP_DDEF');
{? _zws & _fks
|| B_ACTROL.cntx_psh();
   B_ACTROL.index('ACTION'); B_ACTROL.prefix(_fks);
   {!
   |? {? B_ACTROL.first()
      || B_ACTROL.cntx_psh();
         B_ACTROL.prefix();
         B_ACTROL.B_ACTION:=_zws;
         _zmiana+=B_ACTROL.put(1);
         B_ACTROL.cntx_pop();
         1
      ?}
   !};
   B_ACTROL.cntx_pop()
?};
_dom:=exec('domain_ref','#b_domain','FKS');
{? _dom & _zmiana
|| _jest:=0;
   B_USRDOM.cntx_psh();
   B_USRDOM.index('S1'); B_USRDOM.prefix(_dom);
   {? B_USRDOM.first()
   || {!
      |? B_USRROL.cntx_psh();
         B_USRROL.index('S1'); B_USRROL.prefix(B_USRDOM.USERS);
         {? B_USRROL.first()
         || {!
            |? B_ACTROL.cntx_psh();
               B_ACTROL.index('B_DOMAIN'); B_ACTROL.prefix(B_USRROL.B_ROLE);
               _jest:=B_ACTROL.find_key(_dom);
               B_ACTROL.cntx_pop();
               _jest=0 & B_USRROL.next()
            !}
         ?};
         B_USRROL.cntx_pop();
         {? _jest
         || B_USRDOM.next()
         || B_USRDOM.del()
         ?}
      !}
   ?};
   B_USRDOM.cntx_pop()
?};
{? _zmiana
|| __UPG.msg('Zamiana czynności %1 na %2 w rolach.'@['FKS_KSP_DDEF','ZWS_PAR_FSPR'])
|| __UPG.msg('Zmiany nie były wymagane.'@)
?};
1




\zws_par_fspr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Dodanie czynności ZWS_PAR_FSPR do ról z czynnością FKS_KSP_DDEF. Czynność FKS_KSP_DDEF usuwa z roli -
::       opis
::----------------------------------------------------------------------------------------------------------------------
'Zamiana czynności FKS_KSP_DDEF na ZWS_PAR_FSPR w rolach'


\ROKP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Uzupełnienie nowego pola na rok podatkowy dokumentu
::----------------------------------------------------------------------------------------------------------------------
SRDO.cntx_psh();
_maski:=SRDO.names();
{? _maski.first()
|| {! |?
      SRDO.use(_maski.NAME);
      SRDO.prefix();
      {? SRDO.first()
      || SRDO.for_each("{? SRDO.ROKP=0 || SRDO.ROKP:=SRDO.ROK; SRDO.put() ?}",1)
      ?};
      _maski.next()
   !}
?};
SRDO.cntx_pop();
__UPG.msg('Uzupełniono dane dotyczące roku podatkowego dokumentów środków trwałych.')


\ROKP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Uzupełnienie nowego pola na rok podatkowy dokumentu - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie nowego pola: Rok podatkowy dokumentu w tabeli dokumentów środków trwałych.'


\_zdo_01
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: KOD BŁĘDU: ER/WRT/XP/18.42/1810/0011 (kopia: ER/WRT/XP/12.51/1808/0025)
::       Formuła pomocnicza.
::   WE: [_a] [STRING] - Zakres przetwarzania:
::                       'f' - tabele firmowe [domyślnie];
::                       'w' - tabele wspólne.
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_zakres:={? var_pres('_a')=type_of('') & _a='w' || 'w' || 'f' ?};

_warunek:=$(
   "pth_dir(':%1.mdb' [gsub(_a.name(1),'?','ą')])"+
   {? _zakres='f' || '=' || '<>' ?}+
   '\''+gsub(pth_dir(':ąąąąąąąą.mdb'),'\\','\\\\')+'\''
);

_tTABLE:=type_of(OSOBA);

:: Napis konfigurujący zakres przetwarzania. Definicja jest wspólna dla wielu wersji. Oznacza to, że w konkretnej wersji
:: może nie istnieć jakaś z wymienionych tabel, lub nie mieć wszystkich wymienionych pól.
:: Separator: '|'.
:: Budowa jednego elementu: 'T[:P1:O1[:P2:O2[:P3:O3]]]', gdzie:
::    T  - akronim przetwarzanej tabeli;
::    P1 - akronim pola typu _P [domyślnie: 'P'];
::    O1 - akronim pola typu _OSOBA [domyslnie: 'OSOBA'].
::    ..
::    Pn - akronim pola typu _P;
::    On - akronim pola typu _OSOBA.
_arr:=spli_str(
   '|A_OKRP|DEKL_POD:PRAC_ET:OS_PODAT|NBK|OS_VIEW|P_PZ:P:P_OSO:PZ:PZ_OSO|P_WEB_CX'+
   '|PM_DOMP|PM_DYSP|PM_NAG|PM_PREM|PM_RODZ|R_SZYCH|RD_OKR'+
   '|RP_ETOC:DOD_P:DOD_OS|RP_OSET:WER_P:WER_OS|RP_PRET:WER_P:WER_OS|RP_PROC:WER_P:WER_OS'+
   '|RP_ZAP:WER_P:WER_OS:AKC_P:AKC_OS|STRST_P|SZB_WYK|SZK_PSZK'
   '|SZK_ZAP:P:OSOBA:WER_KTO:WER_OS:ZGL_KTO:ZGL_OS|VAT_DEK:P:OS_PODAT|WT_NWU|ZC'+
   '|ZP_DET|ZP_OKR:AKC_P:AKC_OS|ZP_POZ:AKC_P:AKC_OS:RED_P:RED_OS',
   '|'
);

:: Funkcja budująca treść formuły wykonywanej dla każdego przetwarzanego rekordu.
::    _a [STRING] - Akronim tabeli.
::    _b [STRING] - Akronim pola typu _P.
::    _c [STRING] - Akronim pola typu _OSOBA.
_gf:=$(
   "'{? '+_a+'.'+_b+'<>null() & '+_a+'.'+_c+'<>null() & '+_a+'.'+_c+'<>'+_a+'.'+_b+'().OSOBA'+' '+"+
   "'|| '+_a+'.'+_c+':='+_a+'.'+_b+'().OSOBA; '+"+
   "{? _d || '_put+=1' || 'params_get().stat.put+='+_a+'.put()' ?}+' '+"+
   "'?}; '"
);

:: Zmienna ma charakter informacyjny. Zawiera akronimy pól (w układzie 'T.P'), które nie występują w bieżącej wersji.
_brak:='';

_stat:=obj_new('put');
params_set('stat',_stat);

OSOBA.cntx_psh();
OSOBA.prefix();
P.cntx_psh();
P.prefix();

_step:=0;
_size:=obj_len(_arr);
{! _la:=1 .. _size
|! _tab:=spli_str(_arr[_la],':');
   _step+=1;
   _proc:=(100*_step/_size)$2;
   progress(_proc,'Przetwarzanie danych ('+form(_proc,6,2)+'%) ',FUN.TYT);
   {? var_pres(_tab[1])=_tTABLE
   || _TAB:=($_tab[1])();
      {? _warunek(_TAB)
      || _lt:=obj_len(_tab);
         _fml:='';
         {? _lt=1
         || _vp:=var_pres('P',_TAB)=26;
            _vo:=var_pres('OSOBA',_TAB)=26;
            {? _vp & _vo
            || _fml:=_gf(_tab[1],'P','OSOBA',0)-2
            || {? ~_vp
               || _brak+=_tab[1]+'.P, '
               ?};
               {? ~_vo
               || _brak+=_tab[1]+'.OSOBA, '
               ?}
            ?}
         || {! _lp:=2 // 2 .. _lt
            |! _vp:=var_pres(_tab[_lp],_TAB)=26;
               _vo:=var_pres(_tab[_lp+1],_TAB)=26;
               {? _vp & _vo
               || _fml+=_gf(_tab[1],_tab[_lp],_tab[_lp+1],_lt>3)
               || {? ~_vp
                  || _brak+=_tab[1]+'.'+_tab[_lp]+', '
                  ?};
                  {? ~_vo
                  || _brak+=_tab[1]+'.'+_tab[_lp+1]+', '
                  ?}
               ?}
            !};
            {? _lt>3
            || _fml:='_put:=0; '+_fml+'{? _put || params_get().stat.put+='+_tab[1]+'.put() ?}'
            || _fml:=_fml-2
            ?}
         ?};
         {? _fml<>''
         || _TAB.cntx_psh();
            _NAMES:=_TAB.names();
            _ln:=_NAMES.first();
            _stat.put:=0;
            {!
            |? _ln
            |! _TAB.use(_NAMES.NAME);
               _TAB.prefix();
               _TAB.for_each($_fml);
               _ln:=_NAMES.next()
            !};
            {? _stat.put
            || __UPG.msg('%1: liczba poprawionych zapisów: %2.' [_tab[1],$_stat.put])
            || __UPG.msg('%1: brak błędnych zapisów.' [_tab[1]])
            ?};
            obj_del(_NAMES);
            _TAB.cntx_pop()
         ?}
      ?};
      obj_del(_TAB)
   ?};
   obj_del(_tab)
!};
prgs_clr();

{? _brak<>''
|| __UPG.msg('Zapisy nieobsłużone: %1 .' [_brak-2])
?};

P.cntx_pop();
OSOBA.cntx_pop();

1


\zdo_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach wspólnych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('_zdo_01','upgrade_1842','w')


\zdo_w_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach wspólnych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Naprawa powiązań danych zanonimizowanych w tabelach wspólnych'


\zdo_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach firmowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('_zdo_01','upgrade_1842','f')


\zdo_f_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Naprawa powiązań danych zanonimizowanych w tabelach firmowych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Naprawa powiązań danych zanonimizowanych w tabelach firmowych'


\listopad_12
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Nowe święto 12 listopada 2018 r.
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
_tab:=exec('p_05','napraw_p');
{? type_of(_tab)=118
|| {? _tab.first()
   || _return:=1;
      {!
      |? {? _tab.PARENT=0
         || {? _tab.DONE='T'
            || __UPG.msg('Wykonano zadanie - %1'[_tab.TEXT])
            || {? _tab.SEL='T'
               || __UPG.msg('Brak danych dla zadania - %1'[_tab.TEXT])
               || __UPG.msg('Nie przetwarzano zadania - %1'[_tab.TEXT])
               ?}
            ?}
         ?};
         _tab.next()
      !}
   || _return:=-1
   ?}
?};
_return


\listopad_12_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Nowe święto 12 listopada 2018 r.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja związana z wprowadzeniem nowego święta na 12 listopada 2018 r. '
'związanego z 100-leciem Odzyskania Niepodległości'


\tabkrst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: ER/WRT/XP/18.22/1811/0016
::       Uzupełnienie wskazania na tabelę klasyfikacji środków trwałych w okresach obrachunkowych
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
ROK_F.index('ROKPOCZ');
ROK_F.prefix();
{? ROK_F.find_key(REF.FIRMA,date(2018,1,1)) & ROK_F.ZK='T'
||
:: zmiana wskazania na klasyfikację w okresach
   _krst:=null;
   TAB_KRST.cntx_psh();
   TAB_KRST.index('TABKRST');
   TAB_KRST.prefix(2018);
   {? TAB_KRST.first() || _krst:=TAB_KRST.ref() ?};
   TAB_KRST.cntx_pop();
   {? _krst
   || OKRO_F.cntx_psh();
      OKRO_F.index('ROK');
      OKRO_F.prefix(ROK_F.ref());
      {? OKRO_F.first() & OKRO_F.TAB_KRST<>_krst
      || {! |?
            OKRO_F.TAB_KRST:=_krst;
            OKRO_F.put();
            OKRO_F.next()
         !}
      ?};
      OKRO_F.cntx_pop()
   ?}
?};
ROK_F.cntx_pop();
1


\tabkrst_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: ER/WRT/XP/18.22/1811/0016
::       Uzupełnienie wskazania na tabelę klasyfikacji środków trwałych w okresach obrachunkowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wskazania na nową tabelę klasyfikacji środków trwałych w okresach obrachunkowych roku 2018.'


\plugins
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: PR/WRT/XP/18.42/1812/0027
::       Uzupełnia definicję wtyczek wdrożeniowych
::----------------------------------------------------------------------------------------------------------------------
exec('init','plugins');
__UPG.msg('Uzupełniono definicję wtyczek wdrożeniowych.');
1


\plugins_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: PR/WRT/XP/18.42/1812/0027
::       Uzupełnia definicję wtyczek wdrożeniowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie definicji wtyczek wdrożeniowych:\n'
'BUFFERS_F_001'

:Sign Version 2.0 jowisz:1048 2023/06/23 14:16:37 60a395d3a537b5c1e0df9957d4bf64d8dcdd9151e588b59e6135e7c72b5a47e25ddace7931977549f2e6f9fb50acf3e2f669aa4e22356f88748d6d08b886cdc68c35e4a79e00be9586f58ea13b24ff8e739db3ae055e06754489b33e370680e6b5aa6dc4e418560a72b523dbae5e9aa004a4c770f3781bbd629d8edcab5fd4bd

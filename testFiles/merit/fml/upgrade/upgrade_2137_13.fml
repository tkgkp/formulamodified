:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2137_13.fml
:: Utworzony: 07.02.2022
:: Autor: DG
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.14 na 21.37
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Główna formuła aktualizacji 21.37
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',1,,,,'T');
__UPG.add_task('RU',,,'PPL',1,,,,'T');
__UPG.add_task('stalesys_ini',,,'ZWS',,,,,'T');
__UPG.add_task('kst_pl',,,'PPL',,,,,'T');
__UPG.add_task('rubryki_placowe',,,'PPL',2,,,,'T');
__UPG.add_task('report',,,'ZWS',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('f_zatra_add',,'N','ZWS',1,,,1,'T');
__UPG.add_task('role_personel',,'N','ZWS',1,,,1,'T');
__UPG.add_task('P_NPOD',,'N','PPL',,,,1,'T');
:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
__UPG.add_task('formuly_placowe_Polski_Lad','N','N','PPL',1,,,1,'T');
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [DG] [21.37]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA','.');
_dom:=spli_str('PPL','.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\RU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Uzupełnienie nowego pola w tabeli RU.
::----------------------------------------------------------------------------------------------------------------------
_cnt:=0;
_err:=0;

RU.cntx_psh();
RU.clear();
_size:=RU.size();
{? var_pres('PTPZP',RU)>0
|| _loop:=RU.first();
   {!
   |? _loop
   |! {? RU.PTPZP=''
      || {? RU.K='1'
         || RU.PTPZP:='T'
         || RU.PTPZP:='N'
         ?};
         {? RU.put()
         || _cnt+=1
         || _err+=1
         ?}
      ?};
      _loop:=RU.next()
   !}
?};
RU.cntx_pop();

__UPG.msg('Liczba wszystkich wierszy: %1.'[$_size]);
__UPG.msg('Liczba zaktualizowanych wierszy: %1.'[$_cnt]);
__UPG.msg('Błędy aktualizacji wierszy: %1.'[$_err]);

{? _err>0 || -1 || 1 ?}


\RU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Uzupełnienie nowego pola w tabeli RU - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie nowego pola w tabeli RU.'


\f_zatra_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Lista czynności, dla których przypisane zostaną formy współpracy. Założenia:
:: 1. Czynności są oddzielone przecinkami.
:: 2. W ramach czynności przekazywana jest również informacja o kodach form współpracy (oddzielonych znakiem dwukropka).
::    Zakładamy, że duże litery dotyczą form docelowych.
_lista:=spli_str(
   'PPL_PLL_PNZP:P:Z:K:R:T,'
   'PPL_PLL_RNZP:P:Z:K:R:T',
   ','
);

_ret:=1;
B_ACTION.cntx_psh();
B_ACTION.f_clear();
B_ACTION.index('UNIK');
B_ACTION.prefix();
_lenl:=obj_len(_lista);
{! _lp:=1 .. _lenl
|! _acz:=spli_str(_lista[_lp],':');
   _uid:=_acz[1];
   {? B_ACTION.find_key(_uid,)
   || _lena:=obj_len(_acz);
      {! _lf:=2 .. _lena
      |! _fz:=_acz[_lf];
         __UPG.msg('Forma współpracy: %1 | Czynność: %2 - %3' [_fz,_uid,B_ACTION.NAME]);
         _add:=exec('f_zatra_add','f_zatr',{? -_fz=_fz || 'N' || 'T' ?},_fz,_uid);
         {? _add=''
         || __UPG.msg(' - uprawnienia nadane')
         || __UPG.msg(_add);
            _ret:=-1
         ?}
      !}
   || __UPG.msg('"%1" - nieprawidłowy kod czynności.' [_uid]);
      _ret:=-1
   ?};
   obj_del(_acz)
!};
B_ACTION.cntx_pop();
_ret


\f_zatra_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Nadanie czynnościom uprawnień do form współpracy'


\role_personel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi.
::----------------------------------------------------------------------------------------------------------------------
{? exec('roleActions','upgrade',
      'Przeglądający Zleceniobiorców,'
      'Przeglądający Płace,'
      'Dyrektor HR,'
      'Kierownik ds płac',
      'PPL_PLL_PNZP'
   ) &
   exec('roleActions','upgrade',
      'Specjallista ds. zleceniobiorców,'
      'Specjalista ds. płac',
      'PPL_PLL_RNZP'
   )

|| 1
|| -1
?}


\role_personel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ról/uprawnień związana z czynnościami personelowymi.'


\kst_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie pól do KST związanych z Polskim Ładem (zwolnienia przychodu od podatku i ulgi dla klasy średniej
::----------------------------------------------------------------------------------------------------------------------
:: Dodanie pola określającego limit przychodów dla przesunięcia zaliczki.
{? var_pres('PZ_LP',KST)>0
|| exec('add_acr','#stalesys','PZ_LP','Limit przychodów dla przesunięcia zaliczki','T','T','T','KST','PPL');
   _dodano:=exec('kst_war_add','#stalesys','KST.PZ_LP','0',date(0,0,0));
   _dodano+=exec('kst_war_add','#stalesys','KST.PZ_LP','12800',date(2022,1,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o limicie przychodów dla przesunięcia zaliczki.')
   ?}
?};

1


\kst_pl_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Opis funkcji aktualizacji stałych systemu zwiazanych z Polskim Ładem
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - przesunięcia zaliczki.'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\rubryki_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie rubryk i atrybutów płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

{? +exec('import','rubryki','g','7173,7174,7175,7176,7177,7178,7179,7180,7181',1,0)
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);
{? _result=-1 || return(_result) ?};

__RUB.fill();

:: Aktualizacja atrybutów rubryk.
   exec('add_attr','rubatr',906,9067,'ZDR: składka zdrowotna do odliczenia',1,
         'ZDR: składka zdrowotna do odliczenia od zaliczki wyliczonej wg zasad na dzień 31.12.2021r.',,7149);

exec('add_attr','rubatr',9,907,'Dodatkowe informacje podatkowe',1,
      'Dodatkowe informacje podatkowe potrzebne do naliczenia zaliczki na podatek wg zasad 31.12.2021r.',
         ,7170,7171,7172,7173,7174,7175,7176,7177);
   exec('add_attr','rubatr',907,9071,'OLD: Koszty uzyskania przychodu',1,
         'OLD: Koszty uzyskania przychodu',,7173,7178);
      exec('add_attr','rubatr',9071,90711,'OLD: Koszty uzyskania wg zasad 31.12.2021r.',1,
         'OLD: Koszty uzyskania wyliczone wg zasad na dzień 31.12.2021r.',,7178);
      exec('add_attr','rubatr',9071,90712,'OLD: Koszty uzyskania PA wg zasad 31.12.2021r.',1,
         'OLD: Koszty uzyskania dla praw autorskich wg zasad 31.12.2021r.',,7173);
   exec('add_attr','rubatr',907,9072,'OLD: Ulga podatkowa wg zasad 31.12.2021r.',1,
         'OLD: Ulga podatkowa wyliczona wg zasad na dzień  31.12.2021r.',,7179);
   exec('add_attr','rubatr',907,9073,'Zaliczka na podatek wg zasad 31.12.2021r.',1,
         'OLD: zaliczka na podatek wg zasad 31.12.2021r.',,7171);
   exec('add_attr','rubatr',907,9074,'Zaliczka na podatek od 2022r.',1,
         'Zaliczka na podatek od 2022r.',,7172);
   exec('add_attr','rubatr',907,9075,'OLD: Dochód I próg wg zasad 31.12.2021r.',1,
         'OLD: Dochód I próg wyliczony wg zasad na dzień 31.12.2021r.',,7175);
   exec('add_attr','rubatr',907,9076,'OLD: Dochód II próg wg zasad 31.12.2021r.',1,
         'OLD: Dochód II próg wyliczony wg zasad na dzień 31.12.2021r.',,7176);
   exec('add_attr','rubatr',907,9077,'Nadwyżka wyliczonej zaliczki na podatek',1,
         'Nadwyżka wyliczonej zaliczki na podatek',,7170);
   exec('add_attr','rubatr',907,9078,'Różnica ujemna do rozliczenia',1,
         'Różnica ujemna do zwrotu zaliczki na podatek',,7174);
   exec('add_attr','rubatr',907,9079,'OLD: nominalna zaliczka na podatek',1,
         'OLD: nominalna zaliczka na podatek wyliczona wg zasad na dzień 31.12.2021r.',,7177);
   exec('add_attr','rubatr',907,90701,'OLD: informacje podatkowe narastająco przed 2022r.',1,
      'OLD: nominalna zaliczka na podatek wyliczona wg zasad na dzień 31.12.2021r.',,7180);
   exec('add_attr','rubatr',907,90702,'Informacje podatkowe narastająco po 2021r.',1,
      'Informacje podatkowe narastająco wyliczane wg zasasad od 2022r.',,7181);

__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\rubryki_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie rubryk i atrybutów płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk i atrybutów płacowych.'


\P_NPOD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Wprowadzenie danych dotyczących korekt do tabeli nadwyżek zaliczki na podatek.
::----------------------------------------------------------------------------------------------------------------------
:: Zapisujemy tylko te, które mają nadwyżkę
{? var_pres('P_NPOD')=type_of(SYSLOG)
|| exec('__F_ZATR','object');
   _slo_kod:=exec('kod','ext_slo','P_STAT','LIST_MOD');
   {? _slo_kod
   || _start:=1;
      _koniec:=12;
      P.cntx_psh();
      KT.cntx_psh();
      P_STAT.cntx_psh();
      LS.cntx_psh();
      ZC.cntx_psh();
      RH.cntx_psh();
      O.cntx_psh();
      _firma:=exec('ref_firma','ustawienia');
      _ndx:=O.ndx_tmp(,1,'FIRMA',,,'F_ZATR','KOD',,'RP',,,'MP',,,'D',,,'N',,);
      O.index(_ndx);
      RH.clear();
      P.clear();

::    Przeniesienie zapisów z list KPD
      {! _mc:=_start.._koniec
      |! {? var_pres('_tab')>100 || obj_del(_tab) ?};
         _tab:=sql(
            'select distinct P_STAT.P '
            'from P_STAT '
            'join O join KT '
            'where O.FIRMA=:_a and KT.F=\'P\' and P_STAT.SLO_KOD=:_b and O.RP=:_c and O.MP=:_d ',
            _firma,_slo_kod,2022,_mc);
         O.prefix(_firma,'P',2022,_mc);
         {? _tab.first()
         || {!
            |? {? P.seek(_tab.P) & O.last()
               || _next:=1;
                  {!
                  |? {? LS.use(O.LT)
                     || LS.index('PRACNRRU');
                        LS.prefix(P.ref());
                        {? LS.first()
                        || _next:=0;
                           {? O.T().F='P'
                           || {? LS.find_key(7170)
                              || {? LS.KW>0
                                 || {? exec('p_npod_del','lista_licz','P',P.ref(),O.ref())
                                    || exec('p_npod_add','lista_licz',
                                                LS.P,LS.P().OSOBA,O.ref(),null,'P',O.RP,O.MP,'N',LS.KW,'T')
                                    ?}
                                 ?}
                              ?}
                           ?}
                        ?}
                     ?};
                     O.prev() & _next
                  !}
               ?};
               _tab.next()
            !}
         ?};
::       Pobranie danych z korekt umów zleceń
         {? var_pres('_tab')>100 || obj_del(_tab) ?};
            _tab:=sql(
               'select RH.REFERENCE as REF '
               'from RH '
               'where RH.FIRMA=:_a and RH.R=:_b and RH.M=:_c and RH.KOR=\'T\' ',
               _firma,2022,_mc
            );

         {? _tab.first()
         || {!
            |? {? RH.seek(_tab.REF)
               || RH.O(); RH.ZLE();ZC.P();
                  {? LS.use(O.LT)
                  || LS.index('ZLEC');
                     LS.prefix(RH.ref(),7170);
                     {? LS.first()
                     || {? LS.KW>0
                        || {? exec('p_npod_del','lista_licz','Z',RH.ZLE().P,O.ref(),RH.ref())
                           || exec('p_npod_add','lista_licz',
                                      LS.P,LS.P().OSOBA,O.ref(),RH.ref(),'Z',O.RP,O.MP,'N',LS.KW,'T')
                           ?}
                        ?}
                     ?}
                  ?}
               ?};
               _tab.next()
            !}
         ?}
      !};
      P.cntx_pop();
      KT.cntx_pop();
      P_STAT.cntx_pop();
      LS.cntx_pop();
      ZC.cntx_pop();
      RH.cntx_pop();
      O.ndx_drop(_ndx);
      O.cntx_pop()
   ?}
?};

__UPG.msg('Zakończono uzupełnianie tabeli nadwyżek zaliczki na podatek danymi z korekt.');
1


\P_NPOD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Wprowadzenie danych dotyczących korekt do tabeli nadwyżek zaliczki na podatek - opis.
::----------------------------------------------------------------------------------------------------------------------
'Wprowadzenie danych dotyczących korekt do tabeli nadwyżek zaliczki na podatek.'


\formuly_placowe_Polski_Lad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie formuł płacowych.
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='793,795,797,7149,7173,7177,7178';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,R,D,W,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_Polski_Lad_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Dodanie formuł płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuł płacowych dla zmian: Polski Ład'


\formuly_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

:: Formuły typu 'R' - zleceniobiorcy
{? _tp='R'
||
:: ==================
   {? _rn=793
   || "
{? RH.DWY~1<2022 || DoList.z[10] ?}"

:: ==================
   |? _rn=795
   || "
_pod:=FUNKCJE.Z(790)-FUNKCJE.Z(793);
_zanpod:=DoList.p[1]-DoList.p[2];
{? _zanpod<=0
  || 0
|? _pod>_zanpod
  || _zanpod
  || _pod
?}$(RH.DWY~1<2006)"

:: ==================
   |? _rn=797
   || "
exec('zal_pod_doch','zlec_rh')"

:: ==================
   |? _rn=7149
   || "
{? RH.DWY~1>2021 || DoList.z[10] ?}"

:: ==================
   |? _rn=7177
   || "
DoList.p[18]"
   || ""
   ?}

:: Pozostałe typy formuł
||
:: ==================
   {? _rn=797
   || "
exec('zaliczka_podatek_doch','!ppl_pll_nals')"

:: ==================
   |? _rn=7149
   || "
&kc4"

:: ==================
   |? _rn=7173
   || "
exec('PA_OLD_KU','lista_licz')"

:: ==================
   |? _rn=7177
   || "
exec('oblicz_OLD_podatek','!ppl_pll_nals')"

:: ==================
   |? _rn=7178
   || "
exec('KU_OLD','lista_licz')"
   || ""
   ?}
?}


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:25 9d0d7ef7d0d770ebaeea5100dde1428f58e6a65156327572e6f48d4be86024a4c42172e92c260ae1dd4ea03cd5fabec03a1c9158d48a5c030c701151d7914500c98d51df8641513cf5fc529aa984a6199c65c1158767fdd843b837f41afd276eb2d7b08a9e29446c5f46513e32b96f018ad0002ab28888e357f9becb4663700d

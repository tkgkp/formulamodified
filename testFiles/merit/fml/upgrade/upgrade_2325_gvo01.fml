:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2325_gvo01.fml
:: Utworzony: 10.07.2023
:: Autor: PBS
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 23.25 na 23.25_GVO01
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::
::            Programowe wyłączenie zadania transferowego.
::             W plikach upgrade_rrxx.fml korzystamy z metody
::               __UPG.off_task(<task>,[<operator>],[<value>])
::                 - dodaje do listy zadanie,
::                   które ma zostać wyłączone wg warunku porównania wg operatora porównania i wartości, gdzie:
::                <task> - symbol zadania do wyłączenia;
::                [<operator>] - operator porównania - domyślnie operator '<'; dostępne opcje:
::                                   '<' - wartość mniejsza;
::                                   '<=' - wartość mniejsza lub równa;
::                                   '=' - wartość równa;
::                [<value>] - wersja systemu lub aktualizacji do porównania z wersją zadania
::                            - domyślnie aktualna wersja aktualizacji (wg pliku).
::            W pliku upgrade.fml NALEŻY umieścić wywołanie poniższej metody na końcu formuły \init
::               (po wywołaniach __UPG.add_head()):
::              __UPG.act_off() - wyłączenie wszystkich zadań wskazanych do wyłączenia.
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Główna formuła upgrade do 23.25_GVO01
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',,,,,'T');
__UPG.add_task('add_jpkgv',,,'FKS',,,,,'T');
__UPG.add_task('formula_add',,,'FKS',,,,,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::   WE: [_a] - dziedziny
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności (ciekawe, czy uda się alfabetycznie ...)
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.BIQ','.');
_dom_str:={? var_pres('_a')=type_of('')
          || _a
          || 'FKS'
          ?};
_dom:=spli_str(_dom_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\add_jpkgv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje schemat deklaracji JPK_GV
::----------------------------------------------------------------------------------------------------------------------
_imp:=-1;
{? exec('add_ver','xml','FKS',date(2023,7,1),'GV',1,1)
|| _imp:=exec('upg_imp','xml','FKS','J','GV','JPK_GV(1)_v1-1',date(2023,7,1),'jpk_gv_1.dfg')
?};

_txt:='Deklaracja JPK_GV (1) obowiązująca od 01.07.2023';

{? _imp=1
|| __UPG.msg(_txt+' została dodana.');
   1
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.');
   1
|| __UPG.msg(_txt+' nie została dodana.');
   0
?}


\add_jpkgv_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje schemat deklaracji VIU-DO - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie schematu deklaracji JPK_GV (1)'


\formula_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje nowe formuły na wybór rodzaju dokumentu podczas dekretacji
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('S','KOR_S_GV',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='S';
   FORMULA.SKROT:='KOR_S_GV';
   FORMULA.NAZWA:='Rodzaj dokumentu sprzedaży - korekta';
   FORMULA.FORMULA:='exec(\'rodz_sprz_kor\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=2
?};
{? _ret=2
|| __UPG.msg('Formuła KOR_S_GV jest już dodana w systemie.')
|? _ret=1
|| __UPG.msg('Dodano formułę KOR_S_GV.')
|| __UPG.msg('Dodanie formuły KOR_S_GV nie powiodło się.')
?};
FORMULA.prefix('S','KOR_Z_GV',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='S';
   FORMULA.SKROT:='KOR_Z_GV';
   FORMULA.NAZWA:='Rodzaj dokumentu zakupu - korekta';
   FORMULA.FORMULA:='exec(\'rodz_zak_kor\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=2
?};
{? _ret=2
|| __UPG.msg('Formuła KOR_Z_GV jest już dodana w systemie.')
|? _ret=1
|| __UPG.msg('Dodano formułę KOR_Z_GV.')
|| __UPG.msg('Dodanie formuły KOR_Z_GV nie powiodło się.')
?};
FORMULA.prefix('S','SPRZ_GV',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='S';
   FORMULA.SKROT:='SPRZ_GV';
   FORMULA.NAZWA:='Rodzaj dokumentu sprzedaży';
   FORMULA.FORMULA:='exec(\'rodz_sprz\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=2
?};
{? _ret=2
|| __UPG.msg('Formuła SPRZ_GV jest już dodana w systemie.')
|? _ret=1
|| __UPG.msg('Dodano SPRZ_GV.')
|| __UPG.msg('Dodanie SPRZ_GV nie powiodło się.')
?};
FORMULA.prefix('S','ZAK_GV',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='S';
   FORMULA.SKROT:='ZAK_GV';
   FORMULA.NAZWA:='Rodzaj dokumentu zakupu';
   FORMULA.FORMULA:='exec(\'rodz_zak\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=2
?};
FORMULA.cntx_pop();
{? _ret=2
|| __UPG.msg('Formuła ZAK_GV jest już dodana w systemie.')
|? _ret=1
|| __UPG.msg('Dodano ZAK_GV.')
|| __UPG.msg('Dodanie ZAK_GV nie powiodło się.')
?};
1


\formula_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje nowe formuły na wybór rodzaju dokumentu podczas dekretacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie do systemu formuł na wybór rodzaju dokumentu księgowego podczas dekretacji zakupów/sprzedaży z logistyki.'

:Sign Version 2.0 jowisz:1045 2023/09/13 08:33:33 3a1c48687085e92d7f10081f4d60d789078cdd177d22703dd4471e9e13fb87d746479821ab0cb1340604cc0b633d6e9df793d57b20286dbbdc3bc762b1993c0269956cb948c4f4d35728354fd1891ceaba739036c15c2d02ccc63dfd451738c8ca55e9fd37c6216a21a5e05907fa35f5c53092288cd737b1b8ee46ce0645e231

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: transfer1.fml
:: Utworzony: 07.08.2019
:: Autor: JK
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 12.41
::======================================================================================================================

\fiks_45
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/1806/0005
::       e-deklaracje VAT-7 (18) - bramka MF odrzuca e-deklarację gdy wypełnione pozycje deklaracji 11, 12, 13 lub 14
::  OLD: \fiks_35/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fiks_45a','transfer1','P_11');
exec('fiks_45a','transfer1','P_14');
exec('fiks_45b','transfer1');
1


\fiks_45a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/1806/0005
::       e-deklaracje VAT-7 (18) - bramka MF odrzuca e-deklarację gdy wypełnione pozycje deklaracji 11, 12, 13 lub 14
::   WE: _a - nazwa elementu podrzędnego
::  OLD: \fiks_35a/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:='VAT7';
_nr:=18;
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_typ,_typ,_nr);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_typ,VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key(_a) & ISTDEFS.seek(ISTDEFS.TREE,)
      || ISTDEFS.HIDDEN:='T';
         ISTDEFS.WYM:='N';
         ISTDEFS.cntx_psh();
         ISTDEFS.prefix();
         ISTDEFS.put();
         ISTDEFS.cntx_pop()
      ?}
   ?}
?};
1


\fiks_45b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/1806/0005
::       e-deklaracje VAT-7 (18) - bramka MF odrzuca e-deklarację gdy wypełnione pozycje deklaracji 11, 12, 13 lub 14
::  OLD: \fiks_35b/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:='VAT7';
_nr:=18;
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_typ,_typ,_nr);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_typ,VAT_VER.ref());
   {? ISTDEF.first()
   || null;
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      _p13:={? ISTDEFS.find_key('P_13') || _lp13:=ISTDEFS.LP; ISTDEFS.ref() || null ?};
      _p14:={? ISTDEFS.find_key('P_14') || _lp14:=ISTDEFS.LP; ISTDEFS.ref() || null ?};
      {? _p13 & _p14
      || do();
         {? ISTDEFS.seek(_p13)
         || ISTDEFS.LP:=1000;
            ISTDEFS.put()
         ?};
         {? ISTDEFS.seek(_p14)
         || ISTDEFS.LP:=_lp13;
            ISTDEFS.put()
         ?};
         {? ISTDEFS.seek(_p13)
         || ISTDEFS.LP:=_lp14;
            ISTDEFS.put()
         ?};
         end()
      ?}
   ?}
?};
1


\fiks_46
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/1807/0023
::       Problem z kwotą VAT w przelewie z podzieloną płatnością do faktur z odwrotnym obciążeniem
::  OLD: \fiks_36/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('SP',KS)>0
|| _i2:=KS.ndx_tmp('',1,'ROK',,0,'SP',,0);
   KS.cntx_psh();
   KS.index(_i2);
   VAT_REJ.index('REJ_SYM');
   ROK_F.index('KOD'); ROK_F.prefix();
   {? ROK_F.first()
   || FUN.prg_start(ROK_F.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? ROK_F.cntx_psh();
         KS.prefix(ROK_F.ref(),'T');
         {? KS.first()
         || _rejvat:=tab_tmp(1,'REF','INTEGER',);
            REJ.index('KOD'); REJ.prefix(ROK_F.ref());
            {? REJ.first()
            || {!
               |? _add:=0;
                  VAT_REJ.prefix(REJ.ref());
                  {? VAT_REJ.first()
                  || {!
                     |? _add:=VAT_REJ.RVAT().KVAT().SYM='ZakOpod';
                        _add=0 & VAT_REJ.next()
                     !}
                  ?};
                  {? _add
                  || _rejvat.REF:=REJ.ref(); _rejvat.add()
                  ?};
                  REJ.next()
               !}
            ?};
            {? _rejvat.first()
            || {!
               |? exec('update_ks','fks_sp',KS.SYM,_rejvat);
                  KS.next()
               !}
            ?};
            &_rejvat
         ?};
         ROK_F.cntx_pop();
         FUN.prg_next();
         ROK_F.next()
      !};
      FUN.prg_stop()
   ?};
   KS.cntx_pop();
   KS.ndx_drop(_i2)
?};
1


\fiks_47
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1808/0033
::       Deklaracja VAT7 (18) - weryfikacja negatywna - podatnik będący osobą fizyczną.
::  OLD: \fiks_37/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fiks_47a','transfer1',18,'VAT7')


\fiks_47a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: Formula pomocnicza do fiks_37
::  OLD: \fiks_37a/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS',_b,_b,_a);
{? VAT_VER.first()
|| ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D',_b,VAT_VER.ref());
   {? ISTDEF.first()
   || ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key('etd:OsobaFizyczna')
      || ISTDEFS.OPIS:='tns:OsobaFizyczna';
         ISTDEFS.put
      ?}
   ?}
?};
1


\fiks_48
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: ER/WRT/XP/12.51/1809/0018
::       SP: brak uzupełnienia pola OP.SP - pozostaje puste po zaksięgowaniu dokumentu
::  OLD: \fiks_38/napraw_f.fml
::  OLD: \op_drb/upgrade_2226.fml
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh();
D_RB.cntx_psh();
_op:=OP.names();
{? _op.first()
|| {!
   |? echo('Aktualizacja rozrachunków: '+_op.NAME);
      OP.use(_op.NAME);
      OP.prefix();
      D_RB.use('dr__fk'+(_op.NAME+2)); D_RB.index('OP');
      OP.for_each("
         _put:=0;
         {? OP.SP='' || OP.SP:='N'; _put:=1 ?};
         _raty:=OP.RATY;
         D_RB.prefix(OP.ref());
         OP.RATY:={? D_RB.first() || 'T' || 'N' ?};
         {? _raty<>OP.RATY || _put:=1 ?};
         {? _put || OP.put() ?}
      ",1);
      _op.next()
   !}
?};
D_RB.cntx_pop();
OP.cntx_pop();
1


\fiks_49
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1811/0007
::       JPK_FA dla walut
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','FA',null,date(2016,3,11));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref,'R');
   {? ISTDEFS.first()
   || {! |? {? ISTDEFS.REGULY='$DOK.ref()+$DOK.crc()'
            || ISTDEFS.REGULY:='{? JpkDane || $DOK.ref()+$DOK.crc() || \'\' ?}';
               ISTDEFS.put();0
            || ISTDEFS.next()
            ?}
       !}
   ?}
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
1


\fiks_50
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1902/0009
::       Błędnie wyświetlana wartość wiersza nadrzędnego dla skopiowanych sprawozdań.
::----------------------------------------------------------------------------------------------------------------------
DEFW.cntx_psh();
DEFW.index('DEFW_NAD');
DEFW.prefix(0);
DEFW.last();
DEFW.prefix();
{? DEFW.next()
|| FUN.prg_start(DEFW.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? _kod:='';
      _grupa:=DEFW.GRUPA;
      _next:=null;
      DEFW.cntx_psh();
      {? DEFW.seek(DEFW.DEFW_NAD,)
      || {? DEFW.GRUPA<>_grupa || _kod:=DEFW.KOD ?}
      ?};
      DEFW.cntx_pop();
      {? _kod<>''
      || {? DEFW.next()
         || _next:=DEFW.ref();
            DEFW.prev()
         ?};
         DEFW.cntx_psh();
         DEFW.index('GRUPA'); DEFW.prefix(_grupa);
         _ref:={? DEFW.find_key(_kod) || #DEFW.ref() ?};
         DEFW.cntx_pop();
         DEFW.DEFW_NAD:=_ref;
         DEFW.put();
         FUN.prg_next();
         _next<>null & DEFW.seek(#_next,)
      || FUN.prg_next();
         DEFW.next()
      ?}
   !};
   FUN.prg_stop()
?};
DEFW.cntx_pop();
1


\fiks_51
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1902/0012
::       e-sprawozdania - pomijana kolejna pozycja uszczegóławiająca w pliku xml
::  OLD: \fiks_42/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','SF',);
{? ISTDEF.first()
|| FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'dtsf:PozycjaUszczegolawiajaca');
      {? ISTDEFS.first()
      || {!
         |? {? ISTDEFS.REGULY=$"exec('details','jpk_sf',0)" & (ISTDEFS.FORMULA='' | ISTDEFS.FORM_XML='')
            || ISTDEFS.FORMULA:=$"exec('details','jpk_sf',1,_a)";
               ISTDEFS.FORM_XML:=$"exec('details','jpk_sf',2)";
               ISTDEFS.put()
            ?};
            ISTDEFS.next()
         !}
      ?};
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.first()
      || {!
         |? _txt:=ISTDEFS.memo_txt(,1,'ANOTACJA');
            {? 1+_txt='?'
            || ISTDEFS.memo_set('-'+(1-_txt),'ANOTACJA');
               ISTDEFS.memo_put();
               ISTDEFS.put()
            ?};
            ISTDEFS.next()
         !}
      ?};
      FUN.prg_next();
      ISTDEF.next()
   !};
   FUN.prg_stop()
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
{? var_press('E_KOD',GR_SIK)>0
|| _sql:=sql('select REFERENCE as REF from GR_SIK where E_KOD<>\'\'');
   {? _sql.first()
   || GR_SIK.cntx_psh();
      GR_SIK.prefix();
      DEFW.cntx_psh();
      DEFW.index('LP');
      FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
         || DEFW.prefix(GR_SIK.ref());
            {? DEFW.first()
            || {!
               |? {? DEFW.WSPOL=0
                  || DEFW.WSPOL:=1; DEFW.put()
                  ?};
                  DEFW.next()
               !}
            ?}
         ?};
         FUN.prg_next();
         _sql.next()
      !};
      FUN.prg_stop();
      DEFW.cntx_pop();
      GR_SIK.cntx_pop()
   ?}
?};
1


\fiks_52
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1902/0032
::       Błędne algorytmy w wierszach typu suma w e-sprawozdaniach
::  OLD: \fiks_43/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF','JednostkaInnaWZlotych(1)_v1-0',);
{? ISTDEF.first()
|| _lp:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'dtsf:P_ID_2',);
   {? ISTDEFS.first()
   || _lp:=ISTDEFS.LP
   ?};
   {? _lp
   || ISTDEFS.index('LP'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.find_key(_lp)
      || {!
         |? {? ISTDEFS.OPIS='dtsf:KwotaB' & ISTDEFS.REGULY=$"exec('value','jpk_sf','KwotaA','P')"
            || ISTDEFS.REGULY:=$"exec('value','jpk_sf')";
               ISTDEFS.put()
            ?};
            ISTDEFS.next()
         !}
      ?}
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.prefix('J','SF','JednostkaMala');
{? ISTDEF.first()
|| FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? _ref_pu:=null;
      ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'jma:G_I_A_1');
      {? ISTDEFS.first()
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
         {? ISTDEFS.first()
         || {!
            |? {? ISTDEFS.OPIS='dtsf:PozycjaUszczegolawiajaca'
               || _ref_pu:=ISTDEFS.ref()
               ?};
               ISTDEFS.next()
            !}
         ?};
         ISTDEFS.cntx_pop();

         ISTDEFS.cntx_psh();
         ISTDEFS.prefix();
         {? ISTDEFS.seek(ISTDEFS.TREE,)
         || {? ISTDEFS.OPIS='jma:F_I_1'
            || _ref:=ISTDEFS.ref();
               {? _ref_pu
               || ISTDEFS.cntx_psh();
                  ISTDEFS.prefix();
                  {? ISTDEFS.seek(_ref_pu)
                  || ISTDEFS.TREE:=_ref;
                     ISTDEFS.put()
                  ?};
                  ISTDEFS.cntx_pop()
               ?};
               ISTDEFS.cntx_psh();
               ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
               {? ISTDEFS.first()
               || {!
                  |? {? '|jma:PozycjaUszczegolawiajaca_1|jma:G_I_A_1|jma:PozycjaUszczegolawiajaca_2|'*('|'+ISTDEFS.OPIS+'|')
                     || exec('fiks_52a','transfer1')
                     || ISTDEFS.next()
                     ?}
                  !}
               ?};
               ISTDEFS.cntx_pop()
            ?}
         ?};
         ISTDEFS.cntx_pop()
      ?};
      ISTDEFS.cntx_pop();
      FUN.prg_next();
      ISTDEF.next()
   !};
   FUN.prg_stop()
?};
ISTDEF.cntx_pop();
{? var_press('E_KOD',GR_SIK)>0
|| _sql:=sql(
      'select REFERENCE as REF '+
      'from GR_SIK '+
      'where ('+
         'GR_SIK.E_KOD like \'Bilan%\' or '+
         'GR_SIK.E_KOD like \'RZiSPo%\' or '+
         'GR_SIK.E_KOD like \'Przeplyw%\' or '+
         'GR_SIK.E_KOD like \'RZiSKalk%\''+
      ')'
   );
   {? _sql.first()
   || _wyn:=obj_new('Tab','add','select');
      _wyn.Tab:=tab_tmp(4,
         'FIRMA','STRING[4]','Firma',
         'SYSTEM','STRING[10]','System',
         'S_S','STRING[10]','Sprawozdanie',
         'W_KOD','STRING[15]','Wiersz',
         'K_NAZ','STRING[60]','Kolumna',
         'OPIS','STRING[255]','Opis'
      );
      _wyn.add:="
         _tab:=.Tab;
         _tab.blank(1);
         _tab.FIRMA:=GR_SIK.FIRMA().SYMBOL;
         _tab.SYSTEM:=GR_SIK.SYSTEM;
         _tab.S_S:=GR_SIK.SKROT;
         _tab.W_KOD:={? _b || DEFW.KOD || '' ?};
         _tab.K_NAZ:={? _c || GR_KOL.NAZWA || '' ?};
         _tab.OPIS:=_a;
         _tab.add()
      ";
      _wyn.select:="
         _tab:=.Tab;
         {? _tab.first()
         || _o:=_tab.mk_sel('Naprawione definicje sprawozdań','P',,'#esprnap',,,,,'U');
            _tab.win_fld(_o,,'FIRMA');
            _tab.win_fld(_o,,'SYSTEM');
            _tab.win_fld(_o,,'S_S');
            _tab.win_fld(_o,,'W_KOD');
            _tab.win_fld(_o,,'K_NAZ',,,20);
            _tab.win_fld(_o,,'OPIS',,,100);
            _tab.win_sel(_o);
            _tab.select()
         ?}
      ";
      FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? GR_SIK.cntx_psh();
         GR_SIK.prefix();
         {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
         || DEFW.cntx_psh();
            DEFW.index('GRUPA'); DEFW.prefix(GR_SIK.ref());
            {? 6+GR_SIK.E_KOD='Bilans'
            || _kody:='P_A_II@P_A_II_1|P_A_III@P_A_III_1|P_A_IV@P_A_IV_1@P_A_IV_2';
               _wtym:=1
            |? GR_SIK.E_KOD='RZiSPor'
            || _kody:='A@A_J|B_IV@B_IV_1|B_VI@B_VI_1|G_I@G_I_A@G_I_B|G_I_A@G_I_A_1|G_I_B@G_I_B_1|G_II@G_II_J|G_III@G_III_J|'+
               'H_I@H_I_J|H_II@H_II_J|F_II@F_II_1|F_III@F_III_1'+
               'B_V@B_V_1|B_VI@B_VI_1|D@D_1|E@E_1|F_I@F_I_1|G_I@G_I_1|F_I_1@G_I_A_1';
               _wtym:=0
            |? 9+GR_SIK.E_KOD='Przeplywy'
            || _kody:='A@A_I@A_II|B@B_I@B_II|C@C_I@C_II|E@E_1|G@G_1';
               _wtym:=0
            |? GR_SIK.E_KOD='RZiSKalk'
            || _kody:='A@A_J|B@B_J|J_I_A@J_I_A_1|J_I_B@J_I_B_1|J_II@J_II_J|J_III@J_III_J|K_I@K_I_J|K_II@K_II_J|'+
               'F@F_1|G@G_1|H_I@H_I_1|H_II@H_II_1|H_III@H_III_1|I_I@I_I_1|I_II@I_II_1|';
               _wtym:=0
            ?};
            _zmiana:=0;
            _tab:=spli_str(_kody,'|');
            {! _i:=1..obj_len(_tab)
            |! _tab2:=spli_str(_tab[_i],'@');
               {? DEFW.find_key(_tab2[1],) & DEFW.ALGORYTM='S' & (_wtym=0 | DEFW.NAZWA*'w tym')
               || GR_KOL.cntx_psh();
                  GR_KOL.index('LP'); GR_KOL.prefix(GR_SIK.ref());
                  {? GR_KOL.first()
                  || {!
                     |? DEFA.cntx_psh();
                        DEFA.index('LP'); DEFA.prefix(DEFW.ref(),GR_KOL.ref());
                        {? DEFA.first()
                        || {!
                           |? DEFW.cntx_psh();
                              _kod:=DEFA.ARGUMENT().KOD;
                              _jest:=_tab[_i]*_kod;
                              DEFW.cntx_pop();
                              {? _jest
                              || _zmiana:=1;
                                 _wyn.add('Usunięcie z algorytmu składnika: '+_kod,1,1);
                                 DEFA.del()
                              || DEFA.next()
                              ?}
                           !}
                        ?};
                        DEFA.cntx_pop();
                        GR_KOL.next()
                     !};
                     DEFA.cntx_psh();
                     DEFA.index('LP'); DEFA.prefix(DEFW.ref());
                     {? ~DEFA.first()
                     || DEFW.ALGORYTM:='F';
                        DEFW.put();
                        _wyn.add('Zmiana algorytmu z \'S\' na \'F\'.',1,0);
                        _zmiana:=1
                     ?};
                     DEFA.cntx_pop()
                  ?};
                  GR_KOL.cntx_pop()
               ?};
               &_tab2
            !};
            &_tab;
            DEFW.cntx_pop();
            {? _zmiana & GR_SIK.AKC<>'N'
            || GR_SIK.AKC:='N';
               GR_SIK.put();
               _wyn.add('Wycofanie akceptacji sprawozdania.',0,0)
            ?}
         ?};
         GR_SIK.cntx_pop();
         FUN.prg_next();
         _sql.next()
      !};
      FUN.prg_stop();
      _wyn.select()
   ?}
?};
1


\fiks_52a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1902/0032
::       Błędne algorytmy w wierszach typu suma w e-sprawozdaniach
::  OLD: \fiks_43a/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS.cntx_psh();
ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
{? ISTDEFS.first()
|| {!
   |? exec('fiks_52a','transfer1')
   !}
?};
ISTDEFS.cntx_pop();
ISTDEFS.del()


\fiks_53
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - PR/WRT/XP/12.51/1902/0055
::       Nowa lokalizacja schematów JPK
::  OLD: \fiks_44/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fiks_53a','transfer1','FA','JPK_FA(1)_v1-0',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_FA%281%29_v1-0.xsd/c048cae9-264e-512f-51fe-bcea7fdf9de0'
);
exec('fiks_53a','transfer1','KR','JPK_KR(1)_v1-0',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_KR%281%29_v1-0.xsd/30ee4a20-f2f5-ba7d-6c90-8500facddd60'
);
exec('fiks_53a','transfer1','MAG','JPK_MAG(1)_v1-0',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_MAG%281%29_v1-0.xsd/425b566f-23a7-6b37-876f-6e9592200f1d'
);
exec('fiks_53a','transfer1','VAT','JPK_VAT(3)_v1-1',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_VAT%283%29_v1-1.xsd/ab0741d5-fa6d-9596-b089-6778ea5df160'
);
exec('fiks_53a','transfer1','WB','JPK_WB(1)_v1-0',
   'https://www.gov.pl/documents/2034621/2182793/Schemat_JPK_WB%281%29_v1-0.xsd/194ae091-1f50-9f46-cd56-ce5208653ba2'
);
1


\fiks_53a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zmiana lokalizacji pliku JPL
::   WE: _a - rodzaj pliku
::       _b - nazwa pliku
::       _c - nowa lokalizacja
::  OLD: \fiks_44a/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J',_a,_b,);
{? ISTDEF.first()
|| ISTDEF.XMLNS:=_c;
   ISTDEF.put()
?};
ISTDEF.cntx_pop()


\fiks_54
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1903/0014
::       JPK_SF: błędy w e_sprawozdaniu zmiany w kapitale własnym
::  OLD: \fiks_45/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('E_KOD',GR_SIK)>0
|| _sql:=sql('select REFERENCE as REF from GR_SIK where E_KOD like \'ZestZmianWKapitale%\'');
   {? _sql.first()
   || _wyn:=obj_new('Tab','add','select','FIRMA','SYSTEM','GR_SIK','DEFW','GR_KOL');
      _wyn.Tab:=tab_tmp(6,
         'FIRMA','STRING[4]','Firma',
         'SYSTEM','STRING[10]','System',
         'S_S','STRING[10]','Sprawozdanie',
         'W_KOD','STRING[15]','Wiersz',
         'K_NAZ','STRING[60]','Kolumna',
         'OPIS','STRING[255]','Opis'
      );
      _wyn.add:="
         _tab:=.Tab;
         _tab.blank(1);
         _tab.FIRMA:=.FIRMA;
         _tab.SYSTEM:=.SYSTEM;
         _tab.S_S:=.GR_SIK;
         _tab.W_KOD:=.DEFW;
         _tab.K_NAZ:=.GR_KOL;
         _tab.OPIS:=_a;
         _tab.add()
      ";
      _wyn.select:="
         _tab:=.Tab;
         {? _tab.first()
         || _o:=_tab.mk_sel('Naprawione definicje sprawozdań','P',,'#esprnap',,,,,'U');
            _tab.win_fld(_o,,'FIRMA');
            _tab.win_fld(_o,,'SYSTEM');
            _tab.win_fld(_o,,'S_S');
            _tab.win_fld(_o,,'W_KOD');
            _tab.win_fld(_o,,'K_NAZ',,,20);
            _tab.win_fld(_o,,'OPIS',,,100);
            _tab.win_sel(_o);
            _tab.select()
         ?}
      ";
      GR_SIK.cntx_psh();
      GR_SIK.prefix();
      DEFW.cntx_psh();
      DEFW.index('ALGORYTM');
      DEFA.cntx_psh();
      DEFA.index('LP');
      FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
         || _wyn.FIRMA:=GR_SIK.FIRMA().SYMBOL;
            _wyn.SYSTEM:=GR_SIK.SYSTEM;
            _wyn.GR_SIK:=GR_SIK.SKROT;
            _zmiana:=0;
            DEFW.prefix(GR_SIK.ref(),'S');
            {? DEFW.first()
            || {!
               |? _wyn.DEFW:=DEFW.KOD;
                  DEFW.cntx_psh();
                  DEFA.prefix(DEFW.ref());
                  {? DEFA.first()
                  || {!
                     |? _wyr:=DEFA.WYR;
                        _kol:=DEFA.KOL;
                        _wyn.GR_KOL:=DEFA.KOL().NAZWA;
                        {? 6+DEFA.ARGUMENT().GRUPA().E_KOD='Bilans'
                        || {? DEFA.WYR().NAZWA*'na początek okresu' & DEFA.KOL().E_KOD='KwotaA' & DEFA.ARG_KOL().E_KOD='KwotaA'
                           || GR_KOL.cntx_psh();
                              GR_KOL.index('E_KOD');
                              GR_KOL.prefix(DEFA.ARGUMENT().GRUPA,'KwotaB',);
                              {? GR_KOL.first()
                              || _zmiana:=1;
                                 _wyn.add('Zmieniono kolumnę algorytmu.');
                                 DEFA.ARG_KOL:=GR_KOL.ref();
                                 DEFA.put()
                              ?};
                              GR_KOL.cntx_pop();
                              DEFA.next()
                           |? DEFA.KOL().E_KOD='KwotaB1'
                           || _zmiana:=1;
                              _wyn.add('Usunięto algorytm.');
                              _del:=DEFA.del();
                              DEFA.cntx_psh();
                              DEFA.index('LP');
                              DEFA.prefix(_wyr,_kol);
                              {? DEFA.first()
                              || _lp:=0;
                                 {!
                                 |? DEFA.LP:=(_lp+=1);
                                    DEFA.put();
                                    DEFA.next()
                                 !}
                              ?};
                              DEFA.cntx_pop();
                              _del
                           ?}
                        || DEFA.next()
                        ?}
                     !}
                  ?};
                  DEFW.cntx_pop();
                  DEFW.next()
               !}
            ?};
            {? _zmiana & GR_SIK.AKC<>'N'
            || _wyn.DEFW:=_wyn.GR_KOL:='';
               _wyn.add('Wycofano akceptację sprawozdania.');
               GR_SIK.AKC:='N';
               GR_SIK.put()
            ?}
         ?};
         FUN.prg_next();
         _sql.next()
      !};
      FUN.prg_stop();
      DEFA.cntx_pop();
      DEFW.cntx_pop();
      GR_SIK.cntx_pop();
      _wyn.select()
   ?}
?};
1


\fiks_73
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1903/0016
::       JPK_SF: błędy w e-sprawozdaniach dla jednostki małej w złotych
::  OLD: \fiks_46/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('E_KOD',GR_SIK)>0
|| ISTDEF.cntx_psh();
   ISTDEF.index('JPK'); ISTDEF.prefix('J','SF','JednostkaMalaW');
   {? ISTDEF.first()
   || ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS');
      FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? ISTDEFS.prefix(ISTDEF.ref(),'jma:G_IV',);
         {? ISTDEFS.first()
         || ISTDEFS.cntx_psh();
            ISTDEFS.prefix();
            ISTDEFS.OPIS:='jma:F_IV'; ISTDEFS.put();
            ISTDEFS.cntx_pop()
         ?};
         ISTDEFS.prefix(ISTDEF.ref(),'jma:G_II_J',);
         {? ISTDEFS.first()
         || ISTDEFS.cntx_psh();
            ISTDEFS.prefix();
            ISTDEFS.OPIS:='jma:G_II_1'; ISTDEFS.put();
            ISTDEFS.cntx_pop()
         ?};
         FUN.prg_next();
         ISTDEF.next()
      !};
      FUN.prg_stop();
      ISTDEFS.cntx_pop()
   ?};
   ISTDEF.cntx_pop();
   _sql:=sql('select REFERENCE as REF from GR_SIK where E_KOD=\'RZiSPor\'');
   {? _sql.first()
   || GR_SIK.cntx_psh();
      GR_SIK.prefix();
      DEFW.cntx_psh();
      DEFW.index('E_KOD');
      FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
         || _zmiana:=0;
            DEFW.prefix(GR_SIK.ref());
            {? DEFW.find_key('F_III_1',)
            || {? DEFW.find_key('G_IV')
               || {? DEFW.KOD='G_IV' || DEFW.KOD:='F_IV' ?};
                  DEFW.E_KOD:='F_IV';
                  DEFW.put();
                  _zmiana:=1
               ?};
               {? DEFW.find_key('G_II_J')
               || {? DEFW.KOD='G_II_J' || DEFW.KOD:='G_II_1' ?};
                  DEFW.E_KOD:='G_II_1';
                  DEFW.put();
                  _zmiana:=1
               ?}
            ?};
            {? _zmiana & GR_SIK.AKC<>'N'
            || GR_SIK.AKC:='N';
               GR_SIK.put()
            ?}
         ?};
         FUN.prg_next();
         _sql.next()
      !};
      FUN.prg_stop();
      DEFW.cntx_pop();
      GR_SIK.cntx_pop()
   ?}
?};
1


\fiks_55
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - PR/WRT/XP/12.51/1903/0024
::       JPK_SF: Redagowanie wartości z poprzedniego roku dla sprawozdań
::  OLD: \fiks_47/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('E_KOD',GR_SIK)>0
|| _sql:=sql(
      'select ISTDEFS.REFERENCE as REF '+
      'from ISTDEFS join ISTDEF '+
      'where ISTDEF.RODZAJ=\'SF\' and ISTDEFS.TYPFLD=\'K\' and ISTDEFS.OPIS like \'%KwotaA\''
   );
   ISTDEFS.cntx_psh();
   ISTDEFS.prefix();
   {? _sql.first()
   || FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? {? ISTDEFS.seek(BIT.sqlint(_sql.REF),)
         || ISTDEFS.cntx_psh();
            ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEFS.ISTDEF,ISTDEFS.TREE,);
            {? ISTDEFS.next() & ISTDEFS.TYPFLD='' & ISTDEFS.OPIS+6='KwotaB'
            || ISTDEFS.TYPFLD:='K';
               ISTDEFS.put()
            ?};
            ISTDEFS.cntx_pop()
         ?};
         FUN.prg_next();
         _sql.next()
      !};
      FUN.prg_stop()
   ?};
   ISTDEFS.cntx_pop();
   &_sql;
   _sql:=sql(
      'select REFERENCE as REF '+
      'from GR_SIK '+
      'where E_KOD<>\'\''
   );
   {? _sql.first()
   || GR_SIK.cntx_psh();
      GR_SIK.prefix();
      GR_KOL.cntx_psh();
      GR_KOL.index('LP');
      FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
         || GR_KOL.prefix(GR_SIK.ref());
            {? GR_KOL.first()
            || {!
               |? {? 5+GR_KOL.E_KOD='Kwota' & GR_KOL.E_KOD=GR_KOL.NAZWA
                  || {? 10+GR_SIK.E_KOD='Informacja'
                     || {? GR_KOL.E_KOD='KwotaA'
                        || GR_KOL.NAZWA:='Wartość łączna';
                           GR_KOL.put()
                        |? GR_KOL.E_KOD='KwotaB'
                        || GR_KOL.NAZWA:='Z zysków kapitałowych';
                           GR_KOL.put()
                        |? GR_KOL.E_KOD='KwotaC'
                        || GR_KOL.NAZWA:='Z innych źródeł przychodów';
                           GR_KOL.put()
                        ?}
                     || {? GR_KOL.E_KOD='KwotaA'
                        || GR_KOL.NAZWA:='Kwota na dzień kończący bieżący rok obrotowy';
                           GR_KOL.put()
                        |? GR_KOL.E_KOD='KwotaB'
                        || GR_KOL.NAZWA:='Kwota na dzień kończący poprzedni rok obrotowy';
                           GR_KOL.put()
                        |? GR_KOL.E_KOD='KwotaB1'
                        || GR_KOL.NAZWA:='Przekształcone dane porównawcze za poprzedni rok obrotowy';
                           GR_KOL.put()
                        ?}
                     ?}
                  ?};
                  GR_KOL.next()
               !}
            ?}
         ?};
         FUN.prg_next();
         _sql.next()
      !};
      FUN.prg_stop();
      GR_KOL.cntx_pop();
      GR_SIK.cntx_pop()
   ?}
?};
1


\fiks_56
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1903/0034
::       JPK_SF: błędny algorytm e-Rachunek przepływów metoda poś. dla wiersza A.III
::  OLD: \fiks_48/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('E_KOD',GR_SIK)>0
|| _sql:=sql(
      'select REFERENCE as REF '+
      'from GR_SIK '+
      'where E_KOD=\'PrzeplywyPosr\''
   );
   {? _sql.first()
   || GR_SIK.cntx_psh();
      GR_SIK.prefix();
      DEFW.cntx_psh();
      DEFW.index('E_KOD');
      DEFA.cntx_psh();
      DEFA.index('LP');
      FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
         || DEFW.prefix(GR_SIK.ref(),'A_III',);
            {? DEFW.first()
            || DEFA.prefix(DEFW.ref());
               {? DEFA.first()
               || {!
                  |? {? DEFA.ARGUMENT().E_KOD='A_II' & DEFA.WSP=-1
                     || DEFA.WSP:=1; DEFA.put()
                     ?};
                     DEFA.next()
                  !}
               ?}
            ?}
         ?};
         FUN.prg_next();
         _sql.next()
      !};
      FUN.prg_stop();
      DEFA.cntx_pop();
      DEFW.cntx_pop();
      GR_SIK.cntx_pop()
   ?}
?};
1


\fiks_57
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1903/0044
::       JPK_SF: E-sprawozdania punkt P_5B - błąd w schemacie
::  OLD: \fiks_49/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('E_KOD',GR_SIK)>0
|| ISTDEF.cntx_psh();
   ISTDEF.index('JPK'); ISTDEF.prefix('J','SF',);
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS');
   {? ISTDEF.first()
   || {!
      |? ISTDEFS.prefix(ISTDEF.ref(),'tns:P_5B',);
         {? ISTDEFS.first() & ISTDEFS.REGULY=$"'false'"
         || ISTDEFS.REGULY:=$"'true'";
            ISTDEFS.put()
         ?};
         ISTDEF.next()
      !}
   ?};
   ISTDEFS.cntx_pop();
   ISTDEF.cntx_pop()
?};
1


\fiks_58
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1903/0053
::       JPK_SF: katalog xsd (po aktualizacje e-sprawozdań) i błąd funkcji exec('init','filecopy')
::  OLD: \fiks_51/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_rename:="
   _from:=_a;
   _to:=19+(_from-45)+(_from+45);
   {? _from<>_to & fexists(_from,1)
   || {? fcopy(_from,_to,1,1,1)
      || ferase(_from,1)
      ?}
   ?}
";
_rename('v_CIT-8_O(10)_Z_v1-0E_12958e729da3e860f55814dd8091fc965c6c6ceb.xsd');
_rename('v_CIT-8_O(11)_Z_v1-0E_330e8ded1be58c5c20427762701cc665498a3e98.xsd');
_rename('v_CIT-8_O(12)_Z_v1-0E_e041acc049570ff9261dd2ab1f4d4219caf53520.xsd');
_rename('v_CIT-8_O(13)_Z_v1-0E_6b744bb26029548b753013e529e9fca80fbe37d2.xsd');
_rename('v_CIT-8_O(14)_Z_v1-0E_d553db10723c5aa8e6799824490df702cad8cb46.xsd');
_rename('v_CIT-8_O(8)_Z_v3-0E_d478c212c39350be537bcbf608da60faa7dff960.xsd');
_rename('v_CIT-8_O(9)_Z_v1-0E_fb0c95ed09459a4d58254dea60e70219fbd7bdb5.xsd');
_rename('v_CIT-ST_A(5)_Z_v2-0E_6ffb8e245247e8a2fcde5bfaf9e290a217135298.xsd');
_rename('v_CIT-ST_A(5)_Z_v4-0E_f0fd287f74e98038d7ee6bbdd62fa92c9d24c7f1.xsd');
_rename('v_CIT_8SP(1)_Z_v1-0E_65b627b36aa045c3f0ec4eab83c83ab8f22d181f.xsd');
_rename('v_CIT_8SP(2)_Z_v1-0E_1991340f597ef868dab715e01f8c660e69abb1c0.xsd');
_rename('v_CIT_MIT(1)_Z_v1-0E_eca4b9cebc931512c761b6933f80e052329d6571.xsd');
_rename('v_CIT_MIT(2)_Z_v1-0E_9a21947a8ee0193b35f33bd916da9a8b77b183b0.xsd');
_rename('v_ElementarneTypyDanych_v3-0E_74b9fd4efa3b9293af9a6a8c544fd90162bec623.xsd');
_rename('v_ElementarneTypyDanych_v4-0E_6d47c45907f05c92e4aba075087fd4fdd7195fa0.xsd');
_rename('v_ElementarneTypyDanych_v5-0E_64d5a1d2df51484a29940936ac06fe002932078f.xsd');
_rename('v_JednostkaInnaStrukturyDanychSprFin_v1-0_d1ffa52f3a0773f43d34e086e04b43c9a3bc1cfa.xsd');
_rename('v_JednostkaInnaWTysiacach(1)_v1-0_89d7d7538627ca325b8baf9d539297b86c6f61fd.xsd');
_rename('v_JednostkaInnaWZlotych(1)_v1-0_cade3ffc85e7103aac89af7a64d6f879106847a2.xsd');
_rename('v_JednostkaMalaStrukturyDanychSprFin_v1-0_278b07d7ac8b99677e9dee0426d0d8a8beacb57e.xsd');
_rename('v_JednostkaMalaWTysiacach(1)_v1-0_55ef15b865d15bd126e22eb4ef7090338d4b8cab.xsd');
_rename('v_JednostkaMalaWZlotych(1)_v1-0_52310d7e6560d34d8d5dec54695732ee007be3e3.xsd');
_rename('v_KodyCechKrajow_v3-0E_097abda8d7b68fe8e78965817b129a9c50b9e471.xsd');
_rename('v_KodyKrajowUE_v2-0E_2bd412d9a0bac66bf362338e107824159a3985ea.xsd');
_rename('v_KodyUrzedowSkarbowych_v3-0E_b2a09044e323e2a27b017a82c155f0d555303b81.xsd');
_rename('v_KodyUrzedowSkarbowych_v4-0E_eacdd80247933d94db150c27d85f67c5345b4816.xsd');
_rename('v_KodyUrzedowSkarbowych_v5-0E_3fcbbbc88d14f9c53d4363cdf54096fa3e646d53.xsd');
_rename('v_SSE-R_A(2)_Z_v2-0E_738dd6a2ab7e9fd7649de3d8e6db2c42bd10b215.xsd');
_rename('v_SSE-R_A(3)_Z_v2-0E_235c282cb53de948e577be811ec3b91452392db5.xsd');
_rename('v_SSE-R_A(3)_Z_v3-0E_96a99d82f9a8e3af33d9456d12763fac612a3f7b.xsd');
_rename('v_StrukturyDanychSprFin_v1-0_85032dda228324d24bca930ab8bdda784c7d7c21.xsd');
_rename('v_StrukturyDanych_v3-0E_11b1ae2ae7de1f9cf19a3ac8726b35145704f1bc.xsd');
_rename('v_StrukturyDanych_v4-0E_29db0c7898fdeb5ddb3bb4b3e19d07ff3daf2705.xsd');
_rename('v_StrukturyDanych_v5-0E_4616ef3e134d599988a1bee7f43a43ef4482427a.xsd');
1


\fiks_59
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Aktualizacja 12.51_40 - uwupełnienie definicji schematów e-Sprawozdań
::  OLD: \fiks_52/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('E_KOD',GR_SIK)>0
|| LastLp:="
      _lp:=ISTDEFS.LP;
      ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
      {? ISTDEFS.last()
      || _lp:=LastLp()
      ?};
      ISTDEFS.cntx_pop();
      _lp
   ";
   ISTDEF.cntx_psh();
   ISTDEF.index('JPK'); ISTDEF.prefix('J','SF',);
   ISTDEFS.cntx_psh();
   {? ISTDEF.first()
   || FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? ISTDEFS.index('TYPPOLA'); ISTDEFS.prefix(ISTDEF.ref(),'SP');
         {? ISTDEFS.first()
         || {!
            |? ISTDEFS.cntx_psh();
               ISTDEFS.index('DRZEWO');
               ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
               {? ISTDEFS.first()
               || {!
                  |? ISTDEFS.cntx_psh();
                     ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
                     {? ISTDEFS.first()
                     || {!
                        |? {? ISTDEFS.OPIS='dtsf:Kwota'
                           || ISTDEFS.cntx_psh();
                              ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
                              {? ISTDEFS.last() & ISTDEFS.OPIS='dtsf:RP'
                              || ISTDEFS.REGULY:=$"exec('okres','jpk_sf','P'); exec('podat_rp','jpk_sf')";
                                 ISTDEFS.put()
                              ?};
                              ISTDEFS.cntx_pop()
                           |? ISTDEFS.OPIS='dtsf:PozycjaUzytkownika' | ISTDEFS.OPIS='dtsf:Pozostale'
                           || {? ISTDEFS.OPIS='dtsf:PozycjaUzytkownika'
                              || ISTDEFS.FORMULA:=$"exec('esprp_first','jpk_sf',_a,'N')";
                                 ISTDEFS.FORM_XML:=$"exec('esprp_next','jpk_sf')";
                                 ISTDEFS.PARSER:=$"exec('p_esprp','jpk_sf',_a)"
                              || ISTDEFS.FORMULA:=$"exec('esprp_first','jpk_sf',_a,'T')";
                                 ISTDEFS.FORM_XML:=$"exec('esprp_next','jpk_sf')";
                                 ISTDEFS.PARSER:=$"exec('p_esprp','jpk_sf',_a,'Pozostałe')"
                              ?};
                              ISTDEFS.REGULY:='';
                              ISTDEFS.put();
                              _lp:=LastLp();
                              _kw:=-3;
                              ISTDEFS.cntx_psh();
                              ISTDEFS.index('LP'); ISTDEFS.prefix(ISTDEF.ref());
                              {? ISTDEFS.next()
                              || {!
                                 |? {? ISTDEFS.OPIS='dtsf:NazwaPozycji'
                                    || ISTDEFS.REGULY:=$"exec('esprp_nazwa','jpk_sf')";
                                       ISTDEFS.PARSER:=$"exec('p_esprp_naz','jpk_sf',_a,_b)";
                                       ISTDEFS.put()
                                    |? ISTDEFS.OPIS='dtsf:Kwota'
                                    || ISTDEFS.REGULY:='';
                                       ISTDEFS.PARSER:='';
                                       ISTDEFS.put()
                                    |? ISTDEFS.OPIS='dtsf:KwotaA' | ISTDEFS.OPIS='dtsf:KwotaB' | ISTDEFS.OPIS='dtsf:KwotaC'
                                    || {? ISTDEFS.OPIS='dtsf:KwotaA' || _kw+=3 ?};
                                       _nr:=(%(ISTDEFS.OPIS+1)-64)+_kw;
                                       ISTDEFS.REGULY:=$"exec('esprp_value','jpk_sf'"+{? _kw || ',\'P\'' || '' ?}+')';
                                       ISTDEFS.PARSER:=$"exec('p_v_pod','jpk_sf',_a,_b,"+$_nr+')';
                                       ISTDEFS.put()
                                    |? ISTDEFS.OPIS='dtsf:Art' | ISTDEFS.OPIS='dtsf:Ust' | ISTDEFS.OPIS='dtsf:Pkt' | ISTDEFS.OPIS='dtsf:Lit'
                                    || ISTDEFS.REGULY:='E_SPR_P.'+(~-(ISTDEFS.OPIS+3));
                                       ISTDEFS.PARSER:=$"exec('p_esprp_pp','jpk_sf',_a,_b)";
                                       ISTDEFS.put()
                                    |? ISTDEFS.OPIS='dtsf:Podpozycja'
                                    || ISTDEFS.REGULY:=$"exec('esprp_pod','jpk_sf')";
                                       ISTDEFS.PARSER:=$"exec('p_esprp_pod','jpk_sf',_a,_b)";
                                       ISTDEFS.put()
                                    |? ISTDEFS.OPIS='dtsf:RP'
                                    || ISTDEFS.REGULY:=$"exec('podat_rp','jpk_sf')";
                                       ISTDEFS.put()
                                    ?};
                                    ISTDEFS.LP<_lp & ISTDEFS.next()
                                 !}
                              ?};
                              ISTDEFS.cntx_pop()
                           ?};
                           ISTDEFS.next()
                        !}
                     ?};
                     ISTDEFS.cntx_pop();
                     ISTDEFS.next()
                  !}
               ?};
               ISTDEFS.cntx_pop();
               ISTDEFS.next()
            !}
         ?};
         FUN.prg_next();
         ISTDEF.next()
      !};
      FUN.prg_stop()
   ?};
   ISTDEFS.cntx_pop();
   ISTDEF.cntx_pop()
?};
1


\fiks_60
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: KOD - PR/WRT/XP/19.02/1903/0044
::       Błąd w kodzie wiersza sprawozdania CIT-8/O_26 oraz pozycji deklaracji
::  OLD: \fiks_53/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
::poprawa błędu w wierszu sprawozdania
DEFW.cntx_psh(); DEFW.index('LP'); DEFW.prefix();
GR_SIK.cntx_psh(); GR_SIK.index('NAZWA'); GR_SIK.prefix();
_tmp:=sql('select GR_SIK.REFERENCE as REF from GR_SIK where SKROT=\'CIT-8/O_26\'');
{? _tmp.first()
|| FUN.prg_start(_tmp.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? GR_SIK.seek(BIT.sqlint(_tmp.REF),)
      || DEFW.prefix(GR_SIK.ref,100);
         {? DEFW.first() & DEFW.KOD='133'
         || DEFW.KOD:='133.';
            DEFW.put()
         ?}
      ?};
      FUN.prg_next();
      _tmp.next()
   !};
   FUN.prg_stop()
?};
GR_SIK.cntx_pop(); DEFW.cntx_pop();
::poprawa błedu w pozycji deklaracji
VAT_POZ.cntx_psh(); VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix();
_vp:=sql('select VAT_POZ.REFERENCE as REF from VAT_POZ join VAT_DEK join VAT_VER where VAT_VER.NAZWA=\'CIT8\' and '+
         ' (VAT_VER.NR=26) and VAT_POZ.LP=\'CIT8/O 133\'');
{? _vp.first()
|| FUN.prg_start(_vp.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? VAT_POZ.seek(BIT.sqlint(_vp.REF),)
      || VAT_POZ.POZ1:=0;
         VAT_POZ.POZ2:=133;
         VAT_POZ.put()
      ?};
      FUN.prg_next();
      _vp.next()
   !};
   FUN.prg_stop()
?};
VAT_POZ.cntx_pop();
1


\fiks_61
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: KOD - ER/WRT/XP/12.51/1903/0069
::       Poprawa błędu pozycji w e-deklaracji CIT-8/O_26
::  OLD: \fiks_54/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS.cntx_psh; ISTDEFS.index('LP'); ISTDEFS.prefix();
_idefs:=sql('select ISTDEFS.REFERENCE as REF from ISTDEFS join ISTDEF where (ISTDEF.VER=\'CIT-8 (26)\''
           +' or ISTDEF.VER=\'CIT-8 (27)\') and (ISTDEFS.OPIS=\'P_101\' or ISTDEFS.OPIS=\'P_102\')');
{? _idefs.first()
|| FUN.prg_start(_idefs.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? ISTDEFS.seek(BIT.sqlint(_idefs.REF),)
      || {? ISTDEFS.WYM='T'
         || ISTDEFS.WYM:='N';
            ISTDEFS.put()
         ?}
      ?};
      FUN.prg_next();
      _idefs.next()
   !};
   FUN.prg_stop()
?};
ISTDEFS.cntx_pop();
1


\fiks_62
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1903/0072
::       JPK_SF: - błędne algorytmy w sprawozdaniu RZiS w wersji porównawczej dla jednostki małej.
::  OLD: \fiks_55/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('E_KOD',GR_SIK)>0
|| _sql:=sql(
      'select REFERENCE as REF '+
      'from GR_SIK '+
      'where E_KOD=\'RZiSPor\''
   );
   {? _sql.first()
   || GR_SIK.cntx_psh();
      GR_SIK.prefix();
      DEFW.cntx_psh();
      DEFW.index('E_KOD');
      DEFA.cntx_psh();
      DEFA.index('LP');
      FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
      {!
      |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
         || _zmiana:=0;
            DEFW.prefix(GR_SIK.ref(),'B_V',);
            {? DEFW.first() & DEFW.ALGORYTM<>'F'
            || _ref:=DEFW.ref();
               DEFA.prefix(DEFW.ref());
               {? DEFA.first()
               || {!
                  |? {? DEFA.ARGUMENT().E_KOD='B_V_1'
                     || DEFA.del()
                     || DEFA.next()
                     ?}
                  !}
               ?};
               {? ~DEFA.first() & DEFW.seek(_ref)
               || DEFW.ALGORYTM:='F';
                  DEFW.put();
                  _zmiana:=1
               ?}
            ?};
            DEFW.prefix(GR_SIK.ref(),'C',);
            {? DEFW.first() & DEFW.ALGORYTM='S'
            || DEFA.prefix(DEFW.ref());
               {? DEFA.first()
               || {!
                  |? {? DEFA.ARGUMENT().E_KOD='B' & DEFA.WSP=1
                     || DEFA.WSP:=-1;
                        DEFA.put();
                        _zmiana:=1
                     ?};
                     DEFA.next()
                  !}
               ?}
            ?};
            {? _zmiana & GR_SIK.AKC<>'N'
            || GR_SIK.AKC:='N';
               GR_SIK.put()
            ?}
         ?};
         FUN.prg_next();
         _sql.next()
      !};
      FUN.prg_stop();
      DEFA.cntx_pop();
      DEFW.cntx_pop();
      GR_SIK.cntx_pop()
   ?}
?};
1


\fiks_63
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1903/0080
::       CIT-8 - błedne wartości w polach 104 i 109 dla sprawozdań CIT8-_26 i CIT8-_27
::  OLD: \fiks_56/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
GR_SIK.cntx_psh(); DEFW.cntx_psh();
_it:=GR_SIK.ndx_tmp('',1,'SKROT',,0);
GR_SIK.index(_it);
DEFW.index('GRUPA');
GR_SIK.prefix('CIT-8_26',);
{? GR_SIK.first()
|| FUN.prg_start(GR_SIK.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? DEFW.prefix(GR_SIK.ref(),'104.',);
      {? DEFW.first()
      || {? DEFW.WSPOLF='{? SIK.WARTOSC<0 || 0 || SIK.WARTOSC:=SIK.WARTOSC#0; 1 ?}'
         || DEFW.WSPOLF:='{? SIK.WARTOSC<0 || 0 || SIK.WARTOSC:=SIK.WARTOSC$0; 1 ?}';
            DEFW.put
         ?}
      ?};
      DEFW.prefix(GR_SIK.ref(),'109.',);
      {? DEFW.first()
      || {? DEFW.WSPOLF='SIK.WARTOSC:=SIK.WARTOSC#0; 1'
         || DEFW.WSPOLF:='SIK.WARTOSC:=SIK.WARTOSC$0; 1';
            DEFW.put()
         ?}
      ?};
      FUN.prg_next();
      GR_SIK.next()
   !};
   FUN.prg_stop()
?};
GR_SIK.prefix('CIT-8_27',);
{? GR_SIK.first()
|| FUN.prg_start(GR_SIK.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? DEFW.prefix(GR_SIK.ref(),'104.',);
      {? DEFW.first()
      || {? DEFW.WSPOLF='{? SIK.WARTOSC<0 || 0 || SIK.WARTOSC:=SIK.WARTOSC#0; 1 ?}'
         || DEFW.WSPOLF:='{? SIK.WARTOSC<0 || 0 || SIK.WARTOSC:=SIK.WARTOSC$0; 1 ?}';
            DEFW.put()
         ?}
      ?};
      DEFW.prefix(GR_SIK.ref(),'109.',);
      {? DEFW.first()
      || {? DEFW.WSPOLF='SIK.WARTOSC:=SIK.WARTOSC#0; 1'
         || DEFW.WSPOLF:='SIK.WARTOSC:=SIK.WARTOSC$0; 1';
            DEFW.put()
         ?}
      ?};
      FUN.prg_next();
      GR_SIK.next()
   !};
   FUN.prg_stop()
?};
DEFW.cntx_pop(); GR_SIK.cntx_pop();
1


\fiks_64
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Zmiana okresu obowiązywania CIT8(27)
::  OLD: \fiks_57/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
VAT_VER.cntx_psh(); ISTDEF.cntx_psh(); VAT_DEK.cntx_psh();
VAT_VER.index('VER_OD');
VAT_VER.prefix('FKS','CIT8','CIT8');
{? VAT_VER.find_key(date(2018,1,1),27)
|| _ref18:=VAT_VER.ref();
   {? VAT_VER.find_key(date(2019,1,1),27)
   || _ref19:=VAT_VER.ref();
      {? VAT_VER.count()=0
      || {? VAT_VER.del() || _ok:=1 ?}
      || ISTDEF.index('DATA');
         ISTDEF.prefix('FKS','D','CIT8',);
         {? ISTDEF.first()
         || {!
            |? {? ISTDEF.NR=_ref19
               || ISTDEF.NR:=_ref18;
                  ISTDEF.put()
               ?};
               ISTDEF.next()
            !}
         ?};
         VAT_DEK.index('VAT_VER');
         VAT_DEK.prefix(_ref19);
         {? ~VAT_DEK.first()
         || {? VAT_VER.del() || _ok:=1 ?}
         ?}
      ?}
   || _ok:=1
   ?}
|| {? VAT_VER.find_key(date(2019,1,1),27)
   || VAT_VER.OD:=date(2018,1,1);
      {? VAT_VER.put() || _ok:=1 ?}
   ?}
?};
VAT_DEK.cntx_pop(); ISTDEF.cntx_pop(); VAT_VER.cntx_pop();
_ok


\fiks_65
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1905/0018
::       Błędna reguła dla atrybutu 'wersjaSchema' w Zalaczniku ORD-ZU definicji CIT-8(26)
::  OLD: \fiks_58/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFI.cntx_psh; ISTDEFI.index('LP'); ISTDEFI.prefix();
_idefi:=sql('select ISTDEFI.REFERENCE as REF from ISTDEFI join ISTDEFS join ISTDEF'
           +' where (ISTDEF.VER=\'CIT-8 (26)\')'
           +' and (ISTDEFS.OPIS=\'zzu:KodFormularza\') and ISTDEFI.NAZ=\'wersjaSchemy\'');
{? _idefi.first()
|| FUN.prg_start(_idefi.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? ISTDEFI.seek(BIT.sqlint(_idefi.REF),)
      || {? ISTDEFI.REGULY='\'2-0E\''
         || ISTDEFI.REGULY:='\'1-0E\'';
            ISTDEFI.put()
         ?}
      ?};
      FUN.prg_next();
      _idefi.next()
   !};
   FUN.prg_stop()
?};
ISTDEFI.cntx_pop();
1


\fiks_67
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1908/0011 - błędnie wypełniana wartość pola P_10 w załączniku VAT - ZT
::  OLD: \fiks_60/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS.cntx_psh; ISTDEFS.index('LP'); ISTDEFS.prefix();
_istdefs:=sql('select ISTDEFS.REFERENCE as REF from ISTDEFS join ISTDEF'
           +' where (ISTDEF.VER=\'VAT-7 (18)\')'
           +' and (ISTDEFS.OPIS=\'vzt:P_10\')');
{? _istdefs.first()
|| FUN.prg_start(_istdefs.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? ISTDEFS.seek(BIT.sqlint(_istdefs.REF),)
      || {? ISTDEFS.REGULY='exec(\'poz_edek\',\'xml\',58)'
         || ISTDEFS.REGULY:='exec(\'poz_edek\',\'xml\',59)';
            ISTDEFS.put()
         ?}
      ?};
      FUN.prg_next();
      _istdefs.next()
   !};
   FUN.prg_stop()
?};
ISTDEFS.cntx_pop();
1


\fiks_68
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/1912/0040 - Błąd w schemacie_deklaracja VAT7(20)_Osoba Fizyczna
::  OLD: \fiks_61/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=sql(
   'select ISTDEFS.REFERENCE as REF from ISTDEFS join ISTDEF '+
   'where ISTDEF.VER=\'VAT-7 (20)\' and ISTDEFS.OPIS=\'tns:OsobaFizyczna\''
);
{? _tab.first()
|| ISTDEFS.cntx_psh();
   ISTDEFS.index('DRZEWO');
   ISTDEFS.prefix();
   {? ISTDEFS.seek(BIT.sqlint(_tab.REF),)
   || ISTDEFS.OPIS:='OsobaFizyczna';
      ISTDEFS.put();
      ISTDEFS.prefix(ISTDEFS.ISTDEF,#ISTDEFS.ref());
      {? ISTDEFS.first()
      || {!
         |? {? ISTDEFS.OPIS='etd:DataUrodzenia'
            || ISTDEFS.del()
            |? 4+ISTDEFS.OPIS='etd:'
            || ISTDEFS.OPIS:=4-ISTDEFS.OPIS;
               {? ISTDEFS.OPIS='NIP'
               || ISTDEFS.REGULY:=$"STR.gsub(XINFO.NIP,'-','')"
               ?};
               ISTDEFS.put();
               ISTDEFS.next()
            ?}
         !}
      ?}
   ?};
   ISTDEFS.cntx_pop()
?};
1


\fiks_69
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/2001/0067 - BLP - sprawdzenie jednego kontrahenta przypisywane jest innym kontrahentom
::       Formuła usuwająca błędne wpisy w historii sprawdzeń w wykazie podatników VAT.
::  OLD: \fiks_63/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
KHCHKNIP.cntx_psh();
KHCHKNIP.index('NIP');
KHCHKNIP.prefix();
KH.cntx_psh();
{? KHCHKNIP.first()
|| FUN.prg_start(KHCHKNIP.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? FUN.prg_next();
      {? KHCHKNIP.NIP<>'' & KHCHKNIP.KH().SNIP<>'' & KHCHKNIP.NIP<>KH.SNIP
      || KHCHKNIP.del()
      || KHCHKNIP.next()
      ?}
   !};
   FUN.prg_stop()
?};
KH.cntx_pop();
KHCHKNIP.cntx_pop();
1


\fiks_70
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/2003/0032
::       Błędne nip bez kresek w tabeli kontrahentów SNIP
::  OLD: \fiks_65/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
KH.trig_off('*','*');
KH.cntx_psh();
KH.prefix();
KH.for_each("
   _put:=0;
   {? KH.NIP*' ' || _snip:=exec('niptostr','#string',KH.NIP); KH.SNIP:=_snip; _put:=1 ?};
   {? KH.GRVAT='' || KH.GRVAT:='N'; _put:=1 ?};
   {? KH.TAGGER=''
   || {? KH.NAZ<>'Kontrahent jednorazowy' & PAR_SKID.get(120)<>KH.KOD || KH.TAGGER:='T' || KH.TAGGER:='N' ?};
      _put:=1
   ?};
   {? _put || KH.put(1) ?}
   ",1);
KH.trig_on('*','*');
KH.cntx_pop();
1


\fiks_71
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: kod - ER/WRT/XP/12.51/2003/0018
::       JPK_FA(2) i JPK_FA(3) brak NIP UE
::  OLD: \fiks_66/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','FA',null,date(2019,7,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'P_5B');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[50]';
      ISTDEFS.put()
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'P_4B');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[50]';
      ISTDEFS.put()
   ?}
?};

ISTDEF.prefix('FKS','J','FA',null,date(2019,11,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'P_5B');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[50]';
      ISTDEFS.put()
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'P_4B');
   {? ISTDEFS.first()
   || ISTDEFS.TYPFLD:='STRING[50]';
      ISTDEFS.put()
   ?}
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
1


\fiks_72
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2004/0006
::       JPK_SF:Błędne wyświetlanie opisów dla P_5A i P_5B
::  OLD: \fiks_68/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF','Jednostka');
{? ISTDEF.first()
|| FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? ISTDEF.VER+4='v1-2'
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS');
         ISTDEFS.prefix(ISTDEF.ref(),'tns:P_5A',);
         _ile:=ISTDEFS.size();
         {? ISTDEFS.first()
         || _lp:={? _ile=1 || 2 || 1 ?};
            {!
            |? ISTDEFS.memo_set(
                  {? _lp=1
                  || 'Wskazanie, czy sprawozdanie finansowe zostało sporządzone przy założeniu kontynuowania działalności gospodarczej przez jednostkę w dającej się przewidzieć przyszłości: true - sprawozdanie sporządzone przy założeniu kontynuowania działalności, false - sprawozdanie zostało sporzadzone przy zalożeniu, że działalność nie będzie kontynuowana'
                  || 'Wskazanie, czy sprawozdanie finansowe zostało sporządzone przy założeniu kontynuowania działalności gospodarczej przez jednostkę w dającej się przewidzieć przyszłości'
                  ?},
                  'ANOTACJA'
               );
               ISTDEFS.memo_put(,'ANOTACJA');
               ISTDEFS.put();
               _lp+=1;
               ISTDEFS.next()
            !}
         ?};
         ISTDEFS.prefix(ISTDEF.ref(),'tns:P_5B',);
         {? ISTDEFS.first()
         || {!
            |? ISTDEFS.memo_set(
                  'Wskazanie, czy nie istnieją okoliczności wskazujące na zagrożenie kontynuowania przez nią działalności: true - Brak okoliczności wskazujących na zagrożenie kontynuowania działalności; false - Wystąpiły okoliczności wskazujące na zagrożenie kontynuowania działalności',
                  'ANOTACJA'
               );
               ISTDEFS.memo_put(,'ANOTACJA');
               ISTDEFS.put();
               ISTDEFS.next()
            !}

         ?};
         ISTDEFS.cntx_pop()
      ?};
      FUN.prg_next();
      ISTDEF.next()
   !};
   FUN.prg_stop()
?};
ISTDEF.cntx_pop();
1


\dek_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Uaktualnienie pola DEK_NAG.NAZ
::----------------------------------------------------------------------------------------------------------------------
DEK_NAG.prefix();
DEK_NAG.for_each("
   {? DEK_NAG.NAZ=''
   || DEK_NAG.NAZ:=exec('dek_nag_naz','dok_fks_aut_dok','',0);
      DEK_NAG.put()
   ?}
",1);
1


\dek_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Wprowadzenie numeracji pozycji schematów dziedzinowych (DEK_POZ.LP)
::----------------------------------------------------------------------------------------------------------------------
DEK_NAG.for_each("
   _lp:=0;
   DEK_POZ.index('DEK_POZ'); DEK_POZ.prefix(DEK_NAG.ref());
   {? DEK_POZ.first()
   || {!
      |? DEK_POZ.LP:=(_lp+=1);
         DEK_POZ.put();
         DEK_POZ.next()
      !}
   ?}
",1);
1


\slo_rodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Aktualizacja wartości nowych pól w tabeli SLO_RODZ: LP, SYSTEM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:="{? var_pres('__kom')=var_pres('JCQ',@.CLASS) || exec('add_kom','#message',_a,4,'rkdx') || FUN.info(_a) ?}";
_ret:=0;
_NAG:=sql('select distinct SLO_RODZ.SLO_KOD from SLO_RODZ order by 1');
{? type_of(_NAG)=type_of(~~)
|| _msg('Błąd wewnetrzny. Procedurę należy powtórzyć.'@)
|? _NAG.first()
|| FUN.prg_start(_NAG.size());
   _kod:=exec('kod','ext_slo','P_DODINF','KONTAKT');
   SLO_RODZ.cntx_psh();
   SLO_RODZ.trig_off('*','*');
   SLO_RODZ.prefix();
   _lzmian:=0;
   _ok:=1;
   {!
   |? SLO_RODZ.f_set('LP,KOD',,'"SLO_RODZ"."SLO_KOD"=\':_a\'',_NAG.SLO_KOD);
      do();
      {? SLO_RODZ.f_first()
      || SLO_RODZ.SLO_KOD();
         _lp:=1;
         {!
         |? _put:=0;
            {? SLO_RODZ.LP<>_lp
            || SLO_RODZ.LP:=_lp;
               _put+=1
            ?};
            {? SLO_RODZ.SYSTEM=''
            || SLO_RODZ.SYSTEM:={? SLO_RODZ.SLO_KOD=_kod & ',WEW,KOM,E_MAIL,'*',%1,' [SLO_RODZ.KOD] || 'T' || 'N' ?};
               _put+=1
            ?};
            {? _put
            || _lzmian+=SLO_RODZ.put()
            ?};
            _lp+=1;
            SLO_RODZ.f_next()
         !}
      ?};
      {? ~end()
      || _ok:=0
      ?};
      FUN.prg_next();
      _NAG.next()
   !};
   FUN.prg_stop();
   SLO_RODZ.f_clear();
   SLO_RODZ.trig_on('*','*');
   SLO_RODZ.cntx_pop();

   {? ~_ok
   || _msg('Aktualizacja wartości pól SLO_RODZ.LP i SLO_RODZ.SYSTEM nie powiodła się.');
      _ret:=-1
   || _ret:=1
   ?}
|| _ret:=1
?};
_ret


\r_marg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [20.42]
:: OPIS: Aktualizacja wartości nowego pola w tabeli R_MARG: RODZAJ.
::   WE:
::   WY:
::  ORG: \margines_we_wy/upgrade_2042.fml
::----------------------------------------------------------------------------------------------------------------------
_msg:="{? var_pres('__kom')=var_pres('JCQ',@.CLASS) || exec('add_kom','#message',_a,4,'rkdx') || FUN.info(_a) ?}";
_ret:=0;
R_MARG.cntx_psh();
R_MARG.use('r_margi');
R_MARG.prefix();
{? R_MARG.for_each("{? R_MARG.RODZAJ='' || R_MARG.RODZAJ:='S'; R_MARG.put() ?}",1)
|| _ret:=1
|| _msg('Aktualizacja wartości pól R_MARG.RODZAJ nie powiodła się.');
   _ret:=-1
?};
R_MARG.cntx_pop();
_ret


\fiks_74
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2003/0060
::       Błąd przy jpk-sf 1.2 dla jednostki małej w  tys
::  OLD: \fiks_70/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
_fix:=-1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','SF','JednostkaMalaWTysiacach(1)_v1-2',);
{? ISTDEF.first()
|| _fix:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'jma:F_I_1',);
   {? ISTDEFS.first()
   || _start:=_next:=0;
      ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.TREE,);
      {? ISTDEFS.next()
      || _next:=ISTDEFS.LP
      ?};
      ISTDEFS.cntx_pop();
      ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),#ISTDEFS.ref());
      {? ISTDEFS.first()
      || {!
         |? {? ISTDEFS.OPIS='jma:PozycjaUszczegolawiajaca_1'
            || ISTDEFS.OPIS:='jma:PozycjaUszczegolawiajaca';
               ISTDEFS.put()
            |? ISTDEFS.OPIS='jma:G_I_A_1'
            || _start:=ISTDEFS.LP
            ?};
            _start=0 & ISTDEFS.next()
         !}
      ?};
      ISTDEFS.cntx_pop();
      {? _next & _start
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('LP'); ISTDEFS.prefix(ISTDEF.ref());
         {! _ii:=_next-1//-1.._start
         |! {? ISTDEFS.find_key(_ii)
            || ISTDEFS.del()
            ?}
         !};
         {? ISTDEFS.find_key(_next)
         || {!
            |? ISTDEFS.LP:=_start;
               ISTDEFS.put();
               _start+=1;
               ISTDEFS.next()
            !}
         ?};
         ISTDEFS.cntx_pop();
         _fix:=1
      ?}
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
1


\fiks_75
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2002/0048
::       Nieprawidłowo wyliczana kwota VAT split payment
::  OLD: \fiks_71/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Fix');
Fix:=0;
_start:=date(2019,1,1);
_ar:=SSTALE.AR;
_ao:=SSTALE.AO;
ROK_F.cntx_psh();
ROK_F.index('ROKPOCZ');
ROK_F.prefix();
{? ROK_F.first()
|| OKRO_F.cntx_psh();
   OKRO_F.index('ROK');
   {!
   |? {? ROK_F.ZAM<>'T' & ROK_F.POCZ_ROK>=_start
      || SSTALE.AR:=ROK_F.ref();
         OKRO_F.prefix(SSTALE.AR);
         {? OKRO_F.first()
         || {!
            |? SSTALE.AO:=OKRO_F.ref();
               exec('open_tabele','open_tab');
               echo('Naprawa dokumentów - Firma: '+ROK_F.FIRMA().SYMBOL+', rok: '+ROK_F.NAZ+', okres: '+OKRO_F.NAZ);
               DOK.prefix();
               _sql:=sql(
                  'select DOK.REFERENCE as ref, count(*) as size '+
                  'from DOK join VPOZ where DOK.A=\'T\' '+
                  'group by DOK.REFERENCE, DOK.NK '+
                  'HAVING count(*)>1 '+
                  'order by 1'
               );
               {? _sql.first()
               || {!
                  |? {? DOK.seek(BIT.sqlint(_sql.REF),)
                     || exec('upd_dok','fks_sp',"Fix+=1; exec('update_ks1','fks_sp')")
                     ?};
                     _sql.next()
                  !}
               ?};
               &_sql;
               OKRO_F.next()
            !}
         ?}
      ?};
      ROK_F.next()
   !};
   OKRO_F.cntx_pop()
?};
ROK_F.cntx_pop();
{? SSTALE.AO<>_ao | SSTALE.AR<>_ar
|| SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   exec('open_tabele','open_tab')
?};
echo();
VAR_DEL.delete('Fix');
1


\napraw_walut
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MP] [20.42]
:: OPIS: Naprawa wartości wn i ma dla tabeli POZ dla rekordów walutowych
::----------------------------------------------------------------------------------------------------------------------
_poz:=POZ.names();
_r1:=0;
{? _poz.first()
|| POZ.cntx_psh();
   DOK.cntx_psh();
   FUN.prg_start(_poz.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? POZ.use(_poz.NAME); POZ.prefix();
      DOK.use('doku'+(_poz.NAME+4)); DOK.prefix();
      POZ.for_each("{? POZ.SUMW$2<>0 & POZ.MA_WAL$2=0 & POZ.WN_WAL$2=0 || _r1:=1; POZ.put() ?} ");
      FUN.prg_next();
      _poz.next()
   !};
   FUN.prg_stop();
   DOK.cntx_pop();
   POZ.cntx_pop()
?};
1


\fiks_76
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.14]
:: OPIS: kod - ER/WRT/XP/20.14/2004/0029 : E-Bilans mała
::  OLD: \fiks_67/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
_s_bil:='eBilansM'; _ret:=0;
_sql:=sql('select REFERENCE as REF from GR_SIK where SKROT=\':_a\'',_s_bil);
{? _sql.first()
|| GR_SIK.cntx_psh();
   GR_SIK.prefix();
   FUN.prg_start(_sql.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? GR_SIK.seek(BIT.sqlint(_sql.REF),)
      || _r1:=exec('fiks_67a','upgrade_2014',GR_SIK.ref(),'Aktywa_B_III_A','Aktywa_B_III_A','A_B_III_A',
                   'a)krótkoterminowe aktywa finansowe, w tym:'
                   );
         _r2:=exec('fiks_67a','upgrade_2014',GR_SIK.ref(),'Pasywa_B_II','Pasywa_B_II','P_B_II',
                   'Zobowiązania długoterminowe, w tym:'
                   );
         _r3:=exec('fiks_67a','upgrade_2014',GR_SIK.ref(),'Pasywa_B_II_1','Pasywa_B_II_1','P_B_II_1',
                   '- z tytułu kredytu i pożyczek'
                   );
         {? (_r1 | _r2 | _r3) & GR_SIK.AKC<>'N'
         || GR_SIK.AKC:='N'; GR_SIK.put()
         ?};
         {? _r1 | _r2 | _r3 || _ret:=1 ?};
         ~~
      ?};
      FUN.prg_next();
      _sql.next()
   !};
   FUN.prg_stop();
   GR_SIK.cntx_pop()
?};
&_sql;
1


\fiks_77
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: kod: ER/WRT/XP/12.51/2009/0002 - JPK_V7: Brak możliwości oznaczenia pozycji w wierszu KorektaPodstawyOpodt dla
::            sprzedaży
::  OLD: \fiks_73/upgrade_2014.fml
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','V7');
{? ISTDEF.first()
|| FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'KorektaPodstawyOpodt',);
      {? ISTDEFS.first() & ISTDEFS.REGULY=$"{? VAT_PS.TYP='S' || '1' || '' ?}"
      || ISTDEFS.REGULY:=$"{? VAT_PS.TYP='N' || '1' || '' ?}";
         {? ISTDEFS.put()
         || _jest:=1;
            __UPG.msg('Poprawiono schemat: %1'[ISTDEF.VER])
         ?}
      ?};
      ISTDEFS.cntx_pop();
      FUN.prg_next();
      ISTDEF.next()
   !};
   FUN.prg_stop()
?};
ISTDEF.cntx_pop();
1


\formula_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [19.42]
:: OPIS: ER/WRT/XP/19.42/2005/0004 Tworzenie podziałów dla  dokumentów magazynowych
::----------------------------------------------------------------------------------------------------------------------
FORMULA.for_each("FORMULA.FORMULA:=gsub(FORMULA.FORMULA,'sch_par','%control');FORMULA.put()",1)


\alg_par_wyl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [19.42]
:: OPIS: Naprawa rekordów ALG_PAR po błędnym dodawaniu z poziomu planu kont
::----------------------------------------------------------------------------------------------------------------------
ALG_PAR.cntx_psh();
ALG_PAR.index('ALG_PAR1');
ALG_PAR.prefix();
__liczba:=0;
ALG_PAR.for_each("
    {? ALG_PAR.WYL='' || __liczba+=1; ALG_PAR.WYL:='N'; ALG_PAR.put() ?}
    ",1);
ALG_PAR.cntx_pop();
VAR_DEL.delete('__liczba');
1


\kwaldost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: Utworzenie domyslnych statusow i nadadnie
::----------------------------------------------------------------------------------------------------------------------
exec('nowedane','statexam');
STATS.clear();
STATS.index('NN');
{? STATS.find_key('~Zgodny','~Zgodny')
|| exec('status_magazyny','transfer1',STATS.ref());
   exec('status_typydok' ,'transfer1');
   exec('status_dostaw'  ,'transfer1',STATS.ref());
   ~~
|| FUN.error('Nie odnaleziono domyślnego statusu ~Zgodny.\nNie nadano domyślnych statusów w systemie');
   ~~
?};
~~


\status_magazyny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: wypelnia pole MG.DEFSTATS wartoscia domyslna
::       [rr] i dodatkowo uzupelnia dane zwiazane z domyslnymi parametrami dla palet ;)
::   WE: _a - STATS.ref()
::UWAGA: brak kontroli typow parametrow
::  OLD: \status_magazyny/skid_trn.fml
::----------------------------------------------------------------------------------------------------------------------
:: statusy dostaw tylko dla magazynow typu dostawy
MG.cntx_psh();
MG.clear();
MG.index('MAGAZYNY');
{? MG.first()
|| FUN.prg_start(MG.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? MG.SPADD='' || MG.SPADD:='N' ?};
      {? MG.DOPAS='' || MG.DOPAS:='N' ?};
      MG.put(1);
      {? MG.DEFSTATS=null()
       & 1+MG.TYP='D'
      || MG.DEFSTATS:=_a;
         MG.put(1)
      ?};
      FUN.prg_next();
      MG.next()
   !};
   FUN.prg_stop()
?};
MG.cntx_pop();
~~


\status_typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: uzupelnia pola STATS_Z,STATS_O,STATS_N
::  OLD: \status_typydok/skid_trn.fml
::----------------------------------------------------------------------------------------------------------------------
TYPYDOK.cntx_psh();
TYPYDOK.clear();
TYPYDOK.blank();
_STATS_Z:=TYPYDOK.STATS_Z;
_STATS_O:=TYPYDOK.STATS_O;
_STATS_N:=TYPYDOK.STATS_N;
TYPYDOK.index('TYP');
{? TYPYDOK.first()
|| FUN.prg_start(TYPYDOK.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? TYPYDOK.ZLEC='' || TYPYDOK.ZLEC:='N' ?};
      {? TYPYDOK.WYR=''  || TYPYDOK.WYR:='N' ?};
      {? TYPYDOK.P='N'
      || {?     +TYPYDOK.STATS_Z=0
          | 'TN'*TYPYDOK.STATS_Z=0 || TYPYDOK.STATS_Z:=_STATS_Z ?};
         {?     +TYPYDOK.STATS_O=0
          | 'TN'*TYPYDOK.STATS_O=0 || TYPYDOK.STATS_O:=_STATS_O ?};
         {?     +TYPYDOK.STATS_N=0
          | 'TN'*TYPYDOK.STATS_N=0 || TYPYDOK.STATS_N:=_STATS_N ?};
         ~~
      |? TYPYDOK.P='T'
      || TYPYDOK.STATS_Z:='N';
         TYPYDOK.STATS_O:='N';
         TYPYDOK.STATS_N:='N';
         ~~
      ?};
      TYPYDOK.put(1);
      FUN.prg_next();
      TYPYDOK.next()
   !};
   FUN.prg_stop()
?};
TYPYDOK.cntx_pop();
~~


\status_dostaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: uzupelnia pole status dla DK i SC
::   WE: _a - STATS.ref() - domyslny status
::UWAGA: SC nadpisywane sa wszystkie
::       DK tylko dla dokumentow przychodowych
::  OLD: \status_dostaw/skid_trn.fml
::----------------------------------------------------------------------------------------------------------------------
_ndm:=ND.names();

MG.cntx_psh();
ND.cntx_psh();   _ND:=5+ND.name();
DK.cntx_psh();   _DK:=5+DK.name();
DK_HS.cntx_psh();_DK_HS:=5+DK_HS.name();
SC.cntx_psh();   _SC:=5+SC.name();

_mydo:=do_state=0;

{?_ndm.first()
|| _ndsum:=0;
   {!|? _ndsum+=_ndm.SIZE; _ndm.next() !};

   {? _ndsum>0 || FUN.prg_start(_ndsum,'Uzupełnienie statusu na dokumentach.',,,1) ?};
   _ndm.first();
   {!
   |?
      {? _ndm.SIZE>0
      || _mask:=_ndm.NAME+3;
         ND.use(_ND+_mask);      ND.clear();    ND.index('CONTROL');
         DK.use(_DK+_mask);      DK.clear();    DK.index('DOKMAG');
         DK_HS.use(_DK_HS+_mask);   DK_HS.clear(); DK_HS.index('DK');
         SC.use(_SC+_mask);      SC.clear();    SC.index('SN');

         {? ND.first()
         || {!
            |?
               {? 1+ND.MAG().TYP='D'
                & ND.TYP().P='T'
                & ND.Z='T'
               || {? _mydo || do() ?};
                  exec('nd_dk_stats','transfer1',ND.ref());
                  {? _mydo || end() ?};
                  ~~
               |? 1+ND.MAG().TYP='D'
                  & ND.TYP().P='N'
               ||
                  DK.index('DOKMAG');
                  DK.prefix(ND.ref);
                  {? DK.first
                  ||
                     _stats:=DK.N().MAG().DEFSTATS;
                     {!
                     |?
                        {? DK.STATS=null || DK.STATS:=_stats ?};
                        DK.put & DK.next
                     !}
                  ?}
               ?};
               FUN.prg_next();
               ND.next()
            !}
         ?}
      ?};
      _ndm.next()
   !};
   {? _ndsum>0 || FUN.prg_stop() ?};
   ~~
?};

SC.cntx_pop();
DK_HS.cntx_pop();
DK.cntx_pop();
ND.cntx_pop();
MG.cntx_pop();
~~


\nd_dk_stats
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: Nadaje/aktualizuje statusy dostaw
::   WE: _a - ND.ref()
::       _b - wymuszenie nadania statusu (domyslnie 0)
::   WY: ~~
::UWAGA: Formula wywolywana w transakcji w akc_dok/mag_fun.fml
::  OLD: \nd_dk_stats/skid_trn.fml
::----------------------------------------------------------------------------------------------------------------------
:: jezeli nie jestesmy w transakcji to nie ma sensu tracic energii
{? do_state()=1
|| _NDref:={?_>0 & type_of(_a)=type_of(ND.ref()) || _a || return() ?};
   _force:={?_>1 & type_of(_b)=type_of(1)        || _b || 0 ?};
   ND.cntx_psh();
   DK.cntx_psh();

   DK.index('DOKMAG');
   DK.prefix(_NDref);
   {? DK.first()
    & ND.TYP().P='T'
    & 1+ND.MAG().TYP='D'
   ||
      _dfs_mg:=DK.N().MAG().DEFSTATS;
::    materialy dla magazynu (dokladniej domyslny status materialu w magazynie)
      MX.cntx_psh();
      MX.index('M_MG');
      MX.prefix(DK.N().MAG);
      MX.first();
::    stany cenowe dla magazynu
      SC.cntx_psh();
      SC.index('SN');
      SC.prefix(DK.N().MAG);
      SC.first();
::    teraz spacer po pozycjach dokumentu
      {!
      |?
         {? DK.STATS=null
         ||
            _kind:='A';
            _stats:=_dfs_mg;
            {? MX.find_key(DK.M)
             & MX.A='T'
             & MX.DEFSTATS<>null()
            || _stats:=MX.DEFSTATS;
               ~~
            ?};
            {? DK.STATS=null()
             | _force
            || _kind:='N';
               DK.STATS:=_stats;
               DK.put()
            ?};
::          aktualizacja na SC
            {? SC.find_key(DK.M,#DK.ref(),DK.name())
            || SC.STATS:=DK.STATS;
               SC.put();
               ~~
            ?};
            exec('DK_HS_new4DK','statexam',DK.ref(),_kind,,,DK.STATS)
         ?};
         DK.next()
      !};
      MX.cntx_pop();
      SC.cntx_pop();
      ~~
   ?};
   DK.cntx_pop();
   ND.cntx_pop();
   ~~
?}


\os_us
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Uzupełnienie nowych pól dla tabeli OS_US
::  OLD: \os_us/upgrade_2042_02.fml
::----------------------------------------------------------------------------------------------------------------------
OS_US.cntx_psh();
OS_US.clear();
OS_US.for_each("OS_US.put(1)",1);
OS_US.cntx_pop()


\h_um_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [21.14]
:: OPIS: Uzupełnienie nowych pól dla tabeli H_UM
::  OLD: \akt_32/napraw_p.fml
::  OLD: \h_um/upgrade_2114.fml
::  OLD: \h_um_pola/upgrade_2226.fml
::  OLD: \pola/upgrade_2226_nru01.fml
::----------------------------------------------------------------------------------------------------------------------
:: Zmiany dotyczące rubryk przeniesiono do \r_imp.
:: Zmiany dotyczące atrybutów przeniesiono do \rubatr.
_MSK:=H_UM.names();
_MSK.clear();
{? _MSK.first()
|| H_UM.cntx_psh();
   H_UM.trig_off('*','*');
   {!
   |? H_UM.use(_MSK.NAME);
      H_UM.clear();
      H_UM.for_each("_put:=0;
                     {? H_UM.WYM_UZAS=''
                     || H_UM.WYM_UZAS:='N';
                        _put+=1
                     ?};
                     {? H_UM.EKW_LICZ=''
                     || H_UM.EKW_LICZ:='N';
                        _put+=1
                     ?};
                     {? ~(H_UM.P_OD<>date(0,0,0))
                     || H_UM.P_OD:=H_UM.OD;
                        H_UM.P_DO:=H_UM.DO;
                        _put+=1
                     ?};
                     {? ~(',T,N,'*',%1,'[H_UM.PRZ_USPR])
                     || H_UM.PRZ_USPR:='N';
                        _put+=1
                     ?};
                     {? ~(',T,N,'*',%1,'[H_UM.ZAM_ZAW])
                     || H_UM.ZAM_ZAW:='N';
                        _put+=1
                     ?};
                     {? _put
                     || H_UM.put(1)
                     ?}",1);
      _MSK.next()
   !};
   H_UM.trig_on('*','*');
   H_UM.cntx_pop()
?};
obj_del(_MSK);
1


\rh_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [21.14]
:: OPIS: Uzupełnienie nowych pól dla tabeli RH
::  OLD: \akt_24/napraw_p.fml
::  OLD: \rh_godz/upgrade_2114.fml
::----------------------------------------------------------------------------------------------------------------------
RH.cntx_psh();
RH.clear();
RH.trig_off('*','*');
RH.for_each("_put:=0;
             {? RH.KOR=''
             || RH.KOR:='N';
                _put:=1
             ?};
             {? RH.GODZ=''
             || RH.GODZ:='N';
                _put:=1
             ?};
             {? _put
             || RH.put(1)
             ?}",1);
RH.trig_on('*','*');
RH.cntx_pop();
1


\fiks_78
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: kod: ER/WRT/XP/12.51/2012/0004 - JPK_V7 Kodkraju nadania
::       kod: ER/WRT/XP/12.51/2009/0002 oraz ER/WRT/XP/12.51/2011/0040 - JPK_V7: Brak możliwości oznaczenia pozycji w
::       wierszu KorektaPodstawyOpodt dla sprzedaży
::  OLD: \fiks_73i74/upgrade_2042.fml
::----------------------------------------------------------------------------------------------------------------------
_licz:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','V7');
{? ISTDEF.first()
|| FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'KodKrajuNadaniaTIN',);
      {? ISTDEFS.first()
      || {! |? ISTDEFS.cntx_psh();
               ISTDEFS.TYPFLD:=$"STRING[2]";
               ISTDEFS.NAZTAB:=$"KH_KRAJ";
               _licz+=ISTDEFS.put();
               ISTDEFS.cntx_pop();
               ISTDEFS.next()
          !}
       ?};
       ISTDEFS.prefix(ISTDEF.ref(),'KorektaPodstawyOpodt',);
       {? ISTDEFS.first() &
          (
             ISTDEFS.REGULY=$"{? VAT_PS.TYP='S' || '1' || '' ?}" |
             ISTDEFS.REGULY=$"{? VAT_PS.TYP='N' || '1' || '' ?}"
          )
       || ISTDEFS.REGULY:=$"{? -VAT_PS.TYP='n' || '1' || '' ?}";
          _licz+=ISTDEFS.put()
       ?};
       ISTDEFS.cntx_pop();
       FUN.prg_next();
       ISTDEF.next()
   !};
   FUN.prg_stop()
?};
ISTDEF.cntx_pop();
1


\fiks_79
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: kod: PR/WRT/XP/20.42/2101/0051 - JPK FA(3) - brak obsługi węzła [Zamówienia] i [ZamówieniaCtrl]
::  OLD: \fiks_75/upgrade_2042.fml
::----------------------------------------------------------------------------------------------------------------------
_fml:="
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),_a,);
   {? ISTDEFS.first()
   || ISTDEFS.REGULY:=_b;
      ISTDEFS.FORMULA:=_c;
      ISTDEFS.FORM_XML:=_d;
      ISTDEFS.NAZTAB:=_e;
      ISTDEFS.TYPFLD:=_f;
      {? ISTDEFS.put()
      || __UPG.msg('%1 - poprawiono'[_a])
      ?}
   ?};
   ISTDEFS.cntx_pop()
";
_add:="
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),_a,);
   {? ISTDEFS.first()
   || _lp:=ISTDEFS.LP+1;
      _nad:=ISTDEFS.TREE;
      ISTDEFS.cntx_psh();
      ISTDEFS.index('LP'); ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.next() & ISTDEFS.OPIS<>'R'
      || {? ISTDEFS.last()
         || {!
            |? ISTDEFS.LP+=1;
               ISTDEFS.put();
               ISTDEFS.LP-1>_lp & ISTDEFS.prev()
            !}
         ?};
         ISTDEFS.blank(1);
         ISTDEFS.ISTDEF:=ISTDEF.ref();
         ISTDEFS.TREE:=_nad;
         ISTDEFS.LP:=_lp;
         ISTDEFS.OPIS:='R';
         ISTDEFS.REGULY:=_b;
         ISTDEFS.NAZTAB:='R';
         ISTDEFS.TYPFLD:='STRING[27]';
         ISTDEFS.COMMENT:='T';
         ISTDEFS.WYM:='N';
         ISTDEFS.HIDDEN:='N';
         ISTDEFS.prefix();
         {? ISTDEFS.add()
         || __UPG.msg('R - dodano za %1'[_a])
         ?}
      ?};
      ISTDEFS.cntx_pop()
   ?};
   ISTDEFS.cntx_pop()
";
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','FA','JPK_FA(3)_v1-0');
{? ISTDEF.first()
|| _fml('Zamowienie','',$"exec('fa_nag_f','jpk',_a,1)",$"exec('fa_nag_n','jpk')",'FA_ZAL','');
   _fml('P_2AZ',$"exec('fa_naz','jpk')",$"",$"",'ZAL_SYM','STRING[50]');
   _fml('WartoscZamowienia',$"form(sFB,,2,'9.')",$"",$"",'WZ','REAL');
   _fml('ZamowienieWiersz',$"",$"exec('fa_zal_p','jpk',_a)",$"exec('fa_zal_p','jpk',2)",'ZAL_POZ','REFERENCE');
   _fml('P_7Z',$"FaPoz[1]",$"",$"",'SYM','STRING[100]');
   _fml('P_8AZ',$"FaPoz[2]",$"",$"",'MIARA','STRING[8]');
   _fml('P_8BZ',$"FaPoz[3]",$"",$"",'ILOSC','REAL');
   _fml('P_9AZ',$"FaPoz[4]",$"",$"",'CN','REAL');
   _fml('P_11NettoZ',$"FaPoz[7]",$"",$"",'NETTO','REAL');
   _fml('P_11VatZ',$"FaPoz[11]",$"",$"",'VAT','REAL');
   _fml('P_12Z',$"FaPoz[9]",$"",$"",'SVAT','STRING[2]');
   _fml('ZamowienieCtrl',$"NrJPK",$"",$"",'FA_ZAL_C','');
   _fml('LiczbaZamowien',$"$NrJPK",$"",$"",'LF','INTEGER');
   _fml('WartoscZamowien',$"form(SumaB,,2,'9.')",$"",$"",'WF','REAL');
   _add('WartoscZamowienia',$"$DOK.ref()+$DOK.crc()");
   _add('P_12Z',$"FaPoz[10]")
?};
ISTDEF.cntx_pop();
1


\add_ire
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: PR/WRT/XP/20.42/2101/0019 - Zmiany związane z BREXIT - Irlandia Północna
::  OLD: \fiks_76/upgrade_2042.fml
::----------------------------------------------------------------------------------------------------------------------
KRAJE.cntx_psh(); KRAJE.index('KRAJE'); KRAJE.prefix();
SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
SLUAPPL.cntx_psh(); SLUAPPL.index('NAZ'); SLUAPPL.prefix('F');
SLO.cntx_psh(); SLO.index('SL'); SLO.prefix();
_ret:=0;
{? ~KRAJE.find_key('XI')
|| {? SLU.find_key('WALUTY')
   || {? SLO.find_key(SLU.ref(),'GBP')
      || _wal:=SLO.ref
      || _wal:=0
      ?}
   ?};
   {? _wal
   || KRAJE.blank();
      KRAJE.SYM:=KRAJE.KODZUS:=KRAJE.KODISO:='XI'; KRAJE.NAZ:='Irlandia Północna'; KRAJE.WAL:=_wal; KRAJE.IBAN:='T';
     _ret:=KRAJE.add()
   ?}


?};
{? _ret
|| {? SLU.find_key('~KRAJE UE')
   || {? ~SLO.find_key(SLU.ref(),KRAJE.SYM)
      || SLO.blank();SLO.KOD:=KRAJE.SYM; SLO.TR:=KRAJE.NAZ;
         _ret:=SLO.add(1)
      ?}
   ?}
?};
_uchwyt:=fopen('stawki_ue.dfg','ur',1);
_licz:=0; _vat:=0;
{? _uchwyt
|| {! |?
      _str:=fread(_uchwyt);
      {? _str<>'\n' & +|_str & _str*'~STAWKI VAT XI'
      || {? _vat=0
         || {? ~SLU.find_key(14+_str) | SLU.NAZ<>14+_str
            || SLU.blank(); SLU.SYSTEM:='T'; SLU.NAZ:=14+_str; SLU.WZ:='prosty'; SLU.DL:=8;
               SLU.OP:='Stawki VAT w kraju '+{? KRAJE.find_key(SLU.NAZ+2) || KRAJE.NAZ || '' ?};
               _vat:=SLU.add()
            || _vat:=1
            ?}
         ?};
         {? _vat
         || _slu:=SLU.ref();
            {? ~SLUAPPL.find_key(14+_str) | SLUAPPL.SLU().NAZ<>14+_str
            || SLUAPPL.blank(1); SLUAPPL.GDZIE:='F'; SLUAPPL.SLU:=_slu; SLUAPPL.add()
            ?};
            SLUAPPL.SLU();
            _str:=15-_str;
            {? ~SLO.find_key(SLU.ref(),1-form(('Q'+(6+_str))))
            || SLO.blank(); SLO.KOD:=1-form(('Q'+(6+_str))); SLO.TR:=7-_str;
               _licz+=SLO.add(1)
            ?}
         ?}
      ?};
      _str<>'\n'
   !};
fclose(_uchwyt)
?};
KRAJE.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop(); SLO.cntx_pop();
exec('kodZUSKraju','dane_startowe');
1


\upd_poz_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: kod: ER/WRT/XP/20.42/2101/0042 - Błędne uzupełnienie wartości w walucie na pozycjach dokumentu
::----------------------------------------------------------------------------------------------------------------------
_poz:=POZ.names();
licz:=0;
{? _poz.first()
|| POZ.cntx_psh();
   DOK.cntx_psh();
   FUN.prg_start(_poz.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? POZ.use(_poz.NAME); POZ.prefix();
      DOK.use('doku'+(_poz.NAME+4)); DOK.prefix();
      POZ.for_each("{? POZ.WAL<>FINFO.NAROD & POZ.SUMW<>0 & (POZ.STR='Ma' & POZ.MA_WAL=0 | POZ.STR='Wn' & POZ.WN_WAL=0)
                    || licz+=POZ.put()
                    ?}");
      FUN.prg_next();
      _poz.next()
   !};
   FUN.prg_stop();
   DOK.cntx_pop();
   POZ.cntx_pop()
?};
{? licz
|| __UPG.msg('Poprawiono %1 pozycji walutowych.'@[$licz])
|| __UPG.msg('Nie poprawiono pozycji walutowych')
?};
licz;
1


\fiks_80
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: kod: ER/WRT/XP/20.42/2104/0020
::       JPK_V7 - ujęcie korekty podatku (VAT na plus) z wykorzystaniem ulgi na złe długi u wierzyciela
::----------------------------------------------------------------------------------------------------------------------
_fix:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','V7');
{? ISTDEF.first()
|| FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'KorektaPodstawyOpodt',);
      {? ISTDEFS.first() &
         (
            ISTDEFS.REGULY=$"{? VAT_PS.TYP='S' || '1' || '' ?}" |
            ISTDEFS.REGULY=$"{? VAT_PS.TYP='N' || '1' || '' ?}"
         )
      || ISTDEFS.REGULY:=$"{? -VAT_PS.TYP='n' || '1' || '' ?}";
         ISTDEFS.put();
         __UPG.msg('Poprawiono schemat: %1.'[ISTDEF.VER]);
         _fix:=1
      ?};
      ISTDEFS.cntx_pop();
      FUN.prg_next();
      ISTDEF.next()
   !};
   FUN.prg_stop()
?};
ISTDEF.cntx_pop();
{? _fix=0
|| __UPG.msg('Schematy JPK_V7 są poprawne.')
?};
1


\r_sys_std
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [20.42]
:: OPIS: Ustalenie wartości pola R.SYSTEM, przeniesione z \r_kdx/transfer.fml poz głoszenie błędu
::       ER/WRT/XP/20.42/2104/0037 pkt. 6
::  ORG: \r/upgrade_1902.fml
::----------------------------------------------------------------------------------------------------------------------
:: Weryfikujemy, czy mamy licencję na PPL.
:: Po stronie Xpertis zakładamy, że parametr 7 jest ustawiony na wartość 'N' przez co pomijamy zadania naprawcze.
{? ~exec('lic','#b_domain','PPL')
|| return(~~)
?};

:: wczytaj rubryki standardowe
_BUF:=exec('buf_std_rub','rubryki');
_BUF.index(_BUF.ndx_tmp(,,'RN',,));

: skonstruuj formułę porównującą
_len:=R.fld_num();
_cmp:='1';
{! _ii:=1.._len
|! _acr:=R.fld_acr(_ii);
   {? ',LP,NOTA,SYSTEM,IDADD,IDPUT,'*(','+_acr+',')=0
   || {? var_pres(_acr,_BUF)=var_pres(_acr,R)
      || _cmp+=' & _a.%1=_b.%1'[_acr]
      ?}
   ?}
!};
_cmp:=$_cmp;

_err:=0;
R.cntx_psh();
R.trig_off('*','*');
R.index('RUBKOD');
R.prefix();
_prg_start:=_dn:=R.first();
{? _prg_start || FUN.prg_start(R.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]) ?};
{!
|? _dn
|! {? R.SYSTEM=''
   || R.SYSTEM:={? _BUF.find_key(R.RN) || {? _cmp(R,_BUF) || 'T' || 'X' ?} || 'N' ?};
      R.put()
   ?};
   FUN.prg_next();
   _dn:=R.next()
!};
{? _prg_start || FUN.prg_stop() ?};
R.trig_on('*','*');
R.cntx_pop();
1


\ezal_numer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.xx]
:: OPIS: Podmianka formuły na symbol dla tabeli EZAL - Nagłówki zaliczek w obiegu
::----------------------------------------------------------------------------------------------------------------------
ETYPZAL.cntx_psh();
ETYPZAL.index('UNIK');
ETYPZAL.prefix(REF.FIRMA);
{? ETYPZAL.first()
|| FUN.prg_start(ETYPZAL.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? ETYPZAL.FORMSYMZ='exec(''numeruj'',''skid_zal'')'
      || ETYPZAL.FORMSYMZ:='exec(''numeruj'',''zaliczki'')';
         ETYPZAL.put()
      ?};
      FUN.prg_next();
      ETYPZAL.next()
   !};
   FUN.prg_stop()
?};
ETYPZAL.cntx_pop();
1


\KARO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Uzupełnia zapisy w KARO: zamienia wskazanie do widoków osób na wskazanie do tabeli pracowników,
::       zamienia rodzaj wyposażenia na wskazanie na grupę materiałową. Uzupełnia wskazania do dokumentów magazynowych
::----------------------------------------------------------------------------------------------------------------------
KARO.cntx_psh();
RODZO.cntx_psh();
DK.cntx_psh();
KARO.trig_off('*','*');
DK.trig_off('*','*');
_formula:="
   _put:=0;
   {? KARO.P=null() & KARO.OS_VIEW<>null()
   || KARO.P:=exec('OS_VIEW2P','transfer',KARO.OS_VIEW);
      {? KARO.P<>null()
      || KARO.OS_VIEW:=null()
      ?};
     _put:=1
   ?};
   {? KARO.MGRP=null() & KARO.RODZO<>null()
   || KARO.MGRP:=exec('mgrp4rodzo','transfer',KARO.RODZO);
      {? KARO.MGRP<>null()
      || KARO.RODZO:=null()
      ?};
      _put:=1
   ?};
   {? KARO.M=null() & KARO.RODZO<>null()
   || KARO.M:=KARO.M:=exec('FindAndGet','#table',M,KARO.RODZO().M,M.name(),\"ref()\",null());
      _put:=1
   ?};
   {? KARO.GENDK=''
   || KARO.GENDK:=KARO.SRDK;
      KARO.SRDK:=$KARO.DK;
      _put:=1
   ?};

   {? KARO.DK_C=null() & KARO.DK<>null()
   || DK.use(8+$KARO.DK);
      KARO.DK_C:=KARO.DK().DK_C;
      KARO.RDKC:=KARO.DK().RDKC;
      _put:=1
   ?};
   _formula:=\"
         exec('FindAndGet','#table',DK,DK.PRDK,,\"\"{? DK.WYP<>1 || DK.WYP:=1; DK.put() ?}\"\",~~);
         DK.PRDK
      \";
   _prdk:=exec('FindAndGet','#table',DK,KARO.SRDK,,_formula,'');
   {? KARO.PRDK='' & KARO.SRDK<>''
   || KARO.PRDK:=_prdk;
      _put:=1
   ?};
   {? _put
   || KARO.put()
   ?}
";
KARO.for_each(_formula,1);
KARO.trig_on('*','*');
DK.trig_on('*','*');
RODZO.cntx_pop();
DK.cntx_pop();
KARO.cntx_pop()


\POLWO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Uzupelnia dane w nagłówkach poleceń wydania: zamienia wskazanie na pracownika z OS_VIEW na P i ustala STAT_REJ
::----------------------------------------------------------------------------------------------------------------------
POLWO.cntx_psh();
_formula:="
   _put:=0;
   {? POLWO.P=null() & POLWO.OS_VIEW<>null()
   || POLWO.P:=exec('OS_VIEW2P','transfer',POLWO.OS_VIEW);
      {? POLWO.P<>null()
      || POLWO.OS_VIEW:=null()
      ?};
     _put:=1
   ?};
   {? POLWO.STAT_REJ=''
   || POLWO.STAT_REJ:='T';
      _put:=1
   ?};
   {? _put
   || POLWO.put()
   ?}
";
POLWO.for_each(_formula,1);
POLWO.cntx_pop()


\PWOPOZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Podmienia wskazanie na rodzaj wyposażenie z RODZO na MGRP
::----------------------------------------------------------------------------------------------------------------------
PWOPOZ.cntx_psh();
_formula:="
   _put:=0;
   {? PWOPOZ.MGRP=null() & PWOPOZ.RODZO<>null()
   || PWOPOZ.MGRP:=exec('mgrp4rodzo','transfer',PWOPOZ.RODZO);
      {? PWOPOZ.MGRP<>null()
      || PWOPOZ.RODZO:=null()
      ?};
      _put:=1
   ?};
   {? _put
   || PWOPOZ.put()
   ?}
";
PWOPOZ.for_each(_formula,1);
PWOPOZ.cntx_pop()


\OSNODZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Uzupelnia dane pracowników wykluczonych z list zapotrzebowania:
::       zamienia wskazanie na pracownika z OS_VIEW na P.
::----------------------------------------------------------------------------------------------------------------------
OSNODZ.cntx_psh();
OSNODZ.for_each("
   {? OSNODZ.P=null() & OSNODZ.OS_VIEW<>null()
   || OSNODZ.P:=exec('OS_VIEW2P','transfer',OSNODZ.OS_VIEW);
      {? OSNODZ.P<>null()
      || OSNODZ.OS_VIEW:=null();
         OSNODZ.OSOBA:=null()
      ?};
      OSNODZ.put()
   ?}
",1);
OSNODZ.cntx_pop()


\ODZAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Uzupełnia dane nagłówków list zapotrzebowania: podgrupa materiałowa na podstawie rodzaju wyposażenia
::       i grupa materiałowa na podstawie grupy rodzajów wyposażenia
::----------------------------------------------------------------------------------------------------------------------
ODZAM.cntx_psh();
_formula:="
   {? ODZAM.MGR=null() & ODZAM.PAKASORT<>null()
   || ODZAM.MGR:=exec('mgr4pakasort','transfer',ODZAM.PAKASORT);
      {? ODZAM.MGR<>null()
      || ODZAM.PAKASORT:=null()
      ?}
   ?};
   ODZAM.STAN:='N';
   ODZAM.put()
   ";
ODZAM.for_each(_formula,1);
:: Ustalenie aktywnej listy
ODZAM.index('SYM');
{? ODZAM.last()
|| ODZAM.STAN:='T';
   ODZAM.put()
?};
ODZAM.cntx_pop()


\ODZAMPT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Uzupełnia pozycje list zapotrzebowania na podstawie tabel ODZAMPP i ODZAMPT
::----------------------------------------------------------------------------------------------------------------------
ODZAMPT.cntx_psh();
ODZAMPP.cntx_psh();
RODZO.cntx_psh();
ODZAMPP.index('POZP');
ODZAMPT.index('S1');
ODZAMPT.prefix(null());
{? ODZAMPT.first()
|| FUN.prg_start(ODZAMPT.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? ODZAMPP.prefix(ODZAMPT.ref());
      _can_continue:=0;
      {? ODZAMPP.first()
      || {!
         |? {? ODZAMPT.MGRP=null()
            || _nowe:=0;
               {? ODZAMPT.RODZO<>null
               || ODZAMPT.MGRP:=exec('mgrp4rodzo','transfer',ODZAMPT.RODZO);
                  ODZAMPT.M:=exec('FindAndGet','#table',M,ODZAMPT.RODZO().M,M.name(),"ref()",null());
                  {? var_pres('_tab')>100
                  || obj_del(_tab)
                  ?};
                  _tab:=exec('rozmlt2war','transfer',ODZAMPT.ref(),1);
                  {? type_of(_tab)>0
                  || ODZAMPT.DK_C:=exec('m_atr_sprdod','mat_atr',_tab[1],_tab[2],_tab[3],
                                                                 _tab[4],_tab[5],_tab[6],
                                                                 _tab[7],_tab[8],_tab[9],
                                                                 _tab[10],ODZAMPT.RODZO().M_ATR)
                  ?};
                  ODZAMPT.RODZO:=null()
               ?}
            || _nowe:=1
            ?};
            ODZAMPT.P:=exec('OS_VIEW2P','transfer',ODZAMPP.OS_VIEW);
            ODZAMPT.IL:=ODZAMPP.IL;
            ODZAMPT.cntx_psh();
            ODZAMPT.clear();
            {? _nowe
            || _can_continue:=ODZAMPT.add()
            || _can_continue:=ODZAMPT.put()
            ?};
            ODZAMPT.cntx_pop();
            {? _can_continue
            || ODZAMPP.del()
            || ODZAMPP.next()
            ?}
         !}
      ?};
      {? ODZAMPT.RODZO<>null
      || ODZAMPT.MGRP:=exec('mgrp4rodzo','transfer',ODZAMPT.RODZO);
         ODZAMPT.M:=exec('FindAndGet','#table',M,ODZAMPT.RODZO().M,M.name(),"ref()",null());
         {? var_pres('_tab')>100
         || obj_del(_tab)
         ?};
         _tab:=exec('rozmlt2war','transfer',ODZAMPT.ref(),1);
         {? type_of(_tab)>0
         || ODZAMPT.DK_C:=exec('m_atr_sprdod','mat_atr',_tab[1],_tab[2],_tab[3],
                                                        _tab[4],_tab[5],_tab[6],
                                                        _tab[7],_tab[8],_tab[9],
                                                        _tab[10],ODZAMPT.RODZO().M_ATR)
         ?};
         ODZAMPT.RODZO:=null();
         ODZAMPT.cntx_psh();
         ODZAMPT.clear();
         ODZAMPT.put();
         ODZAMPT.cntx_pop()
      ?};
      {? _can_continue
      || ROZMLT.cntx_psh();
         ROZMLT.index('OP');
         ROZMLT.prefix(ODZAMPT.ref);
         {? ROZMLT.first()
         || {!
            |?
               ROZMLT.del()
            !}
         ?};
         ROZMLT.cntx_pop()
      ?};
      FUN.prg_next();
      ODZAMPT.first()
   !};
   FUN.prg_stop()
?};
RODZO.cntx_pop();
ODZAMPP.cntx_pop();
ODZAMPT.cntx_pop()


\fst_set_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.14]
:: OPIS: Kod: ER/WRT/XP/21.14/2105/0058 kopia błędu ER/WRT/XP/20.42/2105/0009
::       Niespójność w danych. Brak przypisania środka do pracownika na podstawie osoby
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
SRSR.cntx_psh();
_name:=SRSR.names();
{? _name.first()
|| echo('Aktualizacja tabeli środków trwałych...'@);
   {! |?
      SRSR.use(_name.NAME);
      SRSR.prefix();
      {? SRSR.first()
      || SRSR.for_each("
         {? SRSR.P=null & SRSR.OSOBA<>null
         || P.cntx_psh();
            P.index('PRACOWA');
            P.prefix(exec('ref_firma','ustawienia'),'P',SRSR.JORG,SRSR.OSOBA().NAZWISKO,SRSR.OSOBA().PIERWSZE,SRSR.OSOBA().PESEL);
            {? P.first()
            || SRSR.P:=P.ref(); SRSR.put()
            ?};
            P.cntx_pop()
         ?}
         ",0)
      ?};
      _name.next()
   !};
   echo()
?};
SRSR.cntx_pop();

SRST.cntx_psh();
_nameSRST:=SRST.names();
{? _nameSRST.first()
|| echo('Aktualizacja tabeli środków trwałych...'@);
   {! |?
      SRST.use(_nameSRST.NAME);
      SRST.prefix();
      {? SRST.first()
      || SRST.for_each("
         {? SRST.P=null & SRST.OSOBA<>null
         || P.cntx_psh();
            P.index('PRACOWA');
            P.prefix(exec('ref_firma','ustawienia'),'P',SRST.JORG,SRST.OSOBA().NAZWISKO,SRST.OSOBA().PIERWSZE,SRST.OSOBA().PESEL);
            {? P.first()
            || SRST.P:=P.ref(); SRST.put()
            ?};
            P.cntx_pop()
         ?}
         ",0)
      ?};
      _nameSRST.next()
   !};
   echo()
?};
SRST.cntx_pop();

SRDO.cntx_psh();
_nameSRDO:=SRDO.names();
{? _nameSRDO.first()
|| echo('Aktualizacja tabeli środków trwałych...'@);
   {! |?
      SRDO.use(_nameSRDO.NAME);
      SRDO.prefix();
      {? SRDO.first()
      || SRDO.for_each("
         SRDT.use('srdt'+(SRDO.name-3+1)+(SRDO.name+3));
         {? SRDO.PRC=null & SRDO.OSOBA<>null & SRDO.TYP().TYP='MT'
         || P.cntx_psh();
            P.index('PRACOWA');
            P.prefix(exec('ref_firma','ustawienia'),'P',SRDO.JORG,SRDO.OSOBA().NAZWISKO,SRDO.OSOBA().PIERWSZE,SRDO.OSOBA().PESEL);
            {? P.first()
            || SRDO.PRC:=P.ref(); SRDO.put()
            ?};
            P.cntx_pop()
         |? SRDO.PRC=null & SRDO.OSOBA<>null & SRDO.TYP().TYP='PU'
         || P.cntx_psh();
            P.index('PRACONAZ');
            P.prefix(exec('ref_firma','ustawienia'),'P',SRDO.OSOBA().NAZWISKO,SRDO.OSOBA().PIERWSZE,SRDO.OSOBA().PESEL);
            {? P.first()
            || SRDO.PRC:=P.ref(); SRDO.put()
            ?};
            P.cntx_pop()
         ?}
         ",0)
      ?};
      _nameSRDO.next()
   !};
   echo()
?};
SRDO.cntx_pop();
_res


\espr_i_podat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: ER/WRT/XP/21.14/2106/0011
::       Sprawozdania skonsolidowane SePodat
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
E_SPR_F.cntx_psh();
E_SPR_F.index('FIRMA'); E_SPR_F.prefix();
{? E_SPR_F.first()
|| FUN.prg_start(E_SPR_F.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? {? E_SPR_F.I_PODAT & E_SPR_F.FIRMA<>E_SPR_F.I_PODAT().FIRMA & E_SPR_F.I_PODAT().FIRMA<>REF.GRUPA
      || _new:=null;
         GR_SIK.cntx_psh();
         GR_SIK.index('E_KOD');
         GR_SIK.prefix(E_SPR_F.FIRMA,E_SPR_F.I_PODAT().E_KOD,E_SPR_F.I_PODAT().SKROT,);
         _new:={? GR_SIK.first() || GR_SIK.ref() || null ?};
         GR_SIK.cntx_pop();

         E_SPR_F.I_PODAT:=_new;
         {? E_SPR_F.put()
         || _jest:=1
         ?}
      ?};
      FUN.prg_next();
      E_SPR_F.next()
   !};
   FUN.prg_stop()
?};
E_SPR_F.cntx_pop();
{? _jest
|| 0
|| 1
?}


\etypy_atr_g1r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: ER/WRT/XP/21.14/2106/0021
::       Modyfikacja pola w tabeli ETYPY.
::       Zależność pomiędzy redakcją w nagłówku, a generowaniem pierwszego kompletu informacji dodatkowej.
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
ETYPY.index('UNIK');
ETYPY.prefix();
ETYPY.for_each("
   {? ETYPY.IDINHEAD || ETYPY.ATR_G1R:=1; ETYPY.put() ?}
",1);
ETYPY.cntx_pop();
1


\fiks_81
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/21.14/2106/0026 - Nieprawidłowe opisy pozycji JPK_SF skonsolidowanym jednostka inna w PLN - SeBilans
::----------------------------------------------------------------------------------------------------------------------
_fix:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF','SkonsolidowanaJednostkaInnaW');
{? ISTDEF.first()
|| FUN.prg_start(ISTDEF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   _fun:="
      {? ISTDEFS.find_key(_a,)
      || ISTDEFS.memo_set(_b,'ANOTACJA');
         ISTDEFS.memo_put(,'ANOTACJA');
         {? _a+1='4'
         || ISTDEFS.PARSER:=$\"exec('p_wiersz','jpk_sf',_a)\"
         ?};
         {? ISTDEFS.put()
         || 1
         ?}
      ?}
   ";
   {!
   |? {? ISTDEF.VER+1='2'
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS');
         ISTDEFS.prefix(ISTDEF.ref());
         _fix+=_fun('sin:Aktywa_B_III_1_C_1','- udziały lub akcje');
         _fix+=_fun('sin:Aktywa_B_III_1_C_2','- inne papiery wartościowe');
         _fix+=_fun('sin:Aktywa_B_III_1_C_3','- udzielone pożyczki');
         _fix+=_fun('sin:Aktywa_B_III_1_C_4','- inne krótkoterminowe aktywa finansowe');
         ISTDEFS.cntx_pop()
      ?};
      FUN.prg_next();
      ISTDEF.next()
   !};
   FUN.prg_stop()
?};
ISTDEF.cntx_pop();
1


\fiks_82
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2106/0020 - CIT-8(30) błąd w wierszu 185
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAT_VER.cntx_psh();
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','CIT8','CIT8',30);
_ver:={? VAT_VER.first() || VAT_VER.ref() || null ?};
VAT_VER.cntx_pop();
{? _ver
|| ISTDEF.cntx_psh();
   ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',_ver);
   {? ISTDEF.first()
   || ISTDEFS.cntx_psh();
      ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'P_185',);
      {? ISTDEFS.first()
      || _fml:=$"exec('poz_edek_dod','xml','CIT8 ',1,185)";
         ISTDEFS.REGULY:=_fml;
         {? ~ISTDEFS.put()
         || _res:=0
         ?}
      ?};
      ISTDEFS.cntx_pop()
   ?};
   ISTDEF.cntx_pop()
?};
_res


\dok_jpk_fix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: ER/WRT/XP/12.51/2105/0032 - formuła naprawiająca dane dla tabeli DOK_JPK
::----------------------------------------------------------------------------------------------------------------------
DOK_JPK.cntx_psh(); DVAT.cntx_psh(); PVAT.cntx_psh(); VAT_DEK.cntx_psh(); VAT_PS.cntx_psh();
VAT_DEK.prefix();
_maski:=DOK_JPK.names();
_ret:=0;
{? _maski.first()
|| FUN.prg_start(_maski.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {!
   |? DOK_JPK.use(_maski.NAME);
      DOK_JPK.prefix();
      {? DOK_JPK.first()
      || {!
         |? {? DOK_JPK.VAT_DEK<>''
            || _maska:=4-DOK_JPK.name()-2;
               DVAT.use('dvat__'+_maska);
               DVAT.index('DOK');
               DVAT.prefix(DOK_JPK.DOK);
               {? DVAT.first()
               || {!
                  |? {? $DVAT.DOK=$DOK_JPK.DOK
                     || PVAT.use('pvat__'+_maska);
                        PVAT.index('NR');
                        PVAT.prefix(DVAT.ref());
                        {? PVAT.first()
                        || {!
                           |? {? VAT_DEK.seek(DOK_JPK.VAT_DEK)
                              || VAT_PS.use('vat_ps'+_maska);
                                 VAT_PS.index('PVAT_REF');
                                 VAT_PS.prefix(VAT_DEK.ref(),$PVAT.ref());
                                 {? VAT_PS.first()
                                 || {!
                                    |? {? VAT_PS.CRC=PVAT.crc() & VAT_PS.JPK_GTU+1<>':' & VAT_PS.JPK_GTU+1<>';'
                                       || {? DOK_JPK.JPK_GTU<>VAT_PS.JPK_GTU | DOK_JPK.JPK_PROC<>VAT_PS.JPK_PROC
                                          || DOK_JPK.JPK_GTU:=VAT_PS.JPK_GTU;
                                             DOK_JPK.JPK_PROC:=VAT_PS.JPK_PROC;
                                             _ret+=1;
                                             DOK_JPK.put()
                                          ?}
                                       ?};
                                       VAT_PS.next()
                                    !}
                                 ?}
                              ?};
                              PVAT.next()
                           !}
                        ?}
                     ?};
                     DVAT.next()
                  !}
               ?}
            ?};
            DOK_JPK.next()
         !}
      ?};
      FUN.prg_next();
      _maski.next()
   !};
   FUN.prg_stop()
?};
DOK_JPK.cntx_pop(); DVAT.cntx_pop(); PVAT.cntx_pop(); VAT_DEK.cntx_pop(); VAT_PS.cntx_pop();
1


\nbp_spec_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Naprawa specyfikacji importu tabeli kursów - NBP kursy walut (Proces)
::----------------------------------------------------------------------------------------------------------------------
_spec:='NBP kursy walut (Proces)';
HBPKI.cntx_psh();
HBPKI.index('HBPKITP');
HBPKI.prefix(1,'K','10100000','K',_spec,);
{? HBPKI.first()
|| HBP.cntx_psh();
   HBP.index('HBNAZWA');
   HBP.prefix(HBPKI.ref());
   {? HBP.first()
   || _jast:=0;
      _txt:=HBP.memo_txt(,1,'FML');
      _from:='_temp:=myURL.ToString(\'http://www.nbp.pl/kursy/xml/\'+_dir,\'http\');';
      _to:='_temp:=myURL.ToString(\'http://www.nbp.pl/kursy/xml/\'+_dir); ';
      {? _txt*_from
      || _txt:=gsub(_txt,_from,_to);
         _jast:=1
      ?};
      _from:='_temp:=myURL.ToString(\'http://www.nbp.pl/kursy/xml/\'+_TDIR.FILE+\'.xml\',\'http\');';
      _to:='_temp:=myURL.ToString(\'http://www.nbp.pl/kursy/xml/\'+_TDIR.FILE+\'.xml\');';
      {? _txt*_from
      || _txt:=gsub(_txt,_from,_to);
         _jast:=1
      ?};
      {? _jast
      || HBP.memo_set(_txt,'FML');
         HBP.memo_put(,'FML');
         HBP.put()
      ?}
   ?};
   HBP.cntx_pop()
?};
HBPKI.cntx_pop();
1


\dok_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Uzupelnienie nowych pol dokumentu księgowego
::  OLD: \dok_kh/skid_trn.fml
::  OLD: \dok_kh/transfer.fml
::----------------------------------------------------------------------------------------------------------------------
DOK.cntx_psh();
ND.cntx_psh();
echo('Pobieranie informacji o maskach tabeli DOK ...'@);
_tab:=DOK.names(); _mask:='';
{? _tab.first()
|| echo('Zliczanie rekordów DOK ...'@);
   _size:=0; _lp:=0;
   {!
   |? {? _tab.SIZE<>-1 & _tab.DEL_SIZE<>-1
      || _size+=_tab.SIZE
      || DOK.use(_tab.NAME);
         DOK.index('REJ');
         DOK.prefix();
         _size+=DOK.size()
      ?};
      _tab.next()
   !};
   _tab.first();
   KH.index('SKR');
   KH.prefix(2);
   {!
   |? DOK.use(_tab.NAME);
      DOK.index('REJ');
      DOK.prefix();
      DVAT.use('dvat__'+(_tab.NAME+2));
      DVAT.index('DOK');
      {? DOK.first()
      || echo('Uzupełnianie danych kontrahenta w dokumentach: %1'@[DOK.name()]);
         {!
         |? _lp+=1; _ok:=0;
            {? DOK.KH<>''
            || {? KH.find_key(DOK.KH,) & DOK.KH=KH.SKR & DOK.KH_FULL='' & DOK.MIASTO='' & DOK.UL='' & DOK.POCZ='' &
                  DOK.DOM='' & DOK.LOKAL='' & DOK.KPOCZ=''
               || DOK.KH_FULL:=KH.NAZ_P;
                  DOK.KH_KRAJ:=KH.KRAJ; DOK.KH_KRISO:=KH.KRAJISO;
                  DOK.MIASTO:=KH.MIASTO;
                  DOK.UL:=KH.UL;
                  DOK.DOM:=KH.DOM;
                  DOK.LOKAL:=KH.LOKAL;
                  DOK.POCZ:=KH.POCZ;
                  DOK.KPOCZ:=KH.KPOCZ
               |? DOK.KH_FULL=''
               || DOK.KH_FULL:=DOK.KH
               ?};
               _ok:=1
            ?};
            {? 1+DOK.DOKZRODL='M' & DOK.MG=''
            || _refdmag:=#(4-DOK.DOKZRODL);
               {? _mask<>(3+(1-DOK.DOKZRODL))
               || _mask:=(3+(1-DOK.DOKZRODL));
                  ND.use('nagdo'+_mask)
               ?};
               ND.prefix();
               {? ND.seek(_refdmag,ND.name())
               || DOK.MG:=ND.MAG().SYM
               ?};
               _ok:=1
            ?};
            {? DOK.JPK_PROC<>'' & DOK.PROC=null
            || _valsAr:=spli_str(DOK.JPK_PROC,',');
               {! _i:=1..obj_len(_valsAr)
               |! {? DOK.PROC=null
                  || DOK.PROC:=exec('FindInSet','#table','JPK_SLO','KOD',_valsAr[_i],1,,1,'PROC_FAKS',null())
                  ?}
               !};
               obj_del(_valsAr);
               {? DOK.PROC
               || _ok:=1
               ?}
            ?};
            {? DOK.KSEF_ERR='' || DOK.KSEF_ERR:='N'; _ok:=1 ?};
            {? _ok || DOK.put ?};
            DOK.next()
         !}
      ?};
      _tab.next()
   !};
   echo()
?};
&_tab;

DVAT.cntx_psh();
echo('Pobieranie informacji o maskach tabeli DVAT ...'@);
_tab:=DVAT.names();
{? _tab.first()
||  echo('Zliczanie rekordów DVAT ...'@);
   _size:=0; _lp:=0;
   {!
   |? {? _tab.SIZE<>-1 & _tab.DEL_SIZE<>-1
      || _size+=_tab.SIZE
      || DVAT.use(_tab.NAME);
         DVAT.index('DOK');
         DVAT.prefix();
         _size+=DVAT.size()
      ?};
      _tab.next()
   !};
   _tab.first();
   {!
   |? DVAT.use(_tab.NAME);
      DVAT.index('DOK'); DVAT.prefix();
      {? DVAT.first()
      || echo('Uzupełnianie danych kontrahenta w dokumentach VAT: %1'@[DVAT.name()]);
         {!
         |? _lp+=1;
            _name:=ref_name(DVAT.DOK);
            DOK.use(_name);
            DOK.prefix();
            {? DVAT.DOK().KH_FULL<>'' & DVAT.KH='' & DVAT.MIASTO='' & DVAT.UL='' & DVAT.POCZ=''
            || DVAT.KH:=DOK.KH_FULL;
               DVAT.UL:=DOK.UL;
               DVAT.MIASTO:=DOK.MIASTO;
               DVAT.POCZ:=DOK.POCZ;
               DVAT.DOM:=DOK.DOM;
               DVAT.LOKAL:=DOK.LOKAL;
               DVAT.KPOCZ:=DOK.KPOCZ;
               DVAT.put()
            ?};
            DVAT.next()
         !}
      ?};
      _tab.next()
   !};
   prgs_clr();
   echo()
?};
ND.cntx_pop();
DVAT.cntx_pop();
DOK.cntx_pop()


\dek_nag_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [17.14]
:: OPIS: Formula zmieniająca pole system z tabeli DEK_NAG
::----------------------------------------------------------------------------------------------------------------------
DEK_NAG.cntx_psh();
_ndx:=DEK_NAG.ndx_tmp(,1,'ROK','NAZ',);
DEK_NAG.index(_ndx); DEK_NAG.prefix();
{? DEK_NAG.first()
|| FUN.prg_start(DEK_NAG.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]);
   {! |?
      {? DEK_NAG.RODZ='SV' | DEK_NAG.RODZ='SN'
      || DEK_NAG.SYSTEM:='KAS'
      |? DEK_NAG.RODZ='LM'
      || DEK_NAG.SYSTEM:='LMG'
      |? DEK_NAG.RODZ='KL' | DEK_NAG.RODZ='KU'
      || DEK_NAG.SYSTEM:='PPL'
      |? DEK_NAG.RODZ='LS' | DEK_NAG.RODZ='LK' | DEK_NAG.RODZ='LP'
      || DEK_NAG.SYSTEM:='LSP'
      |? DEK_NAG.RODZ='EF' | DEK_NAG.RODZ='EP' | DEK_NAG.RODZ='ED' | DEK_NAG.RODZ='ET' | DEK_NAG.RODZ='EW'
      || DEK_NAG.SYSTEM:='FST'
      |? DEK_NAG.RODZ='LZ'
      || DEK_NAG.SYSTEM:='LZK'
      || DEK_NAG.SYSTEM:=''
      ?};
      DEK_NAG.put();
      FUN.prg_next();
      DEK_NAG.next()
   !};
   FUN.prg_stop()
?};
DEK_NAG.cntx_pop()


\POZPG
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Dla tabeli POZPG zamienia wskazania do tabeli widoków osób (OS_VIEW) na pracowników (P)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
POZPG.cntx_psh();
POZPG.for_each("
   {? POZPG.P=null() & POZPG.OS_VIEW<>null()
   || POZPG.P:=exec('OS_VIEW2P','transfer',POZPG.OS_VIEW);
      {? POZPG.P<>null()
      || POZPG.OS_VIEW:=null();
         POZPG.OSOBA:=null()
      ?};
      POZPG.put()
   ?}
",1);
POZPG.cntx_pop()


\NORMWSP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Dla tabeli NORMWSP zamienia wskazania do tabeli widoków osób (OS_VIEW) na pracowników (P)
::       oraz wskazania do tabeli rodzajów wyposażenia na podgrupy materiałowe
::----------------------------------------------------------------------------------------------------------------------
NORMWSP.cntx_psh();
_formula:="
   _put:=0;
   {? NORMWSP.P=null() & NORMWSP.OS_VIEW<>null()
   || NORMWSP.P:=exec('OS_VIEW2P','transfer',NORMWSP.OS_VIEW);
      {? NORMWSP.P<>null()
      || NORMWSP.OS_VIEW:=null();
         NORMWSP.OSOBA:=null()
      ?};
     _put:=1
   ?};
   {? NORMWSP.MGRP=null() & NORMWSP.RODZO<>null()
   || NORMWSP.MGRP:=exec('mgrp4rodzo','transfer',NORMWSP.RODZO);
      {? NORMWSP.MGRP<>null()
      || NORMWSP.RODZO:=null()
      ?};
      _put:=1
   ?};
   {? _put
   || NORMWSP.put()
   ?}
";
NORMWSP.for_each(_formula,1);
NORMWSP.cntx_pop()


\SLO_platn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Weryfikacja poprawności słownika płatności do US
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO);
{? XINFO.SLTYPPL & ~exec('chk_pl_us','slo_sys')
|| SLO.cntx_psh();
   SLO.index('SL_TR');
   SLO.prefix(XINFO.SLTYPPL);
   {? ~SLO.find_key('CIT',)
   || SLO.blank(1);
      SLO.SLU:=XINFO.SLTYPPL;
      SLO.TR:='CIT';
      SLO.cntx_psh();
      SLO.index('SL');
      SLO.prefix(XINFO.SLTYPPL);
      {? SLO.last() & #SLO.KOD>0 || _kod:=form(#SLO.KOD+1,-3) || _kod:='001' ?};
      SLO.cntx_pop();
      SLO.KOD:=_kod;
      SLO.add(1)
   ?};
   {? ~SLO.find_key('PIT',)
   || SLO.blank(1);
      SLO.SLU:=XINFO.SLTYPPL;
      SLO.TR:='PIT';
      SLO.cntx_psh();
      SLO.index('SL');
      SLO.prefix(XINFO.SLTYPPL);
      {? SLO.last() & #SLO.KOD>0 || _kod:=form(#SLO.KOD+1,-3) || _kod:='001' ?};
      SLO.cntx_pop();
      SLO.KOD:=_kod;
      SLO.add(1)
   ?};
   {? ~SLO.find_key('VAT',)
   || SLO.blank(1);
      SLO.SLU:=XINFO.SLTYPPL;
      SLO.TR:='VAT';
      SLO.cntx_psh();
      SLO.index('SL');
      SLO.prefix(XINFO.SLTYPPL);
      {? SLO.last() & #SLO.KOD>0 || _kod:=form(#SLO.KOD+1,-3) || _kod:='001' ?};
      SLO.cntx_pop();
      SLO.KOD:=_kod;
      SLO.add(1)
   ?};
   SLO.cntx_pop()
?}


\szk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Uzupełnienie nowych pól w tabelach związanych ze szkoleniami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
SZK_ZAP.cntx_psh();
SZK_ZAP.trig_off('*','*');
SZK_PSZK.cntx_psh();
SZK_PSZK.trig_off('*','*');

_ZM:=SZK_ZAP.names();
{? _ZM.first()
|| {!
   |? SZK_ZAP.use(_ZM.NAME);
      SZK_ZAP.prefix();
      SZK_ZAP.for_each("
         _put:=0;
         {? SZK_ZAP.FIRMA<>REF.FIRMA
         || SZK_ZAP.FIRMA:=REF.FIRMA;
            _put+=1
         ?};
         {? SZK_ZAP.OSOBA<>SZK_ZAP.P().OSOBA
         || SZK_ZAP.OSOBA:=SZK_ZAP.P().OSOBA;
            _put+=1
         ?};
         {? _put
         || SZK_ZAP.put()
         ?}
      ",1);
      _ZM.next()
   !}
?};

_ZP:=SZK_PSZK.names();
{? _ZP.first()
|| {!
   |? SZK_PSZK.use(_ZP.NAME);
      SZK_PSZK.prefix();
      SZK_PSZK.for_each("
         {? SZK_PSZK.P=null | SZK_PSZK.OSOBA=null |
            SZK_PSZK.UD_SKL=null | SZK_PSZK.STN=null | SZK_PSZK.KK=null
         || exec('szk_pszk_modb','phr_sz_tab');
            SZK_PSZK.put()
         ?}
      ",1);
      _ZP.next()
   !}
?};

SZK_PSZK.trig_on('*','*');
SZK_PSZK.cntx_pop();
SZK_ZAP.trig_on('*','*');
SZK_ZAP.cntx_pop();
P.cntx_pop();
~~


\za_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: Uzupełnia kolumnę RODZ tabeli ZA_TEST na podstawie zawartości pliku tekstowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Pomijamy wykonanie dla firmy 000
FIRMA.cntx_psh();
{? REF.WFIRM & REF.FIRMA().SYMBOL='000'
|| FIRMA.cntx_pop();
   return(~~)
?};
_kod:={? REF.WFIRM || '_'+REF.FIRMA().SYMBOL || '' ?};
FIRMA.cntx_pop();

ZA_TEST.cntx_psh();
SLO_KOD.cntx_psh();
ZA_TEST.index('INDEKS');
SLO_KOD.index('KOD');
ZA_TEST.prefix();
SLO_KOD.prefix();

_fn:=ZA_TEST.name()+_kod+'.tra';
{? ~fexists(_fn,1)
|| __UPG.msg('Brak dostępu do pliku %1.\nProcedurę należy powtórzyć.'@[_fn]);
   SLO_KOD.cntx_pop();
   ZA_TEST.cntx_pop();
   return(0)
?};

_BUF:=tab_tmp(1,
   'REF','STRING[16]','Ref',
   'OPCJA','STRING[16]','Ref opcji'
);
_BUF.import(_fn,1,1,%1+';','UTF-8,pth,header',,
   'REF',,1,,
   'OPCJA',,2,
);

{? _BUF.size()=0
|| SLO_KOD.cntx_pop();
   ZA_TEST.cntx_pop();
   return()
?};

_prg_start:=_loop:=_BUF.first();
{? _prg_start || FUN.prg_start(_BUF.size(),'FORMUŁA: %1/%2'[form_stack().file,form_stack().name]) ?};
{!
|? _loop
|! {? ZA_TEST.seek(_BUF.REF)
   ||
      {? SLO_KOD.seek(_BUF.OPCJA)
      || ZA_TEST.RODZ:=SLO_KOD.ref();
         ZA_TEST.put()
      ?}
   ?};
   FUN.prg_next();
   _loop:=_BUF.next()
!};
{? _prg_start || FUN.prg_stop() ?};
SLO_KOD.cntx_pop();
ZA_TEST.cntx_pop();
~~


\KP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Zmiana danych i pozycji składnika w karcie podatkowej na zgodne z Merit.
::       Formuła stanowi zastępstwo części formuły \zm_param/tran_exp.fml
::UWAGA: Formuła nie powinna być uruchamiana wielokrotnie!
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Zmiana danych w karcie podatkowej na zgodne z Merit.
KP.cntx_psh();
KP.clear();
KP.for_each(
   "  KP.S18:=KP.S16;
      KP.S16:=0;
      KP.S25:=KP.S17;
      KP.S17:=0;
      KP.put(1)
   ",1
);
KP.cntx_pop();

:: Zmiana pozycji składnika w karcie podatkowej na zgodną z Merit.
exec('__RUB','object');
__RUB.fill();
R.cntx_psh();
R.prefix();
{? R.seek(__RUB.ref(7094))
|| {? R.RSKP<>18
   || R.RSKP:=18;
      R.put()
   ?}
?};
{? R.seek(__RUB.ref(7178))
|| {? R.RSKP<>25
   || R.RSKP:=25;
      R.put()
   ?}
?};
R.cntx_pop();
~~


\params_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Uaktualnienie listy parametrów
::   WE: [_a] - parametr źródłowy
::       [_b] - liczba-parametr docelowy lub napis - nowa wartość parametru
::----------------------------------------------------------------------------------------------------------------------
_from:={? var_pres('_a')=type_of(1) || _a || -1 ?};
_to:={? var_pres('_b')=type_of(1) | var_pres('_b')=type_of('') || _b || ~~ ?};
{? _from=-1
|| exec('params_upd','transfer1',63,29);
   exec('params_upd','transfer1',63,'N');
   RACHBANK.FIRMA:=null
|? type_of(_to)=type_of(1)
|| PARAMS.cntx_psh();
   PARAMS.index('NUMER');
   PARAMS.prefix();
   {? PARAMS.find_key(_from)
   || _tr:=PARAMS.TRESC;
      {? PARAMS.find_key(_to)
      || PARAMS.TRESC:=_tr;
         PARAMS.put()
      ?}
   ?};
   PARAMS.cntx_pop()
|? type_of(_to)=type_of('')
|| PARAMS.cntx_psh();
   PARAMS.index('NUMER');
   PARAMS.prefix();
   {? PARAMS.find_key(_from)
   || PARAMS.TRESC:=_to;
      PARAMS.put()
   ?};
   PARAMS.cntx_pop()
?}


\prtfis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizuje pola OZ.TYPFISK, FIS.TYPFISK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OZ.cntx_psh();
OZ.prefix();
OZ.for_each("
   {? OZ.TYPFISK='posnet' || OZ.TYPFISK:='fis_thermal'; OZ.put()
   |? OZ.TYPFISK='posnet1' || OZ.TYPFISK:='fis_posnet'; OZ.put()
   |? OZ.TYPFISK='elzab' || OZ.TYPFISK:='fis_elzab'; OZ.put()
   |? OZ.TYPFISK='torell' || OZ.TYPFISK:='fis_torell'; OZ.put()
   ?}
",1);
OZ.cntx_pop();
FIS.cntx_psh();
FIS.prefix();
FIS.for_each("
   {? FIS.TYPFISK='posnet' || FIS.TYPFISK:='fis_thermal'; FIS.put()
   |? FIS.TYPFISK='posnet1' || FIS.TYPFISK:='fis_posnet'; FIS.put()
   |? FIS.TYPFISK='elzab' || FIS.TYPFISK:='fis_elzab'; FIS.put()
   |? FIS.TYPFISK='torell' || FIS.TYPFISK:='fis_torell'; FIS.put()
   ?}
",1);
FIS.cntx_pop()


\edidef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Import definicji EDI w formacie new xml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fname:='edidef.tra';
_sep:=';';

_file:={? fexists(_fname,1) || fopen(_fname,'r',1,1) || 0 ?};

{? _file
||
   ISTXML.cntx_psh();
   ISTDEF.cntx_psh();
   ISTDEF.index('K');
   ISTDEF.prefix();
   {!
   |? (_txt:=fread(_file))<>'\n'
   |!
      _txt_spli:=spli_str(_txt,_sep);
      _istdef:=_txt_spli[1];
      _fname:=_txt_spli[2];
      obj_del(_txt_spli);
      {? ISTDEF.seek(_istdef)
      ||
         ISTXML.index('TREE');
         ISTXML.prefix(ISTDEF.ref());
         {? ~ISTXML.first()
         ||
:: w tym miejscu nie można użyć kodu bo brak pola ISTDEF.FIRMA
:: kod importujący definicje edi w firmach znajduje się w \ISTDEF/upgradexperti2.fml
::            _put:=ISTDEF.FIRMA='' | ISTDEF.FIRMA=REF.FIRMA().SYMBOL;
::            ISTDEF.FIRMA:=REF.FIRMA().SYMBOL;
::            {? _put || ISTDEF.put() || ISTDEF.add() ?};
            exec('def_imp','transfer1',_fname,ISTDEF.ref(),1,'tra')
         ?}
      ?}
   !};
   ISTDEF.cntx_pop();
   ISTXML.cntx_pop()
?};
{? _file || fclose(_file) ?};
~~


\def_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Definicja xml - Importuj
::   WE: _a - file name
::       _b - ISTDEF.ref()
::       _c - 0/1-import z excel
::       [_d] - ext
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fname:=_a;
EDI_Z.C:=_b;
_xlsx:=_c;
_ext:={? var_pres('_d')=type_of('') || _d || 'dem' ?};

_wg_pth:=_fname<>'';
_file:='';

_tmp_dir:=fmk_tmp_dir(0);
_tmp_pth:=_tmp_dir.get_path();

:: zakres importu
_choice:={? _xlsx || 3 || 0 ?};
{? _xlsx=0
||
   ISTXML.cntx_psh();
   ISTXML.index('TREE');
   ISTXML.prefix(EDI_Z.C);
   {? ISTXML.first()
   || {? FUN.ask('Zaimportować formuły?'@) || _choice:=2 ?}
   || {? FUN.ask('Zaimportować strukturę?'@) || _choice:=1 ?}
   ?};
   ISTXML.cntx_pop();
   {? ~_choice || return() ?};
   _file:=
      {? _choice=1
      || dlg_upload(_tmp_pth,0,'.xml,.xsd,.dem',1)
      |? _choice=2
      || dlg_upload(_tmp_pth,0,'.dem',1)
      || ''
      ?};
   {? _file='' || return() ?};
   _file:=_tmp_pth+'/'+_file
?};
{? _choice=3 || _file:='%1_xsd.%2'[_fname,_ext] ?};
:: import struktury
{? _choice=3 | _choice=1
||
   _env:=exec('env','edi_xml');
   params_set('env_xml',_env);
   _filext:=_file+4;
   _merge:=1;
   {? _filext='.xsd'
   ||
      _mrge:=0

   |? _filext='.xml'
   ||
      _merge:=FUN.ask('Czy łączyć powtarzające się elementy struktury?'@)
   ?};
   exec('def_z_xsd','edi_xml',_file,_wg_pth,_merge,_xlsx)
?};
:: import formuł
{? _choice=3 | _choice=2
||
   {? _choice=3 || _file:='%1_form.%2'[_fname,_ext] ?};
   _form:=fopen(_file,'Ur',_wg_pth,,1);
   {? ~_form.is_open()
   ||
      FUN.info(exec('msg','edi_wspolne','ERR_FOPEN')+_file+'.');
      return(0)
   ?};
   ISTXML.cntx_psh();
   ISTXML.index('TREE');
   ISTXML.prefix(EDI_Z.C);
   _Hash:=exec('def_hash','edi_xml',EDI_Z.C,0,'',~~);
   {!
   |? (_text:=fread(_form))<>'\n'
   |!
      _hash:=40+_text;
      _type:=10+(40-_text);
      _text:=50-_text;
      {? _Hash.find_key(_hash)
      ||
         {? ISTXML.seek(_Hash.REF,)
         ||
            {? _type='_f_start__' || ISTXML.F_START:=_text
            |? _type='_f_end____' || ISTXML.F_END:=_text
            |? _type='_f_text___' || ISTXML.F_TEXT:=_text
            |? _type='_is_fld___' || ISTXML.IS_FLD:=_text
            ?};
            ISTXML.put()
         ?}
      ?}
   !};
   ISTXML.cntx_pop()
?}


\update_np_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Uzupełnia pole NP_DOK w wybranych tabelach
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_files:=spli_str(
   'BIPNAG|BIPODB|BIPPYTN|BIPULP|BIPODN|',
   '|'
);

_msg:=form('Aktualizacja kolumny NP_DOK...'@,80)+'\n';
_nfo:='';
_cnt:=0;

_size:=tab_num();
{! _ti:=1.._size
|! _pgs:=100*(_cnt+=1)/_size;
   _prc:='\n'+$int(_pgs)+'%';
   {? tab_real(_ti)
   || _TAB:=($tab_acr(_ti))();
      {? var_pres('NP_DOK',_TAB)>0
      || _TAB.trig_off('*','*');
         _nfo:=_msg+form(_TAB.comment(),80);
         progress(_pgs,_nfo+_prc,FUN.TYT);
         _src:='';
         {! _fi:=1.._TAB.fld_num()
         |? _src=''
         |! {? _TAB.fld_join(_fi)='ZZ_DOK'
            || _src:=_TAB.fld_acr(_fi)
            ?}
         !};
         {? _src<>''
         || _TAB.cntx_psh();
            _MASK:=_TAB.names();
            _loop:=_MASK.first();
            {!
            |? _loop
            |! _TAB.use(_MASK.NAME);
               _TAB.clear();
               _TAB.for_each(
                  $('{? %1.NP_DOK=\'\' & %1.%2<>null '
                    '|| %1.NP_DOK:=ref_name(%1.%2); '
                    '   %1.put() '
                    '?}'[tab_acr(_ti),_src]),
                  0
               );
               _loop:=_MASK.next()
            !};
            obj_del(_MASK);
            _TAB.cntx_pop()
         ?};
         _TAB.trig_on('*','*')
      ?};
      &_TAB
   ?}
!};

prgs_clr()


\zz_kryt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Aktualizacja kryterium doboru. Uzupełnienie danych w tabeli ZZ_KRYT.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZZ_KRYT.cntx_psh();
ZZ_KRYT.clear();
ZZ_KRYT.for_each("
   _kd:=ZZ_KRYT.TEST;
   {? _kd*'zz_data'<>0
   || ZZ_KRYT.TEST:=gsub(_kd,'zz_data','phr_dane');
      ZZ_KRYT.put()
   ?}
",1);
ZZ_KRYT.cntx_pop();
~~


\es_skld_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Import danych elementów środków do tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_symbol:=REF.FIRMA().SYMBOL;
_s:='@';
_fn:='skld_'+_symbol+'.tra';
{? ~fexists(_fn,1) | exec('es_size_file','transfer',_fn)=0
|| return(0)
?};

VAR_DEL.delete('__SKLD');

progress(,'Import pliku %1 do tabeli tymczasowej...'@[_fn],'Trwa import danych'@,1);

:: tabela tymczasowa na dane elementów z pliku tekstowego:
__SKLD:=tab_tmp(1,'ID','INTEGER','ID',
                  'NRI','STRING[20]','NRI',
                  'O_NRI','STRING[20]','O_NRI',
                  'ODD','STRING[8]','ODD',
                  'DE','DATE','DE',
                  'DSE','DATE','DSE',
                  'DZ','DATE','DZ',
                  'ME','STRING[20]','ME',
                  'MOD_CZAS','TIME','MOD_CZAS',
                  'MOD_DATA','DATE','MOD_DATA',
                  'MOD_KTO','STRING[20]','MOD_KTO',
                  'NF','STRING[25]','NF',
                  'NRST','STRING[15]','NRST',
                  'NST','STRING[40]','NST',
                  'OPI','STRING[100]','OPI',
                  'WARF','REAL','WARF',
                  'WARP','REAL','WARP'
               );

_f:=fopen(_fn,'Ur',1);
{? _f
|| {! |? (_wiersz:=fread(_f))<>'\n' |!
      {? form(_wiersz)<>''
      || _tmp:=spli_str(_wiersz,_s);
         __SKLD.blank(1);
         __SKLD.ID:=#_tmp[1];
         __SKLD.NRI:=_tmp[2];
         __SKLD.DE:=exec('str2date','transfer',_tmp[3]);
         __SKLD.DSE:=exec('str2date','transfer',_tmp[4]);
         __SKLD.DZ:=exec('str2date','transfer',_tmp[5]);
         __SKLD.ME:=_tmp[6];
         __SKLD.MOD_CZAS:=exec('str2time','#convert',_tmp[7]);
         __SKLD.MOD_DATA:=exec('str2date','transfer',_tmp[8]);
         __SKLD.MOD_KTO:=_tmp[9];
         __SKLD.NF:=_tmp[10];
         __SKLD.NRST:=_tmp[11];
         __SKLD.NST:=exec('at','transfer',_tmp[12]);
         __SKLD.OPI:=_tmp[13];
         __SKLD.WARF:=#(gsub(_tmp[14],',','.'));
         __SKLD.WARP:=#(gsub(_tmp[15],',','.'));
         __SKLD.add();
         obj_del(_tmp)
      ?}
   !};
   fclose(_f)
?};
:: przenumerowanie powielonych NRI
:: najpierw uzupełnienie oddziału w elementach
__SRSR.prefix();
{? __SKLD.first()
|| {!|? _id:=__SKLD.ID;
        {? __SRSR.find_key(_id)
        || __SKLD.ODD:=__SRSR.ODD;
           __SKLD.put()
        ?};
        __SKLD.next()
   !}
?};
_tab1:=sql('select ODD, NRI, count(*) ILE from :_a group by ODD, NRI having count(*)>1', __SKLD);
{? _tab1.first()
||
   __SKLD.cntx_psh();
:: element który ma najświeższą datę zakupu zostaje z oryginalnym numerem, starsze zostają przenumerowane
   __SKLD.index(__SKLD.ndx_tmp(,, 'ODD',,, 'NRI',,, 'DZ',,));
   {!|? __SKLD.prefix(_tab1.ODD, _tab1.NRI);
        {? __SKLD.last()
        || {? __SKLD.prev()
           || _ii:=1;
              {!|? __SKLD.cntx_psh();
                   {? __SKLD.prev()
                   || _nast:=__SKLD.ref
                   || _nast:=null
                   ?};
                   __SKLD.cntx_pop();
                   _nnri:={? __SKLD.NRI+1='.' || (__SKLD.NRI-1)+'/'+$_ii+'.' || __SKLD.NRI+'/'+$_ii ?};
                   __SKLD.O_NRI:=__SKLD.NRI;
                   __SKLD.NRI:=_nnri;
                   __SKLD.cntx_psh();
                   __SKLD.prefix();
                   __SKLD.put();
                   _ii+=1;
                   __SKLD.cntx_pop();
                   {? _nast<>null
                   || __SKLD.seek(_nast)
                   || 0
                   ?}
              !}
           ?}
        ?};
        _tab1.next()
   !};
   __SKLD.cntx_pop()
?};
obj_del(_tab1)


\es_skld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [1700]
:: OPIS: Utworzenie elementów składowych SRSR na podstawie wcześniej wczytanych danych z tabeli SKLD
::  ORG: \srsr_zb/upgrade_1742.fml
::       \SRSR_AMOR100/upgrade_1742.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__SRSR')<=0 | var_pres('__SKLD')<=0 || return (0) ?};

SRST.cntx_psh(); SRSR.cntx_psh();
SRST.prefix(); SRSR.prefix();
SRSR.trig_b('add',"");
__SRSR.prefix();

{? __SKLD.first()
|| {! |?
      _id:=__SKLD.ID;
      _ref:=null;
      {? __SRSR.find_key(_id)
      || {? SRSR.seek(__SRSR.REF)
         || _ref:=SRSR.ref();
            _r_srsr:=SRSR.R;
            _okro_f:=SRSR.OKRO_F;
            _rok_zes:=SRSR.ROK;
            _mp:=SRSR.MP;
            _mf:=SRSR.MF;
            _stap:=SRSR.STAP;
            _staf:=SRSR.STAF;
            _konpod:=SRSR.KONPOD;
            _konfin:=SRSR.KONFIN;
            _t_odd:=SRSR.ODD;
            _jorg:=SRSR.JORG;
            _wkp:=SRSR.WKP;
            _wkf:=SRSR.WKF;
            _gr:=SRSR.GR
         ?}
      ?};

      {? _ref
      || progress(,'Import elementu składowego: %1'@[__SKLD.NRI],'Trwa import danych',1);
         SRSR.blank(1);
         SRSR.TREE:=_ref;
         SRSR.GRP:='E';
         SRSR.KIND:='N';
         SRSR.Z:='N';
         SRSR.R:=_r_srsr;
         SRSR.ODD:=_t_odd;
         SRSR.JORG:=_jorg;
         SRSR.WLASNY:='T';
         SRSR.U_SR:='N';
         SRSR.NRI:=__SKLD.NRI;
         SRSR.NST:=__SKLD.NST;
         SRSR.DE:=__SKLD.DE;
         SRSR.DES:=__SKLD.DSE;
         {? SRSR.DES<>date(0,0,0) || SRSR.Z:='T' ?};
         SRSR.DZ:=__SKLD.DZ;
:: jeżeli nie udało się ustalić okresu na podstawie daty eksploatacji (data wcześniejsza niż założenie systemu)
:: to ustawia okres z zestawu, podobnie jeżeli element jest starszy niż zestaw!
         {? _rok_zes>SRSR.DE~1
         || SRSR.OKRO_F:=_okro_f
         || SRSR.OKRO_F:=exec('find_okro_f','fst',SRSR.DE)
         ?};
         {? ~SRSR.OKRO_F || SRSR.OKRO_F:=_okro_f ?};
         {? SRSR.OKRO_F<>null || SRSR.ROK_F:=SRSR.OKRO_F().ROK ?};
         {? SRSR.OKRO_F<>null || SRSR.OKRES:=SRSR.OKRO_F().OES ?};
         {? SRSR.OKRO_F<>null || SRSR.ROK:=SRSR.OKRO_F().RES ?};
         SRSR.MOD_CZAS:=__SKLD.MOD_CZAS;
         SRSR.MOD_DATA:=__SKLD.MOD_DATA;
         SRSR.MOD_KTO:=__SKLD.MOD_KTO;
         SRSR.NF:=__SKLD.NF;
         SRSR.U:='Wartość podatkowa: '+$__SKLD.WARP+', wartość bilansowa: '+$__SKLD.WARF+', opis: '+__SKLD.OPI;
         {? __SKLD.O_NRI<>''
         || SRSR.U:='Oryginalny nri: '+__SKLD.O_NRI+'. '+SRSR.U
         ?};
         SRSR.MP:=_mp;
         SRSR.MF:=_mf;
         SRSR.STAP:=_stap;
         SRSR.STAF:=_staf;
         SRSR.WKP:=_wkp;
         SRSR.WKF:=_wkf;
         SRSR.KONPOD:=_konpod;
         SRSR.KONFIN:=_konfin;
         SRSR.GR:=_gr;
         SRSR.ZB:='N';
         SRSR.AMOR100:='N';
         {? SRSR.add()
         || _e_ref:=SRSR.ref();
            {? SRSR.OKRO_F<>null
            ||
               SRST.cntx_psh();
               SRST.index('PODAT');
               SRST.prefix(SRSR.TREE);
               {? SRST.find_key(SRSR.ROK,SRSR.OKRES)
               || {! |?
                     _srst:=SRST.ref();
                     _r:=SRST.ROK_F;
                     _rp:=SRST.ROK;
                     _o:=SRST.OKRO_F;
                     _op:=SRST.OKRES;
                     _rodz:=SRST.R;
                     _odd:=SRST.ODD;
                     _jorg:=SRST.JORG;
                     _grupa:=SRST.GR;
                     _mp:=SRST.MP;
                     _mf:=SRST.MF;
                     _stap:=SRST.STAP;
                     _staf:=SRST.STAF;
                     _konpod:=SRST.KONPOD;
                     _konfin:=SRST.KONFIN;
                     _wkp:=SRST.WKP;
                     _wkf:=SRST.WKF;
                     _nal:=SRST.NAL;
                     SRST.cntx_psh();
                     SRST.blank(1);
                     SRST.KIND:='N';
                     SRST.GRP:='E';
                     SRST.SRSR:=_e_ref;
                     SRST.TREE:=_srst;
                     SRST.ROK_F:=_r;
                     SRST.OKRO_F:=_o;
                     SRST.ROK:=_rp;
                     SRST.OKRES:=_op;
                     SRST.R:=_rodz;
                     SRST.ODD:=_odd;
                     SRST.NRI:=SRSR.NRI;
                     SRST.NST:=SRSR.NST;
                     SRST.JORG:=_jorg;
                     SRST.GR:=_grupa;
                     SRST.MP:=_mp;
                     SRST.MF:=_mf;
                     SRST.STAP:=_stap;
                     SRST.STAF:=_staf;
                     SRST.KONPOD:=_konpod;
                     SRST.KONFIN:=_konfin;
                     SRST.WKP:=_wkp;
                     SRST.WKF:=_wkf;
                     SRST.NAL:=_nal;
                     SRST.prefix();
                     SRST.add();
                     SRST.cntx_pop();

                     SRST.next()
                  !}
               ?};
               SRST.cntx_pop()
:: SRD.updateRecState(SRSR.OKRO_F)
            ?}
         ?}
      ?};
      __SKLD.next()
   !}
?};
SRSR.trig_b('add',"exec('trig_b_srsr_add','fst_ext')");
SRST.cntx_pop(); SRSR.cntx_pop()


\staz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Uzupełnienie wartości w nowych polach tabel STAZ i STAZ_H
::  ORG: \STAZ_praca_r/upgrade_rrxx.fml
::----------------------------------------------------------------------------------------------------------------------
STAZ.trig_off('*','*');
STAZ.cntx_psh();
STAZ.prefix();
{? STAZ.first()
|| {!
   |? {? STAZ.PRACA_R=''
      || STAZ.WU_R:=STAZ.WU;
         STAZ.WUG_R:=STAZ.WUG;
         STAZ.WUZ_R:=STAZ.WUZ;
         STAZ.WUZG_R:=STAZ.WUZG;
         STAZ.WC_R:=STAZ.WC;
         STAZ.WMD_R:=STAZ.WMD;
         STAZ.WMDG_R:=STAZ.WMDG;
         STAZ.WUNSP_R:=STAZ.WUNSP;
         STAZ.WUNSPG_R:=STAZ.WUNSPG;
         STAZ.PRACA_R:='N';
         STAZ.put()
      ?};
      STAZ.next()
   !}
?};
STAZ.cntx_pop();
STAZ.trig_on('*','*');
STAZ_H.trig_off('*','*');
STAZ_H.cntx_psh();
STAZ_H.prefix();
{? STAZ_H.first()
|| {!
   |? {? ~(STAZ_H.RWY*STAZ_H.RWYL*STAZ_H.RWYM)
      || {? STAZ_H.RWY | STAZ_H.RWYL | STAZ_H.RWYM
         || 1
         || STAZ_H.RWY:=STAZ_H.RWYL:=STAZ_H.RWYM:=1;
            STAZ_H.put()
         ?}
      ?};
      STAZ_H.next()
   !}
?};
STAZ_H.cntx_pop();
STAZ_H.trig_on('*','*');
1


\es_atruse_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Import tabeli wartości pierwotnych cech dodatkowych do tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_symbol:=REF.FIRMA().SYMBOL;
_s:='@';
_fn:='eat'+'_'+_symbol+'.tra';

:: jeżeli brak pliku lub plik pusty formuła zwraca 0
{? ~fexists(_fn,1) | exec('es_size_file','transfer',_fn)=0
||
   return(0)
?};

progress(,'Import pliku %1 do tabeli tymczasowej...'@[_fn],'Trwa import danych'@,1);

VAR_DEL.delete('__ATRUSE_E');

__ATRUSE_E:=tab_tmp(3,'SRODID','INTEGER','SRODID',
                     'SYSTEM','STRING[10]','SYSTEM',
                     'TYP_CECH','STRING[10]','TYP_CECH',
                     'AKRONIM','STRING[8]','AKRONIM',
                     'DOKUREF','STRING[16]','DOKUREF',
                     'ATRDEFWD','STRING[16]','ATRDEFWD',
                     'WARS','STRING[255]','WARS',
                     'WARR','REAL','WARR',
                     'KON','STRING[35]','KON',
                     'WALKOD','STRING[8]','WALKOD',
                     'WALNAZ','STRING[20]','WALNAZ',
                     'SUM','REAL','SUM',
                     'SUMW','REAL','SUMW',
                     'KURS','REAL','KURS',
                     'ID','STRING[20]','ID',
                     'TID','STRING[3]','TID',
                     'DO','DATE','DO',
                     'TP','DATE','TP',
                     'DATA_R','DATE','DATA_R',
                     'OP','STRING[30]','OP',
                     'ROZDZ','STRING[20]','ROZDZ',
                     'SLOKOD','STRING[8]','SLOKOD',
                     'SLONAZ','STRING[20]','SLONAZ',
                     'ASQLREF','STRING[16]','ASQLREF',
                     'REF','STRING[16]','REF');
_f:=fopen(_fn,'Ur',1);
{? _f
|| {! |? (_wiersz:=fread(_f))<>'\n' |!
      {? form(_wiersz)<>''
      || _tmp:=spli_str(_wiersz,_s);
         __ATRUSE_E.blank();
         __ATRUSE_E.SRODID:=#_tmp[1];
         __ATRUSE_E.SYSTEM:=_tmp[2];
         __ATRUSE_E.TYP_CECH:=_tmp[3];
         __ATRUSE_E.AKRONIM:=_tmp[4];
         __ATRUSE_E.DOKUREF:=_tmp[5];
         __ATRUSE_E.ATRDEFWD:=_tmp[6];
         __ATRUSE_E.WARS:=_tmp[7];
         __ATRUSE_E.WARR:=#(gsub(_tmp[8],',','.'));
         __ATRUSE_E.KON:=_tmp[9];
         __ATRUSE_E.WALKOD:=_tmp[10];
         __ATRUSE_E.WALNAZ:=_tmp[11];
         __ATRUSE_E.SUM:=#(gsub(_tmp[12],',','.'));
         __ATRUSE_E.SUMW:=#(gsub(_tmp[13],',','.'));
         __ATRUSE_E.KURS:=#(gsub(_tmp[14],',','.'));
         __ATRUSE_E.ID:=_tmp[15];
         __ATRUSE_E.TID:=_tmp[16];
         __ATRUSE_E.DO:=exec('str2date','transfer',_tmp[17]);
         __ATRUSE_E.TP:=exec('str2date','transfer',_tmp[18]);
         __ATRUSE_E.DATA_R:=exec('str2date','transfer',_tmp[19]);
         __ATRUSE_E.OP:=_tmp[20];
         __ATRUSE_E.ROZDZ:=_tmp[21];
         __ATRUSE_E.SLOKOD:=_tmp[22];
         __ATRUSE_E.SLONAZ:=_tmp[23];
         __ATRUSE_E.ASQLREF:=_tmp[24];
         __ATRUSE_E.REF:='';
         __ATRUSE_E.add()
      ?};
      obj_del(_tmp)
   !};
   fclose(_f)
?};

_compare:=obj_new(24);
{? __ATRUSE_E.first()
|| {! |?
      _compare[1]:=__ATRUSE_E.SRODID;
      _compare[2]:=__ATRUSE_E.SYSTEM;
      _compare[3]:=__ATRUSE_E.TYP_CECH;
      _compare[4]:=__ATRUSE_E.AKRONIM;
      _compare[5]:=__ATRUSE_E.DOKUREF;
      _compare[6]:=__ATRUSE_E.ATRDEFWD;
      _compare[7]:=__ATRUSE_E.WARS;
      _compare[8]:=__ATRUSE_E.WARR;
      _compare[9]:=__ATRUSE_E.KON;
      _compare[10]:=__ATRUSE_E.WALKOD;
      _compare[11]:=__ATRUSE_E.WALNAZ;
      _compare[12]:=__ATRUSE_E.SUM;
      _compare[13]:=__ATRUSE_E.SUMW;
      _compare[14]:=__ATRUSE_E.KURS;
      _compare[15]:=__ATRUSE_E.ID;
      _compare[16]:=__ATRUSE_E.TID;
      _compare[17]:=__ATRUSE_E.DO;
      _compare[18]:=__ATRUSE_E.TP;
      _compare[19]:=__ATRUSE_E.DATA_R;
      _compare[20]:=__ATRUSE_E.OP;
      _compare[21]:=__ATRUSE_E.ROZDZ;
      _compare[22]:=__ATRUSE_E.SLOKOD;
      _compare[23]:=__ATRUSE_E.SLONAZ;
      _compare[24]:=__ATRUSE_E.ASQLREF;
      __ATRUSE_E.cntx_psh();
      {? __ATRUSE_E.next()
      || {! |?
            {? _compare[1]=__ATRUSE_E.SRODID &
               _compare[2]=__ATRUSE_E.SYSTEM &
               _compare[3]=__ATRUSE_E.TYP_CECH &
               _compare[4]=__ATRUSE_E.AKRONIM &
               _compare[5]=__ATRUSE_E.DOKUREF &
               _compare[6]=__ATRUSE_E.ATRDEFWD &
               _compare[7]=__ATRUSE_E.WARS &
               _compare[8]=__ATRUSE_E.WARR &
               _compare[9]=__ATRUSE_E.KON &
               _compare[10]=__ATRUSE_E.WALKOD &
               _compare[11]=__ATRUSE_E.WALNAZ &
               _compare[12]=__ATRUSE_E.SUM &
               _compare[13]=__ATRUSE_E.SUMW &
               _compare[14]=__ATRUSE_E.KURS &
               _compare[15]=__ATRUSE_E.ID &
               _compare[16]=__ATRUSE_E.TID &
               _compare[17]=__ATRUSE_E.DO &
               _compare[18]=__ATRUSE_E.TP &
               _compare[19]=__ATRUSE_E.DATA_R &
               _compare[20]=__ATRUSE_E.OP &
               _compare[21]=__ATRUSE_E.ROZDZ &
               _compare[22]=__ATRUSE_E.SLOKOD &
               _compare[23]=__ATRUSE_E.SLONAZ &
               _compare[24]=__ATRUSE_E.ASQLREF
            || __ATRUSE_E.del()
            || __ATRUSE_E.next()
            ?}
         !}
      ?};
      __ATRUSE_E.cntx_pop();
      __ATRUSE_E.next()
   !}
?}


\es_atruse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Import cech dodatkowych z tabeli tymczasowej do ATRUSE_E
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__SRSR')<=0 | var_pres('__DOKU')<=0 | var_pres('__ATRUSE_E')<=0 || return(0) ?};
SRDO.cntx_psh(); SRSR.cntx_psh(); SRST.cntx_psh(); ATRDEFWD.cntx_psh(); SLU.cntx_psh(); SLO.cntx_psh();
ATRUSE_E.cntx_psh(); ATRDEF.cntx_psh();
SRDO.prefix(); SRSR.prefix(); SRST.prefix(); ATRDEFWD.prefix(); ATRUSE_E.prefix();
ATRDEF.index('TYP'); ATRDEF.prefix();
SLU.index('NAZ'); SLU.prefix();
SLO.index('SL_NAZ'); SLO.prefix();
__DOKU.cntx_psh();
_dndx:=__DOKU.ndx_tmp(,,'REFSQL',,);
__DOKU.index(_dndx); __DOKU.prefix();
__SRSR.prefix();

{? __ATRUSE_E.first()
|| progress(,'Import cech dodatkowych.'@,'Trwa import danych'@,1);
   {! |?
      ATRUSE_E.blank();
      _id:=__ATRUSE_E.SRODID;
      _doku:=__ATRUSE_E.DOKUREF;
      _ref:=_dref:=_awd:=_wal:=_slo:=_def:=null;

      {? __SRSR.find_key(_id)
      || {? SRSR.seek(__SRSR.REF)
         || _ref:=SRSR.ref()
         ?}
      ?};

      {? _doku<>'' & __DOKU.find_key(_doku)
      || {? SRDO.seek(__DOKU.REF)
         || _dref:=SRDO.ref()
         ?}
      ?};

      {? ATRDEFWD.seek(__ATRUSE_E.ATRDEFWD)
      || _awd:=ATRDEFWD.ref()
      ?};

      SLO.prefix(__ATRUSE_E.WALNAZ,__ATRUSE_E.WALKOD);
      {? SLO.first()
      || _wal:=SLO.ref()
      ?};

      SLO.prefix(__ATRUSE_E.SLONAZ,__ATRUSE_E.SLOKOD);
      {? SLO.first()
      || _slo:=SLO.ref()
      ?};

      ATRDEF.prefix(__ATRUSE_E.SYSTEM,__ATRUSE_E.TYP_CECH,__ATRUSE_E.AKRONIM);
      {? ATRDEF.first()
      || _def:=ATRDEF.ref()
      ?};

      ATRUSE_E.SRSR:=_ref;
      ATRUSE_E.SRDO:=_dref;
      ATRUSE_E.CECHA:=_def;
      ATRUSE_E.WAL:=_wal;
      ATRUSE_E.ATRDEFWD:=_awd;
      ATRUSE_E.SLO:=_slo;
      ATRUSE_E.WARS:=__ATRUSE_E.WARS;
      ATRUSE_E.WARR:=__ATRUSE_E.WARR;
      ATRUSE_E.KON:=__ATRUSE_E.KON;
      ATRUSE_E.SUM:=__ATRUSE_E.SUM;
      ATRUSE_E.SUMW:=__ATRUSE_E.SUMW;
      ATRUSE_E.KURS:=__ATRUSE_E.KURS;
      ATRUSE_E.ID:=__ATRUSE_E.ID;
      ATRUSE_E.TID:=__ATRUSE_E.TID;
      ATRUSE_E.DO:=__ATRUSE_E.DO;
      ATRUSE_E.TP:=__ATRUSE_E.TP;
      ATRUSE_E.DATA_R:=__ATRUSE_E.DATA_R;
      ATRUSE_E.OP:=__ATRUSE_E.OP;
      ATRUSE_E.ROZDZ:=__ATRUSE_E.ROZDZ;
      {? ATRUSE_E.add()
      || __ATRUSE_E.REF:=$ATRUSE_E.ref();
         __ATRUSE_E.put()
      ?};
      __ATRUSE_E.next()
   !}
?};

SRDO.cntx_pop(); SRSR.cntx_pop(); SRST.cntx_pop(); ATRDEFWD.cntx_pop(); SLU.cntx_pop(); SLO.cntx_pop();
ATRDEF.cntx_pop(); ATRUSE_E.cntx_pop();
__DOKU.ndx_drop(_dndx); __DOKU.cntx_pop()


\es_wyre_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Import wyróżników dla wartości cech do tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_symbol:=REF.FIRMA().SYMBOL;
_s:='@';
_fn:='ewr'+'_'+_symbol+'.tra';

:: jeżeli brak pliku lub plik pusty formuła zwraca 0
{? ~fexists(_fn,1) | exec('es_size_file','transfer',_fn)=0
||
   return(0)
?};

progress(,'Import pliku %1 do tabeli tymczasowej...'@[_fn],'Trwa import danych'@,1);

VAR_DEL.delete('__ATR_WYRE');
__ATR_WYRE:=tab_tmp(3,'ATRUSE','STRING[16]','ATRUSE',
                     'KW','REAL','KW',
                     'LP','INTEGER','LP',
                     'SLOKOD','STRING[8]','SLOKOD',
                     'SLONAZ','STRING[20]','SLONAZ',
                     'SLUG','STRING[1]','SLUG',
                     'SLUN','STRING[20]','SLUN');
_f:=fopen(_fn,'Ur',1);
{? _f
|| {! |? (_wiersz:=fread(_f))<>'\n' |!
      {? form(_wiersz)<>''
      || _tmp:=spli_str(_wiersz,_s);
         __ATR_WYRE.blank();
         __ATR_WYRE.ATRUSE:=_tmp[1];
         __ATR_WYRE.KW:=#(gsub(_tmp[2],',','.'));
         __ATR_WYRE.LP:=#_tmp[3];
         __ATR_WYRE.SLOKOD:=_tmp[4];
         __ATR_WYRE.SLONAZ:=_tmp[5];
         __ATR_WYRE.SLUG:=_tmp[6];
         __ATR_WYRE.SLUN:=_tmp[7];
         __ATR_WYRE.add()
      ?};
      obj_del(_tmp)
   !};
   fclose(_f)
?}


\es_wyre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Import danych z tabeli tymczasowej do ATR_WYRE
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__ATR_WYRE')<=0 | var_pres('__ATRUSE_E')<=0 || return(0) ?};
SLO.cntx_psh(); SLU.cntx_psh(); SLUAPPL.cntx_psh(); ATRUSE_E.cntx_psh(); ATR_WYRE.cntx_psh();
ATRUSE_E.prefix(); ATR_WYRE.prefix();
SLO.index('SL_NAZ'); SLO.prefix();
SLU.index('NAZ'); SLU.prefix();
SLUAPPL.index('NAZ'); SLUAPPL.prefix();
__ATRUSE_E.cntx_psh();
_andx:=__ATRUSE_E.ndx_tmp(,,'ASQLREF',,); __ATRUSE_E.index(_andx); __ATRUSE_E.prefix();

{? __ATR_WYRE.first()
|| {! |?
      ATR_WYRE.blank();
      __ATRUSE_E.prefix(__ATR_WYRE.ATRUSE);
      _usee:=_slo:=_sluappl:=null;
      {? __ATRUSE_E.first()
      || {? ATRUSE_E.seek(__ATRUSE_E.REF)
         || _usee:=ATRUSE_E.ref()
         ?}
      ?};
      SLO.prefix(__ATR_WYRE.SLONAZ,__ATR_WYRE.SLOKOD);
      {? SLO.first()
      || _slo:=SLO.ref()
      ?};
      SLUAPPL.prefix(__ATR_WYRE.SLUG,__ATR_WYRE.SLUN);
      {? SLUAPPL.first()
      || _sluappl:=SLUAPPL.ref()
      ?};
      ATR_WYRE.ATRUSE:=_usee;
      ATR_WYRE.LP:=__ATR_WYRE.LP;
      ATR_WYRE.SLU:=_sluappl;
      ATR_WYRE.SLO:=_slo;
      ATR_WYRE.KW:=__ATR_WYRE.KW;
      {? ATR_WYRE.add() || 1 ?};
      __ATR_WYRE.next()
   !}
?};

SLO.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop(); ATRUSE_E.cntx_pop(); ATR_WYRE.cntx_pop();
__ATRUSE_E.ndx_drop(_andx); __ATRUSE_E.cntx_pop()


\es_atrdefwd_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Import wartości źródłowych tabeli ATRDEFWD do tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_symbol:=REF.FIRMA().SYMBOL;
_s:='@';
_fn:='atrdefwd_'+_symbol+'.tra';

:: jeżeli brak pliku lub plik pusty formuła zwraca 0
{? ~fexists(_fn,1) | exec('es_size_file','transfer',_fn)=0
||
   return(0)
?};

progress(,'Import pliku %1 do tabeli tymczasowej...'@[_fn],'Trwa import danych'@,1);

VAR_DEL.delete('__ATRDEFWD');
__ATRDEFWD:=tab_tmp(,'REFSQL','STRING[16]','REFSQL',
                     'ID','INTEGER','ID',
                     'DOKUREF','STRING[16]','DOKUREF');
_f:=fopen(_fn,'Ur',1);
{? _f
|| {! |? (_wiersz:=fread(_f))<>'\n' |!
      {? form(_wiersz)<>''
      || _tmp:=spli_str(_wiersz,_s);
         __ATRDEFWD.blank();
         __ATRDEFWD.REFSQL:=_tmp[1];
         __ATRDEFWD.ID:=#_tmp[2];
         __ATRDEFWD.DOKUREF:=_tmp[3];
         __ATRDEFWD.add()
      ?};
      obj_del(_tmp)
   !};
   fclose(_f)
?}


\es_atrdefwd_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła aktualizuje wartości ATRDEFWD na podstawie tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__SRSR')<=0 | var_pres('__DOKU')<=0 | var_pres('__ATRDEFWD')<=0 || return(0) ?};
SRSR.cntx_psh(); SRDO.cntx_psh(); ATRDEFWD.cntx_psh();
SRSR.prefix(); SRDO.prefix(); ATRDEFWD.prefix();
__DOKU.cntx_psh();
_dndx:=__DOKU.ndx_tmp(,,'REFSQL',,);
__DOKU.index(_dndx); __DOKU.prefix();
__SRSR.prefix();
_ref:=_dref:=null;
{? __ATRDEFWD.first()
|| {! |?
      ATRDEFWD.blank();
      {? ATRDEFWD.seek(__ATRDEFWD.REFSQL)
      || {? __ATRDEFWD.DOKUREF=''
         || {? __SRSR.find_key(__ATRDEFWD.ID)
            || {? SRSR.seek(__SRSR.REF)
               || _ref:=SRSR.ref();
                  ATRDEFWD.MASKA:=ref_name(_ref);
                  ATRDEFWD.REF:=ref_num(_ref);
                  ATRDEFWD.SQLREF:=$_ref;
                  ATRDEFWD.put()
               ?}
            ?}
         || {? __DOKU.find_key(__ATRDEFWD.DOKUREF)
            || {? SRDO.seek(__DOKU.REF)
               || _dref:=SRDO.ref();
                  ATRDEFWD.MASKA:=ref_name(_dref);
                  ATRDEFWD.REF:=ref_num(_dref);
                  ATRDEFWD.SQLREF:=$_dref;
                  ATRDEFWD.put()
               ?}
            ?}
         ?}
      ?};
      __ATRDEFWD.next()
   !}
?};
{? ATRDEFWD.first()
|| {! |?
      {? 4+ATRDEFWD.MASKA='srod' & ATRDEFWD.count()=0
      || ATRDEFWD.del()
      || ATRDEFWD.next()
      ?}
   !}
?};

SRSR.cntx_pop(); SRDO.cntx_pop(); ATRDEFWD.cntx_pop();
__DOKU.ndx_drop(_dndx); __DOKU.cntx_pop()


\es_atr_erase
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wyczyszczenie zawartości tabel ATR_WYRE i ATRUSE_E
::----------------------------------------------------------------------------------------------------------------------
ATR_WYRE.cntx_psh(); ATRUSE_E.cntx_psh();
_wyre:=ATR_WYRE.names();
_usee:=ATRUSE_E.names();
{? _wyre.first()
|| {! |?
      ATR_WYRE.use(_wyre.NAME);
      ATR_WYRE.erase();
      _wyre.next()
   !}
?};
{? _usee.first()
|| {! |?
      ATRUSE_E.use(_usee.NAME);
      ATRUSE_E.erase();
      _usee.next()
   !}
?};
ATR_WYRE.cntx_pop(); ATRUSE_E.cntx_pop()


\es_atrdef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Zmienia nazwę systemu z ESTRA na FST w tabeli ATRDEF
::----------------------------------------------------------------------------------------------------------------------
ATRDEF.cntx_psh();
ATRDEF.prefix();
ATRDEF.for_each("{? ATRDEF.SYSTEM='FST' & ATRDEF.count()<=0 || ATRDEF.del() ?}");
ATRDEF.for_each("{? ATRDEF.SYSTEM='ESTRA'
                 || ATRDEF.SYSTEM:='FST';
                    ATRDEF.put()
                 ?}");
ATRDEF.cntx_pop()


\zm_param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Zmiana numerów atrybutów (krzyżowa różnica w numeracji między Xpertis a Merit)
::       Formuła stanowi zastępstwo części formuły \zm_param/tran_exp.fml
::UWAGA: Formuła nie powinna być uruchamiana wielokrotnie!
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');
__RUB.fill();

:: Atrybuty rubryk - zmiana numerów na zgodne z Merit.
_przenies:=
   "RA_DEF.f_set(,,'RA_DEF.RODZAJ=\\'S\\' and cast(RA_DEF.SYMBOL as STRING_TYPE) like \\':_a\\'',($_a)+'%');
    {? RA_DEF.f_first()
    || {!
       |? RA_DEF.SYMBOL:=#($_b+(3-$(RA_DEF.SYMBOL)));
          RA_DEF.put(1);

          RA_DEF.f_next()
       !}
    ?};
    RA_DEF.f_clear()
   ";
RA_DEF.cntx_psh();
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
{? ~RA_DEF.find_key(999)
|| RA_DEF.prefix();
:: Przeniesienie gałęzi 901 tymczasowo na 999
   _przenies(901,999);
:: Przeniesienie gałęzi 902 na 901
   _przenies(902,901);
:: Przeniesienie tymczasowej gałęzi 999 na 902
   _przenies(999,902)
?};
RA_DEF.cntx_pop();
~~


\es_dok_fks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła aktualizuję powiązania dokumentów księgowych z dokumentami wartościowymi środków trwałych.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__DOKU')>0
|| progress(,'Aktualizacja powiązań dokumentów księgowych z dokumentami wartościowymi środków trwałych...'@,'Trwa import danych'@,1);
   SRDO.cntx_psh(); DOK.cntx_psh();
   DOK.trig_off('*','*');
   __DOKU.prefix();
   __DOKU.for_each("{? __DOKU.REF<>'' & __DOKU.DOKKSIEG<>''
                    || {? SRDO.seek(__DOKU.REF,8+__DOKU.REF,1)
                       || {? SRDO.DOKKSIEG<>'' & __DOKU.DOKKSIEG=SRDO.DOKKSIEG
                          || {? DOK.seek(#(4-SRDO.DOKKSIEG),'doku'+(4+SRDO.DOKKSIEG),1)
                             || _srdo_ref:=$(#SRDO.ref());
                                {? DOK.DOKZRODL<>_srdo_ref
                                || DOK.DOKZRODL:=_srdo_ref;
                                   DOK.put()
                                ?};
                                {? SRDO.K<>'T'
                                || SRDO.K:='T';
                                   SRDO.put()
                                ?}
                             ?}
                          ?}
                       ?}
                    ?}");
   DOK.trig_on('*','*');
   SRDO.cntx_pop(); DOK.cntx_pop()
?};
echo()

:Sign Version 2.0 jowisz:1045 2023/12/27 14:47:06 6709a3a5c11884a33ae96ebc8e1913849590b71c17a03e99d53658fb9d05bc1012ee676cdec3350d8db4e6902cf2c34c006b98f23637c3a13f67bd7ec1e40651cb8bfecf51a42555de727fa28d83288075abdfd09b28c53cad01674c82c8566b172df92adb4f8153934583621676c30359a72bcbc71e925864e64f3036be51ac

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tar_up.fml
:: Utworzony: 27.08.2019
:: Autor: MW
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu i importu uprawnień do danych - tabela TAR_UP
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USRUPR';

_def.FILE:='uprawnienia.xlsx';

_def.TAB_IMP:="TARUP";

_def.BEFORE:="TARUP.cntx_psh()";

exec('SelUsersTab','xls_users_up');

~~


\init_s
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
exec('init','xls_tar_up',_def);

_def.ID:='UPR_TARS';

_def.SHEET:='Cenniki sprzedaży=1,1';
_def.NAME:='Uprawnienia do cenników sprzedaży'@;
_def.DESC:='Uprawnienia do cenników sprzedaży';

_def.TABLE:="exec('table','xls_tar_up','S')";
_def.FIELDS:="exec('fields','xls_tar_up',_a,'S')";

_def.PREFIX:="exec('prefix','xls_tar_up','S')";
_def.EXPORT:="exec('export','xls_tar_up',_a,_b,'S')";

_def.VALIDATE:="exec('validate','xls_tar_up',_a,_b,_c,'S')";
_def.IMPORT:="exec('import','xls_tar_up',_a,_b,_c,'S')";
_def.AFTER:="TARUP.cntx_pop();
             {? _a.KIND='IMPORT' || exec('usr_upr_napraw','users_upraw','TARS') ?}";

_def.SELECT:="exec('select','xls_tar_up',_a,_b,'S')";

~~


\init_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
exec('init','xls_tar_up',_def);

_def.ID:='UPR_TARD';

_def.SHEET:='Cenniki dostawców=1,1';
_def.NAME:='Uprawnienia do cenników dostawców'@;
_def.DESC:='Uprawnienia do cenników dostawców';

_def.TABLE:="exec('table','xls_tar_up','D')";
_def.FIELDS:="exec('fields','xls_tar_up',_a,'D')";

_def.PREFIX:="exec('prefix','xls_tar_up','D')";
_def.EXPORT:="exec('export','xls_tar_up',_a,_b,'D')";

_def.VALIDATE:="exec('validate','xls_tar_up',_a,_b,_c,'D')";
_def.IMPORT:="exec('import','xls_tar_up',_a,_b,_c,'D')";
_def.AFTER:="TARUP.cntx_pop();
             {? _a.KIND='IMPORT' || exec('usr_upr_napraw','users_upraw','TARD') ?}";

_def.SELECT:="exec('select','xls_tar_up',_a,_b,'D')";

~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | (_a<>'S' & _a<>'D')
|| _a:='S'
?};
_rodz:=_a;
__SelUsers.prefix('TAR'+_rodz);
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa uchwyt do tabeli
::   WE: _a - rodzaj uprawnienia do cennika (S, D)
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | (_a<>'S' & _a<>'D')
|| _a:='S'
?};
_rodz:=_a;
exec('TabTarUp','users_upraw',_rodz)


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::       _b - rodzaj uprawnienia do cennika (S, D)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of('') | (_b<>'S' & _b<>'D')
|| _b:='S'
?};
_def:=_a;
_rodz:=_b;

_tooltip:=MS.name(TARUP,'USER')+' '+'(max. %1 znaków). '@[$MS.fld_len(USERS,'KOD')];
_tooltip+='Pole wymagane'@;
_def.define('USER','Użytkownik'@,1,,_tooltip);

_tooltip:=MS.name(TARUP,'TARGN')+' '+'(max. %1 znaków). '@[$MS.fld_len(TARGN,'KOD')];
_tooltip+='Pole wymagane'@;
_def.define('TARGN','Grupa uprawnień do cenników'@,1,,_tooltip);

_tooltip:='Uprawniony (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('DOSTEP','Czy uprawniony?'@,1,,_tooltip);

_tooltip:='Pełne uprawnienia (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('PELNE','Czy pełne uprawnienia?'@,1,,_tooltip);

~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::       _c - rodzaj uprawnienia do cennika (S, D)
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<>type_of('') | (_c<>'S' & _c<>'D')
|| _c:='S'
?};

_excel:=_a;
_obj:=_b;
_rodz:=_c;
_tab:=_excel.table();

{? __SelUsers.first()
|| _result:=__SelUsers.find_key(_tab.USER_REF)
|| _result:=1
?};

{? _result
|| _obj.USER.VALUE:=_tab.USER_KOD;
   _obj.TARGN.VALUE:=_tab.KOD;
   _obj.DOSTEP.VALUE:=_tab.ACCESS;
   _obj.PELNE.VALUE:=_tab.FULL
?};

_result


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - rodzaj uprawnienia do cennika (S, D)
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_d')<>type_of('') | (_d<>'S' & _d<>'D')
|| _d:='S'
?};

_def:=_a;
_obj:=_a;
_mode:=_b;
_validate:=_c;
_rodz:=_d;

TARUP.prefix();
{? _validate.ACTION='add'
|| TARUP.blank()
?};
TARUP.USER:=_validate.OBJ.USER;
TARUP.TARGN:=_validate.OBJ.TARGN;
TARUP.USERSGRP:=null();
TARUP.ZAST_NAG:=null();
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER   - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obj_new() - obiekt z resultem, wynik działania exec('args_valid','#excel_imex').
::                        Obiekt ten jest przekazywany do formuły na IMPORT. Pole RESULT decyduje czy formuła
::                        na import się wykona. Pole MSG służy to odpisania komunikatu
::       _d - rodzaj uprawnienia do cennika (S, D)
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_d')<>type_of('') | (_d<>'S' & _d<>'D')
|| _d:='S'
?};

_obj:=_a;
_mode:=_b;
_result:=_c;
_rodz:=_d;

_can_continue:=1;
:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _obj.USER=''
|| _can_continue:=0;
   _result.msg_empty('USER')
?};
{? _obj.TARGN=''
|| _can_continue:=0;
   _result.msg_empty('TARGN')
?};

_user:=null();
{? _can_continue
|| _user:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1);
   {? _user=null()
   || _can_continue:=0;
      _result.msg_norec('USER')
   ?}
?};

_targn:=null();
{? _can_continue
|| _targn:=exec('FindInSet','#table','TARGN','S',_obj.TARGN,_rodz,,1);
   {? _targn=null()
   || _can_continue:=0;
      _result.msg_norec('TARGN')
   ?}
?};

{? _obj.DOSTEP<>'T' & _obj.DOSTEP<>'N'
|| _can_continue:=0;
   _result.msg_inset('DOSTEP','T','N')
?};

{? _obj.PELNE<>'T' & _obj.PELNE<>'N'
|| _can_continue:=0;
   _result.msg_inset('PELNE','T','N')
?};

{? _can_continue & _obj.DOSTEP='T'
|| {? ~exec('user_upraw_dostep','users_upraw','TAR'+_rodz,_user)
   || _can_continue:=0;
      _msg:='Użytkownik nie może mieć uprawnienia, nie ma odpowiedniej roli.'@;
      {? _obj.PELNE='T'
      || _result.msg_value('PELNE',_msg)
      || _result.msg_value('DOSTEP',_msg)
      ?}
   ?}
?};

_bperm_ref:=exec('bperm_ref','users_upraw','TAR'+_rodz);
B_PERM_U.index('USER');
B_PERM_U.prefix(REF.FIRMA,_user,_bperm_ref);
{? ~B_PERM_U.first()
|| _can_continue:=0
?};

_result.RESULT:=_can_continue;

:: OK
{? _result.RESULT & _obj.PELNE='T'
|| {? B_PERM_U.FULL<>'T'
   || _result.ACTION:='padd';
      _result.OBJ:=obj_new('USER','PELNE');
      _result.OBJ.USER:=_user;
      _result.OBJ.PELNE:='T'
   ?}
|? _result.RESULT & (_mode=1 | _obj.DOSTEP='T')
|| TARUP.index('USER');
   TARUP.prefix(_user,null(),_targn);
   {? TARUP.first()
   || {? _obj.DOSTEP='N'
      || _result.ACTION:='del'
      |? _mode=1 & B_PERM_U.FULL='T'
      || _result.ACTION:='pdel';
         _result.OBJ:=obj_new('USER','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.PELNE:='N'
      ?}
   || {? _obj.DOSTEP='T'
      || _result.ACTION:='add';
         _result.OBJ:=obj_new('USER','TARGN','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.TARGN:=_targn;
         {? _mode=1 & B_PERM_U.FULL='T'
         || _result.OBJ.PELNE:='N'
         || _result.OBJ.PELNE:=''
         ?};
         exec('record','xls_tar_up',_obj,_mode,_result,_rodz)
      |? _mode=1 & B_PERM_U.FULL='T'
      || _result.ACTION:='pdel';
         _result.OBJ:=obj_new('USER','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.PELNE:='N'
      ?}
   ?}
?};

~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - rodzaj uprawnienia do cenników (S, D)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_d')<>type_of('') | (_d<>'S' & _d<>'D')
|| _d:='S'
?};

_obj:=_a;
_mode:=_b;
_validate:=_c;
_rodz:=_d;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TARUP.add(1)
   |? _validate.ACTION='del'
   || {? TARUP.count()=0 & TARUP.del(1,1)<>0
      || _result:=1
      ?}
   |? _validate.ACTION='padd'
   || B_PERM_U.FULL:='T';
      B_PERM_U.ACCESS:='T';
      _result:=B_PERM_U.put();
      {? _result
      || exec('user_upraw_ustaw_pelne','users_upraw','TAR'+_rodz,_validate.OBJ.USER)
      ?}
   ?};
   {? ((_validate.ACTION='add' | _validate.ACTION='del') & _validate.OBJ.PELNE='N' & _result)
      | _validate.ACTION='pdel'
   || B_PERM_U.FULL:='N';
      _result:=B_PERM_U.put()
   ?}
?};

{? _result=0
|| _validate.MSG:='';
   {? _validate.ACTION='add'
   || {? _rodz='D'
      || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do cennika dostawców %2 nie powiodło się.'@
                        [_obj.USER,_obj.TARGN]
      || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do cennika sprzedaży %2 nie powiodło się.'@
                        [_obj.USER,_obj.TARGN]
      ?}
   |? _validate.ACTION='del'
   || {? _rodz='D'
      || _validate.MSG:='Usunięcie uprawnienia użytkownika %1 do cennika dostawców %2 nie powiodło się.'@
                        [_obj.USER,_obj.TARGN]
      || _validate.MSG:='Usunięcie uprawnienia użytkownika %1 do cennika sprzedaży %2 nie powiodło się.'@
                        [_obj.USER,_obj.TARGN]
      ?}
   |? _validate.ACTION='padd'
   || {? _rodz='D'
      || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do wszystkich cenników dostawców nie powiodło się.'@
                        [_obj.USER]
      || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do wszystkich cenników sprzedaży nie powiodło się.'@
                        [_obj.USER]
      ?}
   ?}
|| {? _validate.ACTION='padd'
   || {? _rodz='D'
      || _validate.MSG:='Dodano uprawnienie użytkownika %1 do wszystkich cenników dostawców.'@[_obj.USER]
      || _validate.MSG:='Dodano uprawnienie użytkownika %1 do wszystkich cenników sprzedaży.'@[_obj.USER]
      ?}
   |? ((_validate.ACTION='add' | _validate.ACTION='del') & _validate.OBJ.PELNE='N') | _validate.ACTION='pdel'
   || {? _rodz='D'
      || _validate.MSG:='Usunięto uprawnienie użytkownika %1 do wszystkich cenników dostawców.'@[_obj.USER]
      || _validate.MSG:='Usunięto uprawnienie użytkownika %1 do wszystkich cenników sprzedaży.'@[_obj.USER]
      ?}
   ?};
   _validate.MSG:=''
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - obj_new - środowisko mechanizmu
::       _c - rodzaj uprawnienia  do cenników (S, D)
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<>type_of('') | (_c<>'S' & _c<>'D')
|| _c:='S'
?};

_selected:=_a;
_excel:=_b;
_rodz:=_c;

exec('select','xls_users_up',_selected,_excel,'TAR'+_rodz)

:Sign Version 2.0 jowisz:1045 2024/01/18 14:42:41 89f36482dc5875515739fdf68ce35d1bb964679b7f0901cf4645442124011ca870915cf6b5f8faf26f10907ffa35119e8adc7e32e8ef216087558430473337d4f5375eef9c55fbe10fbf8257369e5c9a00ecf0d3a586e6394d262bd460eff9a213efffa025662f0181dcd912edfc111684f9c395b93cedf4265c5cdfa805ac9d

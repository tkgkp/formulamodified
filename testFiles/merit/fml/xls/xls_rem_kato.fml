:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_rem_kato.fml
:: Utworzony: 01.10.2019
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły do obsługi opiekunów kategorii zgłoszeń remontowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='REM_KATO';
_def.DOMAIN:=exec('name','#b_domain','TRE');
_def.FUNPAR:='ZWS_PAR_TRKZ';

_def.FILE:='remonty.xlsx';
_def.SHEET:='Opiekunowie kategorii zasobów'@+'=1,1';
_def.NAME:='Opiekunowie kategorii zasobów'@;
_def.DESC:='Opiekunowie kategorii zasobów remontowych'@;

_def.PREFIX:="REM_KATO.index('REM_KAT'); REM_KATO.prefix()";
_def.TABLE:="REM_KATO";
_def.FIELDS:="exec('fields','xls_rem_kato',_a)";

_def.BEFORE:="REM_KATO.cntx_psh()";
_def.AFTER:="REM_KATO.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_rem_kato',_a,_b)";
_def.VALIDATE:="exec('validate','xls_rem_kato',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rem_kato',_a,_b,_c)";
_def.SELECT:="exec('select','xls_rem_kato',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić - wynik działania exec('args_fields','#excel_imex')
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_tooltip:=MS.name(REM_KAT,'SYMBOL')+' '+'(max. %1 znaków). '@[$MS.fld_len(REM_KAT,'SYMBOL')];
_tooltip+='Wymagane.'@;
_def.define('REM_KAT','Kategoria zasobów'@,1,,_tooltip,type_of(''));

_tooltip:=MS.name(USERS,'KOD')+' '+'(max. %1 znaków). '@[$MS.fld_len(USERS,'KOD')];
_tooltip+='Wymagane.'@;
_def.define('USERS','Opiekun'@,1,,_tooltip,type_of(''));
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
REM_KATO.prefix();
{? _validate.ACTION='add'
|| REM_KATO.blank();
   REM_KATO.REM_KAT:=_validate.OBJ.REM_KAT;
   REM_KATO.USERS:=_validate.OBJ.USERS
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.REM_KAT.VALUE:=REM_KATO.REM_KAT().SYMBOL;
_excel.write_async('REM_KAT',REM_KATO.REM_KAT);
_table.USERS.VALUE:=REM_KATO.USERS().KOD;
_excel.write_async('USERS',REM_KATO.USERS);
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;
_can_continue:=1;

_result.RESULT:=0;
_result.OBJ:=obj_new('REM_KAT','USERS');

REM_KAT.cntx_psh();
REM_KAT.index('SYMBOL');
REM_KAT.prefix(_obj.REM_KAT,);
{? REM_KAT.first()
|| _result.OBJ.REM_KAT:=REM_KAT.ref()
|| _result.OBJ.REM_KAT:=null()
?};
REM_KAT.cntx_pop();

USERS.cntx_psh();
USERS.index('USR_AKOD');
USERS.prefix('T',_obj.USERS,);
{? USERS.first()
|| _result.OBJ.USERS:=USERS.ref()
|| _result.OBJ.USERS:=null()
?};
USERS.cntx_pop();

:: Błędy
{? _obj.REM_KAT=''
|| _result.msg_empty('REM_KAT');
   _can_continue:=0
?};
{? _obj.USERS=''
|| _result.msg_empty('USERS');
   _can_continue:=0
?};

{? _obj.REM_KAT<>'' & _result.OBJ.REM_KAT=null()
|| _result.msg_norec('REM_KAT');
   _can_continue:=0
?};

{? _obj.USERS<>'' & _result.OBJ.USERS=null()
|| _result.msg_norec('USERS');
   _can_continue:=0
?};

{? _can_continue>0
||
   REM_KATO.cntx_psh();
   REM_KATO.index('REM_KAT');
   REM_KATO.prefix(_result.OBJ.REM_KAT,_result.OBJ.USERS);
   {? ~REM_KATO.first()
   || _result.ACTION:='add';
      exec('record','xls_rem_kato',_obj,_mode,_result);
      {? exec('rem_kato_valid','remonty_zgloszenia',0)<>''
      || _can_continue:=0
      ?}
   || _can_continue:=0
   ?};
   REM_KATO.cntx_pop();
   ~~
?};
{? _can_continue>0
|| _result.RESULT:=1
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| exec('record','xls_rem_kato',_obj,_mode,_validate);
   {? _validate.ACTION='add'
   || _result:=REM_KATO.add(1)
   ?};
   {? _result=0
   || _validate.MSG:='Dodanie opiekuna: %1 kategorii: %2 nie powiodło się.'@[_obj.USERS,_obj.REM_KAT]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'REM_KAT','STRING[100]','Kategoria zasobów'
   ,'USERS','STRING[100]','Opiekun'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'REM_KAT',,,'USERS',,);

REM_KATO.cntx_psh();
REM_KATO.index('REM_KAT');
REM_KATO.prefix();
{? REM_KATO.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$REM_KATO.ref();
      _selected.prefix($REM_KATO.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.REM_KAT:=REM_KATO.REM_KAT().SYMBOL;
      _tab.USERS:=REM_KATO.USERS().KOD;
      _tab.add();
      REM_KATO.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'REM_KAT,USERS',40,'Wybór opiekunów kategorii zasobów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
REM_KATO.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/10/14 09:22:01 a925a1d94c0a3c01b7119523b1f61a48b69976d51a6f17c7ecba83be8f1e8e1309c61b20ea6c4dc28754240e8db3ccc12e613b3c5c546c28c333aa96199bb1acdb4d8b3d590466f9d5980200c34ec239130c26311c6ffd54312a6a766bf9d585dbbbc81d3da7f2b378f1c8614dbb5cf8cac6a343d0fbb23848d6746ff0291616

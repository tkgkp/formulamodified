:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_rt_jezyk.fml
:: Utworzony: 14.05.2020
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_def:=_a;

_def.ID:='RT_JEZYK';
_def.FUNPAR:='ZWS_PAR_RRTL';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_rt_xlsx','xls__init');
_def.SHEET:='Języki tłumaczeń=1,1';
_def.NAME:='Języki tłumaczeń'@;
_def.DESC:='Języki tłumaczeń';
_def.ADD_ROWS:=0;
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="RT_JEZYK";

: prolog
_def.BEFORE:="
   RT_JEZYK.cntx_psh();
   RT_JEZYK.index('KOD');
   RT_JEZYK.prefix();
   SLO_KOD.cntx_psh();
   SLO_KOD.clear();
   ~~
";

: epilog
_def.AFTER:="
   SLO_KOD.cntx_pop();
   RT_JEZYK.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('KOD',   MS.name(SLO_KOD,'KOD'),0,,   MS.comment(SLO_KOD,'KOD'));
   _env.define('NAZWA', MS.name(SLO_KOD,'NAZWA'),0,, MS.comment(SLO_KOD,'NAZWA'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_rt_jezyk',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_rt_jezyk',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rt_jezyk',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_env:=_a;
_buf:=_b;

RT_JEZYK.SLO_KOD();
_buf.KOD.VALUE:=SLO_KOD.KOD;
_buf.NAZWA.VALUE:=SLO_KOD.NAZWA;

_env.write_async('SLO_KOD',RT_JEZYK.SLO_KOD);

1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.KOD='' || 'KOD'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(SLO_KOD,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(SLO_KOD,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};

: weryfikacja spójności referencyjnej
{? (_kod:=exec('kod','ext_slo','JEZRTL',_buf.KOD))=null
|| _val.msg_norec('KOD');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
RT_JEZYK.index('UNIQUE');
{? RT_JEZYK.find_key(_kod)
|| _ref:=RT_JEZYK.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.KOD,_a.NAZWA]",
   'SLO_KOD',_kod
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,RT_JEZYK,"
   _a.SLO_KOD:=_c.SLO_KOD;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 49a43d31264f7697765dec95e42ac36a87a20a5f111473a0c5bdead5ea4d0c10e1c89f83eb871c2f47f3e156e7391af542bf0c04bc43a25636be13fe01834bd6c4a47a2429801b7a9c6a4ee10a1671a9cbe6275fa97e9f0251656e4955792d683f8d454a581952454b6a7a0f589b8fe81a9509b2ec140826f1875c45e4df1fcb

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_pr_kos.fml
:: Utworzony: 07.12.2021
:: Autor: PD
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu kosztów projektów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PR_KOS';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='PROJEKTY';
_def.HIDDEN:='T';
_def.SKIP:='T';
_def.ADD_ROWS:=100;

_def.FILE:='projekty.xlsx';
_def.SHEET:='Koszty projektów'@+'=1,1';
_def.NAME:='Koszty projektów'@;
_def.DESC:='Koszty projektów'@;

_def.PREFIX:="PR_KOS.index('PR_KOS_P');PR_KOS.prefix()
";
_def.TABLE:="PR_KOS";
_def.FIELDS:="exec('fields','xls_pr_kos',_a)";

_def.BEFORE:="PR_KOS.cntx_psh()";
_def.AFTER:="PR_KOS.cntx_pop()";

_def.EXPORT:="exec('export','xls_pr_kos',_a,_b)";
_def.VALIDATE:="exec('validate','xls_pr_kos',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_pr_kos',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_edit:=1;
_def.define('REF','Referencja'@,0,,'Referencja kosztu');
_def.define('PROJEKTY','Projekt'@,_edit,,'Projekt - Identyfikator wewnętrzny');
_def.define('OPIS','Opis'@,_edit,,'Opis kosztu');
_def.define('GR_K','Grupa kosztowa'@,_edit,,'Grupa kosztowa - Kod');
_def.define('M','Indeks'@,_edit,,'Indeks - Kod');
_def.define('IL','Ilość'@,_edit,,'Ilość');
_def.define('SV','Stawka VAT'@,_edit,,'Stawka VAT - Kod');
_def.define('WAL','Waluta'@,_edit,,'Waluta - Kod');
_def.define('KRS','Kurs'@,_edit,,'Kurs');
_def.define('DTK','Data kursu'@,_edit,,'Data kursu');
_def.define('CW','Cena w walucie'@,_edit,,'Cena w walucie');
_def.define('CN','Cena netto'@,_edit,,'Cena netto');
_def.define('CB','Cena brutto'@,_edit,,'Cena brutto');
_def.define('WN','Wartość netto'@,_edit,,'Wartość netto');
_def.define('WB','Wartość brutto'@,_edit,,'Wartość brutto');
_def.define('WW','Wartość w walucie'@,_edit,,'Wartość w walucie');
_def.define('WV','Wartość VAT'@,_edit,,'Wartość VAT');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PR_KOS.prefix();
{? _validate.ACTION='add'
|| PR_KOS.blank();
   PR_KOS.PROJEKTY:=_validate.OBJ.PROJEKTY;
   PR_KOS.OPIS:=_obj.OPIS;
   PR_KOS.GR_K:=_validate.OBJ.GR_K;
   PR_KOS.M:=_validate.OBJ.M;
   PR_KOS.IL:=_obj.IL;
   PR_KOS.SV:=_validate.OBJ.SV;
   PR_KOS.WAL:=_validate.OBJ.WAL;
   PR_KOS.KRS:=_obj.KRS;
   PR_KOS.DTK:=_obj.DTK;
   PR_KOS.CW:=_obj.CW;
   PR_KOS.CN:=_obj.CN;
   PR_KOS.CN:=_obj.CB;
   PR_KOS.WN:=_obj.WN;
   PR_KOS.WB:=_obj.WB;
   PR_KOS.WW:=_obj.WW;
   PR_KOS.WV:=_obj.WV
|? _validate.ACTION='put' & PR_KOS.seek(_obj.REF)
|| PR_KOS.PROJEKTY:=_validate.OBJ.PROJEKTY;
   PR_KOS.OPIS:=_obj.OPIS;
   PR_KOS.GR_K:=_validate.OBJ.GR_K;
   PR_KOS.M:=_validate.OBJ.M;
   PR_KOS.IL:=_obj.IL;
   PR_KOS.SV:=_validate.OBJ.SV;
   PR_KOS.WAL:=_validate.OBJ.WAL;
   PR_KOS.KRS:=_obj.KRS;
   PR_KOS.DTK:=_obj.DTK;
   PR_KOS.CW:=_obj.CW;
   PR_KOS.CN:=_obj.CN;
   PR_KOS.CB:=_obj.CB;
   PR_KOS.WN:=_obj.WN;
   PR_KOS.WB:=_obj.WB;
   PR_KOS.WW:=_obj.WW;
   PR_KOS.WV:=_obj.WV
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.REF.VALUE:=$PR_KOS.ref();
_table.PROJEKTY.VALUE:=PR_KOS.PROJEKTY().ID_WEW;
_table.OPIS.VALUE:=PR_KOS.OPIS;
_table.GR_K.VALUE:=PR_KOS.GR_K().KOD;
_table.M.VALUE:=PR_KOS.M().KTM;
_table.IL.VALUE:=PR_KOS.IL;
_table.SV.VALUE:=PR_KOS.SV().KOD;
_table.WAL.VALUE:=PR_KOS.WAL().KOD;
_table.KRS.VALUE:=PR_KOS.KRS;
_table.DTK.VALUE:=PR_KOS.DTK;
_table.CW.VALUE:=PR_KOS.CW;
_table.CN.VALUE:=PR_KOS.CN;
_table.CB.VALUE:=PR_KOS.CB;
_table.WN.VALUE:=PR_KOS.WN;
_table.WB.VALUE:=PR_KOS.WB;
_table.WW.VALUE:=PR_KOS.WW;
_table.WV.VALUE:=PR_KOS.WV;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1;
_result.OBJ:=obj_new('PROJEKTY','GR_K','M','SV','WAL');

_result.OBJ.PROJEKTY:=  {? _obj.PROJEKTY=''
                        || null()
                        || exec('FindInSet','#table','PROJEKTY','ID_WEW',_obj.PROJEKTY,REF.FIRMA,,1)
                        ?};
_result.OBJ.GR_K:=      {? _obj.GR_K=''
                        || null()
                        || exec('FindInSet','#table','SLO','KOD',_obj.GR_K,,,1)
                        ?};
_result.OBJ.M:=         {? _obj.M=''
                        || null()
                        || exec('FindInSet','#table','M','MATKTM',_obj.M,,,1)
                        ?};
_result.OBJ.SV:=         {? _obj.SV=''
                        || null()
                        || exec('FindInSet','#table','SLO','KOD',_obj.SV,,,1)
                        ?};
_result.OBJ.WAL:=       {? _obj.WAL=''
                        || null()
                        || exec('FindInSet','#table','SLO','KOD',_obj.WAL,,,1)
                        ?};

{? _obj.PROJEKTY<>'' & _result.OBJ.PROJEKTY=null()
|| _result.msg_norec('PROJEKTY');
   _result.RESULT:=0
?};
{? _obj.GR_K<>'' & _result.OBJ.GR_K=null()
|| _result.msg_norec('GR_K');
   _result.RESULT:=0
?};
{? _obj.M<>'' & _result.OBJ.M=null()
|| _result.msg_norec('M');
   _result.RESULT:=0
?};
{? _obj.SV<>'' & _result.OBJ.SV=null()
|| _result.msg_norec('SV');
   _result.RESULT:=0
?};
{? _obj.WAL<>'' & _result.OBJ.WAL=null()
|| _result.msg_norec('WAL');
   _result.RESULT:=0
?};

{? _obj.REF='' || _result.ACTION:='add' || _result.ACTION:='put' ?};
exec('record','xls_pr_kos',_obj,_mode,_result);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| EXCEL_ENV.ID_GRP:='Koszt ' + PR_KOS.OPIS;
   {? _validate.ACTION='add'
   || _result:=PR_KOS.add(1)
   |? _validate.ACTION='put'
   || _result:=PR_KOS.put(1)
   ?}
|| EXCEL_ENV.ID_GRP:=''
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie kosztu %1 nie powiodło się.'@[_obj.OPIS]
   || _validate.MSG:='Poprawa kosztu %1 nie powiodła się.'@[_obj.OPIS]
   ?}
?};
_result


:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 1d383f7f7678a487340d139cfdd1b01ee7fe0b25335c48c1ab5392af2120c6dd486b7f24ebd9aea69194d1005ae433414326049ccb05601d201615762e3431149b24387b15078ba31053038ea01f3e34997262b0ab3b341159ee34907fccbaaa4a00d33c00d262194f2d0213f7dff1af46a84644ccf22133fcf64a4cd6ec105c

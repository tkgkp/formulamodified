:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_r_wewy.fml
:: Utworzony: 17.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu rodzajów wejść/wyjść
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='R_WEWY';
_def.FUNPAR:='ZWS_PAR_PRWE';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Rodzaje wejść i wyjść=1,1';
_def.NAME:='Rodzaje wejść i wyjść'@;
_def.DESC:='Rodzaje wejść i wyjść';

: źródło danych
_def.TABLE:="R_WEWY";

: prolog
_def.BEFORE:="
   R_WEWY.cntx_psh();
   R_WEWY.index('R_STATUS');
   R_WEWY.prefix();
   ~~
";

: epilog
_def.AFTER:="
   R_WEWY.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('ST',MS.name(R_WEWY,'ST'),1,,MS.comment(R_WEWY,'ST'),,0);
   _env.define('TR',MS.name(R_WEWY,'TR'),1,,MS.comment(R_WEWY,'TR'),,0);
   _env.define('MD',MS.name(R_WEWY,'MD'),1,,MS.comment(R_WEWY,'MD'),,0);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.ST.VALUE:=R_WEWY.ST;
   _buf.TR.VALUE:=R_WEWY.TR;
   _buf.MD.VALUE:=R_WEWY.MD;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_r_wewy',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_r_wewy',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.ST=0 || 'ST'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? _buf.ST<0
|| _val.msg_range('ST',0);
   _val.RESULT:=0;
   return()
?};
{? _buf.TR<0 | _buf.TR>1
|| _val.msg_inset('TR','0','1');
   _val.RESULT:=0;
   return()
?};
{? _buf.MD<0 | _buf.MD>1
|| _val.msg_inset('MD','0','1');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? R_WEWY.find_tab(,'ST',,'=',_buf.ST)
|| _ref:=R_WEWY.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1, %2, %3'[_a.ST,_a.TR,_a.MD]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,R_WEWY,"
   _a.ST:=_b.ST;
   _a.TR:=_b.TR;
   _a.MD:=_b.MD;
   _a.TP:={? _b.TR || 'WYJŚCIE' || 'WEJŚCIE' ?};
   _a.RD:={? _b.MD || 'służbowe' || 'normalne' ?};
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:50 75fcced4c617fcb791ac2013a2f2cde15412bd8f532908e9877cce741504b76a1526e3e106efce67a2ea6d8fcdeaff784df36974205d7cd9a80dc320e95f3a459bf38bdcb6d507b983ff6760c2f30b8e3c3a28a22b621999f39984ab387cb5203da3694d950151a73c75b0d8a6ee9a34ea74e59d4cd29f3c03b032b5c45a0cf9

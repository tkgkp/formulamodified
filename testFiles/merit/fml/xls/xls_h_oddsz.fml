:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_h_oddsz.fml
:: Utworzony: 28.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu rodzajów zaświadczeń o ubezpieczeniu
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='H_ODDSZ';
_def.FUNPAR:='ZWS_PAR_PRZU';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Rodz. zaświad. o ubezpieczeniu=1,1';
_def.NAME:='Rodz. zaświad. o ubezpieczeniu'@;
_def.DESC:='Rodz. zaświad. o ubezpieczeniu';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="H_ODDSZ";

: prolog
_def.BEFORE:="
   H_ODDSZ.cntx_psh();
   H_ODDSZ.f_clear();
   H_ODDSZ.index('SYMBOL');
   H_ODDSZ.prefix();
   ~~
";

: epilog
_def.AFTER:="
   H_ODDSZ.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYMBOL',MS.name(H_ODDSZ,'SYMBOL'),1,,MS.comment(H_ODDSZ,'SYMBOL'));
   _env.define('OPIS',  MS.name(H_ODDSZ,'OPIS'),  1,,MS.comment(H_ODDSZ,'OPIS'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYMBOL.VALUE:=H_ODDSZ.SYMBOL;
   _buf.OPIS.VALUE:=H_ODDSZ.OPIS;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_h_oddsz',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_h_oddsz',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYMBOL='' || 'SYMBOL'
   |? _buf.OPIS=''   || 'OPIS'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_fld:=spli_str('SYMBOL,OPIS',',');
{! _ii:=1..obj_len(_fld)
|! _acr:=_fld[_ii];
   _max:=MS.fld_len(H_ODDSZ,_acr);
   {? ($('+_a.%1>_b'[_acr]))(_buf,_max)
   || _val.msg_length(_acr,_max);
      ($('_a.%1:=_b+_a.%1'[_acr]))(_buf,_max)
   ?}
!};
obj_del(_fld);

_ref:=null;
{? H_ODDSZ.find_tab(,'SYMBOL',,'=',_buf.SYMBOL)
|| _ref:=H_ODDSZ.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.SYMBOL,_a.OPIS]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,H_ODDSZ,"
   _a.SYMBOL:=_b.SYMBOL;
   _a.OPIS:=_b.OPIS;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 a84a3afc0f0a0682b21abf2b78586686570128d726919cb7ba993843a573a6505201d804348de410be342dabbea2f5862e7327acb44857a86b6fe27fbbd11eba6bc1a5b22c794e98cb497f055b44a343c88a17bd396f6c7b51b10b68d5d277db05a64e646d6587aa23ab8cf0a38546de4b30ddd7da9caf0d0efa0ea5b957c657

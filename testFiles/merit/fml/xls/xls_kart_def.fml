:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kart_def.fml
:: Utworzony: 13.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji kartotek dodatkowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='KART_DEF';
_def.FUNPAR:='ZWS_PAR_POKD';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Kartoteki dodatkowe=1,1';
_def.NAME:='Kartoteki dodatkowe'@;
_def.DESC:='Kartoteki dodatkowe';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="KART_DEF";

: prolog
_def.BEFORE:="
   SLO_TYP.cntx_psh();
   KART_DEF.cntx_psh();
   KART_DEF.f_clear();
   KART_DEF.index('SYMBOL');
   KART_DEF.prefix();
   ~~
";

: epilog
_def.AFTER:="
   KART_DEF.cntx_pop();
   SLO_TYP.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYMBOL',  MS.name(KART_DEF,'SYMBOL'),  0,,MS.comment(KART_DEF,'SYMBOL'));
   _env.define('NAZWA',   MS.name(KART_DEF,'NAZWA'),   1,,MS.comment(KART_DEF,'NAZWA'));
   _env.define('OPIS',    MS.name(KART_DEF,'OPIS'),    1,,MS.comment(KART_DEF,'OPIS'));
   _env.define('OPISW',   MS.name(KART_DEF,'OPISW'),   1,,MS.comment(KART_DEF,'OPISW'),,0);
   _env.define('OD',      MS.name(KART_DEF,'OD'),      1,,MS.comment(KART_DEF,'OD'));
   _env.define('ODW',     MS.name(KART_DEF,'ODW'),     1,,MS.comment(KART_DEF,'ODW'),,0);
   _env.define('DO',      MS.name(KART_DEF,'DO'),      1,,MS.comment(KART_DEF,'DO'));
   _env.define('DOW',     MS.name(KART_DEF,'DOW'),     1,,MS.comment(KART_DEF,'DOW'),,0);
   _env.define('KT',      MS.name(KART_DEF,'KT'),      1,,MS.comment(KART_DEF,'KT'));
   _env.define('KTW',     MS.name(KART_DEF,'KTW'),     1,,MS.comment(KART_DEF,'KTW'),,0);
   _env.define('CZYWART', MS.name(KART_DEF,'CZYWART'), 1,,MS.comment(KART_DEF,'CZYWART'));
   _env.define('WARTOSC', MS.name(KART_DEF,'WARTOSC'), 1,,MS.comment(KART_DEF,'WARTOSC'));
   _env.define('WARTOSCW',MS.name(KART_DEF,'WARTOSCW'),1,,MS.comment(KART_DEF,'WARTOSCW'),,0);
   _env.define('WER',     MS.name(KART_DEF,'WER'),     1,,MS.comment(KART_DEF,'WER'));
   _env.define('F3',      MS.name(KART_DEF,'F3'),      0,,MS.comment(KART_DEF,'F3'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYMBOL.VALUE:=KART_DEF.SYMBOL;
   _buf.NAZWA.VALUE:=KART_DEF.NAZWA;
   _buf.OPIS.VALUE:=KART_DEF.OPIS;
   _buf.OPISW.VALUE:=KART_DEF.OPISW;
   _buf.OD.VALUE:=KART_DEF.OD;
   _buf.ODW.VALUE:=KART_DEF.ODW;
   _buf.DO.VALUE:=KART_DEF.DO;
   _buf.DOW.VALUE:=KART_DEF.DOW;
   _buf.KT.VALUE:=KART_DEF.KT;
   _buf.KTW.VALUE:=KART_DEF.KTW;
   _buf.CZYWART.VALUE:={? KART_DEF.CZYWART<>'' || 'T' || 'N' ?};
   _buf.WARTOSC.VALUE:=KART_DEF.WARTOSC;
   _buf.WARTOSCW.VALUE:=KART_DEF.WARTOSCW;
   _buf.WER.VALUE:=KART_DEF.WER;
   _buf.F3.VALUE:=KART_DEF.memo_txt(,1,'F3');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_kart_def',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kart_def',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
:  informacje bezwzględnie wymagane
   {? _buf.SYMBOL=''  || 'SYMBOL'
   |? _buf.NAZWA=''   || 'NAZWA'
   |? _buf.OPIS=''    || 'OPIS'
   |? _buf.CZYWART='' || 'CZYWART'
   |? _buf.WER=''     || 'WER'
:  dane wymagane warunkowo
   |? _buf.ODW<>0 & _buf.OD='' || 'OD'
   |? _buf.DOW<>0 & _buf.DO='' || 'DO'
   |? _buf.KTW<>0 & _buf.KT='' || 'KT'
   |? _buf.WARTOSCW<>0 & _buf.WARTOSC='' || 'WARTOSC'
:  dane wymagane zależnie od rodzaju okienka
   |? _buf.WER='KART' || {? _buf.OD='' || 'OD'
                         |? _buf.DO='' || 'DO'
                         |? _buf.KT='' || 'KT'
                         || ''
                         ?}
   |? _buf.WER='KTOD' || {? _buf.OD='' || 'OD'
                         |? _buf.KT='' || 'KT'
                         || ''
                         ?}
   |? _buf.WER='ODDO' || {? _buf.OD='' || 'OD'
                         |? _buf.DO='' || 'DO'
                         || ''
                         ?}
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('TN'*_buf.CZYWART)=0
|| _val.msg_inset('CZYWART','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(KART_DEF,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(KART_DEF,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};
_max:=MS.fld_len(KART_DEF,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};
_max:=MS.fld_len(KART_DEF,'OD');
{? +_buf.OD>_max
|| _val.msg_length('OD',_max);
   _buf.OD:=_max+_buf.OD
?};
_max:=MS.fld_len(KART_DEF,'DO');
{? +_buf.DO>_max
|| _val.msg_length('DO',_max);
   _buf.DO:=_max+_buf.DO
?};
_max:=MS.fld_len(KART_DEF,'KT');
{? +_buf.KT>_max
|| _val.msg_length('KT',_max);
   _buf.KT:=_max+_buf.KT
?};
_max:=MS.fld_len(KART_DEF,'WARTOSC');
{? +_buf.WARTOSC>_max
|| _val.msg_length('WARTOSC',_max);
   _buf.WARTOSC:=_max+_buf.WARTOSC
?};
{? (_buf.WER='KART' | _buf.WER='KTOD') & _buf.F3=''
|| _val.msg_empty('F3')
?};

: weryfikacja poprawności pola F3
{? _buf.F3<>''
|| KART_DEF.WER:=_buf.WER;
   KART_DEF.memo_set(_buf.F3,'F3');
   _res:=exec('kat','kart_dod',0);
   {? _res.STATUS<>''
   || _val.MSG:='Błędna wartość pola "%1". %2'@[MS.name(KART_DEF,'F3'),_res.STATUS];
      _val.RESULT:=0;
      return()
   ?}
?};

_ref:=null;
{? KART_DEF.find_tab(,'SYMBOL',,'=',_buf.SYMBOL)
|| _ref:=KART_DEF.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('KART_DEF','to_str');
_obj.to_str:="'%1 - %2'[_a.SYMBOL,_a.NAZWA]";
_obj.KART_DEF:=_ref;

: sprawdzenie, czy operacja może być wykonana ze względu na unikalne klucze SLO_TYP
_rzS:={? SLO_TYP.find_tab(,'SYMBOL',,'=',_buf.SYMBOL) || SLO_TYP.ref() || null ?};
_rzO:={? SLO_TYP.find_tab(,'OPIS',,'=',_buf.NAZWA) || SLO_TYP.ref() || null ?};
{? _rzS=null
|| {? _rzO<>null
   || _val.msg_unique(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
|| {? _rzO<>null & _rzO<>_rzS
   || _val.msg_unique(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.SYMBOL:=_b.SYMBOL;
   _a.NAZWA:=_b.NAZWA;
   _a.OPIS:=_b.OPIS;
   _a.OPISW:=_b.OPISW;
   _a.OD:=_b.OD;
   _a.ODW:=_b.ODW;
   _a.DO:=_b.DO;
   _a.DOW:=_b.DOW;
   _a.KT:=_b.KT;
   _a.KTW:=_b.KTW;
   _a.CZYWART:={? _b.CZYWART='T' || '_' || '' ?};
   _a.WARTOSC:=_b.WARTOSC;
   _a.WARTOSCW:=_b.WARTOSCW;
   _a.WER:=_b.WER;
   _a.memo_set(_b.F3,'F3');
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? KART_DEF.seek(_obj.KART_DEF)
   || _set(KART_DEF,_buf);
      {? KART_DEF.put(1)
      || KART_DEF.memo_put(,'F3')
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,KART_DEF.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| KART_DEF.blank();
   _set(KART_DEF,_buf);
   {? KART_DEF.add(1)
   || KART_DEF.memo_put(,'F3')
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 d463e1d2b55cca40918621086f8192654afebcd608551f642a00722d7cbfc29f2a17447eae14f22fe336623d69a0d9270bdc138e1ec337eddb16588ffccb4ef0542cf5cdf8f138ad1121a3b4794e61881272d1449e79e8f75d0f26c47a950aca37026a28703aff11b6cf52dc86e9800fa88164869ef53340b6e676ddfd5302a6

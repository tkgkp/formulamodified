:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_jm.fml
:: Utworzony: 19.07.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu numeracji dokumentów kasowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='NUM_DOK';
_def.DOMAIN:=exec('name','#b_domain','KAS');
_def.FUNPAR:='ZWS_PAR_KASNR';

_def.FILE:='kasa.xlsx';
_def.SHEET:='Numery dokumentów kasowych=1,1';
_def.NAME:='Numery dokumentów kasowych'@;
_def.DESC:='Numery dokumentów kasowych';

_def.PREFIX:="";
_def.TABLE:="sql('select REFERENCE REF, (\'\':\'\' || NUM_DOK.IDADD || NUM_DOK.REFERENCE) as UIDREF from @NUM_DOK')";
_def.TAB_IMP:="NUM_DOK";
_def.FIELDS:="exec('fields','xls_num_dok',_a)";

_def.BEFORE:="NUM_DOK.cntx_psh()";
_def.AFTER:="NUM_DOK.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_num_dok',_a,_b)";
_def.VALIDATE:="exec('validate','xls_num_dok',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_num_dok',_a,_b,_c)";
_def.SELECT:="exec('select','xls_num_dok',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('SK_KOD','Kod stanowiska kasowego',1,,'Liczba całkowita z zakresu od 1 do 999 oznaczająca kod stanowiska'
   ,type_of(0),0);
_def.define('TYP_DOK','Typ dokumentu',1,,'7 znakowy typ dokumentu kasowego',type_of(''));
_def.define('OKRES','Okres',1,,'6 znakowy okres o wzorcu rrrrmm lub rrrrRR'
   ' gdzie rrrr-rok, mm-miesiąc, RR-stała dla numeracji ciągłej',type_of(''));
_def.define('NUMER','Numer',1,,'Ostatni numer dokumentu w ramach typu',type_of(0),0);
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_TABLE:=_excel.table();

_result:=1;
NUM_DOK.use(8+_TABLE.REF);
NUM_DOK.cntx_psh();
NUM_DOK.prefix();
{? NUM_DOK.seek(_TABLE.REF)
||
   _table.SK_KOD.VALUE:=#(6+_TABLE.REF+3);
   _table.TYP_DOK.VALUE:=NUM_DOK.TYP_DOK;
   _table.OKRES.VALUE:=NUM_DOK.OKRES;
   _table.NUMER.VALUE:=NUM_DOK.NUMER
?};
NUM_DOK.cntx_pop();
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_rok:=4+_obj.OKRES;
_rok_f:={? +_rok<4 || '' || exec('FindInSet','#table','ROK_F','NAZWA',_rok,REF.FIRMA,"ROK_F.KOD",1,,'') ?};
{? +_obj.OKRES<6
|| _result.msg_value('OKRES','Okres powinien składać się z 6 znaków.');
   _result.RESULT:=0
|? _rok_f=''
|| FUN.info('Nie zdefiniowano roku obrachunkowego %1.'@[4+_rok]);
   _result.RESULT:=0
|| _sk_kod:=form(_obj.SK_KOD,-3,,'99');
   NUM_DOK.use((NUM_DOK.name()-5)+_sk_kod+_rok_f);
   NUM_DOK.cntx_psh();
   NUM_DOK.index('TYP_OKR');
   NUM_DOK.prefix(_obj.TYP_DOK,_obj.OKRES);
   {? NUM_DOK.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=0
   || NUM_DOK.cntx_pop()
   || {? _result.ACTION='add' || NUM_DOK.blank() ?};
      NUM_DOK.TYP_DOK:=_obj.TYP_DOK;
      NUM_DOK.OKRES:=_obj.OKRES;
      NUM_DOK.NUMER:=_obj.NUMER;
      {? exec('rponumdok','kasa_dokument')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=NUM_DOK.add(1)

   |? _validate.ACTION='put'
   ||
      _result:=NUM_DOK.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie numeru dokumentu kasowego [%1,%2] dla kasy %3 nie powiodło się.'@[_obj.TYP_DOK,_obj.OKRES,_obj.SK_KOD]
   ||
      _validate.MSG:='Poprawa numeru dokumentu kasowego [%1,%2] dla kasy %3 nie powiodła się.'@[_obj.TYP_DOK,_obj.OKRES,_obj.SK_KOD]
   ?}
?};
NUM_DOK.cntx_pop();
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'NUMER','STRING[20]','Numer'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'TYP_DOK','STRING[7]','Typ dokumentu');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NUMER',,);

NUM_DOK.cntx_psh();
NUM_DOK.index('TYP_OKR');
NUM_DOK.prefix();
{? NUM_DOK.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$NUM_DOK.ref();
      _selected.prefix($NUM_DOK.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NUMER:=NUM_DOK.NUMER;
      _tab.TYP_DOK:=NUM_DOK.TYP_DOK;
      _tab.add();
      NUM_DOK.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NUMER,TYP_DOK',20,'Wybór numerów dokumentów kasowych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
NUM_DOK.cntx_pop();

_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 2f48e179f9ae21cf11df948e2ba55a754156f344535e383a901922a349b3dd8d9c665d35198fd26c415304861fec96d4dde05639a451bf6a385be0284af2d04b868a2ae204695159de01048de885e702675eff3d653ee0598fa7c8c47d576ff728e4771cdf6fcac273289b58ec831ff1ac6e76086a8101e011a1db15afd05ed0

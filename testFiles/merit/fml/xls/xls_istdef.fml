:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_istdef.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji Intrastat
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ISTDEF';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_DEF_IST';
_def.FILE:='intrastat.xlsx';
_def.SHEET:='Definicja Intrastat=1,1';
_def.NAME:='Definicja Intrastat'@;
_def.DESC:='Definicja Intrastat';
_def.TABLE:="exec('table','xls_istdef')";
_def.FIELDS:="exec('fields','xls_istdef',_a)";
_def.EXPORT:="exec('export','xls_istdef',_a,_b)";
_def.IMPORT:="exec('import','xls_istdef',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_istdef',_a,_b,_c)";
_def.SELECT:="exec('select','xls_istdef',_a)";
_def.PREFIX:="ISTDEF.index('LP'); ISTDEF.prefix('','','I')";
_def.BEFORE:="ISTDEF.cntx_psh()";
_def.AFTER:="ISTDEF.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ISTDEF


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TM_STAM','Tm_stamp',0,,'Znacznik czasowy utworzenia specyfikacji',type_of(''));
_def.define('VER','Wersja',1,,'Wersja');
_def.define('XMLNS','Xmlns',1,,'Atrybut techniczny');
_def.define('EMAIL','E-mail',1,,'Adre e-mail nadawcy');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
ISTDEF.prefix();
{? _validate.ACTION='add'
||
   ISTDEF.blank();
   ISTDEF.SYSTEM:='';
   ISTDEF.TM_STAM:=#_obj.TM_STAM;
   ISTDEF.IST_ISTK:='I';
   ISTDEF.RODZAJ:=''
?};

ISTDEF.VER:=_obj.VER;
ISTDEF.XMLNS:=_obj.XMLNS;
ISTDEF.EMAIL:=_obj.EMAIL;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.TM_STAM.VALUE:=$ISTDEF.TM_STAM;
_table.VER.VALUE:=ISTDEF.VER;
_table.XMLNS.VALUE:=ISTDEF.XMLNS;
_table.EMAIL.VALUE:=ISTDEF.EMAIL;

ISTDEFS.cntx_psh();
ISTDEFS.index('LP');
ISTDEFS.prefix(ISTDEF.ref(),);
_loop:=ISTDEFS.first();
{!
|? _loop
|!
   _excel.write_async('ISTDEFS',ISTDEFS.ref());
   ISTDEFI.cntx_psh();
   ISTDEFI.index('LP');
   ISTDEFI.prefix(ISTDEFS.ref());
   _loop:=ISTDEFI.first();
   {!
   |? _loop
   |!
      _excel.write_async('ISTDEFI',ISTDEFI.ref());
      _loop:=ISTDEFI.next()
   !};
   ISTDEFI.cntx_pop();
   _loop:=ISTDEFS.next()
!} ;
ISTDEFS.cntx_pop();

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? _result.RESULT=1
||
   ISTDEF.index('LP');
   ISTDEF.prefix('','','I',#_obj.TM_STAM);
   {? ISTDEF.first() & ISTDEF.TM_STAM=#_obj.TM_STAM
   ||
      {? _mode=0
      ||
         _result.RESULT:=0
      ||
         _result.ACTION:='put';
         exec('record','xls_istdef',_obj,_mode,_result);
         {? __CHK.record(ISTDEF,,'VER','XMLNS','IST_ISTK','TM_STAM')<>''
         ||
            _result.RESULT:=0
         ?}
      ?}
   ||
      _result.ACTION:='add';
      exec('record','xls_istdef',_obj,_mode,_result);
      {? __CHK.record(ISTDEF,,'VER','XMLNS','IST_ISTK','TM_STAM')<>''
      ||
         _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=ISTDEF.add(1)

   |? _validate.ACTION='put'
   ||
      _result:=ISTDEF.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie definicji Intrastat %1 nie powiodło się.'@[_obj.TM_STAM])
   ||
      FUN.info('Poprawa definicji Intrastat %1 nie powiodła się.'@[_obj.TM_STAM])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'VER','STRING[20]','Wersja'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano?');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'VER',,);

ISTDEF.cntx_psh();
ISTDEF.index('LP');
ISTDEF.prefix('','','I');
{? ISTDEF.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$ISTDEF.ref();
      _selected.prefix($ISTDEF.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.VER:=ISTDEF.VER;
      _tab.add();
      ISTDEF.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'VER',20,'Wybór definicji Intrastat do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
ISTDEF.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 dc873a7d50f930cc3e1631fec4c019ec61a2c14fd73e0110e605facb496dbf52a9f5fc9a3cd58457398192a1087cec1a75e7dedcfe7d46ebc638e7a7990b63f8b801308e2bd4497d3c8696d4a3977adab94f5b5ee00657907bb25686de982b1deb50f7227c0fcad877ebb9c4503814ff9813820d3d9bf6774c2647e3552c70cc

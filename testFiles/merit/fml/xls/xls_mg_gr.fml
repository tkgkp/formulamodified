:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_mg_gr.fml
:: Utworzony: 30.07.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu strefy magazynowania
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='MG_GR';
_def.DOMAIN:=exec('name','#b_domain','LMO');
_def.FUNPAR:='ZWS_PAR_LMAG';

_def.FILE:='magazyn_mobilny.xlsx';
_def.SHEET:='Strefy magazynowania=1,1';
_def.NAME:='Strefy magazynowania'@;
_def.DESC:='Strefy magazynowania';

_def.PREFIX:="exec('prefix','xls_mg_gr')";
_def.TABLE:="exec('table','xls_mg_gr')";
_def.FIELDS:="exec('fields','xls_mg_gr',_a)";

_def.BEFORE:="MG_GR.cntx_psh()";
_def.AFTER:="MG_GR.cntx_pop()";
_def.SELECT:="exec('select','xls_mg_gr',_a)";

_def.EXPORT:="exec('export','xls_mg_gr',_a,_b)";
_def.IMPORT:="exec('import','xls_mg_gr',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_mg_gr',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
MG_GR.index('KOD');
MG_GR.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
MG_GR


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod',0,,'Unikalny kod strefy magazynowania.'@);
_def.define('TYP_ANAL','Analizować',0,,'Czy uwzględniać w analizach strefę magazynowania? - (T/N)'@);
_def.define('OPIS','Opis',0,,'Opis strefy magazynowania'@);
_def.define('DEFSTATS','Strefa dedykowana dla statusu dostawy',1,,'Strefa dedykowana dla wskazanego statusu dostawy'@);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

MG_GR.index('KOD');
MG_GR.prefix();
{? _validate.ACTION='add'
||
   MG_GR.blank();
   MG_GR.KOD:=_obj.KOD
?};

MG_GR.TYP_ANAL:=_obj.TYP_ANAL;
MG_GR.OPIS:=_obj.OPIS;
MG_GR.DEFSTATS:=_validate.OBJ.DEFSTATS;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
{? MG_GR.KH=null()
|| _table.KOD.VALUE:=MG_GR.KOD;
   _table.TYP_ANAL.VALUE:=MG_GR.TYP_ANAL;
   _table.OPIS.VALUE:=MG_GR.OPIS;
   _table.DEFSTATS.VALUE:=MG_GR.DEFSTATS().N; _excel.write_async('STATS',MG_GR.DEFSTATS)
|| _result:=0
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('DEFSTATS');

{? _obj.DEFSTATS<>''
|| _result.OBJ.DEFSTATS:=exec('FindInSet','#table','STATS','NN',_obj.DEFSTATS,,,1);
   {? _result.OBJ.DEFSTATS=null()
   || _result.MSG:='Nie znaleziono dedykowanego statusu dostawy %1.'@[_obj.DEFSTATS];
      _result.RESULT:=0
   ?}
|| _result.OBJ.DEFSTATS:=null()
?};

{? _result.RESULT=1
||
   MG_GR.index('KOD');
   MG_GR.prefix(_obj.KOD,);
   {? MG_GR.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_mg_gr',_obj,_mode,_result);
         {? exec('rekmg_gr','magdok_wspolne',_result.ACTION)<>'' || _result.RESULT:=0 ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_mg_gr',_obj,_mode,_result);
      {? exec('rekmg_gr','magdok_wspolne',_result.ACTION)<>'' || _result.RESULT:=0 ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=MG_GR.add(1)
   |? _validate.ACTION='put'
   || _result:=MG_GR.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:={? _validate.ACTION='add'
                  || 'Dodanie strefy magazynowania %1 nie powiodło się.'@[MG_GR.KOD]
                  || 'Modyfikacja strefy magazynowania %1 nie powiodła się.'@[MG_GR.KOD]
                  ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2,'KOD','STRING[20]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OPIS','STRING[120]','Opis');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

MG_GR.cntx_psh();
MG_GR.index('KOD');
MG_GR.prefix();
{? MG_GR.first()
|| {!
   |?
      {? MG_GR.KH=null()
      || _tab.blank();
         _tab.REF:=$MG_GR.ref();
         _selected.prefix($MG_GR.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.KOD:=MG_GR.KOD;
         _tab.OPIS:=MG_GR.OPIS;
         _tab.add()
      ?};
      MG_GR.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'KOD,OPIS',20,'Wybór strefy magazynowania do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
MG_GR.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 1a57c5001e64198a5a1e892ae569aca871e835a9e795b4cc6296107bf749a6c507f4d042987f2309a0e3ec23c1013a332ee5268fc71127bac96038afe160fb1a97ca049f68d134d2e09f4ba38786dae37b06c73245ff23c67cb360ee237721e31c9dc472fbc1b8de9139954e7f0dba118fe680e7d4315a0edc84e644dedd90f1

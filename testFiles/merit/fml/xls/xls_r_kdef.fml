:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_r_karty.fml
:: Utworzony: 18.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu kartotek dod. źródeł danych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='R_KDEF';
_def.FUNPAR:='ZWS_PAR_PKDZ';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Kartoteki dod. źródeł danych=1,1';
_def.NAME:='Kartoteki dod. źródeł danych'@;
_def.DESC:='Kartoteki dod. źródeł danych';

: źródło danych
_def.TABLE:="R_KDEF";

: prolog
_def.BEFORE:="
   R_KDEF.cntx_psh();
   R_KDEF.index('SYMBOL');
   R_KDEF.prefix();
   ~~
";

: epilog
_def.AFTER:="
   R_KDEF.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYMBOL',MS.name(R_KDEF,'SYMBOL'),0,,MS.comment(R_KDEF,'SYMBOL'));
   _env.define('DT',    MS.name(R_KDEF,'DT'),    1,,MS.comment(R_KDEF,'DT'));
   _env.define('GD',    MS.name(R_KDEF,'GD'),    1,,MS.comment(R_KDEF,'GD'));
   _env.define('KT',    MS.name(R_KDEF,'KT'),    1,,MS.comment(R_KDEF,'KT'));
   _env.define('OPIS',  MS.name(R_KDEF,'OPIS'),  1,,MS.comment(R_KDEF,'OPIS'));
   _env.define('F3',    MS.name(R_KDEF,'F3'),    0,,MS.comment(R_KDEF,'F3'),type_of(''));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYMBOL.VALUE:=R_KDEF.SYMBOL;
   _buf.DT.VALUE:=R_KDEF.DT;
   _buf.GD.VALUE:=R_KDEF.GD;
   _buf.KT.VALUE:=R_KDEF.KT;
   _buf.OPIS.VALUE:=R_KDEF.OPIS;
   _buf.F3.VALUE:=R_KDEF.memo_txt(,1,'F3');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_r_kdef',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_r_kdef',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYMBOL='' || 'SYMBOL'
   |? _buf.OPIS=''   || 'OPIS'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_fld:=spli_str('SYMBOL,DT,GD,KT,OPIS',',');
{! _ii:=1..obj_len(_fld)
|! _acr:=_fld[_ii];
   _max:=MS.fld_len(R_KDEF,_acr);
   {? ($('+_a.%1>_b'[_acr]))(_buf,_max)
   || _val.msg_length(_acr,_max);
      ($('_a.%1:=_b+_a.%1'[_acr]))(_buf,_max)
   ?}
!};
obj_del(_fld);

_rzS:={? R_KDEF.find_tab(,'SYMBOL',,'=',_buf.SYMBOL) || R_KDEF.ref() || null ?};
_rzO:={? R_KDEF.find_tab(,'OPIS',,'=',_buf.OPIS) || R_KDEF.ref() || null ?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=exec('write_obj','xls__util',_rzS,
   "'%1 - %2'[_a.SYMBOL,_a.OPIS]"
);

{? _rzS=null
|| {? _rzO=null
   || _val.ACTION:='add'
   || _val.msg_unique(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
|| {? _rzO=null | _rzO=_rzS
   || _val.ACTION:='put'
   || _val.msg_unique(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,R_KDEF,"
   _a.SYMBOL:=_b.SYMBOL;
   _a.DT:=_b.DT;
   _a.GD:=_b.GD;
   _a.KT:=_b.KT;
   _a.OPIS:=_b.OPIS;
   _a.memo_set(_b.F3,'F3');
   ~~
",'F3');
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:50 3ac880ea6c0af2a17a62ec356c241d9270e197a247cffad272b4fe4188af3821b9239493a57e84b9b5c16b42470192738a02147bd0dbb10c9c8d4801907f5dbbcfb47a94e720b435485b3baceef43468cb347cf19c6b8205dd0050774bb09a93591228e3e797edb3a4b7b485a7e185996db3a88495fa26f8e2af3f45acfc14c0

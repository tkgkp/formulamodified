:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_rt_tekst.fml
:: Utworzony: 14.05.2020
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_def:=_a;

_def.ID:='RT_TEKST';
_def.FUNPAR:='ZWS_PAR_ARTL';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_rt_xlsx','xls__init');
_def.SHEET:='Zawartość rejestru=1,1';
_def.NAME:='Zawartość rejestru'@;
_def.DESC:='Zawartość rejestru';
_def.ADD_ROWS:=0;
_def.HIDDEN:='N';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="RT_TEKST";

: prolog
_def.BEFORE:="
   RT_TEKST.cntx_psh();
   RT_TEKST.index('DEF');
   RT_TEKST.clear();
   RT_KONF.cntx_psh();
   RT_KONF.index('UNIQUE');
   RT_KONF.clear();
   SLO_KOD.cntx_psh();
   SLO_KOD.clear();
   ~~
";

: epilog
_def.AFTER:="
   SLO_KOD.cntx_pop();
   RT_KONF.cntx_pop();
   RT_TEKST.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('TABELA',  MS.name(RT_KONF,'TABELA'),0,,   MS.comment(RT_KONF,'TABELA'));
   _env.define('POLE',    MS.name(RT_KONF,'POLE'),0,,     MS.comment(RT_KONF,'POLE'));
   _env.define('WARTOSC', MS.name(RT_KONF,'WARTOSC'),0,,  MS.comment(RT_KONF,'WARTOSC'));
   _env.define('OPIS',    MS.name(RT_KONF,'OPIS'),0,,     MS.comment(RT_KONF,'OPIS'));
   _env.define('DL_OBCY', MS.name(RT_KONF,'DL_OBCY'),0,,  MS.comment(RT_KONF,'DL_OBCY'),type_of(0),0);
   _env.define('KOD',     MS.name(SLO_KOD,'KOD'),0,,      MS.comment(SLO_KOD,'KOD'));
   _env.define('ORYGINAL',MS.name(RT_TEKST,'ORYGINAL'),0,,MS.comment(RT_TEKST,'ORYGINAL'));
   _env.define('OBCY',    MS.name(RT_TEKST,'OBCY'),1,,    MS.comment(RT_TEKST,'OBCY'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_rt_tekst',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_rt_tekst',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rt_tekst',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_env:=_a;
_buf:=_b;

RT_TEKST.RT_KONF();
_buf.TABELA.VALUE:=RT_KONF.TABELA;
_buf.POLE.VALUE:=RT_KONF.POLE;
_buf.WARTOSC.VALUE:=RT_KONF.WARTOSC;
_buf.OPIS.VALUE:=RT_KONF.OPIS;
_buf.DL_OBCY.VALUE:=RT_KONF.DL_OBCY;
_buf.KOD.VALUE:=RT_TEKST.SLO_KOD().KOD;
_buf.ORYGINAL.VALUE:=RT_TEKST.ORYGINAL;
_buf.OBCY.VALUE:=RT_TEKST.OBCY;

_env.write_async('RT_KONF',RT_TEKST.RT_KONF);
_env.write_async('SLO_KOD',RT_TEKST.SLO_KOD);

1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.TABELA=''   || 'TABELA'
   |? _buf.POLE=''     || 'POLE'
   |? _buf.KOD=''      || 'KOD'
   |? _buf.ORYGINAL='' || 'ORYGINAL'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(RT_KONF,'TABELA');
{? +_buf.TABELA>_max
|| _val.msg_length('TABELA',_max);
   _buf.TABELA:=_max+_buf.TABELA
?};
_max:=MS.fld_len(RT_KONF,'POLE');
{? +_buf.POLE>_max
|| _val.msg_length('POLE',_max);
   _buf.POLE:=_max+_buf.POLE
?};
_max:=MS.fld_len(RT_KONF,'WARTOSC');
{? +_buf.WARTOSC>_max
|| _val.msg_length('WARTOSC',_max);
   _buf.WARTOSC:=_max+_buf.WARTOSC
?};
_max:=MS.fld_len(SLO_KOD,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(RT_TEKST,'ORYGINAL');
{? +_buf.ORYGINAL>_max
|| _val.msg_length('ORYGINAL',_max);
   _buf.ORYGINAL:=_max+_buf.ORYGINAL
?};
_max:=MS.fld_len(RT_TEKST,'OBCY');
{? +_buf.OBCY>_max
|| _val.msg_length('OBCY',_max);
   _buf.OBCY:=_max+_buf.OBCY
?};

: weryfikacja spójności referencyjnej
{? RT_KONF.find_key(_buf.TABELA,_buf.POLE,_buf.WARTOSC,)
|| _cfg:=RT_KONF.ref()
|| _val.msg_norec('TABELA');
   _val.RESULT:=0;
   return()
?};
{? (_kod:=exec('kod','ext_slo','JEZRTL',_buf.KOD))=null
|| _val.msg_norec('KOD');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? RT_TEKST.find_key(_buf.TABELA,_buf.POLE,_buf.WARTOSC,_buf.ORYGINAL,)
|| _ref:=RT_TEKST.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1, %2%3'[_a.TABELA,_a.POLE,{? _a.WARTOSC<>'' || ' (%1)'[_a.WARTOSC] || '' ?}]",
   'RT_KONF',_cfg,
   'SLO_KOD',_kod
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,RT_TEKST,"
   _a.RT_KONF:=_c.RT_KONF;
   _a.SLO_KOD:=_c.SLO_KOD;
   _a.ORYGINAL:=_b.ORYGINAL;
   _a.OBCY:=_b.OBCY;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 3a6094a49ed726a793429854979ff266ae66135578bbfc0337e4b822918bf866f1812b01f026b6bf3aaada4c16117d665ff9a51e9f0c3de99c694c930971bb5e265c01f0fc242bde7ad4d17234a79f96af9258d3ceb2cd054315e16aff3069237309740bea4b392f230c581e72a811118d6ffeb3cbd5d0d5b84520f11aea4ee6

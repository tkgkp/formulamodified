:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_projekty.fml
:: Utworzony: 07.12.2021
:: Autor: PD
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu projektów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PROJEKTY';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='PROJEKTY';
_def.HIDDEN:='T';
_def.SKIP:='T';
_def.ADD_ROWS:=100;

_def.FILE:='projekty.xlsx';
_def.SHEET:='Projekty'@+'=1,1';
_def.NAME:='Projekty'@;
_def.DESC:='Projekty'@;

_def.PREFIX:="PROJEKTY.index('KOL');PROJEKTY.prefix(REF.FIRMA)
";
_def.TABLE:="PROJEKTY";
_def.FIELDS:="exec('fields','xls_projekty',_a)";

_def.BEFORE:="{? _a.KIND='IMPORT'
              || exec('projekty_import_before','projekty',_a)
              || exec('projekty_export_before','projekty',_a) ?}";
_def.AFTER:="{? _a.KIND='IMPORT'
             || params_exec('projekty_import_after','projekty',_a)
             || params_exec('projekty_export_after','projekty',_a) ?}";
_def.SELECT:="exec('select','xls_projekty',_a,_b)";

_def.EXPORT:="exec('export','xls_projekty',_a,_b)";
_def.VALIDATE:="exec('validate','xls_projekty',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_projekty',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_edit:=1;
_def.define('SYM','Symbol'@,0,,'Symbol projektu');
_def.define('REF','Referencja'@,0,,'Referencja projektu');
_def.define('ID_WEW','Identyfikator wewnętrzny'@,_edit,,'Identyfikator wewnętrzny');
_def.define('ID_KSG','Identyfikator księgowy'@,_edit,,'Identyfikator księgowy');
_def.define('POZ_PROJ','Poziom projektu'@,_edit,,'Poziom projektu');
_def.define('T','Typ projektu'@,_edit,,'Typ projektu - typ');
_def.define('KH','Kontrahent'@,_edit,,'Wskazanie na kontrahenta - Kod');
_def.define('KH_ODB','Odbiorca'@,_edit,,'Odbiorca - Kod');
_def.define('HAN','Handlowiec'@,_edit,,'Handlowiec - Imię + Nazwisko');
_def.define('UD_SKL','Struktura organizacyjna'@,_edit,,'Struktura organizacyjna - Typ Symbol + Symbol');
_def.define('ODDZ','Oddział'@,_edit,,'Oddział');
_def.define('SZEF','Szef projektu'@,_edit,,'Szef projektu - Imię + Nazwisko');
_def.define('NAZWA','Nazwa projektu'@,_edit,,'Nazwa projektu');
_def.define('USER_ZAM','Użytkownik, który zamknął projekt'@,_edit,,'Użytkownik, który zamknął projekt');
_def.define('DAT_ZAM','Data zamknięcia projektu'@,_edit,,'Data zamknięcia projektu');
_def.define('GODZ_ZAM','Godzina zamknięcia projektu'@,_edit,,'Godzina zamknięcia projektu');
_def.define('RDATA_OD','Umowna data rozpoczęcia'@,_edit,,'Umowna data rozpoczęcia');
_def.define('RDATA_DO','Umowna data zakończenia'@,_edit,,'Umowna data zakończenia');
_def.define('RDAT_ROZ','Rzeczywista data rozpoczęcia'@,_edit,,'Rzeczywista data rozpoczęcia');
_def.define('RDAT_ZAM','Rzeczywista data zakończenia'@,_edit,,'Rzeczywista data zakończenia');
_def.define('KRAJ','Kraj'@,_edit,,'Lokalizacja projektu - kraj');
_def.define('MIASTO','Miasto'@,_edit,,'Lokalizacja projektu - miasto');
_def.define('UL','Ulica'@,_edit,,'Lokalizacja projektu - ulica');
_def.define('TEL1','Telefon 1'@,_edit,,'Lokalizacja projektu - telefon 1');
_def.define('TEL2','Telefon 2'@,_edit,,'Lokalizacja projektu - telefon 2');
_def.define('PL','Sposób płatności'@,_edit,,'Sposób płatności - Kod');
_def.define('PLANPRZ','Planowany przychód'@,_edit,,'Planowany przychód');
_def.define('PLANKOS','Planowane koszty'@,_edit,,'Planowane koszty');
_def.define('KK','Konto kosztowe'@,_edit,,'Konto kosztowe - Symbol');
_def.define('UDT','Umowa'@,_edit,,'Umowa związana z projektem - Kod');
_def.define('SLO_RODZ','Rodzaj'@,_edit,,'Pozycja słownika rodzaju projektu - Kod');
_def.define('KAL_POD','Kalkulacja z podetapów'@,_edit,,'Kalkulacja z podetapów');
_def.define('STATUS','Status'@,_edit,,'Status - Kod');
_def.define('KH_PRJ','Symbol projektu kontrahenta'@,_edit,,'Symbol projektu kontrahenta');
_def.define('P_VAL','Wartość projektu'@,_edit,,'Wartość projektu');
_def.define('WAL','Waluta'@,_edit,,'Waluta - Kod');
_def.define('KRS','Kurs'@,_edit,,'Kurs');
_def.define('KH_OSOB','Przedstawiciel kontrahenta'@,_edit,,'Przedstawiciel kontrahenta - Nazwisko');
_def.define('KH_PWK','Podwykonawca'@,_edit,,'Podwykonawca - Kod');
_def.define('P_VAL_P','Wartośc w PLN'@,_edit,,'Wartośc w PLN');
_def.define('DTK','Data kursu'@,_edit,,'Data kursu');
_def.define('DPP','Data powołania projektu'@,_edit,,'Data powołania projektu');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PROJEKTY.prefix();
{? _validate.ACTION='add'
|| PROJEKTY.blank();
   PROJEKTY.ID_WEW:=_obj.ID_WEW;
   PROJEKTY.T:=_validate.OBJ.T;
   PROJEKTY.AR:=date()~1;
   PROJEKTY.AM:=date()~2;
   PROJEKTY.D_ADD:=date();
   PROJEKTY.T_ADD:=time();
   PROJEKTY.STAT_REJ:='T';
   PROJEKTY.KOL:=_validate.OBJ.KOL;
   PROJEKTY.SYM:='';
   PROJEKTY.RODZIC:=$__RODZIC;
   PROJEKTY.POZ_PROJ:=_obj.POZ_PROJ;
   PROJEKTY.ID_KSG:={? _obj.ID_KSG<>0 || _obj.ID_KSG || PROJEKTY.ID_KSG ?};
   PROJEKTY.KH:=_validate.OBJ.KH;
   PROJEKTY.KH_ODB:=_validate.OBJ.KH_ODB;
   PROJEKTY.HAN:=_validate.OBJ.HAN;
   PROJEKTY.UD_SKL:=_validate.OBJ.UD_SKL;
   PROJEKTY.ODDZ:=_obj.ODDZ;
   PROJEKTY.SZEF:=_validate.OBJ.SZEF;
   PROJEKTY.NAZWA:=_obj.NAZWA;
   PROJEKTY.USER_ZAM:=_obj.USER_ZAM;
   PROJEKTY.DAT_ZAM:=_obj.DAT_ZAM;
   PROJEKTY.GODZ_ZAM:=_obj.GODZ_ZAM;
   PROJEKTY.RDATA_OD:=_obj.RDATA_OD;
   PROJEKTY.RDATA_DO:=_obj.RDATA_DO;
   PROJEKTY.RDAT_ROZ:=_obj.RDAT_ROZ;
   PROJEKTY.RDAT_ZAM:=_obj.RDAT_ZAM;
   PROJEKTY.KRAJ:=_obj.KRAJ;
   PROJEKTY.MIASTO:=_obj.MIASTO;
   PROJEKTY.UL:=_obj.UL;
   PROJEKTY.TEL1:=_obj.TEL1;
   PROJEKTY.TEL2:=_obj.TEL2;
   PROJEKTY.PL:=_validate.OBJ.PL;
   PROJEKTY.PLANPRZ:=_obj.PLANPRZ;
   PROJEKTY.PLANKOS:=_obj.PLANKOS;
   PROJEKTY.KK:=_validate.OBJ.KK;
   PROJEKTY.UDT:=_validate.OBJ.UDT;
   PROJEKTY.SLO_RODZ:=_validate.OBJ.SLO_RODZ;
   PROJEKTY.KAL_POD:=_obj.KAL_POD;
   PROJEKTY.STATUS:=_validate.OBJ.STATUS;
   PROJEKTY.P_VAL:=_obj.P_VAL;
   PROJEKTY.WAL:=_validate.OBJ.WAL;
   PROJEKTY.KRS:=_obj.KRS;
   PROJEKTY.KH_OSOB:=_validate.OBJ.KH_OSOB;
   PROJEKTY.KH_PWK:=_validate.OBJ.KH_PWK;
   PROJEKTY.DTK:=_obj.DTK;
   PROJEKTY.P_VAL_P:=_obj.P_VAL_P;
   PROJEKTY.DPP:=_obj.DPP
|? _validate.ACTION='put' & PROJEKTY.seek(_obj.REF)
||
   PROJEKTY.ID_WEW:=_obj.ID_WEW;
   PROJEKTY.T:=_validate.OBJ.T;
   PROJEKTY.STAT_REJ:='T';
   PROJEKTY.KOL:=_validate.OBJ.KOL;
   PROJEKTY.POZ_PROJ:=_obj.POZ_PROJ;
   PROJEKTY.ID_KSG:={? _obj.ID_KSG<>0 || _obj.ID_KSG || PROJEKTY.ID_KSG ?};
   PROJEKTY.KH:=_validate.OBJ.KH;
   PROJEKTY.KH_ODB:=_validate.OBJ.KH_ODB;
   PROJEKTY.HAN:=_validate.OBJ.HAN;
   PROJEKTY.UD_SKL:=_validate.OBJ.UD_SKL;
   PROJEKTY.SZEF:=_validate.OBJ.SZEF;
   PROJEKTY.NAZWA:=_obj.NAZWA;
   PROJEKTY.RDATA_OD:=_obj.RDATA_OD;
   PROJEKTY.RDATA_DO:=_obj.RDATA_DO;
   PROJEKTY.RDAT_ROZ:=_obj.RDAT_ROZ;
   PROJEKTY.RDAT_ZAM:=_obj.RDAT_ZAM;
   PROJEKTY.KRAJ:=_obj.KRAJ;
   PROJEKTY.MIASTO:=_obj.MIASTO;
   PROJEKTY.UL:=_obj.UL;
   PROJEKTY.TEL1:=_obj.TEL1;
   PROJEKTY.TEL2:=_obj.TEL2;
   PROJEKTY.PL:=_validate.OBJ.PL;
   PROJEKTY.PLANPRZ:=_obj.PLANPRZ;
   PROJEKTY.PLANKOS:=_obj.PLANKOS;
   PROJEKTY.KK:=_validate.OBJ.KK;
   PROJEKTY.UDT:=_validate.OBJ.UDT;
   PROJEKTY.SLO_RODZ:=_validate.OBJ.SLO_RODZ;
   PROJEKTY.KAL_POD:=_obj.KAL_POD;
   PROJEKTY.STATUS:=_validate.OBJ.STATUS;
   PROJEKTY.P_VAL:=_obj.P_VAL;
   PROJEKTY.WAL:=_validate.OBJ.WAL;
   PROJEKTY.KRS:=_obj.KRS;
   PROJEKTY.KH_OSOB:=_validate.OBJ.KH_OSOB;
   PROJEKTY.KH_PWK:=_validate.OBJ.KH_PWK;
   PROJEKTY.DTK:=_obj.DTK;
   PROJEKTY.P_VAL_P:=_obj.P_VAL_P;
   PROJEKTY.DPP:=_obj.DPP
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
PROJTYPY.cntx_psh();KH.cntx_psh();KH_ODB.cntx_psh();OSOBA.cntx_psh();UD_SKL.cntx_psh();UD_TYP.cntx_psh();
SLO.cntx_psh();KK.cntx_psh();UDT.cntx_psh();PROJ_STA.cntx_psh();KH_OSOB.cntx_psh();

_table.SYM.VALUE:=PROJEKTY.SYM;
_table.REF.VALUE:=$PROJEKTY.ref();
_table.ID_WEW.VALUE:=PROJEKTY.ID_WEW;
_table.ID_KSG.VALUE:=PROJEKTY.ID_KSG;
_table.POZ_PROJ.VALUE:=PROJEKTY.POZ_PROJ;
_table.T.VALUE:=PROJEKTY.T().T;
_table.KH.VALUE:=PROJEKTY.KH().KOD;
_table.KH_ODB.VALUE:=PROJEKTY.KH_ODB().KOD;
_table.HAN.VALUE:={? PROJEKTY.HAN<>null || PROJEKTY.HAN().PIERWSZE + ' ' + PROJEKTY.HAN().NAZWISKO || '' ?};
_table.UD_SKL.VALUE:={? PROJEKTY.UD_SKL<>null || PROJEKTY.UD_SKL().UD_TYP().SYMBOL +' ' +PROJEKTY.UD_SKL().SYMBOL || '' ?};
_table.ODDZ.VALUE:=PROJEKTY.ODDZ;
_table.SZEF.VALUE:={? PROJEKTY.SZEF<>null ||  PROJEKTY.SZEF().PIERWSZE + ' ' + PROJEKTY.SZEF().NAZWISKO || '' ?};
_table.NAZWA.VALUE:=PROJEKTY.NAZWA;
_table.USER_ZAM.VALUE:=PROJEKTY.USER_ZAM;
_table.DAT_ZAM.VALUE:=PROJEKTY.DAT_ZAM;
_table.GODZ_ZAM.VALUE:=PROJEKTY.GODZ_ZAM;
_table.RDATA_OD.VALUE:=PROJEKTY.RDATA_OD;
_table.RDATA_DO.VALUE:=PROJEKTY.RDATA_DO;
_table.RDAT_ROZ.VALUE:=PROJEKTY.RDAT_ROZ;
_table.RDAT_ZAM.VALUE:=PROJEKTY.RDAT_ZAM;
_table.KRAJ.VALUE:=PROJEKTY.KRAJ;
_table.MIASTO.VALUE:=PROJEKTY.MIASTO;
_table.UL.VALUE:=PROJEKTY.UL;
_table.TEL1.VALUE:=PROJEKTY.TEL1;
_table.TEL2.VALUE:=PROJEKTY.TEL2;
_table.PL.VALUE:=PROJEKTY.PL().KOD;
_table.PLANPRZ.VALUE:=PROJEKTY.PLANPRZ;
_table.PLANKOS.VALUE:=PROJEKTY.PLANKOS;
_table.KK.VALUE:=PROJEKTY.KK().SYM;
_table.UDT.VALUE:=PROJEKTY.UDT().KOD;
_table.SLO_RODZ.VALUE:=PROJEKTY.SLO_RODZ().KOD;
_table.KAL_POD.VALUE:=PROJEKTY.KAL_POD;
_table.STATUS.VALUE:=PROJEKTY.STATUS().KOD;
_table.KH_PRJ.VALUE:=PROJEKTY.KH_PRJ;
_table.P_VAL.VALUE:=PROJEKTY.P_VAL;
_table.WAL.VALUE:=PROJEKTY.WAL().KOD;
_table.KRS.VALUE:=PROJEKTY.KRS;
_table.KH_OSOB.VALUE:=PROJEKTY.KH_OSOB().NAZWISKO;
_table.KH_PWK.VALUE:=PROJEKTY.KH_PWK().KOD;
_table.DTK.VALUE:=PROJEKTY.DTK;
_table.P_VAL_P.VALUE:=PROJEKTY.P_VAL_P;
_table.DPP.VALUE:=PROJEKTY.DPP;
PROJTYPY.cntx_pop();KH.cntx_pop();KH_ODB.cntx_pop();OSOBA.cntx_pop();UD_SKL.cntx_pop();UD_TYP.cntx_pop();
SLO.cntx_pop();KK.cntx_pop();UDT.cntx_pop();PROJ_STA.cntx_pop();KH_OSOB.cntx_pop();

PROJEKTY.cntx_psh();
PR_KOS.cntx_psh();
PR_KOS.index('PR_KOS_P');
PR_KOS.prefix(PROJEKTY.ref);
{? PR_KOS.first()
||
   {!
   |?
      _excel.write_async('PR_KOS',PR_KOS.ref());
      PR_KOS.next()
   !}
?};
PR_KOS.cntx_pop();
PROJEKTY.cntx_pop();
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;
{? _obj.POZ_PROJ=1 || __RESULT:=1; __RODZIC+=1 ?};
{? _obj.REF=''
|| _rodzic_prefix:=$__RODZIC;
   _result.ACTION:='add'
|| _rodzic_prefix:=
   (PROJEKTY.cntx_psh();
    PROJEKTY.prefix();
    _wyn:={? PROJEKTY.seek(_obj.REF)
    || PROJEKTY.RODZIC
    || $__RODZIC
    ?};
    PROJEKTY.cntx_pop();
    _wyn);
    _result.ACTION:='put'
?};

_result.RESULT:=__RESULT;

_result.OBJ:=obj_new('T','KH','KH_ODB','HAN','UD_SKL','SZEF','PL','KK','UDT','SLO_RODZ',
                     'STATUS','WAL','KH_OSOB','KH_PWK','KOL');

_result.OBJ.T:=         {? _obj.T=''
                        || null()
                        || exec('FindInSet','#table','PROJTYPY','T',_obj.T,,,1)
                        ?};
_result.OBJ.KH:=        {? _obj.KH=''
                        || null()
                        || exec('FindInSet','#table','KH','KOD1',_obj.KH,,,1)
                        ?};
_result.OBJ.KH_ODB:=    {? _obj.KH_ODB=''
                        || null()
                        || exec('FindInSet','#table','KH_ODB','KH_ODB',_obj.KH_ODB,PROJEKTY.KH,,1)
                        ?};
_result.OBJ.HAN:=       {? _obj.HAN=''
                        || null()
                        || _han:=spli_str(_obj.HAN,' ');
                           {? obj_len(_han)=2
                           || PROJEKTY.HAN:=exec('FindInSet','#table','OSOBA','OSOBA',_han[1],_han[2],,1)
                           ?}
                        ?};
_result.OBJ.UD_SKL:=    {? _obj.UD_SKL=''
                        || null()
                        || _ud_skl:=spli_str(_obj.UD_SKL,' ');
                           {? obj_len(_ud_skl)=2
                           || PROJEKTY.UD_SKL:=exec('FindInSet','#table','UD_SKL','SYMBOL',_ud_skl[2],
                              exec('FindInSet','#table','UD_TYP','SYMBOL',_ud_skl[1],,,1),,1)
                           ?}
                        ?};
_result.OBJ.SZEF:=      {? _obj.SZEF=''
                        || null()
                        || _szef:=spli_str(_obj.SZEF,' ');
                           {? obj_len(_szef)=2
                           || PROJEKTY.SZEF:=exec('FindInSet','#table','OSOBA','OSOBA',_szef[1],_szef[2],,1)
                           ?}
                        ?};
_result.OBJ.PL:=        {? _obj.PL=''
                        || null()
                        || exec('FindInSet','#table','SLO','KOD',_obj.PL,,,1)
                        ?};
_result.OBJ.KK:=        {? _obj.KK=''
                        || null()
                        || exec('FindInSet','#table','KK','KONTASYM',_obj.KK,REF.FIRMA,,1)
                        ?};
_result.OBJ.UDT:=       {? _obj.UDT=''
                        || null()
                        || exec('FindInSet','#table','UDT','UDTKOD',_obj.UDT,REF.FIRMA,,1)
                        ?};
_result.OBJ.SLO_RODZ:=  {? _obj.SLO_RODZ=''
                        || null()
                        || exec('FindInSet','#table','SLO','KOD',_obj.SLO_RODZ,,,1)
                        ?};
_result.OBJ.STATUS:=    {? _obj.STATUS=''
                        || null()
                        || exec('FindInSet','#table','PROJ_STA','KOD',_obj.STATUS,,,1)
                        ?};
_result.OBJ.WAL:=       {? _obj.WAL=''
                        || null()
                        || exec('FindInSet','#table','SLO','KOD',_obj.WAL,,,1)
                        ?};
_result.OBJ.KH_OSOB:=   {? _obj.KH_OSOB=''
                        || null()
                        || exec('FindInSet','#table','KH_OSOB','KH',_obj.KH_OSOB,PROJEKTY.KH,,1)
                        ?};
_result.OBJ.KH_PWK:=    {? _obj.KH_PWK=''
                        || null()
                        || exec('FindInSet','#table','KH','KOD1',_obj.KH_PWK,,,1)
                        ?};
_result.OBJ.KOL:=       {? _obj.POZ_PROJ=1
                        || '001'+ (207*'0')
                        ||
                           _poziom:=_obj.POZ_PROJ;
                           PROJEKTY.cntx_psh();
                           PROJEKTY.index('IDPUT');
                           PROJEKTY.prefix();
                           PROJEKTY.last();
                           {!|?
                               PROJEKTY.POZ_PROJ>=_poziom & PROJEKTY.prev()
                           !};
                           _pozycja:=PROJEKTY.POZ_PROJ;
                           _kolejn:=PROJEKTY.KOL;
                           PROJEKTY.cntx_pop();
                           PROJEKTY.cntx_psh();
                           PROJEKTY.index('POZ');
                           _prefix:=(_pozycja*3)+_kolejn;
                           PROJEKTY.prefix(REF.FIRMA,_rodzic_prefix,_poziom,_prefix);
                           _numer:=PROJEKTY.size()+1;
                           PROJEKTY.cntx_pop();
                           (((_pozycja*3)+_kolejn) + (('00'+$_numer)+3)+((210-_pozycja*3-3)*'0'))
                        ?};

:: sprzwdzenie czy podane dane można wyszukać
{? _result.OBJ.T=null()
|| _result.msg_norec('T');
   _result.RESULT:=0
?};
{? _obj.KH<>'' & _result.OBJ.KH=null()
|| _result.msg_norec('KH');
   _result.RESULT:=0
?};
{? _obj.KH_ODB<>'' & _result.OBJ.KH_ODB=null()
|| _result.msg_norec('KH_ODB');
   _result.RESULT:=0
?};
{? _obj.HAN<>'' & _result.OBJ.HAN=null()
|| _result.msg_norec('HAN');
   _result.RESULT:=0
?};
{? _obj.UD_SKL<>'' & _result.OBJ.UD_SKL=null()
|| _result.msg_norec('UD_SKL');
   _result.RESULT:=0
?};
{? _obj.SZEF<>'' & _result.OBJ.SZEF=null()
|| _result.msg_norec('SZEF');
   _result.RESULT:=0
?};
{? _obj.PL<>'' & _result.OBJ.PL=null()
|| _result.msg_norec('PL');
   _result.RESULT:=0
?};
{? _obj.KK<>'' & _result.OBJ.KK=null()
|| _result.msg_norec('KK');
   _result.RESULT:=0
?};
{? _obj.UDT<>'' & _result.OBJ.UDT=null()
|| _result.msg_norec('UDT');
   _result.RESULT:=0
?};
{? _obj.SLO_RODZ<>'' & _result.OBJ.SLO_RODZ=null()
|| _result.msg_norec('SLO_RODZ');
   _result.RESULT:=0
?};
{? _obj.STATUS<>'' & _result.OBJ.STATUS=null()
|| _result.msg_norec('STATUS');
   _result.RESULT:=0
?};
{? _obj.WAL<>'' & _result.OBJ.WAL=null()
|| _result.msg_norec('WAL');
   _result.RESULT:=0
?};
{? _obj.KH_OSOB<>'' & _result.OBJ.KH_OSOB=null()
|| _result.msg_norec('KH_OSOB');
   _result.RESULT:=0
?};
{? _obj.KH_PWK<>'' & _result.OBJ.KH_PWK=null()
|| _result.msg_norec('KH_PWK');
   _result.RESULT:=0
?};
:: sprawdzenie prawdiłowego wypełnienia
{? exec('FindAndGet','#table',PROJTYPY,_result.OBJ.T,,"PROJTYPY.R='Wewnętrzny'",0)
|| {?  _result.OBJ.UD_SKL=null()
   || _result.msg_norec('UD_SKL');
      _result.RESULT:=0
   ?}
|| {? _result.OBJ.KH=null()
   || _result.msg_norec('KH');
      _result.RESULT:=0
   ?}
?};

{? _obj.RDATA_DO<>date(0,0,0) & _obj.RDATA_OD>_obj.RDATA_DO
|| _result.msg_value('RDATA_DO','Data zakończenia projektu nie może być wcześniejsza od daty rozpoczęcia.'@);
   _result.RESULT:=0
?};
{? _obj.RDAT_ZAM<>date(0,0,0) & _obj.RDAT_ROZ>_obj.RDAT_ZAM
|| _result.msg_value('RDAT_ZAM','Data zakończenia projektu nie może być wcześniejsza od daty rozpoczęcia.'@);
   _result.RESULT:=0
?};

{? _obj.ID_KSG<0
|| _result.msg_value('ID_KSG','Wartość nie może być ujemna.'@);
   _result.RESULT:=0
?};

{? _obj.ID_KSG
|| PROJEKTY.cntx_psh();
   PROJEKTY.index('ID_KSG'); PROJEKTY.prefix(_obj.ID_KSG);
   {? PROJEKTY.first() & _obj.REF<>$PROJEKTY.ref()
   || _result.msg_value('ID_KSG','Istnieje projekt o identyfikatorze księgowym: %1 w firmie: %2.'@[$PROJEKTY.ID_KSG,PROJEKTY.FIRMA().SYMBOL]);
      _result.RESULT:=0
   ?};
   PROJEKTY.cntx_pop()
?};

{? _obj.PLANPRZ<0
|| _result.msg_value('PLANPRZ','Wartość nie może być ujemna.'@);
   _result.RESULT:=0
?};
{? _obj.PLANKOS<0
|| _result.msg_value('PLANKOS','Wartość nie może być ujemna.'@);
   _result.RESULT:=0
?};

{? _result.RESULT & _obj.POZ_PROJ>1 & (_obj.RDATA_OD<>date(0,0,0) | _obj.RDAT_ROZ<>date(0,0,0))
||
   _rdata_od:=_obj.RDATA_OD;
   _rdat_roz:=_obj.RDAT_ROZ;
   _rdata_od_err:=date(0,0,0);
   _rdat_roz_err:=date(0,0,0);
   PROJEKTY.cntx_psh();
   PROJEKTY.index('KOL');
   _poziom:=_obj.POZ_PROJ-1;
   {!|?
      PROJEKTY.prefix(REF.FIRMA,_rodzic_prefix,(((_poziom)*3)+_result.OBJ.KOL)+'000');
      {? PROJEKTY.first()
      ||
         {!|?
            {? PROJEKTY.RDATA_OD<>date(0,0,0) & PROJEKTY.RDATA_OD>_rdata_od & PROJEKTY.RDATA_OD>_rdata_od_err
            ||
               _rdata_od_err:=PROJEKTY.RDATA_OD
            ?};
            {? PROJEKTY.RDAT_ROZ<>date(0,0,0) & PROJEKTY.RDAT_ROZ>_rdat_roz & PROJEKTY.RDAT_ROZ>_rdat_roz_err
            ||
               _rdat_roz_err:=PROJEKTY.RDAT_ROZ
            ?};
            PROJEKTY.next()
         !}
      ?};
      _poziom-=1;
      _poziom>0
   !};
   {? _obj.RDATA_OD<>date(0,0,0) & _rdata_od_err<>date(0,0,0)
   || _result.msg_value('RDATA_OD','Początkowa data realizacji etapu projektu nie powinna być wczesniejsza niż %1.'@[_rdata_od_err$4]);
      _result.RESULT:=0
   ?};
   {? _obj.RDAT_ROZ<>date(0,0,0) & _rdat_roz_err<>date(0,0,0)
   || _result.msg_value('RDAT_ROZ','Rzeczywista data realizacji etapu projektu nie powinna być wczesniejsza niż %1.'@[_rdat_roz_err$4]);
      _result.RESULT:=0
   ?};
   PROJEKTY.cntx_pop()
?};
{? _result.RESULT & _obj.POZ_PROJ>1 & (_obj.RDATA_DO<>date(0,0,0) | _obj.RDAT_ZAM<>date(0,0,0))
||
   _rdata_do:=_obj.RDATA_DO;
   _rdat_zam:=_obj.RDAT_ZAM;
   _rdata_do_err:=date(0,0,0);
   _rdat_zam_err:=date(0,0,0);
   PROJEKTY.cntx_psh();
   PROJEKTY.index('KOL');
   _poziom:=_obj.POZ_PROJ-1;
   {!|?
      PROJEKTY.prefix(REF.FIRMA,_rodzic_prefix,(((_poziom)*3)+_result.OBJ.KOL)+'000');
      {? PROJEKTY.first()
      ||
         {!|?
            {? PROJEKTY.RDATA_DO<>date(0,0,0) & PROJEKTY.RDATA_DO<_rdata_do &
            (PROJEKTY.RDATA_DO<_rdata_do_err | _rdata_do_err=date(0,0,0))
            ||
               _rdata_do_err:=PROJEKTY.RDATA_DO
            ?};
            {? PROJEKTY.RDAT_ZAM<>date(0,0,0) & PROJEKTY.RDAT_ZAM<_rdat_zam &
            (PROJEKTY.RDAT_ZAM<_rdat_zam_err | _rdat_zam_err=date(0,0,0))
            ||
               _rdat_zam_err:=PROJEKTY.RDAT_ZAM
            ?};
            PROJEKTY.next()
         !}
      ?};
      _poziom-=1;
      _poziom>0
   !};
   {? _rdata_do_err<>date(0,0,0)
   ||
      _result.msg_value('RDATA_DO','Początkowa data realizacji etapu projektu nie powinna być późniejsza niż %1.'@[_rdata_od_err$4]);
      _result.RESULT:=0
   ?};
   {? _rdat_zam_err<>date(0,0,0)
   ||
      _result.msg_value('RDAT_ZAM','Rzeczywista data realizacji etapu projektu nie powinna być późniejsza niż %1.'@[_rdat_zam_err$4]);
      _result.RESULT:=0
   ?};
   PROJEKTY.cntx_pop()
?};
:: b) dla etapów niżej
{? _result.RESULT & _obj.RDATA_OD<>date(0,0,0)
||
   _rdata_od:=_obj.RDATA_OD;
   _rdat_roz:=_obj.RDAT_ROZ;
   _rdata_od_err:=date(0,0,0);
   _rdat_roz_err:=date(0,0,0);
   PROJEKTY.cntx_psh();
   PROJEKTY.index('KOL');
   _poziom:=_obj.POZ_PROJ;
   PROJEKTY.prefix(REF.FIRMA,_rodzic_prefix,(((_poziom)*3)+_result.OBJ.KOL));
   {? PROJEKTY.first()
   ||
      {!|?
         {? PROJEKTY.RDATA_OD<>date(0,0,0) & PROJEKTY.RDATA_OD<_rdata_od
         & (PROJEKTY.RDATA_OD<_rdata_od_err | _rdata_od_err=date(0,0,0))
         & $PROJEKTY.ref()<>_obj.REF
         ||
            _rdata_od_err:=PROJEKTY.RDATA_OD
         ?};
         {? PROJEKTY.RDAT_ROZ<>date(0,0,0) & PROJEKTY.RDAT_ROZ<_rdat_roz
         & (PROJEKTY.RDAT_ROZ<_rdat_roz_err | _rdat_roz_err=date(0,0,0))
         & $PROJEKTY.ref()<>_obj.REF
         ||
            _rdat_roz_err:=PROJEKTY.RDAT_ROZ
         ?};
         PROJEKTY.next()
      !}
   ?};
   {? _rdata_od_err<>date(0,0,0)
   ||
      _result.msg_value('RDATA_OD','Planowana data realizacji nie powinna być późniejsza niż %1.'@[_rdata_od_err$4]);
      _result.RESULT:=0
   ?};
   {? _rdat_roz_err<>date(0,0,0)
   ||
      _result.msg_value('RDAT_ROZ','Rzeczywista data realizacji nie powinna być późniejsza niż %1.'@[_rdat_roz_err$4]);
      _result.RESULT:=0
   ?};
   PROJEKTY.cntx_pop()
?};
{? _result.RESULT & _obj.RDATA_DO<>date(0,0,0)
||
   _rdata_do:=_obj.RDATA_DO;
   _rdat_zam:=_obj.RDAT_ZAM;
   _rdata_do_err:=date(0,0,0);
   _rdat_zam_err:=date(0,0,0);
   PROJEKTY.cntx_psh();
   PROJEKTY.index('KOL');
   _poziom:=_obj.POZ_PROJ;
   PROJEKTY.prefix(REF.FIRMA,_rodzic_prefix,(((_poziom)*3)+_result.OBJ.KOL));
   {? PROJEKTY.first()
   ||
      {!|?
         {? PROJEKTY.RDATA_DO<>date(0,0,0) & PROJEKTY.RDATA_DO>_rdata_do & PROJEKTY.RDATA_DO>_rdata_do_err
         ||
            _rdata_do_err:=PROJEKTY.RDATA_DO
         ?};
         {? PROJEKTY.RDAT_ZAM<>date(0,0,0) & PROJEKTY.RDAT_ZAM>_rdat_zam & PROJEKTY.RDAT_ZAM>_rdat_zam_err
         ||
            _rdat_zam_err:=PROJEKTY.RDAT_ZAM
         ?};
         PROJEKTY.next()
      !}
   ?};
   {? _rdata_do_err<>date(0,0,0)
   ||
      _result.msg_value('RDATA_DO','Planowana data realizacji nie powinna być wcześniejsza niż %1.'@[_rdata_do_err$4]);
      _result.RESULT:=0
   ?};
   {? _rdat_zam_err<>date(0,0,0)
   ||
      _result.msg_value('RDAT_ZAM','Rzeczywista data realizacji nie powinna być wcześniejsza niż %1.'@[_rdat_zam_err$4]);
      _result.RESULT:=0
   ?};
   PROJEKTY.cntx_pop()
?};

__RESULT:=_result.RESULT;
exec('record','xls_projekty',_obj,_mode,_result);

~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| EXCEL_ENV.ID_GRP:='Projekt' + PROJEKTY.NAZWA;
   {? _validate.ACTION='add'
   || _result:=PROJEKTY.add(1)
   |? _validate.ACTION='put'
   || _result:=PROJEKTY.put(1)
   ?}
|| EXCEL_ENV.ID_GRP:=''
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie projektu/etapu %1 nie powiodło się.'@[_obj.SYM]
   || _validate.MSG:='Poprawa projektu/etapu %1 nie powiodła się.'@[_obj.SYM]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
exec('exp_tab_sel','projekty',_selected);
_result:=0;

_tab:=tab_tmp(1,
   'SYM','STRING[30]','Symbol'
   ,'NAZWA','STRING[80]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'SYM',,,'NAZWA',,);

PROJEKTY.cntx_psh();
PROJEKTY.index('KOL');
PROJEKTY.prefix(REF.FIRMA);
{? PROJEKTY.first()
|| {!|?
      {? (PROJEKTY.cntx_psh();
          PROJEKTY.index('IDADD');
          PROJEKTY.prefix(PROJEKTY.RODZIC);
          _wyn:=PROJEKTY.first() & exec('proj_upraw_usr','!zws_prj_upra');
          PROJEKTY.cntx_pop();
          _wyn)
      || _tab.blank();
         _tab.REF:=$PROJEKTY.ref();
         _selected.prefix($PROJEKTY.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.SYM:=PROJEKTY.SYM;
         _tab.NAZWA:=PROJEKTY.NAZWA;
         _tab.add()
      ?};
      PROJEKTY.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'SYM,NAZWA',20,'Wybór projektów i etapów do exportu'@);
{? _result>0
||
   __PRJ_SEL:=0;
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PROJEKTY.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 82a26eebc6a0ee0d0a3d60986f3c1a8b01020cef0a0884cd2f75df6bd77ff958a14290fd5590778461611dbc9569b0f845f884096287c624ac6e9891bb9b40a4ec76018100eb0eaa2576a5d875fede8a7fd1191f54208ea71db44908c3d63073a5150bf22a2eaddb106c02f588f1bdbd6a5352051333dc4c5ac60515fdafbcf8

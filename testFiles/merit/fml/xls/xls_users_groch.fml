:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_users.fml
:: Utworzony: 02.07.2018
:: Autor: WH
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - obiekt z definicją, który należy zasilić - wynik działania exec('args_init','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='B_PROTU';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USERS';
_def.FILE:='users.xlsx';
_def.SHEET:='Grupy ochron dla użytkowników=1,3';
_def.NAME:='Grupy ochron dla użytkowników'@;
_def.DESC:='Grupy ochron dla użytkowników'@;
_def.MULTIFIR:='T';

_def.PREFIX:="B_PROTU.index('UNIK');B_PROTU.prefix()";
_def.TABLE:="B_PROTU";
_def.FIELDS:="exec('fields','xls_users_groch',_a)";

_def.BEFORE:="B_PROTU.cntx_psh()";
_def.AFTER:="B_PROTU.cntx_pop()";

_def.EXPORT:="exec('export','xls_users_groch',_a,_b)";
_def.VALIDATE:="exec('validate','xls_users_groch',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_users_groch',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Zwraca eksportowane pola tabeli i ich atrybuty w excel
::   WE: _a - obiekt z definicją pól który należy zasilić - wynik działania exec('args_fields','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_tooltip:='Max. %1 znaków. '@[$MS.fld_len(USERS,'KOD')];
_tooltip+='Wymagane.'@;
_def.define('USERS','Użytkownik'@,1,,_tooltip);

_tooltip:='Max. %1 znaków. '@[$MS.fld_len(B_PROTU,'PROT_KEY')];
_tooltip+='Wymagane.'@;
_def.define('PROT_KEY','Grupa ochron'@,1,,_tooltip);

~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól tabeli MacroBASE
::                      każdy obiekt ma taką strukturę:
::                        _obj_bridge.NAZWA_POLA.VALUE - wartość która będzie zapisana w EXCEL
::                        _obj_bridge.NAZWA_POLA.EDITABLE - czy komórka będzie edytowalna
::                        _obj_bridge.NAZWA_POLA.COLOR - kolor w formacie html czyli #ffffff
::                      Aktualny wewnętrzny obiekt można podejrzeć tu: exec('filler_obj_core','#excel_imex')
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_obj_bridge:=_b;
_result:=1;
_obj_bridge.USERS.VALUE:=B_PROTU.USERS().KOD; _excel.write_async('USERS',B_PROTU.USERS);
_obj_bridge.PROT_KEY.VALUE:=B_PROTU.PROT_KEY;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER   - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obj_new() - obiekt z resultem, wynik działania exec('args_valid','#excel_imex').
::                        Obiekt ten jest przekazywany do formuły na IMPORT. Pole RESULT decyduje czy formuła
::                        na import się wykona. Pole MSG służy to odpisania komunikatu
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_can_continue:=1;

:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _can_continue>0 & _obj.USERS=''
|| _can_continue:=0;
   _result.msg_empty('USERS')
?};
{? _can_continue>0 & _obj.PROT_KEY=''
|| _can_continue:=0;
   _result.msg_empty('PROT_KEY')
?};
{? _can_continue>0
|| _max:=MS.fld_len(USERS,'KOD');
   {? +_obj.USERS>_max
   || _result.msg_length('USERS',_max);
      _obj.USERS:=_max+_obj.USERS
   ?}
?};
{? _can_continue>0
|| _max:=MS.fld_len(B_PROTU,'PROT_KEY');
   {? +_obj.PROT_KEY>_max
   || _result.msg_length('PROT_KEY',_max);
      _obj.PROT_KEY:=_max+_obj.PROT_KEY
   ?}
?};

_users:=null();
{? _can_continue>0
|| USERS.cntx_psh();
   USERS.index('USR_AKOD');
   USERS.prefix('T',_obj.USERS,_obj.USERS);
   {? USERS.first()
   || _users:=USERS.ref()
   || _result.msg_norec('USERS');
      _can_continue:=0
   ?};
   USERS.cntx_pop()
?};

_prot_key:='';
{? _can_continue>0
|| B_PROT.cntx_psh();
   B_PROT.index('UNIK');
   B_PROT.prefix(_obj.PROT_KEY,_obj.PROT_KEY);
   {? B_PROT.first()
   || _prot_key:=B_PROT.PROT_KEY
   || _result.msg_norec('PROT_KEY');
      _can_continue:=0
   ?};
   B_PROT.cntx_pop()
?};

{? _can_continue>0
||
   B_PROTU.index('UNIK');
   B_PROTU.prefix(_prot_key,_users);
   {? B_PROTU.first()=0
   || _result.ACTION:='add';
      _result.OBJ:=obj_new('PROT_KEY','USERS');
      _result.OBJ.PROT_KEY:=_prot_key;
      _result.OBJ.USERS:=_users
   ?}
?};
{? _can_continue=0
|| _result.RESULT:=0
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt z resultem walidatora i resultem importu, wynik działania exec('args_valid','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_can_continue:=1;
{? _result.ACTION='add'
||
   _buf:=_result.OBJ;
   _result.RESULT:=exec('user_prot_add','#b_role',_buf.USERS,0,_buf.PROT_KEY,_result);
   {? _result.RESULT=0
   || _result.msg_insert('Grupa ochron: %1 dla użytkownika: %2'@[_obj.PROT_KEY,_obj.USERS])
   ?}
?};
~~


:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 759373aebd0fd5c4f53097eac6cab7805f77788e520215cb4efec79df39a19f1291ee21e013c0a1f32f826823af28bc86d2a62fc988c1aeb66b051263b8ac43740b2b5dd90f6f77c40994ae4ec403c9c6bcc8e3a692d8d1f9a1419f3b978ea9d14f62c9fc8019feb331a44d4b4532d280a93fd907f953b9bb46308ab7abf8874

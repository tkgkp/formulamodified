:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_badseh.fml
:: Utworzony: 27.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu zestawów badań
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='BADSEH';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='ZWS_PAR_KBAD';

_def.FILE:='logistyka_badania.xlsx';
_def.SHEET:='Zestawy badań'@+'=1,1';
_def.NAME:='Zestawy badań'@;
_def.DESC:='Zestawy badań'@;

_def.PREFIX:="BADSEH.index('NN'); BADSEH.prefix()";
_def.TABLE:="BADSEH";
_def.FIELDS:="exec('fields','xls_badseh',_a)";

_def.BEFORE:="BADSEH.cntx_psh()";
_def.AFTER:="BADSEH.cntx_pop()";
_def.SELECT:="exec('select','xls_badseh',_a)";

_def.EXPORT:="exec('export','xls_badseh',_a,_b)";
_def.VALIDATE:="exec('validate','xls_badseh',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_badseh',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_define:=$(
   '_a.define(_b,'
   '{? var_pres(\'_d\')=type_of(\'\') || _d || exec(\'fldLabel\',\'#field\',BADSEH,_b) ?},_c,,'
   '{? var_pres(\'_e\')=type_of(\'\') || _e || exec(\'fldComment\',\'#field\',BADSEH,_b) ?})'
);
_define(_def,'TYP',0,,'Typ zestawu badań:\n%1 - magazynowy,\n%2 - operacyjny'@['M','O']);
_define(_def,'N',0,,'Nazwa (max. %1 znaków)'@['60']);
_define(_def,'AKT',1);
_define(_def,'ATEST',1);
_define(_def,'STATS_Z',1);
_define(_def,'STATS_O',1);
_define(_def,'STATS_N',1);
_define(_def,'TTOPER',0);
_define(_def,'MGR',0);
_define(_def,'MGR_RODZ',0,'Rodzaj grupy materiałowej','Rodzaj grupy:\n%1 - towarowa,\n%2 - usługowa'@['T','U']);
_define(_def,'TORP',1,,'Miejsce generowania:\n%1 - technologia,\n%2 - przewodnik'@['T','P']);
_define(_def,'BAD_OP',1,,'Czy badanie w trakcie operacji:\n%1 - tak,\n%2 - nie (badanie kontroli jakości)'@['T','N']);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
BADSEH.prefix();
{? _validate.ACTION='add'
|| BADSEH.blank();
   BADSEH.TYP:=_obj.TYP;
   BADSEH.N:=_obj.N
?};
BADSEH.AKT:=_obj.AKT;
BADSEH.ATEST:=_obj.ATEST;
BADSEH.STATS_Z:=_validate.OBJ.STATS_Z;
BADSEH.STATS_O:=_validate.OBJ.STATS_O;
BADSEH.STATS_N:=_validate.OBJ.STATS_N;
BADSEH.TTOPER:=_validate.OBJ.TTOPER;
BADSEH.MGR:=_validate.OBJ.MGR;
BADSEH.TORP:=_obj.TORP;
BADSEH.BAD_OP:=_obj.BAD_OP;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.TYP.VALUE:=BADSEH.TYP;
_table.N.VALUE:=BADSEH.N;
_table.AKT.VALUE:=BADSEH.AKT;
_table.ATEST.VALUE:=BADSEH.ATEST;
_table.STATS_Z.VALUE:=BADSEH.STATS_Z().N; _excel.write_async('STATS',BADSEH.STATS_Z);
_table.STATS_O.VALUE:=BADSEH.STATS_O().N; _excel.write_async('STATS',BADSEH.STATS_O);
_table.STATS_N.VALUE:=BADSEH.STATS_N().N; _excel.write_async('STATS',BADSEH.STATS_N);
BADSEH.cntx_psh();
_table.TTOPER.VALUE:=BADSEH.TTOPER().KOD; _excel.write_async('TTOPER',BADSEH.TTOPER);
BADSEH.cntx_pop();
_table.MGR.VALUE:=BADSEH.MGR().KOD;
_table.MGR_RODZ.VALUE:=BADSEH.MGR().RODZ;
_table.TORP.VALUE:=BADSEH.TORP;
_table.BAD_OP.VALUE:=BADSEH.BAD_OP;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

{? ~((';MO'*_obj.TYP)>1) || _result.msg_inset('TYP','M','O'); _result.RESULT:=0 ?};
{? ~((';TN'*_obj.AKT)>1) || _result.msg_inset('AKT','T','N'); _result.RESULT:=0 ?};
{? +_obj.MGR_RODZ>0 & 'TU'*_obj.MGR_RODZ=0 || _result.msg_inset('MGR_RODZ','T','U'); _result.RESULT:=0 ?};
{? +_obj.TORP>0 & 'TP'*_obj.TORP=0 || _result.msg_inset('TORP','T','P'); _result.RESULT:=0 ?};
{? +_obj.BAD_OP>0 & 'TN'*_obj.BAD_OP=0 || _result.msg_inset('BAD_OP','T','N'); _result.RESULT:=0 ?};

_result.RESULT:=1;
_result.OBJ:=obj_new('STATS_Z','STATS_O','STATS_N','TTOPER','MGR');
_result.OBJ.STATS_Z:=exec('FindInSet','#table','STATS','NN',_obj.STATS_Z,,"STATS.ref()",1);
_result.OBJ.STATS_O:=exec('FindInSet','#table','STATS','NN',_obj.STATS_O,,"STATS.ref()",1);
_result.OBJ.STATS_N:=exec('FindInSet','#table','STATS','NN',_obj.STATS_N,,"STATS.ref()",1);
_result.OBJ.TTOPER:=exec('FindInSet','#table','TTOPER','KOD',_obj.TTOPER,,"TTOPER.ref()",1);
_result.OBJ.MGR:=exec('FindInSet','#table','MGR','KOD',_obj.MGR,_obj.MGR_RODZ,"MGR.ref()",1);

{? _obj.STATS_Z<>'' & _result.OBJ.STATS_Z=null()
|| _result.msg_norec('STATS_Z');
   _result.RESULT:=0
|? _obj.STATS_O<>'' & _result.OBJ.STATS_O=null()
|| _result.msg_norec('STATS_O');
   _result.RESULT:=0
|? _obj.STATS_N<>'' & _result.OBJ.STATS_N=null()
|| _result.msg_norec('STATS_N');
   _result.RESULT:=0
|? _obj.TTOPER<>'' & _result.OBJ.TTOPER=null()
|| _result.msg_norec('TTOPER');
   _result.RESULT:=0
|? _obj.MGR<>'' & _result.OBJ.MGR=null()
|| _result.msg_norec('MGR');
   _result.RESULT:=0

|| BADSEH.index('NN');
   BADSEH.prefix(_obj.N,);
   {? BADSEH.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_badseh',_obj,_mode,_result);
         {? exec('chk_badseh','statexam',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_badseh',_obj,_mode,_result);
      {? exec('chk_badseh','statexam',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=BADSEH.add(1)
   |? _validate.ACTION='put'
   || _result:=BADSEH.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie zestawu badań %1 nie powiodło się.'@[$_obj.N]
   || _validate.MSG:='Poprawa zestawu badań %1 nie powiodła się.'@[$_obj.N]
   ?}
||
   {? _validate.ACTION='add'
   ||
::    Uzupełnienie danych na TTOPER
      TTOPER.cntx_psh();
      TTOPER.index('BAD_NAZ');
      TTOPER.prefix(BADSEH.N);
      {? TTOPER.first()
      || {!
         |?
            {? TTOPER.BADSEH=null()
            || TTOPER.BADSEH:=BADSEH.ref();
               TTOPER.KJ_BAD:='B'
            ?};
            TTOPER.BAD_NAZ:='';
            TTOPER.cntx_psh();
            TTOPER.prefix();
            TTOPER.put();
            TTOPER.cntx_pop();
            TTOPER.first()
         !}
      ?};
      TTOPER.cntx_pop()
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'N','STRING[60]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'TYP','STRING[1]','Typ'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'N',,);

BADSEH.cntx_psh();
BADSEH.index('NN');
BADSEH.prefix();
{? BADSEH.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$BADSEH.ref();
      _selected.prefix($BADSEH.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.N:=BADSEH.N;
      _tab.TYP:=BADSEH.TYP;
      _tab.add();
      BADSEH.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'TYP,N',20,'Wybór zestawów badań do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
BADSEH.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 c90843f8d2a1460d2e5b3a791b634b289c011d019ffc4be63b1ac1edbb9a7d7634b4e56a513a1a5678f8665854133780ee6d9483e6c6e08b35afafffddcd9a72171d681b1615210bff147138f5d1d8c285fac65a563ed9ecf587f16090c15de9da85df05fa0953bde5ff564ba7ff02de8c33a1d655b835c3a0c09366321e5f3a

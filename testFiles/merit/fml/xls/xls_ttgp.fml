:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ttgp.fml
:: Utworzony: 12.08.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu pozycji grup technologicznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TTGP';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='TKTL';
_def.HIDDEN:='T';
_def.SKIP:='T';
_def.ADD_ROWS:=0;

_def.FILE:='produkcja_technologie.xlsx';
_def.SHEET:='Pozycje grup technologicznych'@+'=1,1';
_def.NAME:='Pozycje grup technologicznych'@;
_def.DESC:='Pozycje grup technologicznych'@;

_def.PREFIX:="TTGP.index('GRS'); TTGP.prefix()";
_def.TABLE:="TTGP";
_def.FIELDS:="exec('fields','xls_ttgp',_a)";

_def.BEFORE:="TTGP.cntx_psh()";
_def.AFTER:="TTGP.cntx_pop()";

_def.EXPORT:="exec('export','xls_ttgp',_a,_b)";
_def.VALIDATE:="exec('validate','xls_ttgp',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ttgp',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_edit:=0;
_def.define('GR','Grupa'@,_edit);
_def.define('PT','Surowiec'@,_edit);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TTGP.prefix();
{? _validate.ACTION='add'
|| TTGP.blank();
   TTGP.GR:=_validate.OBJ.GR;
   TTGP.PT:=_validate.OBJ.PT;
   TTGP.SRC_:=TTGP.PT().KTM
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.GR.VALUE:=TTGP.GR().GR;  _excel.write_async('TTG',TTGP.GR);
_table.PT.VALUE:=TTGP.PT().KTM;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('GR','PT');
:: Wyszukanie refów powiązanych rekordów
_result.OBJ.GR:={? _obj.GR=''
                || null()
                || exec('FindInSet','#table','TTG','GR',_obj.GR,,,1,_obj.GR,null())
                ?};
_result.OBJ.PT:={? _obj.PT=''
                || null()
                || exec('FindInSet','#table','M','MATKTM',_obj.PT,,,1,,null())
                ?};
:: Błędy
{? _result.OBJ.GR=null()
|| _result.msg_norec('GR');
   _result.RESULT:=0
?};
{? _result.OBJ.PT=null()
|| _result.msg_norec('PT');
   _result.RESULT:=0
?};
:: OK
{? _result.RESULT>0
|| TTGP.index('GRS');
   TTGP.prefix(_result.OBJ.GR,_result.OBJ.PT);
   {? TTGP.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_ttgp',_obj,_mode,_result);
         {? ~(exec('get','#params',500740,2)='N'
            | exec('FindAndGet','#table','M',_result.OBJ.PT,,"M.R='S' | M.R='P'",0))
         || _result.MSG:=('Pozycja grupy technologicznej \'%1\' może być tylko surowcem lub półfabrykatem (%2).'@
                         [_obj.GR,_obj.PT]);
            _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_ttgp',_obj,_mode,_result);
      {? ~(exec('get','#params',500740,2)='N' | exec('FindAndGet','#table','M',_result.OBJ.PT,,"M.R='S' | M.R='P'",0))
      || _result.MSG:=('Pozycja grupy technologicznej \'%1\' może być tylko surowcem lub półfabrykatem (%2).'@
                      [_obj.GR,_obj.PT]);
         _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TTGP.add(1)
   |? _validate.ACTION='put'
   || _result:=TTGP.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie pozycji (%1) do grupy technologicznej (%2) nie powiodło się.'@[_obj.PT,_obj.GR]
   || _validate.MSG:='Poprawa pozycji (%1) do grupy technologicznej (%2) nie powiodła się.'@[_obj.PT,_obj.GR]
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2020/10/16 15:26:06 7d2f38000dc59d4956b1461c909fa29c5e60a3066943c313b588d850624ef9e614fb857918d1cb245f109843da9988e1aba2608406d1dc17ab7832c39b7f33850927e75f155586350b114d8b869623651e6f1bd5bfa860f4318745c2e5dc0f35f18f1a2c0de29210d3e6cc4fa02af9a9ea8822ff329a03913fcd0ba314d76f07

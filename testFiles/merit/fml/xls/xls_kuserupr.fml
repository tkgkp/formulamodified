:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kuserupr.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu uprawnień stanowisk kasowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KUSERUPR';
_def.DOMAIN:=exec('name','#b_domain','KAS');
_def.FUNPAR:='ZWS_PAR_FKMK';

_def.FILE:='kasa.xlsx';
_def.SHEET:='Uprawnienia multikasa=1,1';
_def.NAME:='Uprawnienia multikasa'@;
_def.DESC:='Uprawnienia użytkowników do stanowisk kasowych';
_def.TABLE:="exec('table','xls_kuserupr')";
_def.FIELDS:="exec('fields','xls_kuserupr',_a)";
_def.EXPORT:="exec('export','xls_kuserupr',_a,_b)";
_def.IMPORT:="exec('import','xls_kuserupr',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_kuserupr',_a,_b,_c)";
_def.SELECT:="exec('select','xls_kuserupr',_a)";
_def.PREFIX:="KUSERUPR.index('KU'); KUSERUPR.prefix(null)";
_def.BEFORE:="KUSERUPR.cntx_psh()";
_def.AFTER:="KUSERUPR.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
KUSERUPR


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('MKASA','Nazwa multikasy',0,,'',type_of(''));
_def.define('KU','Użytkownik',0,,'Wskazanie na użytkownika specjalnego',type_of(''));
_def.define('STANKAS','Stanowisko',0,,'Wskazanie na stanowisko',type_of(''));
_def.define('STANKASN','Nazwa stanowiska',1,,'30 znakowa Nazwa stanowiska kasowego',type_of(''));
_def.define('UPR','Uprawnienia',1,,'Znacznik, czy użytkownik posiada uprawnienia w stanowisku',type_of(''));
_def.define('LP','Lp',1,,'Liczba porządkowa',type_of(0),0);
_def.define('MTAB','Do widoku z zakładkami',1,,'Do widoku z zakładkami',type_of(''));
_def.define('TAB','Nazwa zakładki',1,,'20 znakowa Nazwa zakładki',type_of(''));
_def.define('DOKUMENT','Pokaż dokument',1,,'Pokaż dokumenty',type_of(''));
_def.define('POZDOK','Pokaż pozycje dokumentów',1,,'Pokaż pozycje dokumentów',type_of(''));
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
KUSERUPR.prefix();
{? _validate.ACTION='add'
||
   KUSERUPR.blank();
   KUSERUPR.MKASA:=_validate.OBJ.MKASA;
   KUSERUPR.KU:=_validate.OBJ.KU;
   KUSERUPR.STANKAS:=_validate.OBJ.STANKAS
?};

KUSERUPR.STANKASN:=_obj.STANKASN;
KUSERUPR.UPR:=_obj.UPR;
KUSERUPR.LP:=_obj.LP;
KUSERUPR.MTAB:=_obj.MTAB;
KUSERUPR.TAB:=_obj.TAB;
KUSERUPR.DOKUMENT:=_obj.DOKUMENT;
KUSERUPR.POZDOK:=_obj.POZDOK;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.MKASA.VALUE:=KUSERUPR.MKASA().NAZ;
_excel.write_async('MKASA',KUSERUPR.MKASA);

_table.KU.VALUE:=KUSERUPR.KU().KOD;
_excel.write_async('USERS',KUSERUPR.KU);

_table.STANKAS.VALUE:=form(KUSERUPR.STANKAS().KOD,,,'99');
_excel.write_async('STANKAS',KUSERUPR.STANKAS);

_table.STANKASN.VALUE:=KUSERUPR.STANKASN;
_table.UPR.VALUE:=KUSERUPR.UPR;
_table.LP.VALUE:=KUSERUPR.LP;
_table.MTAB.VALUE:=KUSERUPR.MTAB;
_table.TAB.VALUE:=KUSERUPR.TAB;
_table.DOKUMENT.VALUE:=KUSERUPR.DOKUMENT;
_table.POZDOK.VALUE:=KUSERUPR.POZDOK;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('MKASA', 'KU', 'STANKAS');

{? _obj.MKASA='' & _obj.KU=''
   | _obj.MKASA<>'' & _obj.KU<>''
|| _result.RESULT:=0;
   _result.MSG:='Niewłaściwa parametryzacja uprawnień użytkownika do stanowiska kasowego lub pozycji multikasy.';
   return(~~)
?};

{? _obj.MKASA=''
||
   _result.OBJ.MKASA:=null()
||
   _result.OBJ.MKASA:=exec('FindInSet','#table','MKASA','NAZ',_obj.MKASA,,,1);
   {? _result.OBJ.MKASA=null()
   ||
      _result.MSG:='Nie znaleziono multikasy o nazwie %1.'@[_obj.MKASA];
      _result.RESULT:=0;
      return(~~)
   ?}
?};

{? _obj.KU=''
||
   _result.OBJ.KU:=null()
||
   _result.OBJ.KU:=exec('FindInSet','#table','USERS','USR_AKOD',_obj.KU,'T',,1);
   {? _result.OBJ.KU=null()
   ||
      _result.MSG:='Nie znaleziono użytkownika o kodzie %1.'@[_obj.KU];
      _result.RESULT:=0;
      return(~~)
   ?}
?};

{? _obj.STANKAS=''
||
   _result.OBJ.STANKAS:=null()
||
   _result.OBJ.STANKAS:=exec('FindInSet','#table','STANKAS','KOD',#_obj.STANKAS);
   {? _result.OBJ.STANKAS=null()
   ||
      _result.MSG:='Nie znaleziono stanowiska o kodzie %1.'@[_obj.STANKAS];
      _result.RESULT:=0;
      return(~~)
   ?}
?};

{? _result.RESULT = 1
||
   KUSERUPR.index('KU');
   KUSERUPR.prefix(_result.OBJ.KU, #_obj.STANKAS, _result.OBJ.MKASA);
   {? KUSERUPR.first() & KUSERUPR.MKASA = _result.OBJ.MKASA
   ||
      {? _mode=0
      || _result.RESULT:=0
      ||
         _result.ACTION:='put';
         exec('record','xls_kuserupr',_obj,_mode,_result);
         {? exec('kuserupr_chk','kasa_wspolne')<>''
         ||
            _result.RESULT:=0
         ?}
      ?}
   ||
      _result.ACTION:='add';
      exec('record','xls_kuserupr',_obj,_mode,_result);
      {? exec('kuserupr_chk','kasa_wspolne')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=KUSERUPR.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=KUSERUPR.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie uprawnień multikasy: %1 dla użytkownika: %2 na stanowisku: %3 nie powiodło się.'@[_obj.MKASA, _obj.KU, _obj.STANKAS])
   ||
      FUN.info('Poprawa uprawnień multikasy: %1 dla użytkownika: %2 na stanowisku: %3 nie powiodło się.'@[_obj.MKASA, _obj.KU, _obj.STANKAS])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'STANKASN','STRING[30]','Nazwa stanowiska'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'LP','INTEGER','Lp.'
   ,'MKASA','STRING[100]','Nazwa multikasy'
   ,'KU','STRING[30]','Użytkownik');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'STANKASN',,);

KUSERUPR.cntx_psh();
KUSERUPR.index('KU');
KUSERUPR.prefix(null);
{? KUSERUPR.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$KUSERUPR.ref();
      _selected.prefix($KUSERUPR.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.STANKASN:=KUSERUPR.STANKASN;
      _tab.LP:=KUSERUPR.LP;
      _tab.MKASA:=KUSERUPR.MKASA().NAZ;
      _tab.KU:=KUSERUPR.KU().NAME;
      _tab.add();
      KUSERUPR.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'STANKASN,LP,MKASA,KU',20,'Wybór uprawnień multikasy do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KUSERUPR.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 4e24055c1f7487ac35d048f5b5d84808195ae03fbe829a21fb8fe05402c67e5c25024aaa7e946bb095dbbbaa8c710771e69caec600318dab9e59cdca595f47ee8baeedbdaab29a0a265e57663917220b72f3e9ba43317e714a351e186a7156715ee4524e36f87bbd4358201756a750a56797c1c7bd058ef7e564a736e3f322e4

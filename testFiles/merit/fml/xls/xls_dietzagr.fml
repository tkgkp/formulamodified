:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_dietzagr.fml
:: Utworzony: 28.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu stawek diet zagranicznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='DIETZAGR';
_def.FUNPAR:='ZWS_PAR_DSTA';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Stawki diet zagranicznych=1,1';
_def.NAME:='Stawki diet zagranicznych'@;
_def.DESC:='Stawki diet zagranicznych';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="DIETZAGR";

: prolog
_def.BEFORE:="
   WAL.cntx_psh();
   WAL.prefix();
   KRAJE.cntx_psh();
   KRAJE.prefix();
   DIETZAGR.cntx_psh();
   DIETZAGR.f_clear();
   DIETZAGR.index('KRAJ');
   DIETZAGR.prefix();
   ~~
";

: epilog
_def.AFTER:="
   DIETZAGR.cntx_pop();
   KRAJE.cntx_pop();
   WAL.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('KRAJ',    MS.name(DIETZAGR,'KRAJ'),    1,,MS.comment(DIETZAGR,'KRAJ'));
   _env.define('WAL',     MS.name(DIETZAGR,'WAL'),     1,,MS.comment(DIETZAGR,'WAL'));
   _env.define('STAWKA',  MS.name(DIETZAGR,'STAWKA'),  1,,MS.comment(DIETZAGR,'STAWKA'));
   _env.define('RYCZ_NOC',MS.name(DIETZAGR,'RYCZ_NOC'),1,,MS.comment(DIETZAGR,'RYCZ_NOC'));
   _env.define('OD',      MS.name(DIETZAGR,'OD'),      1,,MS.comment(DIETZAGR,'OD'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.KRAJ.VALUE:=DIETZAGR.KRAJ().NAZ;
   _buf.WAL.VALUE:=DIETZAGR.WAL().SYM;
   _buf.STAWKA.VALUE:=DIETZAGR.STAWKA;
   _buf.RYCZ_NOC.VALUE:=DIETZAGR.RYCZ_NOC;
   _buf.OD.VALUE:=DIETZAGR.OD;
   _env.write_async('KRAJE',DIETZAGR.KRAJ);
   _env.write_async('WAL',DIETZAGR.WAL);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_dietzagr',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_dietzagr',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.KRAJ='' || 'KRAJ'
   |? _buf.WAL=''  || 'WAL'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(KRAJE,'NAZ');
{? +_buf.KRAJ>_max
|| _val.msg_length('KRAJ',_max);
   _buf.KRAJ:=_max+_buf.KRAJ
?};
_max:=MS.fld_len(WAL,'SYM');
{? +_buf.WAL>_max
|| _val.msg_length('WAL',_max);
   _buf.WAL:=_max+_buf.WAL
?};

: weryfikacja spójności referencyjnej
{? ~KRAJE.find_tab(,'NAZ',,'=',_buf.KRAJ)
|| _val.msg_norec('KRAJ');
   _val.RESULT:=0;
   return()
?};
{? ~WAL.find_tab(,'SYM',,'=',_buf.WAL)
|| _val.msg_norec('WAL');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? DIETZAGR.find_tab(,
      'KRAJ',,'=',KRAJE.ref(),
      'OD',,'=',_buf.OD
   )
|| _ref:=DIETZAGR.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1, %2'[_a.KRAJ,$_a.OD]",
   'KRAJ',KRAJE.ref(),
   'WAL',WAL.ref()
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,DIETZAGR,"
   _a.KRAJ:=_c.KRAJ;
   _a.WAL:=_c.WAL;
   _a.STAWKA:=_b.STAWKA;
   _a.RYCZ_NOC:=_b.RYCZ_NOC;
   _a.OD:=_b.OD;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 bd0dab4db0647b35e73a6c807094377ab31420723174663ea9394b72119a14ca4279784807f377e19369ee90e4184bee845ccc31ce4208eb125d82a3e0c5fb574cb320ebb1616d8cfa6a83605c1a98caff25b1ab8f2a5c6df0aa2a77a0c284accc537a2326a8f31242cbf3514b6d6a4036270208595044ffd42d71fcf77cdd29

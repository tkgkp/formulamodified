:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_pd_mg_zn.fml
:: Utworzony: 19.04.2021
:: Autor: AKUL
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu nagłówków zestawów magazynów planu dostaw
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PD_MG_ZN';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PLAN_DST';

_def.FILE:='plan_dostaw.xlsx';
_def.SHEET:='Nagłówki zestawów magazynów=1,1';
_def.NAME:='Nagłówki zestawów magazynów'@;
_def.DESC:='Nagłówki zestawów magazynów';
_def.TABLE:="PD_MG_ZN";
_def.FIELDS:="exec('fields','xls_pd_mg_zn',_a)";
_def.EXPORT:="exec('export','xls_pd_mg_zn',_a,_b)";
_def.IMPORT:="exec('import','xls_pd_mg_zn',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_pd_mg_zn',_a,_b,_c)";
_def.SELECT:="exec('select','xls_pd_mg_zn',_a)";
_def.PREFIX:="PD_MG_ZN.index('VIEW_PD'); PD_MG_ZN.prefix()";
_def.BEFORE:="PD_MG_ZN.cntx_psh()";
_def.AFTER:="PD_MG_ZN.cntx_pop()";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NAZ','Nazwa',0);
_def.define('USER','Użytkownik',0);
_def.define('DEFAULT','Domyślny zestaw');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PD_MG_ZN.prefix();
{? _validate.ACTION='add'
|| PD_MG_ZN.blank();
   PD_MG_ZN.NAZ:=_obj.NAZ;
   PD_MG_ZN.USER:=_validate.OBJ.USER
?};
PD_MG_ZN.DEFAULT:=_obj.DEFAULT;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.NAZ.VALUE:=PD_MG_ZN.NAZ;
_table.USER.VALUE:=PD_MG_ZN.USER().KOD; _excel.write_async('USERS',PD_MG_ZN.USER);
_table.DEFAULT.VALUE:=PD_MG_ZN.DEFAULT;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('USER');

::Błędy
_result.OBJ.USER:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1,,null());
{? _result.OBJ.USER=null()
|| _result.MSG:='Nie znaleziono użytkownika o kodzie %1.'@[_obj.USER];
   _result.RESULT:=0;
   return(~~)
?};
{? +_obj.DEFAULT=0 | 'TN'*_obj.DEFAULT=0
|| _result.msg_inset('DEFAULT','T','N');
   _result.RESULT:=0
?};

{? _result.RESULT=1
|| PD_MG_ZN.index('UNIK');
   PD_MG_ZN.prefix(_obj.NAZ,);
   {? PD_MG_ZN.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_pd_mg_zn',_obj,_mode,_result);
         {? __CHK.record(PD_MG_ZN,,'NAZ','USER')<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_pd_mg_zn',_obj,_mode,_result);
      {? __CHK.record(PD_MG_ZN,,'NAZ','USER')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=PD_MG_ZN.add(1)
   |? _validate.ACTION='put'
   || _result:=PD_MG_ZN.put(1)
   ?}
?};
:: Jeżeli nowy rekord jest zestawym domyślnym, to czyszczę znacznik na dotychczasowym zestawie domyślnym
{? PD_MG_ZN.DEFAULT='T'
|| _ref:=PD_MG_ZN.ref();
   PD_MG_ZN.cntx_psh();
   PD_MG_ZN.index('USER');
   PD_MG_ZN.prefix(PD_MG_ZN.USER,'T');
   {? PD_MG_ZN.first() & PD_MG_ZN.size()>1
   || _next:=1;
      {? PD_MG_ZN.ref()=_ref
      || _next:=PD_MG_ZN.next()
      ?};
      {? _next>0
      || PD_MG_ZN.prefix();
         PD_MG_ZN.DEFAULT:='N';
         PD_MG_ZN.put()
      ?}
   ?};
   PD_MG_ZN.cntx_pop()
?};
{? _result=0
|| {? _validate.ACTION='add'
   || FUN.info('Dodanie nagłówka zestawu magazynów o nazwie %1 nie powiodło się.'@[_obj.NAZ])
   || FUN.info('Poprawa nagłówka zestawu magazynów o nazwie %1 nie powiodła się.'@[_obj.NAZ])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'NAZ','STRING[100]','Nazwa'
   ,'USER','STRING[10]','Użytkownik'
   ,'DEFAULT','STRING[8]','Domyślny zestaw'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZ',,);

PD_MG_ZN.cntx_psh();
PD_MG_ZN.index('UNIK');
PD_MG_ZN.prefix();
{? PD_MG_ZN.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$PD_MG_ZN.ref();
      _selected.prefix($PD_MG_ZN.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NAZ:=PD_MG_ZN.NAZ;
      _tab.USER:=PD_MG_ZN.USER().KOD;
      _tab.DEFAULT:=PD_MG_ZN.DEFAULT;
      _tab.add();
      PD_MG_ZN.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NAZ,USER,DEFAULT[TN]',20,'Wybór nagłówków zestawów formuł planu dostaw do eksportu'@,1);
{? _result>0
|| _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PD_MG_ZN.cntx_pop();

_result

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:15 def3ea30204aad720a048eab9b20ccd6dadd703324e52ea74c81caa4ce70e94337bc30586a10104cda4c7dd013c1b5fdd9c34f88ddf229c1c2d9707efe7d04f0359e932d3e77f97f5dd89edc4f7393fc5547c5fa063cd67c9c955c0806727748704927b51ed5971fb5a35cc73c65844ed1f12baf9382c0a7af6a0433d18740d8

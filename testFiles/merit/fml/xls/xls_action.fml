:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_users.fml
:: Utworzony: 02.07.2018
:: Autor: WH
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - obiekt z definicją, który należy zasilić - wynik działania exec('args_init','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='B_ACTION';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USERS';
_def.ADD_ROWS:=0;
_def.FILE:='users.xlsx';
_def.SHEET:='Czynności nie powiązane z rolą'@+'=1,3';
_def.NAME:='Czynności nie powiązane z rolą'@;
_def.DESC:='Czynności nie powiązane z rolą'@;

_def.TABLE:="exec('table','xls_action',_a,_b)";
_def.TAB_IMP:="";
_def.FIELDS:="exec('fields','xls_action',_a)";

::_def.SELECT:="exec('select','xls_action',_a)";

_def.EXPORT:="exec('export','xls_action',_a,_b)";
_def.VALIDATE:="";
_def.IMPORT:="";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Zwraca eksportowane pola tabeli i ich atrybuty w excel
::   WE: _a - obiekt z definicją pól który należy zasilić - wynik działania exec('args_fields','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_tooltip:=MS.name(B_ACTION,'UID')+' '+'(max. %1 znaków). '@[$MS.fld_len(B_ACTION,'UID')];
_tooltip+='Nieredagowalne.'@;
_def.define('UID','Symbol'@,0,,_tooltip);

_tooltip:=MS.name(B_ACTION,'NAME')+' '+'(max. %1 znaków). '@[$MS.fld_len(B_ACTION,'NAME')];
_tooltip+='Nieredagowalne.'@;
_def.define('NAME','Nazwa'@,0,,_tooltip);
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól tabeli MacroBASE
::                      każdy obiekt ma taką strukturę:
::                        _obj_bridge.NAZWA_POLA.VALUE - wartość która będzie zapisana w EXCEL
::                        _obj_bridge.NAZWA_POLA.EDITABLE - czy komórka będzie edytowalna
::                        _obj_bridge.NAZWA_POLA.COLOR - kolor w formacie html czyli #ffffff
::                      Aktualny wewnętrzny obiekt można podejrzeć tu: exec('filler_obj_core','#excel_imex')
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_obj_bridge:=_b;
_result:=0;

_tab:=_excel.table();

B_ACTION.cntx_psh();
B_ACTION.prefix();
B_ACTROL.cntx_psh();
B_ACTROL.index('DISP2');

{? B_ACTION.seek(_tab.REF)
|| B_ACTROL.prefix(REF.FIRMA,B_ACTION.ref());
   {? B_ACTROL.first()=0
   || _result:=1
   ?}
?};
B_ACTROL.cntx_pop();
B_ACTION.cntx_pop();

{? _result>0
|| _obj_bridge.UID.VALUE:=_tab.UID;
   _obj_bridge.NAME.VALUE:=_tab.NAME
?};
_result


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Zwraca uchwyt do tabeli
::   WE: [_b] - INTEGER - 1/0 - czy wywołanie podczas walidacji, czy podczas prawdziwego eksportu
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_valid:=0;
{? var_pres('_b')=type_of(0)
|| _valid:=_b
?};
_tab:=tab_tmp(1
:: 'POLE','TYP','Nazwa w oknie',
   ,'REF','STRING[16]','SQL Ref czynności'
   ,'SYMBOL','STRING[255]','Identyfikator+nazwa'
   ,'UID','STRING[20]','Identyfikator czynności'
   ,'NAME','STRING[255]','Nazwa czynności'
   ,'UIDREF','STRING[48]','Id rekordu'
);
{? _valid=0
|| B_ACTION.cntx_psh();
   B_ACTION.index('UNIK');
   B_ACTION.prefix();
   {? B_ACTION.first()
   || {!
      |? _tab.blank();
         _tab.REF:=$B_ACTION.ref();
         _tab.SYMBOL:=exec('B_ACTION','#to_string');
         _tab.UID:=B_ACTION.UID;
         _tab.NAME:=B_ACTION.NAME;
         _tab.UIDREF:=$B_ACTION.ref();
         _tab.add();
         B_ACTION.next()
      !}
   ?};
   B_ACTION.cntx_pop()
?};
_tab

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:41 34ec877102437e37fa352052853f2643bff0787bf3fca610bb8415d5a708280f9523bda0710d87664e78f396cf60dbe6c33be17e8dbe0b3f25783848f17336e73f3d6fb4d9ac89b0287a7aa9cd2c5203044dafef6c045d2ab8bd8641de47bd1d6cc3f2fb200b30f92266506097cf9fb989a11565d7e7eec892bc34078d61dca8

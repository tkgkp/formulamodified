:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_xfl_act.fml
:: Utworzony: 04.02.2021
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_def:=_a;

_def.ID:='XFL_ACT';
_def.FUNPAR:='ZWS_PAR_XFL';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_xfl_xlsx','xls__init');
_def.SHEET:='Czynności filtrów=1,1';
_def.NAME:='Czynności filtrów'@;
_def.DESC:='Czynności filtrów';
_def.ADD_ROWS:=0;
_def.HIDDEN:='N';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="XFL_ACT";

: prolog
_def.BEFORE:="
   XFL_ACT.cntx_psh();
   XFL_ACT.f_clear();
   XFL_ACT.clear();
   XFL_ACT.index('KOD');
   B_ACTION.cntx_psh();
   B_ACTION.clear();
   B_ACTION.index('UNIK');
   ~~
";

: epilog
_def.AFTER:="
   B_ACTION.cntx_pop();
   XFL_ACT.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYSTEM',  MS.name(XFL_ACT,'SYSTEM'),0,,  MS.comment(XFL_ACT,'SYSTEM'));
   _env.define('KOD',     MS.name(XFL_ACT,'KOD'),0,,     MS.comment(XFL_ACT,'KOD'),type_of(''));
   _env.define('XFL_DEF', MS.name(XFL_ACT,'XFL_DEF'),0,, MS.comment(XFL_ACT,'XFL_DEF'),type_of(''));
   _env.define('B_ACTION',MS.name(XFL_ACT,'B_ACTION'),0,,MS.comment(XFL_ACT,'B_ACTION'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_xfl_act',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_xfl_act',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_xfl_act',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_env:=_a;
_buf:=_b;

_buf.SYSTEM.VALUE:=XFL_ACT.SYSTEM;
_buf.KOD.VALUE:=XFL_ACT.KOD;
_buf.XFL_DEF.VALUE:=XFL_ACT.XFL_DEF().KOD;
_buf.B_ACTION.VALUE:=XFL_ACT.B_ACTION().UID;

_env.write_async('XFL_DEF',XFL_PAR.XFL_DEF);

1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYSTEM=''   || 'SYSTEM'
   |? _buf.KOD=''      || 'KOD'
   |? _buf.XFL_DEF=''  || 'XFL_DEF'
   |? _buf.B_ACTION='' || 'B_ACTION'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.SYSTEM))=0
|| _val.msg_inset('SYSTEM','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(XFL_ACT,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(XFL_DEF,'KOD');
{? +_buf.XFL_DEF>_max
|| _val.msg_length('XFL_DEF',_max);
   _buf.XFL_DEF:=_max+_buf.XFL_DEF
?};
_max:=MS.fld_len(B_ACTION,'UID');
{? +_buf.B_ACTION>_max
|| _val.msg_length('B_ACTION',_max);
   _buf.B_ACTION:=_max+_buf.B_ACTION
?};

: weryfikacja spójności referencyjnej
_def:=null;
{? XFL_DEF.find_key(_buf.XFL_DEF,)
|| _def:=XFL_DEF.ref()
|| _val.msg_norec('XFL_DEF');
   _val.RESULT:=0;
   return()
?};
_act:=null;
{? B_ACTION.find_key(_buf.B_ACTION,)
|| _act:=B_ACTION.ref()
|| _val.msg_norec('B_ACTION');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? (XFL_ACT.index('KOD'); XFL_ACT.find_key(_buf.KOD,)) |
   (XFL_ACT.index('UNIQUE'); XFL_ACT.find_key(_def,_act))
|| _ref:=XFL_ACT.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2, %3'[_a.KOD,_a.XFL_DEF,_a.B_ACTION]",
   'XFL_DEF',_def,
   'B_ACTION',_act
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,XFL_ACT,"
   _a.SYSTEM:=_b.SYSTEM;
   _a.KOD:=_b.KOD;
   _a.XFL_DEF:=_c.XFL_DEF;
   _a.B_ACTION:=_c.B_ACTION;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 98cbe59211113aa552e4a26d9b8299e35a31917a1006037bd110b11b627ab91366a4c4a1e63754a6521220a572c6fa734e58789404d0ac0e50e850bbfdd189b58b964808f03d68ae4b360932c14ead9af6b8093b482fee6a014e5532a8bc6c8433a2b386b7f317587866039612c8070f088ed287b0801c4bcb6f3199c9526b93

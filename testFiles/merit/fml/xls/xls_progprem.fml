:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_progprem.fml
:: Utworzony: 13.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu progów premii za publ. naukowe
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='PROGPREM';
_def.FUNPAR:='ZWS_PAR_PPPR';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:='personel_stale.xlsx';
_def.SHEET:='Progi premii=1,1';
_def.NAME:='Progi premii'@;
_def.DESC:='Progi premii';

: źródło danych
_def.TABLE:="PROGPREM";

: prolog
_def.BEFORE:="
   PROGPREM.cntx_psh();
   PROGPREM.f_clear();
   PROGPREM.index('ROKPKT');
   PROGPREM.prefix(REF.FIRMA);
   ~~
";

: epilog
_def.AFTER:="
   PROGPREM.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('ROK',   MS.name(PROGPREM,'ROK'),   1,,MS.comment(PROGPREM,'ROK'),,0);
   _env.define('PUNKTY',MS.name(PROGPREM,'PUNKTY'),1,,MS.comment(PROGPREM,'PUNKTY'),,0);
   _env.define('PREMIA',MS.name(PROGPREM,'PREMIA'),1,,MS.comment(PROGPREM,'PREMIA'),,2);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.ROK.VALUE:=PROGPREM.ROK;
   _buf.PUNKTY.VALUE:=PROGPREM.PUNKTY;
   _buf.PREMIA.VALUE:=PROGPREM.PREMIA;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_progprem',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_progprem',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.ROK=0    || 'ROK'
   |? _buf.PUNKTY=0 || 'PUNKTY'
   |? _buf.PREMIA=0 || 'PREMIA'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? _buf.ROK<0
|| _val.msg_range('ROK',0);
   _val.RESULT:=0;
   return()

|? _buf.PUNKTY<0
|| _val.msg_range('PUNKTY',0);
   _val.RESULT:=0;
   return()

|? _buf.PREMIA<0
|| _val.msg_range('PREMIA',0);
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? PROGPREM.find_tab(,'ROK',,'=',_buf.ROK,'PUNKTY',,'=',_buf.PUNKTY)
|| _ref:=PROGPREM.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('PROGPREM','to_str');
_obj.to_str:="'%1: %2'[$_a.ROK,$_a.PUNKTY]";
_obj.PROGPREM:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.ROK:=_b.ROK;
   _a.PUNKTY:=_b.PUNKTY;
   _a.PREMIA:=_b.PREMIA;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? PROGPREM.seek(_obj.PROGPREM)
   || _set(PROGPREM,_buf);
      {? ~PROGPREM.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,PROGPREM.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| PROGPREM.blank();
   _set(PROGPREM,_buf);
   {? ~PROGPREM.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:49 6666574112673e3ae6435eb1282dbe8bbd583b322a1b0653a1674cefb878aafcf90d8396715a2057f7235ca48584a2b079f0f5f833fb52f0d9392802f71f7e103c853df6592c1f145f3e2378948b2d7d43503f40f6fc3b7fa943c2c1bcaef4de5432483fb290baab09392e17401969d39935d1ef3dc63dbcbcac6835f1bc151c

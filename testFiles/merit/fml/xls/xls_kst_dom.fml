:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kst_dom.fml
:: Utworzony: 25.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu dziedzin zestawu
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='KST_DOM';
_def.FUNPAR:='ZWS_PAR_KST_ADM';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.NAME:='Stałe systemu - dziedzina zestawu'@;
_def.FILE:=exec('def_kst_xlsx','xls__init');
_def.SHEET:='Dziedzina zestawu=1,1';
_def.DESC:='Dziedzina zestawu';
_def.ADD_ROWS:=0;
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="KST_DOM";

: prolog
_def.BEFORE:="
   B_DOMAIN.cntx_psh();
   B_DOMAIN.f_clear();
   B_DOMAIN.prefix();

   KST_ZES.cntx_psh();
   KST_ZES.f_clear();
   KST_ZES.prefix();

   KST_DOM.cntx_psh();
   KST_DOM.f_clear();
   KST_DOM.index('ZES_SYM');
   KST_DOM.prefix();
   ~~
";

: epilog
_def.AFTER:="
   KST_DOM.cntx_pop();
   KST_ZES.cntx_pop();
   B_DOMAIN.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('B_DOMAIN',MS.name(KST_DOM,'B_DOMAIN'),0,,MS.comment(KST_DOM,'B_DOMAIN'));
   _env.define('KST_ZES', MS.name(KST_DOM,'KST_ZES'), 0,,MS.comment(KST_DOM,'KST_ZES'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.B_DOMAIN.VALUE:=KST_DOM.B_DOMAIN().SYMBOL;
   _buf.KST_ZES.VALUE:=KST_DOM.KST_ZES().SYMBOL;
   _env.write_async('KST_ZES',KST_DOM.KST_ZES);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_kst_dom',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kst_dom',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.B_DOMAIN='' || 'B_DOMAIN'
   |? _buf.KST_ZES=''  || 'KST_ZES'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(B_DOMAIN,'SYMBOL');
{? +_buf.B_DOMAIN>_max
|| _val.msg_length('B_DOMAIN',_max);
   _buf.B_DOMAIN:=_max+_buf.B_DOMAIN
?};
_max:=MS.fld_len(KST_ZES,'SYMBOL');
{? +_buf.KST_ZES>_max
|| _val.msg_length('KST_ZES',_max);
   _buf.KST_ZES:=_max+_buf.KST_ZES
?};

: weryfikacja spójności referencyjnej
{? ~B_DOMAIN.find_tab(,'SYMBOL',,'=',_buf.B_DOMAIN)
|| _val.msg_norec('B_DOMAIN');
   _val.RESULT:=0;
   return()
?};
{? ~KST_ZES.find_tab(,'SYMBOL',,'=',_buf.KST_ZES)
|| _val.msg_norec('KST_ZES');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
: odszukaj aktualizowany wiersz
{? KST_DOM.find_tab(,
      'B_DOMAIN',,'=',B_DOMAIN.ref(),
      'KST_ZES',,'=',KST_ZES.ref()
   )
|| _ref:=KST_DOM.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1: %2'[_a.B_DOMAIN,_a.KST_ZES]",
   'B_DOMAIN',B_DOMAIN.ref(),
   'KST_ZES',KST_ZES.ref()
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,KST_DOM,"
   _a.B_DOMAIN:=_c.B_DOMAIN;
   _a.KST_ZES:=_c.KST_ZES;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 8481d10228c9c0bca7d39b58a51765d57ddc994bf6f569573c7cd14d1d87ccb5e8e7424ca6ffc84875724b19d3e82f50934f6de9ae56bd0803d4520d33a5025441d3c8594681124de982a2b34136b5e7a8764b22551f9e365c1caf60f9ca8f4e731e5cdec2e7b5f7ea4cd2092632a5e5a1040a1a857cc74e199a5c20ff821846

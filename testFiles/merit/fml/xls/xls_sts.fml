:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_sts.fml
:: Utworzony: 12.07.2018
:: Autor: Mario
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu stanowisk sprzedaży i zakupu
::======================================================================================================================


\init_sts
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='STS';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.FUNPAR:='ZWS_PAR_LSTS';

_def.FILE:='sprzedaz.xlsx';
_def.SHEET:='Stanowiska sprzedaży=1,1';
_def.NAME:='Stanowiska sprzedaży'@;
_def.DESC:='Stanowiska sprzedaży';

_def.PREFIX:="exec('prefix','xls_sts','S')";
_def.TABLE:="STS";
_def.FIELDS:="exec('fields','xls_sts',_a,'S')";

_def.BEFORE:="STS.cntx_psh()";
_def.AFTER:="STS.cntx_pop()";
_def.SELECT:="exec('select','xls_sts',_a,'S')";

_def.EXPORT:="exec('export','xls_sts',_a,_b,'S')";
_def.VALIDATE:="exec('validate','xls_sts',_a,_b,_c,'S')";
_def.IMPORT:="exec('import','xls_sts',_a,_b,_c,'S')";
~~


\init_stz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='STZ';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PAR_LSTZ';

_def.FILE:='zakupy.xlsx';
_def.SHEET:='Stanowiska zakupu=1,1';
_def.NAME:='Stanowiska zakupu'@;
_def.DESC:='Stanowiska zakupu';

_def.PREFIX:="exec('prefix','xls_sts','Z')";
_def.TABLE:="STS";
_def.FIELDS:="exec('fields','xls_sts',_a,'Z')";

_def.BEFORE:="STS.cntx_psh()";
_def.AFTER:="STS.cntx_pop()";
_def.SELECT:="exec('select','xls_sts',_a,'Z')";

_def.EXPORT:="exec('export','xls_sts',_a,_b,'Z')";
_def.VALIDATE:="exec('validate','xls_sts',_a,_b,_c,'Z')";
_def.IMPORT:="exec('import','xls_sts',_a,_b,_c,'Z')";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WE: _a - Sprzedaży, Zakupu
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_spza:=_a;
STS.index('SZ');
STS.prefix(_spza);
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('ODDZ','Oddział',0,,'1 znakowe oznaczenie oddziału');
_def.define('KOD','Kod',1,,'8 znakowe oznaczenie oddziału\nUnikalne w ramach firmy');
_def.define('NAZ','Nazwa',1,,'40 znakowa');
_def.define('KSG','Czy księgować?',1,,'Dopuszczalne wartości T/N');
_def.define('MGR','Czy redagować grupę materiałową?',1,,'Dopuszczalne wartości T/N');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - Sprzedaży, Zakupu
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_spza:=_d;

STS.index('SZ');
STS.prefix(_spza,_obj.KOD,);
{? _validate.ACTION='add'
||
   STS.blank();
   STS.SZ:=_spza;
   STS.ODDZ:=_obj.ODDZ
?};

STS.KOD:=_obj.KOD;
STS.NAZ:=_obj.NAZ;
STS.KSG:=_obj.KSG;
STS.MGR:=_obj.MGR;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.ODDZ.VALUE:=STS.ODDZ;  _excel.write_async('ODDZ',exec('FindInSet','#table','ODDZ','KOD',STS.ODDZ,,,1));

_table.KOD.VALUE:=STS.KOD;
_table.NAZ.VALUE:=STS.NAZ;
_table.KSG.VALUE:=STS.KSG;
_table.MGR.VALUE:=STS.MGR;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::       _d - Sprzedaży, Zakupu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;
_spza:=_d;

_result.RESULT:=1;

STS.index('SZ');
STS.prefix(_spza,_obj.KOD,);
{? STS.first()
||
   {? _mode=0
   || _result.RESULT:=0
   ||
      _result.ACTION:='put';
      exec('record','xls_sts',_obj,_mode,_result,_spza);
      {? exec('chk_sts','typysp','put')<>''
      ||
         _result.RESULT:=0
      ?}
   ?}
||
   _result.ACTION:='add';
   exec('record','xls_sts',_obj,_mode,_result,_spza);
   {? exec('chk_sts','typysp','add')<>''
   || _result.RESULT:=0
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - Sprzedaży, Zakupu
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_spza:=_d;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=STS.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=STS.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie typy dokumentu o kodzie %1 nie powiodło się.'@[_obj.KOD]
   ||
      _validate.MSG:='Poprawa typy dokumentu o kodzie %1 nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - 'S'-sprzedaż 'Z'-zakupy
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'KOD','STRING[8]','Stanowisko'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'ODDZ','STRING[1]','Oddział'
   ,'NAZ','STRING[40]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

STS.cntx_psh();
STS.index('SZ');
STS.prefix(_b);
{? STS.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$STS.ref();
      _selected.prefix($STS.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=STS.KOD;
      _tab.ODDZ:=STS.ODDZ;
      _tab.NAZ:=STS.NAZ;
      _tab.add();
      STS.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'KOD,ODDZ,NAZ',20,'Wybór stanowiska %1 do eksportu'@[{? _b='S' || 'sprzedaży'@ || 'zakupu'@ ?}],1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
STS.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 79d677b98cd91303aacbd5a5469a1acadab091298368674db0f4040e5afeebda997e1971fceb9f2cf51beb7c58a976e0eb0da0d7831e4080c8342c0e495476b46abd59d4e42d93de714601e20678b693f1d4347259cd4b83a6e43bdd51ed2697f95a38afa798e1f6db26df48b5cc662153a9b58211abdadc955cf3df6b383339

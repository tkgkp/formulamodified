:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_is_def.fml
:: Utworzony: 23.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu opisów świadczeń
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='IS_DEF';
_def.FUNPAR:='ZWS_PAR_PSWI';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Opisy świadczeń=1,1';
_def.NAME:='Opisy świadczeń'@;
_def.DESC:='Opisy świadczeń';

: źródło danych
_def.TABLE:="IS_DEF";

: prolog
_def.BEFORE:="
   R.cntx_psh();
   R.prefix();
   IS_DEF.cntx_psh();
   IS_DEF.f_clear();
   IS_DEF.index('OPIS');
   IS_DEF.prefix();
   ~~
";

: epilog
_def.AFTER:="
   IS_DEF.cntx_pop();
   R.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('OPIS',  MS.name(IS_DEF,'OPIS'),  1,,MS.comment(IS_DEF,'OPIS'));
   _env.define('RODZAJ',MS.name(IS_DEF,'RODZAJ'),1,,MS.comment(IS_DEF,'RODZAJ')+' [Z-ZFŚS, P-pozostałe]');
   _env.define('OSOBA', MS.name(IS_DEF,'OSOBA'), 1,,MS.comment(IS_DEF,'OSOBA'));
   _env.define('KW',    MS.name(IS_DEF,'KW'),    1,,MS.comment(IS_DEF,'KW'),,2);
   _env.define('R',     MS.name(IS_DEF,'R'),     1,,MS.comment(IS_DEF,'R'),type_of(0),0);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.OPIS.VALUE:=IS_DEF.OPIS;
   _buf.RODZAJ.VALUE:=IS_DEF.RODZAJ;
   _buf.OSOBA.VALUE:=IS_DEF.OSOBA;
   _buf.KW.VALUE:=IS_DEF.KW;
   _buf.R.VALUE:=IS_DEF.R().RN;
   _env.write_async('RUBRYKI',exec('r','xls__util'));
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_is_def',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_is_def',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.OPIS=''   || 'OPIS'
   |? _buf.RODZAJ='' || 'RODZAJ'
   |? _buf.OSOBA=''  || 'OSOBA'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('PZ'*_buf.RODZAJ)=0
|| _val.msg_inset('RODZAJ','P','Z');
   _val.RESULT:=0;
   return()

|? ('TN'*_buf.OSOBA)=0
|| _val.msg_inset('OSOBA','T','N');
   _val.RESULT:=0;
   return()

|? _buf.KW<0
|| _val.msg_range('KW',0);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(IS_DEF,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

: weryfikacja spójności referencyjnej
_rub:=null;
{? _buf.R<>0
|| {? ~R.find_tab(,'RN',,'=',_buf.R)
   || _val.msg_norec('R');
      _val.RESULT:=0;
      return()
   || _rub:=R.ref()
   ?}
?};

_ref:=null;
{? IS_DEF.find_tab(,'RODZAJ',,'=',_buf.RODZAJ,'OPIS',,'=',_buf.OPIS)
|| _ref:=IS_DEF.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 (%2)'[_a.OPIS,_a.RODZAJ]",
   'R',_rub
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,IS_DEF,"
   _a.OPIS:=_b.OPIS;
   _a.RODZAJ:=_b.RODZAJ;
   _a.OSOBA:=_b.OSOBA;
   _a.KW:=_b.KW;
   _a.R:=_c.R;
   ~~
");
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 d7d3b4bce0c47ed20caa7d75f5477dd6cd469b938534e4f0cca7574650c4948a6344f23dd1223532d4a4a27a14510027fa60b269d40fbf2acbffda95a131f40f0b939139d15798d01ff0eb994239e7c207dfcf1d044f9506f5bbdb556e97301f5ce93818a7e164c77a3e8bd3c77dd9a01b2e3e6cd79592c505c69833fd544414

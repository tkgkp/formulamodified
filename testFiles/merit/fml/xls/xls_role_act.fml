:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_users.fml
:: Utworzony: 02.07.2018
:: Autor: WH
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - obiekt z definicją, który należy zasilić - wynik działania exec('args_init','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='B_ACTROL';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USERS';
_def.FILE:='users.xlsx';
_def.SHEET:='Czynności dla ról'@+'=1,3';
_def.NAME:='Czynności dla ról'@;
_def.DESC:='Czynności dla ról'@;

_def.PREFIX:="B_ACTROL.index('MANUAL');B_ACTROL.prefix(REF.FIRMA,'T')";
_def.TABLE:="B_ACTROL";
_def.FIELDS:="exec('fields','xls_role_act',_a)";

_def.BEFORE:="B_ACTROL.cntx_psh()";
_def.AFTER:="B_ACTROL.cntx_pop()";
_def.SELECT:="exec('select','xls_role_act',_a)";

_def.EXPORT:="exec('export','xls_role_act',_a,_b)";
_def.VALIDATE:="exec('validate','xls_role_act',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_role_act',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Zwraca eksportowane pola tabeli i ich atrybuty w excel
::   WE: _a - obiekt z definicją pól który należy zasilić - wynik działania exec('args_fields','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_tooltip:=MS.name(B_ROLE,'NAME')+' '+'(max. %1 znaków). '@[$MS.fld_len(B_ROLE,'NAME')];
_tooltip+='Wymagane.'@;
_def.define('B_ROLE','Rola'@,1,,_tooltip);

_tooltip:=MS.name(B_ACTION,'UID')+' '+'(max. %1 znaków). '@[$MS.fld_len(B_ACTION,'UID')];
_tooltip+='Wymagane.'@;
_def.define('B_ACTION','Czynność'@,1,,_tooltip);

_tooltip:='Nieredagowalne. Dla nowych wierszy może być puste'@;
_def.define('ACT_NAME','Nazwa czynności'@,0,,_tooltip);
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól tabeli MacroBASE
::                      każdy obiekt ma taką strukturę:
::                        _obj_bridge.NAZWA_POLA.VALUE - wartość która będzie zapisana w EXCEL
::                        _obj_bridge.NAZWA_POLA.EDITABLE - czy komórka będzie edytowalna
::                        _obj_bridge.NAZWA_POLA.COLOR - kolor w formacie html czyli #ffffff
::                      Aktualny wewnętrzny obiekt można podejrzeć tu: exec('filler_obj_core','#excel_imex')
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_obj_bridge:=_b;
_result:=0;
{? B_ACTROL.B_ACTION<>null() & B_ACTROL.B_ROLE<>null()
|| _result:=1;
   _obj_bridge.B_ROLE.VALUE:=B_ACTROL.B_ROLE().NAME; _excel.write_async('B_ROLE',B_ACTROL.B_ROLE);
   _obj_bridge.B_ACTION.VALUE:=B_ACTROL.B_ACTION().UID;
   _obj_bridge.ACT_NAME.VALUE:=B_ACTROL.B_ACTION().NAME
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER   - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obj_new() - obiekt z resultem, wynik działania exec('args_valid','#excel_imex').
::                        Obiekt ten jest przekazywany do formuły na IMPORT. Pole RESULT decyduje czy formuła
::                        na import się wykona. Pole MSG służy to odpisania komunikatu
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_can_continue:=1;
:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _obj.B_ROLE=''
|| _can_continue:=0;
   _result.msg_empty('B_ROLE')
?};
{? _obj.B_ACTION=''
|| _can_continue:=0;
   _result.msg_empty('B_ACTION')
?};

{? _can_continue>0
|| _obj.B_ROLE:=exec('text2print','#string',_obj.B_ROLE)
?};

{? _can_continue>0
|| _obj.B_ACTION:=exec('text2print','#string',_obj.B_ACTION)
?};

_b_role:=null();
{? _can_continue>0
|| _b_role:=exec('FindInSet','#table','B_ROLE','UNIK',_obj.B_ROLE,REF.FIRMA,,1);
   {? _b_role=null()
   || _can_continue:=0;
      _result.msg_norec('B_ROLE')
   ?}
?};

_b_action:=null();
{? _can_continue>0
|| _b_action:=exec('FindInSet','#table','B_ACTION','UNIK',_obj.B_ACTION,,,1);
   {? _b_action=null()
   || _can_continue:=0;
      _result.msg_norec('B_ACTION')
   ?}
?};

{? _can_continue>0
|| _max:=MS.fld_len(B_ROLE,'NAME');
   {? +_obj.B_ROLE>_max
   || _result.msg_length('B_ROLE',_max);
      _can_continue:=0
   ?}
?};

{? _can_continue>0
|| _max:=MS.fld_len(B_ACTION,'UID');
   {? +_obj.B_ACTION>_max
   || _result.msg_length('B_ACTION',_max);
      _can_continue:=0
   ?}
?};

{? _can_continue>0
||
   B_ACTROL.index('UNIK');
   B_ACTROL.prefix(REF.FIRMA,_b_role,_b_action);
   {? B_ACTROL.first()=0
   || B_ACTROL.blank();
      B_ACTROL.FIRMA:=REF.FIRMA;
      B_ACTROL.B_ROLE:=_b_role;
      B_ACTROL.B_ACTION:=_b_action;
      B_ACTROL.MANUAL:='T';
      B_ACTROL.PROCES:='N';
      B_ACTROL.STARTD:=date();
      B_ACTROL.STARTT:=time();
      B_ACTROL.TM_START:=exec('create','#tm_stamp',B_ACTROL.STARTD,B_ACTROL.STARTT);
      B_ACTROL.U1_KOD:=exec('username','#users');
      B_ACTROL.U1_DANE:=userdata();
      _result.ACTION:='add'
   ||
      {? B_ACTROL.MANUAL<>'T'
      ||
         B_ACTROL.U1_KOD:=exec('username','#users');
         B_ACTROL.U1_DANE:=userdata();
         B_ACTROL.MANUAL:='T';
         _result.ACTION:='put'
      ?}
   ?}
?};
{? _can_continue=0
|| _result.RESULT:=0
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt z resultem walidatora i resultem importu, wynik działania exec('args_valid','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_ref:=null();

{? _result.ACTION='add'
||
   {? B_ACTROL.add(1)>0
   || _ref:=B_ACTROL.ref();
      _result.RESULT:=exec('after_add_actrole','#b_usrdom')
   || _result.RESULT:=0
   ?};
   {? _result.RESULT=0
   || _result.msg_insert('Czynność %1 dla roli %2'@[_obj.B_ACTION,_obj.B_ROLE])
   ?}
|? _result.ACTION='put'
|| _result.RESULT:=B_ACTROL.put();
   {? _result.RESULT>0
   || _ref:=B_ACTROL.ref()
   || _result.msg_update('Czynność %1 dla roli %2'@[_obj.B_ACTION,_obj.B_ROLE])
   ?}
?};
~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'B_ROLE','STRING[255]','Rola'@
   ,'B_ACTION','STRING[255]','Czynność'@
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'@
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'B_ROLE',,,'B_ACTION',,);

B_ACTROL.cntx_psh();
B_ACTROL.index('MANUAL');
B_ACTROL.prefix(REF.FIRMA,'T');
{? B_ACTROL.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$B_ACTROL.ref();
      _selected.prefix($B_ACTROL.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.B_ROLE:=B_ACTROL.B_ROLE().NAME;
      _tab.B_ACTION:=B_ACTROL.B_ACTION().UID;
      _tab.add();
      B_ACTROL.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'B_ROLE,B_ACTION',30,'Wybór użytkowników roli do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
B_ACTROL.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 830f367041b2016c6c4963085eef0c07476f964135d8622caa085228b20980da6fb498ec581650972e79c9d45f2eaacff951d3dc3f46e83947a3f9896de9738be6a8a9d285fce24f9a7cbaf1d7f429876d51d7c915f002792fd548a9e874b19f96504a3fae80c074e47bf3c2d60631c02f9009394b2e6eaa6a2627c943bf8a96

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kst_chor.fml
:: Utworzony: 11.10.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu chorób wynagradzanych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='KST_CHOR';
_def.FUNPAR:='ZWS_PAR_PPKD';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_kst_xlsx','xls__init');
_def.SHEET:='Choroby wynagradzane=1,1';
_def.NAME:='Choroby wynagradzane'@;
_def.DESC:='Choroby wynagradzane';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="KST_CHOR";

: prolog
_def.BEFORE:="
   R.cntx_psh();
   R.prefix();
   KST_CHOR.cntx_psh();
   KST_CHOR.f_clear();
   KST_CHOR.index('KST_CHOR');
   KST_CHOR.prefix();
   ~~
";

: epilog
_def.AFTER:="
   KST_CHOR.cntx_pop();
   R.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('WYN',MS.name(KST_CHOR,'WYN'),1,,MS.comment(KST_CHOR,'WYN'),type_of(0),0);
   _env.define('ZAS',MS.name(KST_CHOR,'ZAS'),1,,MS.comment(KST_CHOR,'ZAS'),type_of(0),0);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.WYN.VALUE:=KST_CHOR.WYN().RN;
   _buf.ZAS.VALUE:=KST_CHOR.ZAS().RN;
   _env.write_async('RUBRYKI',exec('r','xls__util'));
   _env.write_async('RUBRYKI',exec('r','xls__util'));
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_kst_chor',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kst_chor',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.WYN=0 || 'WYN'
   |? _buf.ZAS=0 || 'ZAS'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: weryfikacja spójności referencyjnej
{? ~R.find_tab(,'RN',,'=',_buf.WYN)
|| _val.msg_norec('WYN');
   _val.RESULT:=0;
   return()
|| _wyn:=R.ref()
?};
{? ~R.find_tab(,'RN',,'=',_buf.ZAS)
|| _val.msg_norec('ZAS');
   _val.RESULT:=0;
   return()
|| _zas:=R.ref()
?};

_ref:=null;
{? KST_CHOR.find_tab(,'WYN',,'=',_wyn)
|| _ref:=KST_CHOR.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 > %2'[_a.WYN,_a.ZAS]",
   'WYN',_wyn,
   'ZAS',_zas
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,KST_CHOR,"
   _a.WYN:=_c.WYN;
   _a.ZAS:=_c.ZAS;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 99bc7a7175fd856809497e427837d9a65addf78aef3547341fc0cba6cee1d187cf11a8907694a903fc58f86ea0bbc8868396cff2f59a44daba8b4074682243996f7cebb1cc5c15dd87728c3246dc9c6b72b9faf8ca90c934eee802351b99feacc878c45cc9e743768a1bdeede241ca5ed23f8b08b9c5544b1fede5306a16642d

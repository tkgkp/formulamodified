:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_t2mg.fml
:: Utworzony: 26.07.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typy dokumentów przypisane do magazynów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='T2MG';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='ZWS_PAR_LTDM';

_def.FILE:='magazyn.xlsx';
_def.SHEET:='Typy dokumentów w magazynach=1,1';
_def.NAME:='Typy dokumentów w magazynach'@;
_def.DESC:='Typy dokumentów w magazynach';

_def.PREFIX:="exec('prefix','xls_t2mg')";
_def.TABLE:="exec('table','xls_t2mg')";
_def.FIELDS:="exec('fields','xls_t2mg',_a)";

_def.BEFORE:="T2MG.cntx_psh()";
_def.AFTER:="T2MG.cntx_pop()";
_def.SELECT:="exec('select','xls_t2mg',_a)";

_def.EXPORT:="exec('export','xls_t2mg',_a,_b)";
_def.IMPORT:="exec('import','xls_t2mg',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_t2mg',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
T2MG.index('T');
T2MG.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
T2MG


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('MG','Magazyn',0,,'Symbol magazynu: 8 znakowy kod - duże litery.');
_def.define('T','Typ dokument',0,,'Typ dokumentu: 8 znakowy kod - duze litery');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

T2MG.index('T');
T2MG.prefix();
{? _validate.ACTION='add'
||
   T2MG.blank()
?};

T2MG.MG:=_validate.OBJ.MG;
T2MG.T:=_validate.OBJ.T;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
T2MG.cntx_psh();
_table.MG.VALUE:=T2MG.MG().SYM; _excel.write_async('MG',T2MG.MG);
_table.T.VALUE:=T2MG.T().T; _excel.write_async('TYPYDOK',T2MG.T);
T2MG.cntx_pop();
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('MG','T');

_result.OBJ.MG:={? _obj.MG='' || null() || exec('FindInSet','#table','MG','MAGAZYNY',_obj.MG,,,1) ?};
{? _result.OBJ.MG=null()
|| _result.MSG:={? _obj.MG=''
                || 'Nie podano magazynu dla typu dokumentu %1.'@[_obj.T]
                || 'Nie znaleziono magazynu %1.'@[_obj.MG]
                ?};
   _result.RESULT:=0
?};
_result.OBJ.T:={? _obj.T='' || null() || exec('FindInSet','#table','TYPYDOK','TYP',_obj.T,,,1) ?};
{? _result.OBJ.T=null()
|| _result.MSG:={? _obj.T=''
                || 'Nie podano typu dokumentu dla magazynu %1.'@[_obj.MG]
                || 'Nie znaleziono typu dokumentu %1.'@[_obj.T]
                ?};
   _result.RESULT:=0
?};


{? _result.RESULT=1
||
   T2MG.index('T');
   T2MG.prefix(_result.OBJ.MG,_result.OBJ.T);
   {? T2MG.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_t2mg',_obj,_mode,_result)
      ?}
   || _result.ACTION:='add';
      exec('record','xls_t2mg',_obj,_mode,_result)
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=T2MG.add(1)
   |? _validate.ACTION='put'
   || _result:=T2MG.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:='Dodanie typu dokumentu dla magazynu nie powiodło się.'@
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'MG','STRING[8]','Magazyn'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'T','STRING[8]','Typ dokumentu');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'MG',,,'T',,);

MG.cntx_psh();
TYPYDOK.cntx_psh();
T2MG.cntx_psh();
T2MG.index('T');
T2MG.prefix();
{? T2MG.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$T2MG.ref();
      _selected.prefix($T2MG.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.MG:=T2MG.MG().SYM;
      _tab.T:=T2MG.T().T;
      _tab.add();
      T2MG.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'MG,T',20,'Wybór typów dokumentów w magazynach'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
MG.cntx_pop();
TYPYDOK.cntx_pop();
T2MG.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:43 9ec5072ecf8aaaaabcedcba1a7eff3f6a15e9e01f79537f5fc414d4cade36f9960aa3ed2b4705cec68608d1bbf85014e0b7d6874f92f801df863570ea181835707bf169a2e158d6d8ef61b59ac52bddd8165b8b4d080505079ad5c1f5103a68b9e7673d2b96deb6b8aae1fbd078d7533c7604e54ed413a2d35225d5518465d5e

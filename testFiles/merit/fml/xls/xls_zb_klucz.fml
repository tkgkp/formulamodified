:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_zb_klucz.fml
:: Utworzony: 24.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu klucze zestawień zbiorczych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='ZB_KLUCZ';
_def.FUNPAR:='PPL_ZES_RZEZ';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Klucze zestawień zbiorczych=1,1';
_def.NAME:='Klucze zestawień zbiorczych'@;
_def.DESC:='Klucze zestawień zbiorczych (zmiana kolejności nie jest obsługiwana)';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="ZB_KLUCZ";

: prolog
_def.BEFORE:="
   ZB_TYP.cntx_psh();
   ZB_TYP.prefix();
   ZB_KLUCZ.cntx_psh();
   ZB_KLUCZ.f_clear();
   ZB_KLUCZ.index('NUMER');
   ZB_KLUCZ.prefix();
   ~~
";

: epilog
_def.AFTER:="
   ZB_KLUCZ.cntx_pop();
   ZB_TYP.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('TYP',     MS.name(ZB_KLUCZ,'TYP'),     0,,MS.comment(ZB_KLUCZ,'TYP'));
   _env.define('NAZWA',   MS.name(ZB_KLUCZ,'NAZWA'),   1,,MS.comment(ZB_KLUCZ,'NAZWA'));
   _env.define('AKRONIM', MS.name(ZB_KLUCZ,'AKRONIM'), 1,,MS.comment(ZB_KLUCZ,'AKRONIM'));
   _env.define('NAZWA_ZL',MS.name(ZB_KLUCZ,'NAZWA_ZL'),1,,MS.comment(ZB_KLUCZ,'NAZWA_ZL'));
   _env.define('AKRON_ZL',MS.name(ZB_KLUCZ,'AKRON_ZL'),1,,MS.comment(ZB_KLUCZ,'AKRON_ZL'));
   _env.define('FORMULA', MS.name(ZB_KLUCZ,'FORMULA'), 1,,MS.comment(ZB_KLUCZ,'FORMULA'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.TYP.VALUE:=ZB_KLUCZ.TYP().NAZWA;
   _buf.NAZWA.VALUE:=ZB_KLUCZ.NAZWA;
   _buf.AKRONIM.VALUE:=ZB_KLUCZ.AKRONIM;
   _buf.NAZWA_ZL.VALUE:=ZB_KLUCZ.NAZWA_ZL;
   _buf.AKRON_ZL.VALUE:=ZB_KLUCZ.AKRON_ZL;
   _buf.FORMULA.VALUE:=ZB_KLUCZ.FORMULA;
   _env.write_async('ZB_TYP',ZB_KLUCZ.TYP);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_zb_klucz',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_zb_klucz',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.TYP='' || 'TYP'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};
{? _buf.NAZWA='' & _buf.AKRONIM=''
|| _val.MSG:='"%1" lub "%2" musi być podane.'@[MS.name(ZB_KLUCZ,'NAZWA'),MS.name(ZB_KLUCZ,'AKRONIM')];
   _val.RESULT:=0;
   return()
?};

: błędne wartości
_max:=MS.fld_len(ZB_KLUCZ,'FORMULA');
{? +_buf.FORMULA>_max
|| _val.msg_length('FORMULA',_max);
   _val.RESULT:=0;
   return()
?};
_fld:='';
_join:='';
{! _ii:=1..P.fld_num()
|? _fld=''
|! _acr:=P.fld_acr(_ii);
   _name:=P.fld_name(_ii);
   _join:=P.fld_join(_ii);
   {? _acr=_buf.AKRONIM
:     znaleziono wg akronimu pola
   || {? _buf.NAZWA<>'' & _name<>_buf.NAZWA
      || _val.MSG:='"%1" niezgodna z "%2".'@[MS.name(ZB_KLUCZ,'NAZWA'),MS.name(ZB_KLUCZ,'AKRONIM')];
         _val.RESULT:=0;
         return()
      ?};
      _fld:=_acr;
      _buf.NAZWA:=_name

   |? _name=_buf.NAZWA
:     znaleziono wg nazwy pola
   || {? _buf.AKRONIM<>'' & _acr<>_buf.AKRONIM
      || _val.MSG:='"%1" niezgodna z "%2".'@[MS.name(ZB_KLUCZ,'NAZWA'),MS.name(ZB_KLUCZ,'AKRONIM')];
         _val.RESULT:=0;
         return()
      ?};
      _fld:=_acr;
      _buf.AKRONIM:=_acr
   ?}
!};
{? _fld=''
:  nie znaleziono pola
|| _val.MSG:='Pole "%1" nie występuje w kartotece "%2".'@
      [{? _buf.NAZWA='' || _buf.AKRONIM || _buf.NAZWA ?},P.comment()];
   _val.RESULT:=0;
   return()
?};
: obsługa złączenia
{? _join<>''
|| _fld:='';
   {? _buf.NAZWA_ZL='' & _buf.AKRON_ZL=''
   || _val.MSG:='"%1" lub "%2" musi być podane.'@[MS.name(ZB_KLUCZ,'NAZWA_ZL'),MS.name(ZB_KLUCZ,'AKRON_ZL')];
      _val.RESULT:=0;
      return()
   ?};
   _TAB:=($_join)();
   {! _ii:=1.._TAB.fld_num()
   |? _fld=''
   |! _acr:=_TAB.fld_acr(_ii);
      _name:=_TAB.fld_name(_ii);
      _join:=_TAB.fld_join(_ii);
      {? _acr=_buf.AKRON_ZL
:        znaleziono wg akronimu pola
      || {? _buf.NAZWA_ZL<>'' & _name<>_buf.NAZWA_ZL
         || _val.MSG:='"%1" niezgodna z "%2".'@[MS.name(ZB_KLUCZ,'NAZWA_ZL'),MS.name(ZB_KLUCZ,'AKRON_ZL')];
            _val.RESULT:=0;
            return()
         ?};
         _fld:=_acr;
         _buf.NAZWA_ZL:=_name

      |? _name=_buf.NAZWA_ZL
:        znaleziono wg nazwy pola
      || {? _buf.AKRON_ZL<>'' & _acr<>_buf.AKRON_ZL
         || _val.MSG:='"%1" niezgodna z "%2".'@[MS.name(ZB_KLUCZ,'NAZWA_ZL'),MS.name(ZB_KLUCZ,'AKRON_ZL')];
            _val.RESULT:=0;
            return()
         ?};
         _fld:=_acr;
         _buf.AKRON_ZL:=_acr
      ?}
   !};
   {? _fld=''
:     nie znaleziono pola
   || _val.MSG:='Pole "%1" nie występuje w kartotece "%2".'@
         [{? _buf.NAZWA_ZL='' || _buf.AKRON_ZL || _buf.NAZWA_ZL ?},_TAB.comment()];
      _val.RESULT:=0;
      return()
   ?}
?};

: kontrola składni
{? _buf.FORMULA<>''
|| _err:=form_chk($_buf.FORMULA);
   {? _err.first()
   || _val.MSG:='Formuła ma nieprawidłową składnię. Znak: %1, %2'@
         [$_err.ERR_COL,_err.ERR_DESC];
      _val.RESULT:=0;
      return()
   ?}
?};

: ostrzeżenia
_max:=MS.fld_len(ZB_TYP,'NAZWA');
{? +_buf.TYP>_max
|| _val.msg_length('TYP',_max);
   _buf.TYP:=_max+_buf.TYP
?};

: weryfikacja spójności referencyjnej
{? ~ZB_TYP.find_tab(,'NAZWA',,'=',_buf.TYP)
|| _val.msg_norec('TYP');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
ZB_KLUCZ.prefix(ZB_TYP.ref());
_nr:=ZB_KLUCZ.size()+1;
{? ZB_KLUCZ.find_tab(,
      'AKRONIM',,'=',_buf.AKRONIM,
      'AKRON_ZL',,'=',_buf.AKRON_ZL,
      'FORMULA',,'=',_buf.FORMULA
   )
|| _ref:=ZB_KLUCZ.ref();
   _nr:=ZB_KLUCZ.NUMER;
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1) %2, %3'[$_b.NUMER,_a.AKRONIM,_a.AKRON_ZL]",
   'TYP',ZB_TYP.ref(),
   'NUMER',_nr
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,ZB_KLUCZ,"
   _a.TYP:=_c.TYP;
   _a.NUMER:=_c.NUMER;
   _a.NAZWA:=_b.NAZWA;
   _a.AKRONIM:=_b.AKRONIM;
   _a.NAZWA_ZL:=_b.NAZWA_ZL;
   _a.AKRON_ZL:=_b.AKRON_ZL;
   _a.FORMULA:=_b.FORMULA;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 b282c859e4319f21f9b6601f068284ad4fd51f81a8183054dc7d10611721cd369dbb590e628a1f48a292f89e8dc454e09804266c1933ba08728b506b88199761c5e45ae8a1156d89580368be2b8da89fd8016d8f69d1fe7ff405f3ddcf7512e557a6e578d59c707602c47e5fc759c35ae97d61911f40880582a854bb25dd40be

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ro_cfg.fml
:: Utworzony: 04.12.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='RO_CFG';
_def.FUNPAR:='ZWS_PAR_ARDK';
_def.DOMAIN:=exec('name','#b_domain','ROD');
_def.FILE:=exec('def_zdo_xlsx','xls__init');
_def.SHEET:='Konfiguracja=1,1';
_def.NAME:='Konfiguracja dla zarządzania danymi osobowymi'@;
_def.DESC:='Konfiguracja dla zarządzania danymi osobowymi';
_def.ADD_ROWS:=0;
_def.HIDDEN:='N';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="RO_CFG";

: prolog
_def.BEFORE:="
   RO_REQC.cntx_psh();
   RO_FML.cntx_psh();
   RO_CFG.cntx_psh();
   RO_CFG.f_clear();
   RO_CFG.index('NUMER');
   RO_CFG.prefix();
   {? ~_a.is_export()
   || exec('init_slo','ro_req',1)
   ?};
   ~~
";

: epilog
_def.AFTER:="
   RO_CFG.cntx_pop();
   RO_FML.cntx_pop();
   RO_REQC.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('NUMER',   MS.name(RO_CFG,'NUMER'),0,,   MS.comment(RO_CFG,'NUMER'),,0);
   _env.define('RO_REQC', MS.name(RO_CFG,'RO_REQC'),0,, MS.comment(RO_CFG,'RO_REQC'));
   _env.define('KAT',     MS.name(RO_CFG,'KAT'),0,,     MS.comment(RO_CFG,'KAT'));
   _env.define('KOMENT',  MS.name(RO_CFG,'KOMENT'),0,,  MS.comment(RO_CFG,'KOMENT'));
   _env.define('FML_WCEL',MS.name(RO_CFG,'FML_WCEL'),0,,MS.comment(RO_CFG,'FML_WCEL'));
   _env.define('FML_SZUK',MS.name(RO_CFG,'FML_SZUK'),0,,MS.comment(RO_CFG,'FML_SZUK'));
   _env.define('FML_INFO',MS.name(RO_CFG,'FML_INFO'),0,,MS.comment(RO_CFG,'FML_INFO'));
   _env.define('FML_RET', MS.name(RO_CFG,'FML_RET'),0,, MS.comment(RO_CFG,'FML_RET'));
   _env.define('FML_ZAP', MS.name(RO_CFG,'FML_ZAP'),0,, MS.comment(RO_CFG,'FML_ZAP'));
   _env.define('FML_ZGO', MS.name(RO_CFG,'FML_ZGO'),0,, MS.comment(RO_CFG,'FML_ZGO'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.NUMER.VALUE:=RO_CFG.NUMER;
   _buf.RO_REQC.VALUE:=RO_CFG.RO_REQC().CODE;
   _buf.KAT.VALUE:=RO_CFG.KAT;
   _buf.KOMENT.VALUE:=RO_CFG.KOMENT;
   _buf.FML_WCEL.VALUE:=RO_CFG.FML_WCEL().SYMBOL;
   _buf.FML_SZUK.VALUE:=RO_CFG.FML_SZUK().SYMBOL;
   _buf.FML_INFO.VALUE:=RO_CFG.FML_INFO().SYMBOL;
   _buf.FML_RET.VALUE:=RO_CFG.FML_RET().SYMBOL;
   _buf.FML_ZAP.VALUE:=RO_CFG.FML_ZAP().SYMBOL;
   _buf.FML_ZGO.VALUE:=RO_CFG.FML_ZGO().SYMBOL;

   _env.write_async('RO_REQC',RO_CFG.RO_REQC);
   _env.write_async('RO_FML',RO_CFG.FML_WCEL);
   _env.write_async('RO_FML',RO_CFG.FML_SZUK);
   _env.write_async('RO_FML',RO_CFG.FML_INFO);
   _env.write_async('RO_FML',RO_CFG.FML_RET);
   _env.write_async('RO_FML',RO_CFG.FML_ZAP);
   _env.write_async('RO_FML',RO_CFG.FML_ZGO);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_ro_cfg',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ro_cfg',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.NUMER<=0    || 'NUMER'
   |? _buf.RO_REQC=''  || 'RO_REQC'
   |? _buf.KAT=''      || 'KAT'
   |? _buf.KOMENT=''   || 'KOMENT'
   |? _buf.FML_WCEL='' || 'FML_WCEL'
   |? _buf.FML_SZUK='' || 'FML_SZUK'
   |? _buf.FML_INFO='' || 'FML_INFO'
   |? _buf.FML_RET=''  || 'FML_RET'
   |? _buf.FML_ZAP=''  || 'FML_ZAP'
   |? _buf.FML_ZGO=''  || 'FML_ZGO'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(RO_REQC,'CODE');
{? +_buf.RO_REQC>_max
|| _val.msg_length('RO_REQC',_max);
   _buf.RO_REQC:=_max+_buf.RO_REQC
?};
_max:=MS.fld_len(RO_CFG,'KAT');
{? +_buf.KAT>_max
|| _val.msg_length('KAT',_max);
   _buf.KAT:=_max+_buf.KAT
?};
_max:=MS.fld_len(RO_CFG,'KOMENT');
{? +_buf.KOMENT>_max
|| _val.msg_length('KOMENT',_max);
   _buf.KOMENT:=_max+_buf.KOMENT
?};
_fld:=spli_str('FML_WCEL,FML_SZUK,FML_INFO,FML_RET,FML_ZAP,FML_ZGO',',');
_max:=MS.fld_len(RO_FML,'SYMBOL');
_len:=obj_len(_fld);
{! _ii:=1.._len
|! _acr:=_fld[_ii];
   {? ($('+_a.%1>_b'[_acr]))(_buf,_max)
   || _val.msg_length(_acr,_max);
      ($('_a.%1:=_b+_a.%1'[_acr]))(_buf,_max)
   ?}
!};

: weryfikacja spójności referencyjnej
{! _ii:=1.._len
|! {? RO_FML.find_tab(,
         'RODZAJ',,'=',_fld[_ii],
         'SYMBOL',,'=',($('_a.%1'[_fld[_ii]]))(_buf)
      )
   || _fld[_ii]:=RO_FML.ref()
   || _val.msg_norec(_fld[_ii]);
      _val.RESULT:=0;
      return()
   ?}
!};
_ro_reqc:=null;
{? RO_REQC.find_tab(,'CODE',,'=',_buf.RO_REQC)
|| _ro_reqc:=RO_REQC.ref()
|| _val.msg_norec('RO_REQC');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? RO_CFG.find_tab(,'NUMER',,'=',_buf.NUMER)
|| _ref:=RO_CFG.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[$_a.NUMER,_a.KOMENT]",
   'RO_REQC',_ro_reqc,
   'FML_WCEL',_fld[1],
   'FML_SZUK',_fld[2],
   'FML_INFO',_fld[3],
   'FML_RET', _fld[4],
   'FML_ZAP', _fld[5],
   'FML_ZGO', _fld[6]
);

obj_del(_fld);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,RO_CFG,"
   _a.NUMER:=_b.NUMER;
   _a.RO_REQC:=_c.RO_REQC;
   _a.KAT:=_b.KAT;
   _a.KOMENT:=_b.KOMENT;
   _a.FML_WCEL:=_c.FML_WCEL;
   _a.FML_SZUK:=_c.FML_SZUK;
   _a.FML_INFO:=_c.FML_INFO;
   _a.FML_RET:=_c.FML_RET;
   _a.FML_ZAP:=_c.FML_ZAP;
   _a.FML_ZGO:=_c.FML_ZGO;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 644a641181f576be00a770f284184175e8ed3829e60631e992c61731e90c4282b3962074883ed18fc339216e85b9324822faf3e813a1b08e98c9283e2c1d2581f6748c3a6a3cdc9b6b9ca921d67bc9a36149e218fae01a57e442eac2d3199339d77c4907e5a6ad2c337a96973e385876c58b581aabf6fa528a71379652ae7bc3

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_a_scp.fml
:: Utworzony: 18.09.2018
:: Autor: DRO
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu systemów czasu pracy
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='A_SCP';
_def.FUNPAR:='ZWS_PAR_PSCP';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Systemy czasu pracy=1,1';
_def.NAME:='Systemy czasu pracy'@;
_def.DESC:='Systemy czasu pracy';

: źródło danych
_def.TABLE:="A_SCP";

: prolog
_def.BEFORE:="
   A_SCP.cntx_psh();
   A_SCP.index('A_SCP');
   A_SCP.prefix();
   ~~
";

: epilog
_def.AFTER:="
   A_SCP.cntx_pop();
   ~~
";

: definicja kolumn
: ze względu na błąd krytyczny mechanizmu przenoszenia danych przez arkusze Excel,
: odstęp czasowy przechowywany jest w postaci napisu a następnie odtwarzany
_def.FIELDS:="
   _env:=_a;
   _env.define('NAZ',MS.name(A_SCP,'NAZ'),1,,MS.comment(A_SCP,'NAZ'));
   _env.define('ND', MS.name(A_SCP,'ND'), 1,,MS.comment(A_SCP,'ND'),type_of(''));
   _env.define('NST',MS.name(A_SCP,'NST'),1,,MS.comment(A_SCP,'NST'),type_of(''));
   _env.define('OD', MS.name(A_SCP,'OD'), 1,,MS.comment(A_SCP,'OD'),type_of(''));
   _env.define('OT', MS.name(A_SCP,'OT'), 1,,MS.comment(A_SCP,'OT'),type_of(''));
   _env.define('DOM',MS.name(A_SCP,'DOM'),1,,MS.comment(A_SCP,'DOM'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.NAZ.VALUE:=A_SCP.NAZ;
   _buf.ND.VALUE:=A_SCP.ND$3;
   _buf.NST.VALUE:=A_SCP.NST$3;
   _buf.OD.VALUE:=A_SCP.OD$3;
   _buf.OT.VALUE:=A_SCP.OT$3;
   _buf.DOM.VALUE:=A_SCP.DOM;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_a_scp',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_a_scp',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.NAZ='' || 'NAZ'
   |? _buf.DOM='' || 'DOM'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? (_nd:=exec('parseTime','#convert',_buf.ND))=~~
|| _val.MSG:='"%1" nie zawiera poprawnego odstępu czasowego.'@[MS.name(A_SCP,'ND')];
   _val.RESULT:=0;
   return()

|? (_nst:=exec('parseTime','#convert',_buf.NST))=~~
|| _val.MSG:='"%1" nie zawiera poprawnego odstępu czasowego.'@[MS.name(A_SCP,'NST')];
   _val.RESULT:=0;
   return()

|? (_od:=exec('parseTime','#convert',_buf.OD))=~~
|| _val.MSG:='"%1" nie zawiera poprawnego odstępu czasowego.'@[MS.name(A_SCP,'OD')];
   _val.RESULT:=0;
   return()

|? (_ot:=exec('parseTime','#convert',_buf.OT))=~~
|| _val.MSG:='"%1" nie zawiera poprawnego odstępu czasowego.'@[MS.name(A_SCP,'OT')];
   _val.RESULT:=0;
   return()

|? ('TN'*_buf.DOM)=0
|| _val.msg_inset('DOM','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(A_SCP,'NAZ');
{? +_buf.NAZ>_max
|| _val.msg_length('NAZ',_max);
   _buf.NAZ:=_max+_buf.NAZ
?};

_ref:=null;
{? A_SCP.find_tab(,'NAZ',,'=',_buf.NAZ)
|| _ref:=A_SCP.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[_a.NAZ]",
   'ND',_nd,
   'NST',_nst,
   'OD',_od,
   'OT',_ot
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,A_SCP,"
   _a.NAZ:=_b.NAZ;
   _a.ND:=_c.ND;
   _a.NST:=_c.NST;
   _a.OD:=_c.OD;
   _a.OT:=_c.OT;
   _a.DOM:=_b.DOM;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:46 06dbecd4fa666e9e98d05d84df34e05cf99d62005fe8f69a1bfb948b8d9c85b5cd992a6b7af52b67f4cd3504fb7b02166f7cdb2ab24443634b42f47d5cc3acd816652d8948ccfc972f9f4104ef92fd9520abc9e9b778f6793e5f4f4f6e04511741e6665406c4d9d48168012d11bc65cf4df0bb0e9b5ad70df907987fc94b8149

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_eanl.fml
:: Utworzony: 26.07.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu lokalizacji magazynów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='EANL';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='ZWS_PAR_LMAG';

_def.FILE:='magazyn.xlsx';
_def.SHEET:='Lokalizacje=1,1';
_def.NAME:='Lokalizacje magazynów'@;
_def.DESC:='Lokalizacje magazynów';

_def.PREFIX:="exec('prefix','xls_eanl')";
_def.TABLE:="exec('table','xls_eanl')";
_def.FIELDS:="exec('fields','xls_eanl',_a)";

_def.BEFORE:="EANL.cntx_psh()";
_def.AFTER:="EANL.cntx_pop()";
_def.SELECT:="exec('select','xls_eanl',_a)";

_def.EXPORT:="exec('export','xls_eanl',_a,_b)";
_def.IMPORT:="exec('import','xls_eanl',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_eanl',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
EANL.index('KOD');
EANL.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
EANL


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('MG','Magazyn',0,,'Symbol magazynu: 8 znakowy kod - duże litery.');
_def.define('KOD','Kod lokalizacji',0,,'Kod lokalizacji: 20 znaków');
_def.define('EAN','Kod kreskowy',0,,'Kod kreskowy: 30 znaków');
_def.define('OP','Opis lokalizacji',1,,'Opis lokalizacji');
_def.define('DOK','Dok załadunkowy/wyładunkowy',1,,'Czy dok załadunkowy/wyładunkowy? (T/N)');
_def.define('SZER','Szerokość',1,,'Szerokość w [m]');
_def.define('DLUG','Długość',1,,'Długość w [m]');
_def.define('WYS','Wysokość',1,,'Wysokość w [m]');
_def.define('TEMP_OD','Temperatura składowania od',1,,'Temperatura składowania od w stopniach Celsjusza');
_def.define('TEMP_DO','Temperatura składowania do',1,,'Temperatura składowania do w stopniach Celsjusza');
_def.define('MASA','Ładowność',1,,'Ładowność w [kg]');
_def.define('ILPAL','Maksymalna ilość palet',1,,'Maksymalna ilość palet domyślnych');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

EANL.index('KOD');
EANL.prefix();
{? _validate.ACTION='add'
||
   EANL.blank();
   EANL.MG:=_validate.OBJ.MG
?};

EANL.KOD:=_obj.KOD;
EANL.EAN:=_obj.EAN;
EANL.OP:=_obj.OP;
EANL.SZER:=_obj.SZER;
EANL.DLUG:=_obj.DLUG;
EANL.WYS:=_obj.WYS;
EANL.TEMP_OD:=_obj.TEMP_OD;
EANL.TEMP_DO:=_obj.TEMP_DO;
EANL.MASA:=_obj.MASA;
EANL.ILPAL:=_obj.ILPAL;
EANL.DOK:=_obj.DOK;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
EANL.cntx_psh();
_table.MG.VALUE:=EANL.MG().SYM; _excel.write_async('MG',EANL.MG);
_table.KOD.VALUE:=EANL.KOD;
_table.EAN.VALUE:=EANL.EAN;
_table.OP.VALUE:=EANL.OP;
_table.SZER.VALUE:=EANL.SZER;
_table.DLUG.VALUE:=EANL.DLUG;
_table.WYS.VALUE:=EANL.WYS;
_table.TEMP_OD.VALUE:=EANL.TEMP_OD;
_table.TEMP_DO.VALUE:=EANL.TEMP_DO;
_table.MASA.VALUE:=EANL.MASA;
_table.ILPAL.VALUE:=EANL.ILPAL;
_table.DOK.VALUE:=EANL.DOK;
EANL.cntx_pop();
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('MG');

_result.OBJ.MG:={? _obj.MG='' || null() || exec('FindInSet','#table','MG','MAGAZYNY',_obj.MG,,,1) ?};
{? _result.OBJ.MG=null()
|| _result.MSG:={? _obj.MG=''
                || 'Nie podano magazynu dla lokalizacji %1.'@[_obj.KOD]
                || 'Nie znaleziono magazynu %2 dla lokalizacji %1.'@[_obj.KOD,_obj.MG]
                ?};
   EANX.MG:=_result.OBJ.MG;
   _result.RESULT:=0
?};

{? _result.RESULT=1
||
   EANL.index('KOD');
   EANL.prefix(_obj.KOD,);
   {? EANL.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_eanl',_obj,_mode,_result);
         {? exec('chk_eanl','magdok_wspolne',,_result.ACTION)<>'' || _result.RESULT:=0 ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_eanl',_obj,_mode,_result);
      {? exec('chk_eanl','magdok_wspolne',,_result.ACTION)<>'' || _result.RESULT:=0 ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=EANL.add(1)
   |? _validate.ACTION='put'
   || _result:=EANL.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:='Dodanie lokalizacji %1 nie powiodło się.'@[EANL.KOD]
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'KOD','STRING[20]','Kod lokalizacji'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'MG','STRING[8]','Magazyn'
   ,'EAN','STRING[30]','Kod kreskowy'
   ,'OP','STRING[120]','Opis lokalizacji'
   ,'SZER','REAL','Szerokość [m]'
   ,'DLUG','REAL','Długość [m]'
   ,'WYS','REAL','Wysokość [m]'
   ,'TEMP_OD','REAL','Temperatura od [C]'
   ,'TEMP_DO','REAL','Temperatura do [C]'
   ,'MASA','REAL','Ładowność [kg]'
   ,'ILPAL','INTEGER','Ilość palet'
   ,'DOK','STRING[1]','Dok Z/W');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

MG.cntx_psh();
EANL.cntx_psh();
EANL.index('MG');
EANL.prefix();
{? EANL.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$EANL.ref();
      _selected.prefix($EANL.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.MG:=EANL.MG().SYM;
      _tab.KOD:=EANL.KOD;
      _tab.EAN:=EANL.EAN;
      _tab.OP:=EANL.OP;
      _tab.SZER:=EANL.SZER;
      _tab.DLUG:=EANL.DLUG;
      _tab.WYS:=EANL.WYS;
      _tab.TEMP_OD:=EANL.TEMP_DO;
      _tab.TEMP_DO:=EANL.TEMP_DO;
      _tab.MASA:=EANL.MASA;
      _tab.ILPAL:=EANL.ILPAL;
      _tab.DOK:=EANL.DOK;
      _tab.add();
      EANL.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'MG,KOD,EAN,OP,DOK,SZER,DLUG,WYS,TEMP_OD,TEMP_DO,MASA,ILPAL',10,'Wybór lokalizacji w magazynach do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
MG.cntx_pop();
EANL.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 86fc61f5945855e14047e0039cb979a08a036cdbd83353939efebb3097c6f79bbeb0b2dc69641c25abad7956d3a22ff4f751f300e81444cfa54f849391488bd240aad3237a1866f0981d2de4536a575882dc2f42cdb8ef8c7c7bcc6812ec343244ecda96c03366148e6055efd3271e8174f6ac962aca4977ecf1f94a698f5e16

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_dezezw.fml
:: Utworzony: 23.08.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu domyślnych rachunków bankowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='RBK_RD_ODDZ';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_DEF_ODDZ';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Domyślne rachunki bankowe=1,1';
_def.NAME:='Domyślne rachunki bankowe'@;
_def.DESC:='Domyślne rachunki bankowe';

_def.PREFIX:="RBK_RD.index('SKID_RBK'); RBK_RD.prefix()";
_def.TABLE:="RBK_RD";
_def.FIELDS:="exec('fields','xls_rbk_rd',_a)";

_def.BEFORE:="RBK_RD.cntx_psh()";
_def.AFTER:="RBK_RD.cntx_pop()";

_def.EXPORT:="exec('export','xls_rbk_rd',_a,_b)";
_def.VALIDATE:="exec('validate','xls_rbk_rd',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rbk_rd',_a,_b,_c)";
_def.SELECT:="";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('ODDZ','Oddział',1,,'1 znakowy kod oddziału');
_def.define('STS','Stanowisko sprzedaży',1,,'8 znakowy kod stanowiska sprzedaży');
_def.define('SKID_RBK','Rachunek bankowy',1,,'50 znakowy numer rachunku bankowego');
_def.define('FIRMA','FIRMA',1,,'3 znakowy symbol firmy');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_excel.DIR;

_result:=1;
_oddz:=exec('FindInSet','#table','ODDZ','KOD2',RBK_RD.ODDZ,,,1,,null());
_table.ODDZ.VALUE:=RBK_RD.ODDZ; _excel.write_async('ODDZ',_oddz);
_table.STS.VALUE:=RBK_RD.STS().KOD; _excel.write_async('STS',RBK_RD.STS);
_table.SKID_RBK.VALUE:=RBK_RD.SKID_RBK().N; _excel.write_async('SKID_RBK',RBK_RD.SKID_RBK);
_table.FIRMA.VALUE:=3+RBK_RD.SKID_RBK().TAB;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

{? _obj.ODDZ='' & _obj.STS=''
|| _result.RESULT:=0;
   _result.MSG:='Nie podano oddziału albo stanowiska sprzedaży.'@;
   return(~~)
?};

{? _obj.ODDZ<>'' & _obj.STS<>''
|| _result.RESULT:=0;
   _result.MSG:='Można podać oddział albo stanowisko sprzedaży.'@;
   return(~~)
?};

{? _obj.FIRMA=''
|| _result.RESULT:=0;
   _result.MSG:='Nie podano symbolu firmy.'@;
   return(~~)
?};

_result.RESULT:=1;
{? _obj.SKID_RBK=''
|| _result.RESULT:=0;
   _result.MSG:='Nie podano numeru rachunku bankowego.'@;
   return(~~)
?};
_skid_rbk:=exec('FindInSet','#table','SKID_RBK','WEKTOR',_obj.SKID_RBK,_obj.FIRMA+'INFO',,1,,null());
{? _skid_rbk=null()
|| _result.RESULT:=0;
   _result.MSG:='Nie znaleziono rachunku bankowego numer %1.'@[_obj.SKID_RBK];
   return(~~)
?};
_sts:=null;
{? _obj.STS<>''
|| _sts:=exec('FindInSet','#table','STS','SZ',_obj.STS,'S',,1,,null());
   {? _sts=null()
   || _result.RESULT:=0;
      _result.MSG:='Nie znaleziono stanowiska sprzedaży %1.'@[_obj.STS];
      return(~~)
   ?}
|| _oddz:=exec('FindInSet','#table','ODDZ','KOD2',_obj.ODDZ,,,1,,null());
   {? _oddz=null()
   || _result.RESULT:=0;
      _result.MSG:='Nie znaleziono oddziału %1.'@[_obj.ODDZ];
      return(~~)
   ?}
?};
RBK_RD.index('USTAW');
RBK_RD.prefix(_obj.ODDZ,_sts,_skid_rbk);
{? RBK_RD.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT=1
|| {? _result.ACTION='add' || RBK_RD.blank() ?};

   RBK_RD.ODDZ:=_obj.ODDZ;
   RBK_RD.STS:=_sts;
   RBK_RD.SKID_RBK:=_skid_rbk;
   SKID_RBK.cntx_psh();
   RBK_RD.WAL:=RBK_RD.SKID_RBK().WAL;
   SKID_RBK.cntx_pop()
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=RBK_RD.add(1)

   |? _validate.ACTION='put'
   ||
      _result:=RBK_RD.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie domyślnego rachunku bankowego nie powiodło się.'@
   ||
      _validate.MSG:='Poprawa domyślnego rachunku bankowego nie powiodła się.'@
   ?}
?};
_result

:Sign Version 2.0 jowisz:1045 2023/08/24 15:49:34 372a3c11805889caf3b242e69fc2e653ad00bb6d483ca6193cc347ebbdba8adecf01204fd8844deea961540956a070529a449b3fdf4fa4f2d0f23009b842b5d6954995243401bd1ab02eb64a934b219257e6163a4a91d63a0adf885b5f5d8565779c2e6a7527628fece424f709560bad755252849a1ab42ab688939aa34e3421

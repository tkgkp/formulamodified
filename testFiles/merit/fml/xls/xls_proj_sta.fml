:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_proj_sta.fml
:: Utworzony: 23.07.2021
:: Autor: PD
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================
\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [21.37]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PROJ_STA';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_PSTA';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Statusy projektów=1,1';
_def.NAME:='Statusy projektów'@;
_def.DESC:='Statusy projektów';
_def.MULTIFIR:='T';

_def.PREFIX:="exec('prefix','xls_proj_sta')";
_def.TABLE:="exec('table','xls_proj_sta')";
_def.FIELDS:="exec('fields','xls_proj_sta',_a)";

_def.BEFORE:="PROJ_STA.cntx_psh()";
_def.AFTER:="PROJ_STA.cntx_pop()";
_def.SELECT:="exec('select','xls_proj_sta',_a)";

_def.EXPORT:="exec('export','xls_proj_sta',_a,_b)";
_def.VALIDATE:="exec('validate','xls_proj_sta',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_proj_sta',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
PROJ_STA.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
PROJ_STA


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod',1,,'6 znakowy Kod');
_def.define('STATUS','Status',1,,'50 znakowy Status');
_def.define('AKT','Aktywność',1,,'T/N czy dla aktywnych projektów');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PROJ_STA.prefix();
{? _validate.ACTION='add'
||
   PROJ_STA.blank()
?};

PROJ_STA.KOD:=_obj.KOD;
PROJ_STA.STATUS:=_obj.STATUS;
PROJ_STA.AKT:=_obj.AKT;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=PROJ_STA.KOD;
_table.STATUS.VALUE:=PROJ_STA.STATUS;
_table.AKT.VALUE:=PROJ_STA.AKT;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? (+_obj.KOD)>6 || _result.msg_length('KOD',6); _result.RESULT:=0 ?};
{? (+_obj.STATUS)>50 || _result.msg_length('STATUS',50); _result.RESULT:=0 ?};
{? (+_obj.AKT)>1 || _result.msg_length('AKT',1); _result.RESULT:=0 ?};
{? _obj.AKT<>'N' & _obj.AKT<>'T' || _result.msg_inset('AKT','N','T');_result.RESULT:=0 ?};
{? _obj.KOD='' || _result.msg_empty('KOD'); _result.RESULT:=0 ?};
{? _obj.STATUS='' || _result.msg_empty('STATUS'); _result.RESULT:=0 ?};

PROJ_STA.index('KOD');
PROJ_STA.prefix(_obj.KOD,);
{? PROJ_STA.first()
||
   {? _mode=0
   || _result.RESULT:=0
   ||
      _result.ACTION:='put';
      exec('record','xls_proj_sta',_obj,_mode,_result)
   ?}
||
   _result.ACTION:='add';
   exec('record','xls_proj_sta',_obj,_mode,_result)
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      PROJ_STA.prefix();
      _result:=PROJ_STA.add(1)
   |? _validate.ACTION='put'
   ||
      PROJ_STA.prefix();
      _result:=PROJ_STA.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie statusu %1 nie powiodło się.'@[_obj.KOD]
   ||
      _validate.MSG:='Poprawa statusu %1 nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2,'KOD','STRING[6]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'STATUS','STRING[250]','Status'
   ,'AKT','STRING[1]','Aktywność'
   );

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

PROJ_STA.cntx_psh();
PROJ_STA.index('KOD');
PROJ_STA.prefix();
{? PROJ_STA.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$PROJ_STA.ref();
      _selected.prefix($PROJ_STA.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=PROJ_STA.KOD;
      _tab.STATUS:=PROJ_STA.STATUS;
      _tab.AKT:=PROJ_STA.AKT;
      _tab.add();
      PROJ_STA.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'KOD',6,'Wybór statusów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PROJ_STA.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 1699abf10982a547761fe6a4a9f0fe01ba24ae6bc5538514d105c796c64283f3acb08fa77394d05e2faeae46aa5b73b0cf2006492ae120bd45478132095e5111cf62b45f0c31047eb740bca6deb77bda23a74be47f20e6c4e0d55e348ba7b03d45bb7719edc82a48323019fc8f10a307142b315b6f22666376d53b2a3892a825

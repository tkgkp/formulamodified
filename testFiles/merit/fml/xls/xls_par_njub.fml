:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_par_njub.fml
:: Utworzony: 16.09.2019
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu tabeli z progami stażu dla premii jubileuszowej
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='PAR_NJUB';
_def.FUNPAR:='ZWS_PAR_PNJU';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:='personel_stale.xlsx';
_def.SHEET:='Staż do nagrody jubileuszowej=1,1';
_def.NAME:='Staż do nagrody jubileuszowej'@;
_def.DESC:='Staż do nagrody jubileuszowej';

: źródło danych
_def.TABLE:="PAR_NJUB";

: prolog
_def.BEFORE:=
   "  PAR_NJUB.cntx_psh();
      PAR_NJUB.f_clear();
      PAR_NJUB.index('PAR_NJUB');
      PAR_NJUB.prefix()
   ";

: epilog
_def.AFTER:="PAR_NJUB.cntx_pop()";

: definicja kolumn
_def.FIELDS:=
   "  _env:=_a;
      _env.define('STAZ', MS.name(PAR_NJUB,'STAZ'), 1,,MS.comment(PAR_NJUB,'STAZ'),,0);
      _env.define('PROC',MS.name(PAR_NJUB,'PROC'),1,,MS.comment(PAR_NJUB,'PROC'),,2);
      ~~
   ";

: zawartość wiersza
_def.EXPORT:=
   "  _env:=_a;
      _buf:=_b;
      _buf.STAZ.VALUE:=PAR_NJUB.STAZ;
      _buf.PROC.VALUE:=PAR_NJUB.PROC;
      1
   ";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_par_njub',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_par_njub',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.STAZ=0 || 'STAZ'
   || ''
   ?};
{? _fld<>''
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? _buf.STAZ<0
|| _val.msg_range('STAZ',0);
   _val.RESULT:=0;
   return()
|? _buf.PROC<0
|| _val.msg_range('PROC',0);
   _val.RESULT:=0;
   return()
?};

_ref:=null();
{? PAR_NJUB.find_tab(,'STAZ',,'=',_buf.STAZ)
|| _ref:=PAR_NJUB.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('PAR_NJUB','to_str');
_obj.to_str:="'%1: %2'[$_a.STAZ,$_a.PROC]";
_obj.PAR_NJUB:=_ref;

{? _ref=null()
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.STAZ:=_b.STAZ;
   _a.PROC:=_b.PROC;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? PAR_NJUB.seek(_obj.PAR_NJUB)
   || _set(PAR_NJUB,_buf);
      {? ~PAR_NJUB.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,PAR_NJUB.comment());
      _val.RESULT:=0
   ?}
|? _val.ACTION='add'
:  brak ograniczeń
|| PAR_NJUB.blank();
   _set(PAR_NJUB,_buf);
   {? ~PAR_NJUB.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1028 2019/10/14 09:22:00 54abc7843ee54121c786372a9a2bc6a9d9c9d89209dca43529a764179c5bcbb957a306657ceb933ae81fe7a235d7c9b25dd50652193f4950880fc6ea480572d5ce904ea4b2ae7c733413b12f90fec61e20fa59de429be50e7cbabab369f8680e83f097cae30f8e75d983a6159aa047013d3e7f461c2283928cf1499fe0c11422

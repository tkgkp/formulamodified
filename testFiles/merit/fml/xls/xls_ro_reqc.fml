:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ro_reqc.fml
:: Utworzony: 04.12.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu celów przetwarzania
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='RO_REQC';
_def.FUNPAR:='ZWS_PAR_ARDK';
_def.DOMAIN:=exec('name','#b_domain','ROD');
_def.FILE:=exec('def_zdo_xlsx','xls__init');
_def.SHEET:='Cele przetwarzania=1,1';
_def.NAME:='Cele przetwarzania danych osobowych'@;
_def.DESC:='Cele przetwarzania danych osobowych';
_def.ADD_ROWS:=0;
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="RO_REQC";

: prolog
_def.BEFORE:="
   RO_REQC.cntx_psh();
   RO_REQC.f_clear();
   RO_REQC.index('CODE');
   RO_REQC.prefix();
   ~~
";

: epilog
_def.AFTER:="
   RO_REQC.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('CODE',MS.name(RO_REQC,'CODE'),0,,MS.comment(RO_REQC,'CODE'));
   _env.define('DESC',MS.name(RO_REQC,'DESC'),0,,MS.comment(RO_REQC,'DESC'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.CODE.VALUE:=RO_REQC.CODE;
   _buf.DESC.VALUE:=RO_REQC.DESC;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_ro_reqc',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ro_reqc',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.CODE='' || 'CODE'
   |? _buf.DESC='' || 'DESC'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(RO_REQC,'CODE');
{? +_buf.CODE>_max
|| _val.msg_length('CODE',_max);
   _buf.CODE:=_max+_buf.CODE
?};
_max:=MS.fld_len(RO_REQC,'DESC');
{? +_buf.DESC>_max
|| _val.msg_length('DESC',_max);
   _buf.DESC:=_max+_buf.DESC
?};

_ref:=null;
{? RO_REQC.find_tab(,'CODE',,'=',_buf.CODE)
|| _ref:=RO_REQC.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.CODE,_a.DESC]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,RO_REQC,"
   _a.CODE:=_b.CODE;
   _a.DESC:=_b.DESC;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 6d2f9ac2dfd311b19f85f169ace64ff23d5809e174bcf6dfc5aa327891964ab961addbf2047d9e331032184e7e9bf560d39cec34d0f15b5e0f55142fda84150cdeb592202f1b66fcb1d38686b1ef055d629f151840e9c9c4b432d2c36b785cb5a7f872e97d034b47c91fb41c756a5f3e8126f8c01ed8f815879797aebb733c97

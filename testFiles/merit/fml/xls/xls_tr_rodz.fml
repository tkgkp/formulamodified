:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tr_rodz.fml
:: Utworzony: 28.08.2019
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji kategorii transportu
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TR_RODZ';
_def.DOMAIN:=exec('name','#b_domain','LTR');
_def.FUNPAR:='ZWS_ZLE_RODZ';
_def.MULTIFIR:='T';

_def.FILE:='transport.xlsx';
_def.SHEET:='Definicja kategorii transportu=1,1';
_def.NAME:='Definicja kategorii transportu'@;
_def.DESC:='Definicja kategorii transportu';
_def.TABLE:="exec('table','xls_tr_rodz')";
_def.FIELDS:="exec('fields','xls_tr_rodz',_a)";
_def.EXPORT:="exec('export','xls_tr_rodz',_a,_b)";
_def.IMPORT:="exec('import','xls_tr_rodz',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_tr_rodz',_a,_b,_c)";
_def.SELECT:="exec('select','xls_tr_rodz',_a)";
_def.PREFIX:="TR_RODZ.index('KOD'); TR_RODZ.prefix()";
_def.BEFORE:="TR_RODZ.cntx_psh()";
_def.AFTER:="TR_RODZ.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TR_RODZ


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod',1,,'Kod');
_def.define('NAZ','Nazwa',1,,'Nazwa');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TR_RODZ.prefix();
{? _validate.ACTION='add'
||
   TR_RODZ.blank()
?};

TR_RODZ.KOD:=_obj.KOD;
TR_RODZ.NAZ:=_obj.NAZ;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.KOD.VALUE:=TR_RODZ.KOD;
_table.NAZ.VALUE:=TR_RODZ.NAZ;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? _result.RESULT=1
||
   TR_RODZ.index('KOD');
   TR_RODZ.prefix(_obj.KOD,);
   {? TR_RODZ.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_tr_rodz',_obj,_mode,_result);
         {? __CHK.record(TR_RODZ,,'KOD','NAZ')<>''
         ||
            _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_tr_rodz',_obj,_mode,_result);
      {? __CHK.record(TR_RODZ,,'KOD','NAZ')<>''
      ||
         _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=TR_RODZ.add(1)

   |? _validate.ACTION='put'
   ||
      _result:=TR_RODZ.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie kategorii transportu %1 nie powiodło się.'@[_obj.KOD])
   ||
      FUN.info('Poprawa kategorii transportu %1 nie powiodła się.'@[_obj.KOD])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'KOD','STRING[20]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano?');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

TR_RODZ.cntx_psh();
TR_RODZ.index('KOD');
TR_RODZ.prefix();
{? TR_RODZ.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$TR_RODZ.ref();
      _selected.prefix($TR_RODZ.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=TR_RODZ.KOD;
      _tab.add();
      TR_RODZ.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'KOD',20,'Wybór kategorii transportu do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TR_RODZ.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 0e53747877608c931ca7235b35ef8cebb00b603652f8c47dccc018a8ccf238a35cfe6960e94bf4b75ae57f9993d18e801f00340efc56863a0152f9c034ef86595f815cfbcc6e41ebf27425a397b06b3ad9b38c9659011e2ac4db6dd1cad7dd0d971398b8c585b8d44debeb80f1e447d2eb416054ac4c36e97bd2ab5b60756464

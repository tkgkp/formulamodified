:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_slo_rodz.fml
:: Utworzony: 02.08.2018
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu słowników kadrowych - rodzaje.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Inicjowanie mechanizmu.
::   WE:  _a [OBJECT]  - Środowisko mechanizmu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='SLO_RODZ';
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.NAME:='Słowniki rodzajów'@;
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FUNPAR:='ZWS_PAR_PTSL';
_def.SHEET:='Słowniki rodzajów'@+'=1,1';
_def.DESC:='Słowniki rodzajów'@;
_def.MULTIFIR:='T';

_def.TABLE:="SLO_RODZ";
_def.BEFORE:="
   SLO_RODZ.cntx_psh();
   SLO_RODZ.f_clear();
   SLO_RODZ.index('KOD');
   SLO_RODZ.prefix();
   SLO_KOD.cntx_psh();
   SLO_KOD.index('KOD');
   SLO_KOD.prefix();
   SLO_TYP.cntx_psh();
   SLO_TYP.index('SYMBOL');
   SLO_TYP.prefix()
";
_def.AFTER:="
   SLO_TYP.cntx_pop();
   SLO_KOD.cntx_pop();
   SLO_RODZ.cntx_pop()
";

_def.FIELDS:="
   _fld:=_a;
   _fl:=""MS.name(SLO_RODZ,_a)"";
   _fc:=""MS.comment(SLO_RODZ,_a)"";

   _fld.define('SLO_TYP',MS.name(SLO_KOD,'SLO_TYP'),0,,
      MS.comment(SLO_KOD,'SLO_TYP')+' (max. %1 zn.)' [$MS.fld_len(SLO_TYP,'SYMBOL')]
   );
   _fld.define('SLO_KOD',_fl('SLO_KOD'),0,,_fc('SLO_KOD')+' (max. %1 zn.)' [$MS.fld_len(SLO_KOD,'KOD')]);
   _fld.define('LP',_fl('LP'),1,,_fc('LP'),,0);
   _fld.define('KOD',_fl('KOD'),1,,_fc('KOD')+' (max. %1 zn.)' [$MS.fld_len(SLO_RODZ,'KOD')]);
   _fld.define('NAZWA',_fl('NAZWA'),1,,_fc('NAZWA')+' (max. %1 zn.)' [$MS.fld_len(SLO_RODZ,'NAZWA')]);
   _fld.define('UNIKALNY',_fl('UNIKALNY'),0,,_fc('UNIKALNY'));
   _fld.define('SYSTEM',_fl('SYSTEM'),0,,_fc('SYSTEM'));

   ~~
";

_def.EXPORT:="
   _excel:=_a;
   _buf:=_b;

   _buf.SLO_TYP.VALUE:=SLO_RODZ.SLO_KOD().SLO_TYP().SYMBOL; _excel.write_async('SLO_KOD',SLO_RODZ.SLO_KOD);
   _buf.SLO_KOD.VALUE:=SLO_KOD.KOD;
   _buf.LP.VALUE:=SLO_RODZ.LP;
   _buf.KOD.VALUE:=SLO_RODZ.KOD;
   _buf.NAZWA.VALUE:=SLO_RODZ.NAZWA;
   _buf.UNIKALNY.VALUE:=SLO_RODZ.UNIKALNY;
   _buf.SYSTEM.VALUE:=SLO_RODZ.SYSTEM;

   1
";

_def.VALIDATE:="exec('validate','xls_slo_rodz',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_slo_rodz',_a,_b,_c)";

~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła sprawdza, czy można poprawić/dodać rekord.
::   WE: _a [OBJECT] - Obiekt z polami odczytami z Excela.
::       _b [NUMBER] - Tryb aktualizacji: 0 - nie (dodawane są tylko nowe rekordy) / 1 - tak.
::       _c [OBJECT] - Obiekt (RESULT, ACTION, OBJ) - wynik walidacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_update:=_b;
_validate:=_c;

_validate.OBJ:=obj_new('SLO_TYP','SLO_KOD');
_validate.OBJ.SLO_TYP:=null();
_validate.OBJ.SLO_KOD:=null();

:: Błędy krytyczne
{? _buf.SLO_TYP=''
|| _validate.msg_empty('SLO_TYP');
   _validate.RESULT:=0;
   return()
?};
{? SLO_TYP.find_key(_buf.SLO_TYP,)
|| _validate.OBJ.SLO_TYP:=SLO_TYP.ref()
|| _validate.msg_norec('SLO_TYP');
   _validate.RESULT:=0;
   return()
?};
{? _buf.SLO_KOD=''
|| _validate.msg_empty('SLO_KOD');
   _validate.RESULT:=0;
   return()
?};
{? _buf.LP=0
|| _validate.msg_empty('LP');
   _validate.RESULT:=0;
   return()
?};
{? SLO_KOD.find_key(_validate.OBJ.SLO_TYP,_buf.SLO_KOD,)
|| _validate.OBJ.SLO_KOD:=SLO_KOD.ref()
|| _validate.msg_norec('SLO_KOD');
   _validate.RESULT:=0;
   return()
?};

_fld:=
   {? _buf.KOD=''
   || 'KOD'
   |? _buf.NAZWA=''
   || 'NAZWA'
   |? _buf.UNIKALNY=''
   || 'UNIKALNY'
   |? _buf.SYSTEM=''
   || 'SYSTEM'
   || ''
   ?};
{? _fld<>''
|| _validate.msg_empty(_fld);
   _validate.RESULT:=0;
   return()
?};

{? +_buf.UNIKALNY<>1 | 'TN'*_buf.UNIKALNY=0
|| _validate.msg_inset('UNIKALNY','T','N');
   _validate.RESULT:=0;
   return()
?};
{? +_buf.SYSTEM<>1 | 'TN'*_buf.SYSTEM=0
|| _validate.msg_inset('SYSTEM','T','N');
   _validate.RESULT:=0;
   return()
?};

:: Ostrzeżenia
_dl:=MS.fld_len(SLO_RODZ,'KOD');
{? +_buf.KOD>_dl
|| _validate.msg_length('KOD',_dl)
?};
_dl:=MS.fld_len(SLO_RODZ,'NAZWA');
{? +_buf.NAZWA>_dl
|| _validate.msg_length('NAZWA',_dl)
?};

:: Wybór akcji
{? SLO_RODZ.find_key(_validate.OBJ.SLO_KOD,_buf.KOD,)
|| {? _update
   || {? SLO_RODZ.LP<>_buf.LP | SLO_RODZ.NAZWA<>_buf.NAZWA |
         SLO_RODZ.UNIKALNY<>_buf.UNIKALNY  | SLO_RODZ.SYSTEM<>_buf.SYSTEM
      || _validate.ACTION:='put'
      || _validate.ACTION:='~put'
      ?}
   || _validate.RESULT:=0
   ?}
|| _validate.ACTION:='add'
?};

~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła odpowiedzialna za utworzenie lub aktualizację rekordu.
::   WE: _a [OBJECT] - Obiekt z polami odczytami z Excela.
::       _b [NUMBER] - Tryb aktualizacji: 0 - nie (dodawane są tylko nowe rekordy) / 1 - tak.
::       _c [OBJECT] - Obiekt (RESULT, ACTION, OBJ) - wynik walidacji.
::   WY: Status operacji: 0/1
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_update:=_b;
_validate:=_c;

_przypisz:="
   _buf:=_a;
   _update:=_b;
   _validate:=_c;

   SLO_RODZ.SLO_KOD:=_validate.OBJ.SLO_KOD;
   SLO_RODZ.LP:=_buf.LP;
   SLO_RODZ.KOD:=_buf.KOD;
   SLO_RODZ.NAZWA:=_buf.NAZWA;
   SLO_RODZ.UNIKALNY:=_buf.UNIKALNY;
   SLO_RODZ.SYSTEM:=_buf.SYSTEM;

   ~~
";

_key:=_buf.SLO_TYP+'/'+_buf.KOD;

_ret:=1;

{? _validate.ACTION='add'
|| SLO_RODZ.blank();
   _przypisz(_buf,_update,_validate);
   {? ~SLO_RODZ.add(1)
   || _ret:=0;
      _validate.msg_insert(_key)
   ?}
|? _validate.ACTION='put'
|| _przypisz(_buf,_update,_validate);
   {? ~SLO_RODZ.put(1)
   || _ret:=0;
      _validate.msg_update(_key)
   ?}
?};

_ret

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 b198112afeebdb9a3e0bdf66c4ae3c2133e6d6ca7ca95979d173a2ac56b8847546875006437620f7e804389d170fd8a5f1b74c4556bd16ecc35132e83b7da86f21f4e703f6eaaa679c8a4452d50d5895228f009f5b8a488f26e099407352bd9518db76333b12fbb95b24e7a586d8fff51ca7b64f44d8cb74be496f681304ca98

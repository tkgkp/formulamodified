:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_edi.fml
:: Utworzony: 25.07.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji komunikatów edi
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='EDI';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_EDI_DEF';
_def.MULTIFIR:='T';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Komunikaty EDI=1,1';
_def.NAME:='Komunikaty EDI'@;
_def.DESC:='Komunikaty EDI';

_def.PREFIX:="ISTDEF.index('K'); ISTDEF.prefix('ZWS','E')";
_def.TABLE:="ISTDEF";
_def.FIELDS:="exec('fields','xls_edi',_a)";

_def.BEFORE:="ISTDEF.cntx_psh()";
_def.AFTER:="ISTDEF.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_edi',_a,_b)";
_def.VALIDATE:="1";
_def.IMPORT:="exec('import','xls_edi',_a,_b,_c)";
_def.SELECT:="exec('select','xls_edi',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('K','Kod',1,,'20 znakowy kod definicji');
_def.define('N','Nazwa',1,,'60 znakowa nazwa defincji');
_def.define('PLIK','Plik',1,,'Nazwa pliku z definicją');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

{? ISTDEF.FIRMA<>'' & ISTDEF.FIRMA<>__Firma || return(0) ?};

_result:=1;
_table.K.VALUE:=ISTDEF.K;
_table.N.VALUE:=ISTDEF.N;
BPMN.SYM_DOM:='ZWS';
_table.PLIK.VALUE:=exec('edi_exp_core','edi_def',{? EXCEL_ENV.DIR_ON_SERVER || '' || '@' ?}+_excel.DIR,ISTDEF.ref());

DEZEZW.cntx_psh();
DEZEZW.index('S1');
DEZEZW.prefix(ISTDEF.ref());
_loop:=DEZEZW.first();
{!
|? _loop
|!
   _excel.write_async('DEZEZW',DEZEZW.ref());
   _loop:=DEZEZW.next()
!};
DEZEZW.cntx_pop();

_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_dir:={? EXCEL_ENV.DIR_ON_SERVER || '' || '@' ?}+EXCEL_ENV.DIR;
_plik:=_obj.PLIK;
_sep:=exec('sep','#file',{? EXCEL_ENV.DIR_ON_SERVER || 2 || 0 ?});
{? +_plik>64
||
   _result.MSG:='Za długa nazwa pliku %1.'@[_plik];
   _result.RESULT:=0

|? fexists(_dir+_sep+_plik)
||
   _result.MSG:=exec('edi_imp_core','edi_def',_dir,_plik,_mode)
||
   _result.MSG:='Brak pliku %1 w lokalizacji %2.'@[_plik,_dir];
   _result.RESULT:=0
?};

_result.RESULT:=_result.MSG='';
~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'K','STRING[20]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'N','STRING[60]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'N',,);

ISTDEF.cntx_psh();
ISTDEF.index('K');
ISTDEF.prefix();
ISTDEF.f_set('K,N',,'SYSTEM='':_a'' and IST_ISTK='':_b'' and (FIRMA='''' or FIRMA='':_c'')',
   'ZWS','E',__Firma);
{? ISTDEF.f_first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$ISTDEF.ref();
      _selected.prefix($ISTDEF.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.K:=ISTDEF.K;
      _tab.N:=ISTDEF.N;
      _tab.add();
      ISTDEF.f_next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'K,N',20,'Wybór komunikatów EDI do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
ISTDEF.f_clear();
ISTDEF.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 0cd12d8573ec93d2c19c165ffa065ba9587948ea3058f19eeaafd1eb75b0160fe8fb1ceb10507c2f9b593bdd1e85229f29e2f3973c85a5b6a39758f4dd7eef233c707867ca89a8ccf5421c579b0ee6581c4ba95536484ebadb26485437848cfe05f1313f25d644600e78b4e0cd73c959dc69b70e9d8e5de7bb1d081667a2e8e7

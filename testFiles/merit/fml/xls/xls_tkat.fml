:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tkat.fml
:: Utworzony: 25.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi kategorii zaszeregowania
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TKAT';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTAR';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Kategorie zaszeregowania'@+'=1,1';
_def.NAME:='Kategorie zaszeregowania'@;
_def.DESC:='Kategorie zaszeregowania'@;

_def.PREFIX:="TKAT.index('K'); TKAT.prefix()";
_def.TABLE:="TKAT";
_def.FIELDS:="exec('fields','xls_tkat',_a)";

_def.BEFORE:="TKAT.cntx_psh()";
_def.AFTER:="TKAT.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_tkat',_a,_b)";
_def.VALIDATE:="exec('validate','xls_tkat',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_tkat',_a,_b,_c)";
_def.SELECT:="exec('select','xls_tkat',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KAT','Kategoria'@,0,,,,0);
_def.define('PLNH','Stawka godzinowa'@,1);
_def.define('OPIS','Opis'@,1,'Opis (max. 40 znaków)'@);
::_def.define('AKT','Czy zaktualizowany taryfikator'@);
::_def.define('DAKT','Data aktualizacji taryfikatora'@);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TKAT.prefix();
{? _validate.ACTION='add'
|| TKAT.blank();
   TKAT.KAT:=_obj.KAT
?};
TKAT.PLNH:=_obj.PLNH;
TKAT.OPIS:=_obj.OPIS;
::TKAT.AKT:=_obj.AKT;
::TKAT.DAKT:=_obj.DAKT;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KAT.VALUE:=TKAT.KAT;
_table.PLNH.VALUE:=TKAT.PLNH;
_table.OPIS.VALUE:=TKAT.OPIS;
::_table.AKT.VALUE:=TKAT.AKT;
::_table.DAKT.VALUE:=TKAT.DAKT;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

:: Ostrzeżenia
{? +_obj.OPIS>40
|| _result.msg_length('OPIS',40)
?};

:: OK
TKAT.index('K');
TKAT.prefix(_obj.KAT);
{? TKAT.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put';
      exec('record','xls_tkat',_obj,_mode,_result);
      {? exec('chk_tkat','prod_tar',1)<>''
      || _result.RESULT:=0
      ?}
   ?}
|| _result.ACTION:='add';
   exec('record','xls_tkat',_obj,_mode,_result);
   {? exec('chk_tkat','prod_tar',0)<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TKAT.add(1)
   |? _validate.ACTION='put'
   || _result:=TKAT.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie kategorii zaszeregowania %1 nie powiodło się.'@[$_obj.KAT]
   || _validate.MSG:='Poprawa kategorii zaszeregowania %1 nie powiodła się.'@[$_obj.KAT]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'KAT','INTEGER','Kategoria'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'PLNH','REAL','Stawka godzinowa'
   ,'OPIS','STRING[40]','Opis');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KAT',,);

TKAT.cntx_psh();
TKAT.index('K');
TKAT.prefix();
{? TKAT.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$TKAT.ref();
      _selected.prefix($TKAT.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KAT:=TKAT.KAT;
      _tab.PLNH:=TKAT.PLNH;
      _tab.OPIS:=TKAT.OPIS;
      _tab.add();
      TKAT.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'KAT,PLNH,OPIS',20,'Wybór kategorii zaszeregowania do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TKAT.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 9791cd0180f65b3cbb7ba18606b1a33bf83f00e841581b2ae2e59220704c74ac2c3da00a9d631812768131ca092a9eef5601175d6d1d4d9c97a2dba07a91ad92df6c64ca46a2db4c2cd355f64fec7e2e4fc6d5f7a4203c9270c4ed344508360d9ed10b2afd6d10d6417eb1a140ee947ae782402cb8c14d19f3bf54901e5beca4

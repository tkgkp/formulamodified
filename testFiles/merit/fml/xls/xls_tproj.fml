:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tproj.fml
:: Utworzony: 29.08.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji typów projektu
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PROJTYPY';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PRJ_TYPY';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Typy projektów=1,1';
_def.NAME:='Typy projektów'@;
_def.DESC:='Typy projektów';
_def.MULTIFIR:='T';

_def.PREFIX:="exec('prefix','xls_tproj')";
_def.TABLE:="exec('table','xls_tproj')";
_def.FIELDS:="exec('fields','xls_tproj',_a)";

_def.BEFORE:="PROJTYPY.cntx_psh()";
_def.AFTER:="PROJTYPY.cntx_pop()";
_def.SELECT:="exec('select','xls_tproj',_a)";

_def.EXPORT:="exec('export','xls_tproj',_a,_b)";
_def.IMPORT:="exec('import','xls_tproj',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_tproj',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
PROJTYPY.index('T');
PROJTYPY.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
PROJTYPY


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('T','Typ',0,,'Typ projektu - 8 znaków.');
_def.define('NAZ','Nazwa',0,,'Nazwa projektu - 60 znaków.');
_def.define('KOD','Kod numeracji',0,,'3 znakowy kod numeracji.');
_def.define('R','Rodzaj',1,,
'Dopuszczalne wartości:
(Wewnętrzny)
(Zewnętrzny)
');
_def.define('SJO','Schemat j.org.',0,,'Symbol schematu jednostek organizacyjnych.');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

PROJTYPY.index('T');
PROJTYPY.prefix();
{? _validate.ACTION='add'
||
   PROJTYPY.blank();
   PROJTYPY.T:=_obj.T;
   PROJTYPY.NAZ:=_obj.NAZ;
   PROJTYPY.KOD:=_obj.KOD
?};

PROJTYPY.R:=_obj.R;
PROJTYPY.SJO:=_validate.OBJ.SJO;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.T.VALUE:=PROJTYPY.T;
_table.NAZ.VALUE:=PROJTYPY.NAZ;
_table.KOD.VALUE:=PROJTYPY.KOD;
_excel.write_async('NRDOK',exec('FindInSet','#table','NRDOK','NRDOK',PROJTYPY.KOD,,,1,,null()));
_table.R.VALUE:=PROJTYPY.R;
_table.SJO.VALUE:=PROJTYPY.SJO().SYMBOL;
_excel.write_async('UD_SCH',PROJTYPY.SJO);
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? (+form(_obj.T))=0        || _result.msg_empty('T'); _result.RESULT:=0
|? (+form(_obj.T))>8        || _result.msg_length('T',8); _result.RESULT:=0
?};
{? (+form(_obj.NAZ))=0      || _result.msg_empty('NAZ'); _result.RESULT:=0 ?};
{? (+form(_obj.KOD))<>3     || _result.msg_range('KOD','długości 3 znaków','długości 3 znaków'); _result.RESULT:=0
|? ~(';Wewnętrzny;Zewnętrzny;'*(';'+_obj.R+';')) || _result.msg_inset('R','Wewnętrzny','Zewnętrzny'); _result.RESULT:=0
?};

_result.OBJ:=obj_new('SJO');
_result.OBJ.SJO:=null();

{? _obj.SJO<>'' & _obj.R='Wewnętrzny'
|| _ud_typ:=exec('FindInSet','#table','UD_TYP','SYMBOL','PODZORG',,,1,,null());

   {? _ud_typ=null()
   || _result.MSG:='Brak typu schematów danych o symbolu PODZORG.'@;
      _result.RESULT:=0
   || _result.OBJ.SJO:=exec('FindInSet','#table','UD_SCH','SYMBOL',_obj.SJO,_ud_typ,,1,,null());
      {? _result.OBJ.SJO=null()
      || _result.MSG:='Nie znaleziono schematu jednostek organizacyjnych o symbolu %1.'@[_obj.SJO];
         _result.RESULT:=0
      ?}
   ?}
?};

{? _result.RESULT=1
||
   PROJTYPY.index('T');
   PROJTYPY.prefix(_obj.T,);
   {? PROJTYPY.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_tproj',_obj,_mode,_result)
      ?}
   || _result.ACTION:='add';
      exec('record','xls_tproj',_obj,_mode,_result)
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=PROJTYPY.add(1)
   |? _validate.ACTION='put'
   || _result:=PROJTYPY.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:={? _validate.ACTION='add'
                  || 'Dodanie typu projektu %1 nie powiodło się.'@[PROJTYPY.T]
                  || 'Modyfikacja typu projektu %1 nie powiodła się.'@[PROJTYPY.T]
                  ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'T','STRING[8]','Typ'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[60]','Nazwa'
   ,'KOD','STRING[3]','Kod numeracji'
   ,'R','STRING[20]','Rodzaj'
   ,'SJO','STRING[8]','Symbol schematu');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'T',,);

PROJTYPY.cntx_psh();
PROJTYPY.index('T');
PROJTYPY.prefix();
{? PROJTYPY.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$PROJTYPY.ref();
      _selected.prefix($PROJTYPY.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.T:=PROJTYPY.T;
      _tab.NAZ:=PROJTYPY.NAZ;
      _tab.KOD:=PROJTYPY.KOD;
      _tab.R:=PROJTYPY.R;
      _tab.SJO:=PROJTYPY.SJO().SYMBOL;
      _tab.add();
      PROJTYPY.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'T,NAZ,KOD,R,SJO',20,'Wybór parametrów typów projektów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PROJTYPY.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 ad97e750dc2be3420735a00209f97ad8e4b3912ca09ca6d7216fdcb19a5c3773fcf6bcd033a8c76946f31463ca86a9fc763ebe995765a3a4f189f6ec6a7afb252fcf5e8bda30847896f44737e4628487b0f31ee708226704544b0782afb8a90fc28ab51a54522a62408ce7e499eb060218ecc036ed77d872f98426ce1efd7cec

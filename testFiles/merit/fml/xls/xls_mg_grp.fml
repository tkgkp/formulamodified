:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_mg_grp.fml
:: Utworzony: 30.07.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu pozycji strefy magazynowania
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='MG_GRP';
_def.DOMAIN:=exec('name','#b_domain','LMO');
_def.FUNPAR:='ZWS_PAR_LMAG';

_def.FILE:='magazyn_mobilny.xlsx';
_def.SHEET:='Pozycje strefy mag.=1,1';
_def.NAME:='Pozycje stref magazynowania'@;
_def.DESC:='Pozycje stref magazynowania';

_def.PREFIX:="exec('prefix','xls_mg_grp')";
_def.TABLE:="exec('table','xls_mg_grp')";
_def.FIELDS:="exec('fields','xls_mg_grp',_a)";

_def.BEFORE:="MG_GRP.cntx_psh()";
_def.AFTER:="MG_GRP.cntx_pop()";
_def.SELECT:="exec('select','xls_mg_grp',_a)";

_def.EXPORT:="exec('export','xls_mg_grp',_a,_b)";
_def.IMPORT:="exec('import','xls_mg_grp',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_mg_grp',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
MG_GRP.index('MG_GR');
MG_GRP.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
MG_GRP


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('MG_GR','Strefa magazynowania',0,,'Unikalny kod strefy magazynowania - 20 znaków.');
_def.define('LP','LP',0,,'Liczba porządkowa w danej strefie.');
_def.define('EANL','Lokalizacja magazynu',0,,'Kod lokalizacji magazynu - 20 znaków.');
_def.define('MG','Magazyn',0,,'Kod magazynu - 8 znaków - zgodny z magazynem lokalizacji.');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

MG_GRP.index('MG_GR');
MG_GRP.prefix();
{? _validate.ACTION='add'
||
   MG_GRP.blank();
   MG_GRP.MG_GR:=_validate.OBJ.MG_GR
?};

MG_GRP.MG:=_validate.OBJ.MG;
MG_GRP.EANL:=_validate.OBJ.EANL;
MG_GRP.LP:=_obj.LP;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
MG_GRP.cntx_psh();
{? MG_GRP.MG_GR().KH=null()
|| _table.MG_GR.VALUE:=MG_GRP.MG_GR().KOD; _excel.write_async('MG_GR',MG_GRP.MG_GR);
   _table.MG.VALUE:=MG_GRP.MG().SYM; _excel.write_async('MG',MG_GRP.MG);
   _table.EANL.VALUE:=MG_GRP.EANL().KOD; _excel.write_async('EANL',MG_GRP.EANL);
   _table.LP.VALUE:=MG_GRP.LP
|| _result:=0
?};
MG_GRP.cntx_pop();
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('MG_GR','MG','EANL');

_result.OBJ.MG_GR:={? _obj.MG_GR='' || null() || exec('FindInSet','#table','MG_GR','KOD',_obj.MG_GR,,,1) ?};
{? _result.OBJ.MG_GR=null()
|| _result.MSG:={? _obj.MG_GR=''
                || 'Nie podano strefy magazynowania.'@
                || 'Nie znaleziono strefy magazynowania %1.'@[_obj.MG_GR]
                ?};
   _result.RESULT:=0
|? exec('FindAndGet','#table',MG_GR,_result.OBJ.MG_GR,,"KH<>null()",0)
|| _result.MSG:='Podana strefa %1 jest przypisana do kontrahenta. Opcja niedostępna.'@[_obj.MG_GR];
   _result.RESULT:=0
?};

_result.OBJ.MG:={? _obj.MG='' || null() || exec('FindInSet','#table','MG','MAGAZYNY',_obj.MG,,,1) ?};
{? _result.OBJ.MG=null()
|| _result.MSG:={? _obj.MG=''
                || 'Nie podano magazynu.'@
                || 'Nie znaleziono magazynu %1.'@[_obj.MG]
                ?};
   _result.RESULT:=0
|? exec('FindAndGet','#table',MG,_result.OBJ.MG,,"MWS<>'T'",0)
|| _result.MSG:='Podany magazyn %1 nie jest Magazynem Wysokiego Składowania. Opcja niedostępna.'@[_obj.MG];
   _result.RESULT:=0
?};

_result.OBJ.EANL:={? _obj.EANL='' || null() || exec('FindInSet','#table','EANL','KOD',_obj.EANL,,,1) ?};
{? _result.OBJ.EANL=null()
|| _result.MSG:={? _obj.EANL=''
                || 'Nie podano lokalizacji w magazynie.'@
                || 'Nie znaleziono lokalizacji w magazynie %1.'@[_obj.EANL]
                ?};
   _result.RESULT:=0
|? exec('FindAndGet','#table',EANL,_result.OBJ.EANL,,"MG",null())<>_result.OBJ.MG
|| _result.MSG:='Podana lokalizacja %1 jest przypisana do innego magazynu %2. Opcja niedostępna.'@[_obj.EANL,_obj.MG];
   _result.RESULT:=0
?};

{? _result.RESULT=1
||
   MG_GRP.index('MG_GR');
   MG_GRP.prefix(_result.OBJ.MG_GR,_result.OBJ.MG,_obj.LP,_result.OBJ.EANL);
   {? MG_GRP.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_mg_grp',_obj,_mode,_result)
      ?}
   || _result.ACTION:='add';
      exec('record','xls_mg_grp',_obj,_mode,_result)
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=MG_GRP.add(1)
   |? _validate.ACTION='put'
   || _result:=MG_GRP.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:={? _validate.ACTION='add'
                  || 'Dodanie pozycji strefy magazynowania %1 nie powiodło się.'@[MG_GRP.MG_GR().KOD]
                  || 'Modyfikacja pozycji strefy magazynowania %1 nie powiodła się.'@[MG_GRP.MG_GR().KOD]
                  ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2,'MG_GR','STRING[20]','Strefa magazynowania'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'MG','STRING[8]','Magazyn'
   ,'EANL','STRING[20]','Lokalizacja'
   ,'LP','INTEGER','Lp');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'MG_GR',,,'LP',,);

MG.cntx_psh();
EANL.cntx_psh();
MG_GR.cntx_psh();
MG_GRP.cntx_psh();
MG_GRP.index('MG_GR');
MG_GRP.prefix();
{? MG_GRP.first()
|| {!
   |?
      {? MG_GRP.MG_GR().KH=null()
      || _tab.blank();
         _tab.REF:=$MG_GRP.ref();
         _selected.prefix($MG_GRP.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.MG_GR:=MG_GRP.MG_GR().KOD;
         _tab.MG:=MG_GRP.MG().SYM;
         _tab.EANL:=MG_GRP.EANL().KOD;
         _tab.LP:=MG_GRP.LP;
         _tab.add()
      ?};
      MG_GRP.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'MG_GR,LP,MG,EANL',20,'Wybór strefy magazynowania do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
MG.cntx_pop();
EANL.cntx_pop();
MG_GR.cntx_pop();
MG_GRP.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:48 1f8133db1a9254b36195869d7efe53b87afd87e3a30349955860ca49f883e0b3ada3a0bcfb7edbccfc2e27e0292678f5509b688e7188f87c52f446cf2c43c63767b0f3569a7c42407ed42ca5fff638df0fa4fcc3a17e6a3ba9fe359eb89946b903c43d73f999ea54837a3b3fd3e53a7df3b962f3ddd18eb0ebe890b6f7701b89

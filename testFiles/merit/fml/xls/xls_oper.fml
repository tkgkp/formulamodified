:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_oper.fml
:: Utworzony: 20.07.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typów operacji kasowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='OPER';
_def.DOMAIN:=exec('name','#b_domain','KAS');
_def.FUNPAR:='ZWS_PAR_FKTO';

_def.FILE:='kasa.xlsx';
_def.SHEET:='Typy operacji kasowych=1,1';
_def.NAME:='Typy operacji kasowych'@;
_def.DESC:='Typy operacji kasowych';

_def.PREFIX:="OPER.index('KOD'); OPER.prefix()";
_def.TABLE:="OPER";
_def.FIELDS:="exec('fields','xls_oper',_a)";

_def.BEFORE:="OPER.cntx_psh()";
_def.AFTER:="OPER.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_oper',_a,_b)";
_def.VALIDATE:="exec('validate','xls_oper',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_oper',_a,_b,_c)";
_def.SELECT:="exec('select','xls_oper',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Typ operacji',1,,'8 znakowy typ operacji');
_def.define('NAZWA','Nazwa',1,,'30 znakowa nazwa operacji');
_def.define('PLUS_MIN','Przychód/rozchód',1,,'Znak + oznacza przychód, znak - oznacza rozchód');
_def.define('CZY_DOK','Wymagane wystawienie dokumentu KP/KW',1,,'T/N');
::_def.define('CZY_BLOK','Czy blokować?',1,,'T/N');
_def.define('TYP_DOK','Typ dokumentu',1,,'3 znakowy typ dokumentu');
_def.define('WYDRUK','Automatyczny wydruk dokumentu',1,,'T/N');
_def.define('DOTYCZY','Dotyczy',1,,'20 znakowa nazwa słownika użytkownika');
_def.define('CZY1_POZ','Dokument zawiera jedną pozycję',1,,'T/N');
_def.define('ANALIT','Analityka',1,,'8 znakowy tekst wykorzystywany w automacie księgującym');
_def.define('CZY_ZAL','Operacja dotyczy zaliczek',1,,'T/N');
::_def.define('TEREN','Pakiet TEREN?',1,,'[ML] TEREN');
::_def.define('F_TEREN','Dotyczy operacji',1,,'[ML] TEREN');
_def.define('RAP_ZB','Grupowanie operacji na wydruku zbiorczym',1,,'T/N');
_def.define('L_KOPII','Liczba kopii na wydruku',1,,'T/N');
::_def.define('ZNAKI','Znaki?',1,,'T/N');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=OPER.KOD;
_table.NAZWA.VALUE:=OPER.NAZWA;
_table.PLUS_MIN.VALUE:=OPER.PLUS_MIN;
_table.CZY_DOK.VALUE:=OPER.CZY_DOK;
::_table.CZY_BLOK.VALUE:=OPER.CZY_BLOK;
_table.TYP_DOK.VALUE:=OPER.TYP_DOK;
_table.WYDRUK.VALUE:=OPER.WYDRUK;
_table.DOTYCZY.VALUE:=OPER.DOTYCZY().NAZ;
_excel.write_async('SLU',OPER.DOTYCZY);
_table.CZY1_POZ.VALUE:=OPER.CZY1_POZ;
_table.ANALIT.VALUE:=OPER.ANALIT;
_table.CZY_ZAL.VALUE:=OPER.CZY_ZAL;
::_table.TEREN.VALUE:=OPER.TEREN;
::_table.F_TEREN.VALUE:=_excel.write_async('FORMULA',OPER.F_TEREN);
_table.RAP_ZB.VALUE:=OPER.RAP_ZB;
_table.L_KOPII.VALUE:=OPER.L_KOPII;
::_table.ZNAKI.VALUE:=OPER.ZNAKI;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
OPER.index('KOD');
OPER.prefix(_obj.KOD,);
{? OPER.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT=1
|| {? _result.ACTION='add' || OPER.blank() ?};
   OPER.KOD:=_obj.KOD;
   OPER.NAZWA:=_obj.NAZWA;
   OPER.PLUS_MIN:=_obj.PLUS_MIN;
   OPER.CZY_DOK:=_obj.CZY_DOK;
::   OPER.CZY_BLOK:=_obj.CZY_BLOK;
   OPER.TYP_DOK:=_obj.TYP_DOK;
   OPER.WYDRUK:=_obj.WYDRUK;
   OPER.DOTYCZY:=exec('FindInSet','#table','SLU','NAZ',_obj.DOTYCZY,,,1,,null());
   OPER.CZY1_POZ:=_obj.CZY1_POZ;
   OPER.ANALIT:=_obj.ANALIT;
   OPER.CZY_ZAL:=_obj.CZY_ZAL;
::   OPER.TEREN:=_obj.TEREN;
::   OPER.F_TEREN:=exec('FindInSet','#table','FORMULA','FORMULA3',_obj.F_TEREN,'N',,1,,null());
   OPER.RAP_ZB:=_obj.RAP_ZB;
   OPER.L_KOPII:=_obj.L_KOPII;
::    OPER.ZNAKI:=_obj.ZNAKI;
   {? exec('ope_wal','kasa_wspolne')<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=OPER.add(1)

   |? _validate.ACTION='put'
   ||
      _result:=OPER.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie typu operacji kasowej %1 nie powiodło się.'@[_obj.KOD]
   ||
      _validate.MSG:='Poprawa typu operacji kasowej %1 nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'KOD','STRING[20]','Typ operacji'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZWA','STRING[60]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

OPER.cntx_psh();
OPER.index('KOD');
OPER.prefix();
{? OPER.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$OPER.ref();
      _selected.prefix($OPER.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=OPER.KOD;
      _tab.NAZWA:=OPER.NAZWA;
      _tab.add();
      OPER.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'KOD,NAZWA',20,'Wybór typów operacji kasowych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
OPER.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:49 830e7b1119d21159468d44f6f7ccd1afa077df67299c458d783bcd9f493307bc02909f86a69fe2f3c7b2cd5f881b1a1bed1b6e1d23087acacee1075cdd082936e3d8ca719f5676407998808ef855ea603d4c4ca21b8b8f888e5f68b6cd87bbf1d22a0a4df42a928ec9bcaccc0155896bc1888d807f3b458050ff80cd6576f8ad

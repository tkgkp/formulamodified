:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_upr_zawk.fml
:: Utworzony: 24.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji uprawnień zawodowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='UPR_ZAWK';
_def.FUNPAR:='ZWS_PAR_PUZW';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Uprawnienia zawodowe=1,1';
_def.NAME:='Uprawnienia zawodowe'@;
_def.DESC:='Uprawnienia zawodowe';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="UPR_ZAWK";

: prolog
_def.BEFORE:="
   ADRES.cntx_psh();
   ADRES.prefix();
   UPR_ZAWK.cntx_psh();
   UPR_ZAWK.f_clear();
   UPR_ZAWK.index('SYMBOL');
   UPR_ZAWK.prefix();
   ~~
";

: epilog
_def.AFTER:="
   UPR_ZAWK.cntx_pop();
   ADRES.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYMBOL', MS.name(UPR_ZAWK,'SYMBOL'), 1,,MS.comment(UPR_ZAWK,'SYMBOL'));
   _env.define('NAZWA',  MS.name(UPR_ZAWK,'NAZWA'),  1,,MS.comment(UPR_ZAWK,'NAZWA'));
   _env.define('SLO_TYP',MS.name(UPR_ZAWK,'SLO_TYP'),1,,MS.comment(UPR_ZAWK,'SLO_TYP'));
   _env.define('ZAWIES', MS.name(UPR_ZAWK,'ZAWIES'), 1,,MS.comment(UPR_ZAWK,'ZAWIES'));
   _env.define('NR',     MS.name(UPR_ZAWK,'NR'),     1,,MS.comment(UPR_ZAWK,'NR'));
   _env.define('OD',     MS.name(UPR_ZAWK,'OD'),     1,,MS.comment(UPR_ZAWK,'OD'));
   _env.define('DO',     MS.name(UPR_ZAWK,'DO'),     1,,MS.comment(UPR_ZAWK,'DO'));
   _env.define('UTRATA', MS.name(UPR_ZAWK,'UTRATA'), 1,,MS.comment(UPR_ZAWK,'UTRATA'));
   _env.define('DOK_NR', MS.name(UPR_ZAWK,'DOK_NR'), 1,,MS.comment(UPR_ZAWK,'DOK_NR'));
   _env.define('DOK_DT', MS.name(UPR_ZAWK,'DOK_DT'), 1,,MS.comment(UPR_ZAWK,'DOK_DT'));
   _env.define('DOK_WYD',MS.name(UPR_ZAWK,'DOK_WYD'),1,,MS.comment(UPR_ZAWK,'DOK_WYD'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYMBOL.VALUE:=UPR_ZAWK.SYMBOL;
   _buf.NAZWA.VALUE:=UPR_ZAWK.NAZWA;
   _buf.SLO_TYP.VALUE:=UPR_ZAWK.SLO_TYP().SYMBOL;
   _buf.ZAWIES.VALUE:=UPR_ZAWK.ZAWIES;
   _buf.NR.VALUE:=UPR_ZAWK.NR;
   _buf.OD.VALUE:=UPR_ZAWK.OD;
   _buf.DO.VALUE:=UPR_ZAWK.DO;
   _buf.UTRATA.VALUE:=UPR_ZAWK.UTRATA;
   _buf.DOK_NR.VALUE:=UPR_ZAWK.DOK_NR;
   _buf.DOK_DT.VALUE:=UPR_ZAWK.DOK_DT;
   _buf.DOK_WYD.VALUE:=UPR_ZAWK.DOK_WYD;
   _env.write_async('SLO_TYP',UPR_ZAWK.SLO_TYP);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_upr_zawk',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_upr_zawk',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYMBOL=''  || 'SYMBOL'
   |? _buf.NAZWA=''   || 'NAZWA'
   |? _buf.ZAWIES=''  || 'ZAWIES'
   |? _buf.NR=''      || 'NR'
   |? _buf.OD=''      || 'OD'
   |? _buf.DO=''      || 'DO'
   |? _buf.UTRATA=''  || 'UTRATA'
   |? _buf.DOK_NR=''  || 'DOK_NR'
   |? _buf.DOK_DT=''  || 'DOK_DT'
   |? _buf.DOK_WYD='' || 'DOK_WYD'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('RPN'*_buf.ZAWIES)=0
|| _val.msg_inset('ZAWIES','R','P','N');
   _val.RESULT:=0;
   return()

|? ('NOW'*_buf.NR)=0
|| _val.msg_inset('NR','N','O','W');
   _val.RESULT:=0;
   return()

|? ('NOW'*_buf.OD)=0
|| _val.msg_inset('OD','N','O','W');
   _val.RESULT:=0;
   return()

|? ('NOW'*_buf.DO)=0
|| _val.msg_inset('DO','N','O','W');
   _val.RESULT:=0;
   return()

|? ('NO'*_buf.UTRATA)=0
|| _val.msg_inset('UTRATA','N','O');
   _val.RESULT:=0;
   return()

|? ('NOW'*_buf.DOK_NR)=0
|| _val.msg_inset('DOK_NR','N','O','W');
   _val.RESULT:=0;
   return()

|? ('NOW'*_buf.DOK_DT)=0
|| _val.msg_inset('DOK_DT','N','O','W');
   _val.RESULT:=0;
   return()

|? ('NOW'*_buf.DOK_WYD)=0
|| _val.msg_inset('DOK_WYD','N','O','W');
   _val.RESULT:=0;
   return()

|? _buf.SLO_TYP<>'' & _buf.DOK_WYD='N'
|| _val.msg_inset('DOK_WYD','O','W');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(UPR_ZAWK,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(UPR_ZAWK,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};
_max:=MS.fld_len(SLO_TYP,'SYMBOL');
{? +_buf.SLO_TYP>_max
|| _val.msg_length('SLO_TYP',_max);
   _buf.SLO_TYP:=_max+_buf.SLO_TYP
?};

: weryfikacja spójności referencyjnej
_typ:=null;
{? _buf.SLO_TYP<>''
|| {? (_typ:=exec('slo_typ','ext_slo',_buf.SLO_TYP))=null
   || _val.msg_norec('SLO_TYP');
      _val.RESULT:=0;
      return()
   ?}
?};

: ustalenie, rekordów według kluczy unikalnych
_rzS:={? UPR_ZAWK.find_tab(,'SYMBOL',,'=',_buf.SYMBOL) || UPR_ZAWK.ref() || null ?};
_rzN:={? UPR_ZAWK.find_tab(,'NAZWA',,'=',_buf.NAZWA) || UPR_ZAWK.ref() || null ?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=exec('write_obj','xls__util',_rzS,
   "'%1 - %2'[_a.SYMBOL,_a.NAZWA]",
   'SLO_TYP',_typ
);

{? _rzS=null
|| {? _rzN=null
   || _val.ACTION:='add'
   || _val.msg_unique(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
|| {? _rzN=null | _rzN=_rzS
   || _val.ACTION:='put'
   || _val.msg_unique(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,UPR_ZAWK,"
   _a.SYMBOL:=_b.SYMBOL;
   _a.NAZWA:=_b.NAZWA;
   _a.SLO_TYP:=_c.SLO_TYP;
   _a.ZAWIES:=_b.ZAWIES;
   _a.NR:=_b.NR;
   _a.OD:=_b.OD;
   _a.DO:=_b.DO;
   _a.UTRATA:=_b.UTRATA;
   _a.DOK_NR:=_b.DOK_NR;
   _a.DOK_DT:=_b.DOK_DT;
   _a.DOK_WYD:=_b.DOK_WYD;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 644a48ce91f6457aab50558656b788ff6710f55a724d392a1a2efd58c35c9154bbd0ddd36aa08dd61552e4582024d1dae85b1d2475217ffd0fa0a15c2983e18e807fc3a7d1c891b805db3fade77ba76fb32e772ff3ea03f38ce5bc35a666fe208b3199d763456f962d942a87fc8ad5771626e3a5cd656e689f222d3cbdc7d6cf

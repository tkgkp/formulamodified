:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_zb_typ.fml
:: Utworzony: 24.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typów zestawień zbiorczych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='ZB_TYP';
_def.FUNPAR:='PPL_ZES_RZEZ';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Typy zestawień zbiorczych=1,1';
_def.NAME:='Typy zestawień zbiorczych'@;
_def.DESC:='Typy zestawień zbiorczych (zmiana kolejności nie jest obsługiwana)';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="ZB_TYP";

: prolog
_def.BEFORE:="
   ZB_TYP.cntx_psh();
   ZB_TYP.f_clear();
   ZB_TYP.index('NUMER');
   ZB_TYP.prefix();
   ~~
";

: epilog
_def.AFTER:="
   ZB_TYP.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('NAZWA',MS.name(ZB_TYP,'NAZWA'),0,,MS.comment(ZB_TYP,'NAZWA'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.NAZWA.VALUE:=ZB_TYP.NAZWA;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_zb_typ',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_zb_typ',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.NAZWA='' || 'NAZWA'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(ZB_TYP,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};

_ref:=null;
_nr:=ZB_TYP.size()+1;
{? ZB_TYP.find_tab(,'NAZWA',,'=',_buf.NAZWA)
|| _ref:=ZB_TYP.ref();
   _nr:=ZB_TYP.NUMER;
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1) %2'[$_nr,_a.NAZWA]",
   'NUMER',_nr
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,ZB_TYP,"
   _a.NUMER:=_c.NUMER;
   _a.NAZWA:=_b.NAZWA;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 d88eb87519e79052dbd2b7c898474162fd8a5ac90507852cf1656a49f4905cc13250c1bf12545f312cb81c1babd26a720d64a0ae88fe8062dc8627697c8e957ebf77db1e3fa71c15be97f0d19d573578528c7667646a3a2df7b9eb168707bc3ec8bd1f24aac9761b8ce751636069819b6c9d29a10df7f0c8a47474f2246513e9

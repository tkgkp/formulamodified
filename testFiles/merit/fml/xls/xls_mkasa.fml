:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_mkasa.fml
:: Utworzony: 18.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu multikasy
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='MKASA';
_def.DOMAIN:=exec('name','#b_domain','KAS');
_def.FUNPAR:='ZWS_PAR_FKMK';

_def.FILE:='kasa.xlsx';
_def.SHEET:='Multikasa=1,1';
_def.NAME:='Multikasa'@;
_def.DESC:='Multikasa';
_def.TABLE:="exec('table','xls_mkasa')";
_def.FIELDS:="exec('fields','xls_mkasa',_a)";
_def.EXPORT:="exec('export','xls_mkasa',_a,_b)";
_def.IMPORT:="exec('import','xls_mkasa',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_mkasa',_a,_b,_c)";
_def.SELECT:="exec('select','xls_mkasa',_a)";
_def.PREFIX:="MKASA.index('NAZ'); MKASA.prefix()";
_def.BEFORE:="MKASA.cntx_psh()";
_def.AFTER:="MKASA.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
MKASA


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NAZ','Nazwa',1,,'100 znakowa Nazwa');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
MKASA.prefix();
{? _validate.ACTION='add'
||
   MKASA.blank()
?};

MKASA.NAZ:=_obj.NAZ;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.NAZ.VALUE:=MKASA.NAZ;

KUSERUPR.cntx_psh();
KUSERUPR.index('MKASA');
KUSERUPR.prefix(MKASA.ref());
{? KUSERUPR.first()
|| {!
   |? _excel.write_async('KUSERUPR',KUSERUPR.ref());
      KUSERUPR.next()
   !}
?};
KUSERUPR.cntx_pop();

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
MKASA.index('NAZ');
MKASA.prefix(_obj.NAZ,);
{? MKASA.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT
|| exec('record','xls_mkasa',_obj,_mode,_result);
   {? exec('mkasa_chk','kasa_wspolne',_result.ACTION)<>''
   || _result.RESULT:=0
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=MKASA.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=MKASA.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie multikasy o nazwie %1 nie powiodło się.'@[_obj.NAZ])
   ||
      FUN.info('Poprawa multikasy o nazwie %1 nie powiodła się.'@[_obj.NAZ])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[40]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZ',,);

MKASA.cntx_psh();
MKASA.index('NAZ');
MKASA.prefix();
{? MKASA.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$MKASA.ref();
      _selected.prefix($MKASA.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NAZ:=MKASA.NAZ;
      _tab.add();
      MKASA.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NAZ',20,'Wybór multikasy do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
MKASA.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:48 710eb79983db465bd6201e14d9bf84d42a02c4e0685e8d7e0d5ecb2cad5d279624d544f4261262b64789ddb0b37a95b6781fc304abad678e09d3ccc9959f48325b2c62897828d4d3eeace50a239084f44b22402c054f10bf899136630c1525fa82abe89ee73b2f29deb668e6d638a05067d0b6402d5da6c22996d54ef5984397

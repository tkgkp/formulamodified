:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_slo.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu pozycji słowników użytkownika
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='SLO';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_KSLO';
_def.HIDDEN:='T';
_def.MULTIFIR:='T';
_def.FILE:='slowniki_uzytkownika.xlsx';
_def.SHEET:='Pozycje słowników użytkownika=1,1';
_def.NAME:='Pozycje słowników użytkownika'@;
_def.DESC:='Pozycje słowników użytkownika';
_def.TABLE:="exec('table','xls_slo',_a)";
_def.FIELDS:="exec('fields','xls_slo',_a)";
_def.EXPORT:="exec('export','xls_slo',_a,_b)";
_def.IMPORT:="exec('import','xls_slo',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_slo',_a,_b,_c)";
_def.SELECT:="exec('select','xls_slo',_a)";
_def.PREFIX:="SLO.index('SL_SYSWZ'); SLO.prefix()";
_def.BEFORE:="SLO.cntx_psh()";
_def.AFTER:="SLO.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::   WE: _a - tryb pracy: 1-eksport wszystkiego, 2-eksport jednego rekordu
::----------------------------------------------------------------------------------------------------------------------
SLO


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('SLU','Słownik użytkownika',0,,'Wskazanie na słownik użytkownika');
_def.define('KOD','Kod',1,,'Kod pozycji w słowniku');
_def.define('TR','Treść',1,,'Nazwa pozycji w słowniku do 80 znaków');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_tab:=_excel.table();
_result:=1;
SLO.SLU();
{? SLU.SYSTEM='N' & (SLU.WZ='prosty' | (1+SLU.WZ)='~' | SLU.WZ='Bank')
|| _table.SLU.VALUE:=SLU.NAZ;
   _excel.write_async('SLU',SLO.SLU);
   _table.KOD.VALUE:=SLO.KOD;
   _table.TR.VALUE:=SLO.TR;
   ZR_SLO.cntx_psh();
   ZR_SLO.index('SLO_NR');
   ZR_SLO.prefix(SLO.ref(),);
   _loop:=ZR_SLO.first();
   {!
   |? _loop
   |! _excel.write_async('ZR_SLO',ZR_SLO.ref());
      _loop:=ZR_SLO.next()
   !};
   ZR_SLO.cntx_pop()
|| _result:=0
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('SLU');

_result.OBJ.SLU:=exec('FindInSet','#table','SLU','NAZ',_obj.SLU);

{? _result.RESULT = 1
||
   SLO.index('SL');
   SLO.prefix(_result.OBJ.SLU, _obj.KOD);
   {? SLO.first() & SLO.KOD = _obj.KOD
   ||
      {? _mode=0
      || _result.RESULT:=0
      ||
         _result.ACTION:='put';
         exec('record','xls_slo',_obj,_mode,_result);
         {? exec('spr_slo','slo_slu', 0)<>''
         ||
            _result.RESULT:=0
         ?}
      ?}
   ||
      _result.ACTION:='add';
      exec('record','xls_slo',_obj,_mode,_result);
      {? exec('spr_slo','slo_slu', 1)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
SLO.prefix();
{? _validate.ACTION='add'
|| SLO.blank();
   SLO.SLU:=_validate.OBJ.SLU
?};

SLO.KOD:=_obj.KOD;
SLO.TR:=_obj.TR;
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=SLO.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=SLO.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie pozycji słownika o kodzie %1 nie powiodło się.'@[_obj.KOD])
   ||
      FUN.info('Poprawa pozycji słownika o kodzie %1 nie powiodła się.'@[_obj.KOD])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'SLU','STRING[20]','Słownik użytkownika'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'KOD','STRING[8]','Kod');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'SLU',,);

SLO.cntx_psh();
SLO.index('SL_SYSWZ'); SLO.prefix('prosty','N');
{? SLO.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$SLO.ref();
      _selected.prefix($SLO.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.SLU:=SLO.SLU().NAZ;
      _tab.KOD:=SLO.KOD;
      _tab.add();
      SLO.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'SLU,KOD',20,'Wybór pozycji słowników użytkownika do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
SLO.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 a1352b1be7e4519f6621ea654f56aebdb5c5d9b78d333d30aaecbe8ff1850cc6de59bafba6644010fa6afc551befafb408611a7cfa694ae738fd0a2a752b85cbbe3363c067c0763efe12efdf72d263d1e34ada063e85ccbffeca1c1a804353bbd90df3dab19289dc9c889621794922610f1f9d0c0238b75705c1e45cbcf0f10b

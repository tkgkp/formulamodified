:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_edi_imp.fml
:: Utworzony: 16.08.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu mechanizmu importu
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - dziedzina
::       _c - nazwa
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_b_domain:=_b;
_name:=_c;

_name:=_def.env.LANG_PARAMS.translate(_name);

_def.ID:='EDI_IMP_%1'[_b_domain];
_def.DOMAIN:=exec('name','#b_domain',_b_domain);
_def.FUNPAR:='ZWS_IMPORTY_%1'[_b_domain];
_def.MULTIFIR:='T';

_def.FILE:='mechanizm_importow.xlsx';
_def.SHEET:='%1=1,1'[_name];
_txt:='Mechanizm importów - %1'@[_name];
_def.NAME:=_txt;
_def.DESC:=_txt;

_def.PREFIX:=$("EDI_S.index('SYSTEM'); EDI_S.prefix("+'\''+_b_domain+'\''+")");
_def.TABLE:="EDI_S";
_def.FIELDS:="exec('fields','xls_edi_imp',_a)";

_def.BEFORE:="EDI_S.cntx_psh()";
_def.AFTER:="EDI_S.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_edi_imp',_a,_b)";
_def.VALIDATE:="1";
_def.IMPORT:="exec('import','xls_edi_imp',_a,_b,_c)";
_def.SELECT:=$("exec('select','xls_edi_imp',_a,"+'\''+_b_domain+'\''+")");
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('DOMAIN','Dziedzina',1,,'3 znakowy kod dziedziny');
_def.define('ISTDEF','Definicja importu',1,,'20 znakowy kod definicji');
_def.define('PLIK','Plik',1,,'Nazwa pliku z definicją');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.DOMAIN.VALUE:=EDI_S.SYSTEM;
_table.ISTDEF.VALUE:=EDI_S.ISTDEF().K;
_table.PLIK.VALUE:=exec('edidefek_core','edi_imp',{? EXCEL_ENV.DIR_ON_SERVER || '' || '@' ?}+_excel.DIR,EDI_S.ref());

_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_dir:={? EXCEL_ENV.DIR_ON_SERVER || '' || '@' ?}+EXCEL_ENV.DIR;
_plik:=_obj.PLIK;
_sep:=exec('sep','#file',{? EXCEL_ENV.DIR_ON_SERVER || 2 || 0 ?});

{? +_plik>64
||
   _result.MSG:='Za długa nazwa pliku %1.'@[_plik];
   _result.RESULT:=0

|? fexists(_dir+_sep+_plik)
||
   EDI_Z.B_DOMAIN:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_obj.DOMAIN,,,1,,null());
   _result.MSG:=exec('edidefim','edi_imp',_dir,_plik,_mode,~EXCEL_ENV.IMP_MSG);
   _result.RESULT:=_result.MSG=''
||
   _result.MSG:='Brak pliku %1 w lokalizacji %2.'@[_plik,_dir];
   _result.RESULT:=0
?};
~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - dziedzina
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_bdomain:=_b;

_result:=0;

_tab:=tab_tmp(2
   ,'DOMAIN','STRING[3]','Domena'
   ,'K','STRING[20]','Kod'
   ,'N','STRING[60]','Nazwa'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'REF','STRING[16]','SQL ref');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'N',,);

EDI_S.cntx_psh();
EDI_S.index('SYSTEM');
EDI_S.prefix(_bdomain);
{? EDI_S.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$EDI_S.ref();
      _selected.prefix($EDI_S.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.DOMAIN:=EDI_S.SYSTEM;
      _tab.K:=EDI_S.ISTDEF().K;
      _tab.N:=ISTDEF.N;
      _tab.add();
      EDI_S.next()
   !}
?};
EDI_S.cntx_pop();
_result:=exec('select_action','#table',_tab
 ,'DOMAIN,K,N',20,'Wybór definicji importu do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 dc3e63014f94c6892cc758b18a0c6dac8e5835b5481eed7cc1d631408e3dff9c9e54e8842caa485df7973bf6a9a4c9bc9ff28d0b48d8f8cf77f9e771c81cfab406787138dd38c189c3da63ea8b6e1f2f39b570959d43deb2e62691089407751ab5154479863bc568fc121304b5baf600f3b8ac6547a459d9352da69c35a98158

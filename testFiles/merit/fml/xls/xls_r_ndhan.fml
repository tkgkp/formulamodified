:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_r_ndhan.fml
:: Utworzony: 24.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu niedziel handlowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='R_NDHAN';
_def.FUNPAR:='ZWS_PAR_PNDH';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Niedziele handlowe=1,1';
_def.NAME:='Niedziele handlowe'@;
_def.DESC:='Niedziele handlowe';

: źródło danych
_def.TABLE:="R_NDHAN";

: prolog
_def.BEFORE:="
   R_NDHAN.cntx_psh();
   R_NDHAN.f_clear();
   R_NDHAN.index('DATA');
   R_NDHAN.prefix();
   ~~
";

: epilog
_def.AFTER:="
   R_NDHAN.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('DATA',MS.name(R_NDHAN,'DATA'),1,,MS.comment(R_NDHAN,'DATA'));
   _env.define('OPIS',MS.name(R_NDHAN,'OPIS'),1,,MS.comment(R_NDHAN,'OPIS'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.DATA.VALUE:=R_NDHAN.DATA;
   _buf.OPIS.VALUE:=R_NDHAN.OPIS;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_r_ndhan',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_r_ndhan',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.DATA=date(0,0,0) || 'DATA'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(R_NDHAN,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

_ref:=null;
{? R_NDHAN.find_tab(,'DATA',,'=',_buf.DATA)
|| _ref:=R_NDHAN.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[$_a.DATA]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,R_NDHAN,"
   _a.ROK:=_b.DATA~1;
   _a.DATA:=_b.DATA;
   _a.OPIS:=_b.OPIS;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:50 e253f235bb451afca2f281988a45eb5df31f82143ddbb06b33f5d94a474e2479462c8cb0361db49a0cdefbffd244fb73b5b4515979844861ce9c5823ebd55ef437db476416ae214018a17114e8f598e80f1706f724dc8abaad0e185f4ee5993bb425d3374a4342ffe764fad8ab89ab6a472e5a892c4f123512d121c6947cdbba

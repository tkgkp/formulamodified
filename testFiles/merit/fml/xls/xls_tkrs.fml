:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tkrs.fml
:: Utworzony: 30.08.2018
:: Autor: IFZ
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu tabel kursowych.
::======================================================================================================================


\filename
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Nazwa pliku do eksportu struktur księgowych
::  OLD: \filename/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
'tabele_kursowe.xlsx'


\TKRS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  OLD: \TKRS/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TKRS';
_def.DOMAIN:=exec('name','#b_domain','FKS');
_def.FUNPAR:='ZWS_PAR_KRRS';
_def.FILE:=exec('filename','xls_tkrs');
_def.SHEET:='Tabele kursowe=1,1';
_def.NAME:='Tabele kursowe';
_def.DESC:='Tabele kursowe';
_def.MULTIFIR:='T';

_def.PREFIX:="TKRS.index('S1');TKRS.prefix()";
_def.TABLE:="TKRS";
_def.FIELDS:="exec('TKRS_fld','xls_tkrs',_a)";

_def.BEFORE:="TKRS.cntx_psh()";
_def.AFTER:="TKRS.cntx_pop()";
_def.SELECT:="exec('TKRS_select','xls_tkrs',_a)";

_def.EXPORT:="exec('TKRS_exp','xls_tkrs',_a,_b)";
_def.VALIDATE:="exec('TKRS_valid','xls_tkrs',_a,_b,_c)";
_def.IMPORT:="exec('TKRS_imp','xls_tkrs',_a,_b,_c)";
~~


\TKRS_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  OLD: \TKRS_fld/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('SBANK','Słownik bank',1,,'Słownik nazwa banku, pozycja słownika ogólnego',2);
_def.define('BANK','Nazwa banku',1,,'Nazwa banku, pozycja słownika ogólnego',2);
_def.define('DT','Data',1,,'Data tabeli kursów',4);
_def.define('SYM','Symbol tabeli',1,,'Symbol tabeli kursów',2);
_def.define('CZAS','Czas kursu',1,,'Czas tabeli kursów',5);
_def.define('SWAL','Słownik waluta',1,,'Słownik wskazanie na walutę',2);
_def.define('WAL','Waluta',1,,'Wskazanie na walutę',2);
~~


\TKRS_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_result:=0;

_tab:=tab_tmp(1,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'BANK','STRING[80]','Nazwa banku'
   ,'WAL','STRING[8]','Waluta'
   ,'SYM','STRING[80]','Symbol tabeli'
   ,'DT','STRING[50]','Data'
   ,'CZAS','STRING[50]','Czas');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'BANK',,);

TKRS.cntx_psh();
TKRS.index('TKRS_SM');
TKRS.prefix();
{? TKRS.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$TKRS.ref();
      _selected.prefix($TKRS.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.BANK:=TKRS.BANK().TR;
      _tab.WAL:=TKRS.WAL().KOD;
      _tab.DT:=$TKRS.DT;
      _tab.SYM:=TKRS.SYM;
      _tab.CZAS:=$TKRS.CZAS;
      _tab.add();
      TKRS.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'BANK,WAL,SYM,DT,CZAS',30,'Wybór tabel kursów do eksportu'@,1);
{? _result>0
|| _selected.erase();
   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TKRS.cntx_pop();
_result


\TKRS_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async()l
::  OLD: \TKRS_exp/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_tab:=_excel.table();

_result:=0;

{? _tab=TKRS | TKRS.seek(_tab.REF)
|| _table.SBANK.VALUE:={? TKRS.BANK & TKRS.BANK().SLU
                       || _excel.write_async('SLU',TKRS.BANK().SLU);
                          TKRS.BANK().SLU().NAZ
                       || ''
                       ?};
   _table.BANK.VALUE:={? TKRS.BANK
                      || _excel.write_async('SLO',TKRS.BANK);
                         TKRS.BANK().TR
                      || ''
                      ?};
   _table.DT.VALUE:=TKRS.DT;
   _table.SYM.VALUE:=TKRS.SYM;
   _table.CZAS.VALUE:=TKRS.CZAS;
   _table.SWAL.VALUE:=TKRS.WAL().SLU().NAZ; {? TKRS.WAL & TKRS.WAL().SLU || _excel.write_async('SLU',TKRS.WAL().SLU) ?};
   _table.WAL.VALUE:=TKRS.WAL().KOD;  {? TKRS.WAL || _excel.write_async('SLO',TKRS.WAL) ?};
:: eksport kursów z bieżącej tabeli kursów
   KRS.cntx_psh();
   KRS.index('KRS_KRAJ'); KRS.prefix(TKRS.ref());
   {? KRS.first()
   || {!
      |? _excel.write_async('KRS',KRS.ref());
         KRS.next()
      !}
   ?};
   KRS.cntx_pop()
?};
_result:=1;
_result


\TKRS_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  OLD: \TKRS_valid/xls_b.fml
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1;
{? _table.SBANK<>'' || _slu:=exec('FindInSet','#table','SLU','NAZ',_table.SBANK,,,1) || _slu:=null ?};
{? _table.BANK<>'' || _bank:=exec('FindInSet','#table','SLO','SL_TR',_table.BANK,_slu,,1) || _bank:=null ?};
{? _bank=null
|| _result.MSG:='Nie odnaleziono %1'@[_table.BANK];
   _result.RESULT:=0
?};

_sluwal:=exec('FindInSet','#table','SLU','NAZ',_table.SWAL,,,1);
_wal:=exec('FindInSet','#table','SLO','SL',_table.WAL,_sluwal,,1);
{? _wal=null
|| _result.MSG:='Nie odnaleziono %1'@[_table.WAL];
   _result.RESULT:=0
?};

{? _result.RESULT=1
|| TKRS.index('TKRS_SM');
   TKRS.prefix(_bank, _wal, _table.SYM, _table.SYM);
   {? TKRS.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || TKRS.blank(1) ?};
      TKRS.BANK:=_bank;
      TKRS.DT:=_table.DT;
      TKRS.SYM:=_table.SYM;
      TKRS.CZAS:=_table.CZAS;
      TKRS.WAL:=_wal
   ?}
?};
~~


\TKRS_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukcml
::  OLD: \TKRS_imp/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TKRS.add(1)
   |? _validate.ACTION='put'
   || _result:=TKRS.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie tabeli kursowej nie powiodło się.'@
   || _validate.MSG:='Poprawa tabeli kursowej nie powiodła się.'@
   ?}
?};
_result


\KRS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  OLD: \KRS/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KRS';
_def.DOMAIN:=exec('name','#b_domain','FKS');
_def.FILE:=exec('filename','xls_tkrs');
_def.SHEET:='Kursy walut=1,1';
_def.NAME:='Kursy walut';
_def.DESC:='Kursy walut';
_def.FUNPAR:='ZWS_PAR_KRRS';
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

_def.PREFIX:="KRS.index('S1');KRS.prefix()";
_def.TABLE:="KRS";
_def.FIELDS:="exec('KRS_fld','xls_tkrs',_a)";

_def.BEFORE:="KRS.cntx_psh()";
_def.AFTER:="KRS.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('KRS_exp','xls_tkrs',_a,_b)";
_def.VALIDATE:="exec('KRS_valid','xls_tkrs',_a,_b,_c)";
_def.IMPORT:="exec('KRS_imp','xls_tkrs',_a,_b,_c)";
~~


\KRS_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  OLD: \TKRS_fld/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('TKRS','Tabela kursów',1,,'Wskazanie na nagłówek tabeli kursów');
_def.define('BTKRS','Bank tabeli kursów',1,,'Kod banku tabeli kursów');
_def.define('B_SLO','Słownik banków tabeli',1,,'Nazwa słownika banków tabeli kursów');
_def.define('WTKRS','Waluta tabeli kursów',1,,'Symbol waluty tabeli kursów');
_def.define('W_SLO','Słownik walut tabeli',1,,'Nazwa słownika walut tabeli kursów');
_def.define('WAL','Waluta',1,,'Wskazanie na walutę');
_def.define('WAL_SLO','Słownik walut',1,,'Nazwa słownika walut');
_def.define('KRAJ','Kraj',1,,'Oznaczenie znakowe kraju');
_def.define('KP','Kupno',1,,'Kurs zakupu',,4);
_def.define('SP','Sprzedaż',1,,'Kurs sprzedaży',,4);
_def.define('SR','Średni',1,,'Kurs średni',,4);
_def.define('KC','Celny',1,,'Kurs do ustalania wartości celnej',,4);
~~


\KRS_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async()l
::  OLD: \TKRS_exp/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_tab:=_excel.table();
_result:=0;

_table.TKRS.VALUE:=KRS.TKRS().SYM; _excel.write_async('TKRS',KRS.TKRS);
_table.BTKRS.VALUE:=KRS.TKRS().BANK().KOD; _excel.write_async('SLO',KRS.TKRS().BANK);
_table.B_SLO.VALUE:=KRS.TKRS().BANK().SLU().NAZ; _excel.write_async('SLU',KRS.TKRS().BANK().SLU);
_table.WTKRS.VALUE:=KRS.TKRS().WAL().KOD; _excel.write_async('SLO',KRS.TKRS().WAL);
_table.W_SLO.VALUE:=KRS.TKRS().WAL().SLU().NAZ; _excel.write_async('SLU',KRS.TKRS().WAL().SLU);
_table.WAL.VALUE:=KRS.WAL().KOD; _excel.write_async('SLO',KRS.WAL);
_table.WAL_SLO.VALUE:=KRS.WAL().SLU().NAZ; _excel.write_async('SLU',KRS.WAL().SLU);
_table.KRAJ.VALUE:=KRS.KRAJ;
_table.KP.VALUE:=KRS.KP;
_table.SP.VALUE:=KRS.SP;
_table.SR.VALUE:=KRS.SR;
_table.KC.VALUE:=KRS.KC;

_result:=1;
_result


\KRS_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  OLD: \TKRS_valid/xls_b.fml
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1;

: wychwyć błędy krytyczne
_fld:={? _table.TKRS='' || 'TKRS' || '' ?};
{? _fld<>''
:  jeżeli problem
|| _result.msg_empty(_fld);
   _result.RESULT:=0;
   return()
?};

_slu:=exec('FindInSet','#table','SLU','NAZ',_table.B_SLO,,,1);
_btkrs:=exec('FindInSet','#table','SLO','SL',_table.BTKRS,_slu,,1);
_slu_w:=exec('FindInSet','#table','SLU','NAZ',_table.W_SLO,,,1);
_wtkrs:=exec('FindInSet','#table','SLO','SL',_table.WTKRS,_slu_w,,1);

TKRS.cntx_psh();
TKRS.index('TKRS_SM');
TKRS.prefix(_btkrs,_wtkrs,_table.TKRS,_table.TKRS);
{? TKRS.first()
|| _tkrs:=TKRS.ref()
|| _result.msg_norec('TKRS');
   _result.RESULT:=0
?};
TKRS.cntx_pop();

{? _result.RESULT=1
|| KRS.index('KRS_KRAJ');
   KRS.prefix(_tkrs,_table.KRAJ,);
   {? KRS.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || KRS.blank(1) ?};
      KRS.TKRS:=_tkrs;
      _slu_wal:=exec('FindInSet','#table','SLU','NAZ',_table.WAL_SLO,,,1);
      KRS.WAL:=exec('FindInSet','#table','SLO','SL',_table.WAL,_slu_wal,,1);
      KRS.KRAJ:=_table.KRAJ;
      KRS.KP:=_table.KP;
      KRS.SP:=_table.SP;
      KRS.SR:=_table.SR;
      KRS.KC:=_table.KC
   ?}
?};
~~


\KRS_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukcml
::  OLD: \KRS_imp/xls_tkrs.fml
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KRS.add(1)
   |? _validate.ACTION='put'
   || _result:=KRS.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie kursów walut nie powiodło się.'@
   || _validate.MSG:='Poprawa kursów walut nie powiodła się.'@
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 ad232f38da9c57e1f9af32e67060f3bd2e7953ea3454b185209ffc189b256363940a0d2ca50dc2228a98bd5ba242f5e111ef6334d827e6c1363d030f3cf3ab18446c462d4e8def4e44fb0db122fb0204a982e35936d4adbde34f534a524aaa8e87098d34c75f0c822749576869cf8dfc28e87bcddc135d6ca61fbe9e93f4aa24

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_koperkas.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu operacje dostępne dla kas
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KOPERKAS';
_def.DOMAIN:=exec('name','#b_domain','KAS');
_def.FUNPAR:='ZWS_PAR_FKOK';

_def.FILE:='kasa.xlsx';
_def.SHEET:='Operacje dostępne dla kas=1,1';
_def.NAME:='Operacje dostępne dla kas'@;
_def.DESC:='Operacje dostępne dla kas';
_def.TABLE:="exec('table','xls_koperkas')";
_def.FIELDS:="exec('fields','xls_koperkas',_a)";
_def.EXPORT:="exec('export','xls_koperkas',_a,_b)";
_def.IMPORT:="exec('import','xls_koperkas',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_koperkas',_a,_b,_c)";
_def.SELECT:="exec('select','xls_koperkas',_a)";
_def.PREFIX:="KOPERKAS.index('STANKAS1'); KOPERKAS.prefix()";
_def.BEFORE:="KOPERKAS.cntx_psh()";
_def.AFTER:="KOPERKAS.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
KOPERKAS


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('OPER','Operacja',0,,'Wskazanie na operacje',type_of(''));
_def.define('STANKAS','Stanowisko',0,,'Wskazanie na stanowisko',type_of(''));
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
KOPERKAS.prefix();
{? _validate.ACTION='add'
||
   KOPERKAS.blank();
   KOPERKAS.STANKAS:=_validate.OBJ.STANKAS;
   KOPERKAS.OPER:=_validate.OBJ.OPER
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.STANKAS.VALUE:=form(KOPERKAS.STANKAS().KOD,,,'99');
_excel.write_async('STANKAS',KOPERKAS.STANKAS);

_table.OPER.VALUE:=KOPERKAS.OPER().KOD;
_excel.write_async('OPER',KOPERKAS.OPER);

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('STANKAS', 'OPER');

_result.OBJ.STANKAS:=exec('FindInSet','#table','STANKAS','KOD',#_obj.STANKAS);
{? _result.OBJ.STANKAS=null()
||
   _result.MSG:='Nie znaleziono stanowiska o kodzie %1.'@[_obj.STANKAS];
   _result.RESULT:=0;
   return(~~)
?};

_result.OBJ.OPER:=exec('FindInSet','#table','OPER','KOD',_obj.OPER,,,1);
{? _result.OBJ.OPER=null()
||
   _result.MSG:='Nie znaleziono operacji o kodzie %1.'@[_obj.OPER];
   _result.RESULT:=0;
   return(~~)
?};

{? _result.RESULT = 1
||
   KOPERKAS.index('STANKAS1');
   KOPERKAS.prefix(_result.OBJ.STANKAS, _obj.OPER);
   {? KOPERKAS.first()
   ||
      {? _mode=0
      || _result.RESULT:=0
      ||
         _result.ACTION:='put';
         exec('record','xls_koperkas',_obj,_mode,_result);
         {?__CHK.record(KOPERKAS,,'STANKAS', 'OPER')<>''
         ||
            _result.RESULT:=0
         ?}
      ?}
   ||
      _result.ACTION:='add';
      exec('record','xls_koperkas',_obj,_mode,_result);
      {? __CHK.record(KOPERKAS,,'STANKAS', 'OPER')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=KOPERKAS.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=KOPERKAS.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie pozycji nie powiodło się.'@)
   ||
      FUN.info('Poprawa pozycji nie powiodła się.'@)
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'STANKAS','STRING[30]','Stanowisko'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OPER','STRING[30]','Operacja');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'STANKAS',,);

KOPERKAS.cntx_psh();
KOPERKAS.index('STANKAS1');
KOPERKAS.prefix();
{? KOPERKAS.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$KOPERKAS.ref();
      _selected.prefix($KOPERKAS.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.STANKAS:=KOPERKAS.STANKAS().NAZWA;
      _tab.OPER:=KOPERKAS.OPER().NAZWA;
      _tab.add();
      KOPERKAS.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'STANKAS,OPER',20,'Wybór operacji dostępnych dla kas do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KOPERKAS.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:48 7c4efb917f8d78e93227c0290f74f0b995bf77ac5d88527ff8344b834e6a72ad05fc91d0df80ab274e8fc9985d12cb19a9108d7e03b29ccaf6a3c82122af402ef1e45df9f3812253172fe8dd7cd0bde4766ca39cca25cd9eef844829b50d9b67636dff12e3152cac75c64669bfb75bba95d18ad2cf6dfd3af03cd977a416810e

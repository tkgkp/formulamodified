:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kuser_upr.fml
:: Utworzony: 30.08.2019
:: Autor: MW
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu i importu uprawnień do danych - tabela KUSERUPR
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USRUPR';
_def.ID:='UPR_STANKAS';

_def.FILE:='uprawnienia.xlsx';
_def.SHEET:='Stanowiska kasowe=1,1';
_def.NAME:='Uprawnienia do stanowisk kasowych'@;
_def.DESC:='Uprawnienia do stanowisk kasowych';

_def.TABLE:="exec('table','xls_kuser_upr')";
_def.FIELDS:="exec('fields','xls_kuser_upr',_a)";

_def.TAB_IMP:="KUSERUPR";

_def.PREFIX:="exec('prefix','xls_kuser_upr')";
_def.EXPORT:="exec('export','xls_kuser_upr',_a,_b)";

_def.VALIDATE:="exec('validate','xls_kuser_upr',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kuser_upr',_a,_b,_c)";
_def.BEFORE:="KUSERUPR.cntx_psh()";
_def.AFTER:="KUSERUPR.cntx_pop();
             {? _a.KIND='IMPORT' || exec('usr_upr_napraw','users_upraw','KAS') ?}";

_def.SELECT:="exec('select','xls_kuser_upr',_a,_b)";

exec('SelUsersTab','xls_users_up');

~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
__SelUsers.prefix('KAS');
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('TabKUserUpr','users_upraw')



\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_tooltip:=MS.name(KUSERUPR,'KU')+' '+'(max. %1 znaków). '@[$MS.fld_len(USERS,'KOD')];
_tooltip+='Pole wymagane'@;
_def.define('USER','Użytkownik'@,1,,_tooltip);

_tooltip:=MS.name(KUSERUPR,'STANKAS')+' (numer). ';
_tooltip+='Pole wymagane'@;
_def.define('STANKAS','Stanowisko'@,1,,_tooltip,type_of(0),0);

_tooltip:='Uprawniony (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('DOSTEP','Czy uprawniony?'@,1,,_tooltip);

_tooltip:='Pełne uprawnienia (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('PELNE','Czy pełne uprawnienia?'@,1,,_tooltip);

~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------

_excel:=_a;
_obj:=_b;
_tab:=_excel.table();

{? __SelUsers.first()
|| _result:=__SelUsers.find_key(_tab.USER_REF)
|| _result:=1
?};

{? _result
|| _obj.USER.VALUE:=_tab.USER_KOD;
   _obj.STANKAS.VALUE:=_tab.KOD;
   _obj.DOSTEP.VALUE:=_tab.ACCESS;
   _obj.PELNE.VALUE:=_tab.FULL
?};

_result


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_obj:=_a;
_mode:=_b;
_validate:=_c;

KUSERUPR.prefix();
{? _validate.ACTION='add'
|| KUSERUPR.blank()
?};
KUSERUPR.KU:=_validate.OBJ.USER;
KUSERUPR.STANKAS:=_validate.OBJ.STANKAS;
KUSERUPR.UPR:=_obj.DOSTEP;
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER   - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obj_new() - obiekt z resultem, wynik działania exec('args_valid','#excel_imex').
::                        Obiekt ten jest przekazywany do formuły na IMPORT. Pole RESULT decyduje czy formuła
::                        na import się wykona. Pole MSG służy to odpisania komunikatu
::----------------------------------------------------------------------------------------------------------------------

_obj:=_a;
_mode:=_b;
_result:=_c;

_can_continue:=1;
:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _obj.USER=''
|| _can_continue:=0;
   _result.msg_empty('USER')
?};
{? type_of(_obj.STANKAS)=type_of(0) & int(_obj.STANKAS)=_obj.STANKAS
|| {? _obj.STANKAS<0
   || _can_continue:=0;
      _result.msg_value('STANKAS', 'Wymagany jest numer stanowiska.')
   ?}
|| _can_continue:=0;
   _result.msg_int('STANKAS')
?};

_user:=null();
{? _can_continue
|| _user:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1);
   {? _user=null()
   || _can_continue:=0;
      _result.msg_norec('USER')
   ?}
?};

_stankas:=null();
{? _can_continue
|| _stankas:=exec('FindInSet','#table','STANKAS','KOD',_obj.STANKAS);
   {? _stankas=null()
   || _can_continue:=0;
      _result.msg_norec('STANKAS')
   ?}
?};

{? _obj.DOSTEP<>'T' & _obj.DOSTEP<>'N'
|| _can_continue:=0;
   _result.msg_inset('DOSTEP','T','N')
?};

{? _obj.PELNE<>'T' & _obj.PELNE<>'N'
|| _can_continue:=0;
   _result.msg_inset('PELNE','T','N')
?};

{? _can_continue & (_obj.DOSTEP='T' | _obj.PELNE='T')
|| {? ~exec('user_upraw_dostep','users_upraw','KAS',_user)
   || _can_continue:=0;
      _msg:='Użytkownik nie może mieć uprawnienia, nie ma odpowiedniej roli.'@;
      {? _obj.PELNE='T'
      || _result.msg_value('PELNE',_msg)
      || _result.msg_value('DOSTEP',_msg)
      ?}
   ?}
?};

_bperm_ref:=exec('bperm_ref','users_upraw','KAS');
B_PERM_U.index('USER');
B_PERM_U.prefix(REF.FIRMA,_user,_bperm_ref);
{? ~B_PERM_U.first()
|| _can_continue:=0
?};

_result.RESULT:=_can_continue;

:: OK
{? _result.RESULT & _obj.PELNE='T'
|| {? B_PERM_U.FULL<>'T'
   || _result.ACTION:='padd';
      _result.OBJ:=obj_new('USER','PELNE');
      _result.OBJ.USER:=_user;
      _result.OBJ.PELNE:='T'
   ?}
|? _result.RESULT & (_mode=1 | _obj.DOSTEP='T')
|| KUSERUPR.index('KU_ST');
   KUSERUPR.prefix(_user,_stankas);
   {? KUSERUPR.first()
   || {? _obj.DOSTEP='N'
      || _result.ACTION:='del'
      |? _mode=1 & B_PERM_U.FULL='T'
      || _result.ACTION:='pdel';
         _result.OBJ:=obj_new('USER','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.PELNE:='N'
      ?}
   || {? _obj.DOSTEP='T'
      || _result.ACTION:='add';
         _result.OBJ:=obj_new('USER','STANKAS','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.STANKAS:=_stankas;
         {? _mode=1 & B_PERM_U.FULL='T'
         || _result.OBJ.PELNE:='N'
         || _result.OBJ.PELNE:=''
         ?};
         exec('record','xls_kuser_upr',_obj,_mode,_result)
      |? _mode=1 & B_PERM_U.FULL='T'
      || _result.ACTION:='pdel';
         _result.OBJ:=obj_new('USER','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.PELNE:='N'
      ?}
   ?}
?};

~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KUSERUPR.add(1)
   |? _validate.ACTION='del'
   || {? KUSERUPR.count()=0 & KUSERUPR.del(1,1)<>0
      || _result:=1
      ?}
   |? _validate.ACTION='padd'
   || B_PERM_U.FULL:='T';
      B_PERM_U.ACCESS:='T';
      _result:=B_PERM_U.put();
      {? _result
      || exec('user_upraw_ustaw_pelne','users_upraw','KAS',_validate.OBJ.USER)
      ?}
   ?};
   {? ((_validate.ACTION='add' | _validate.ACTION='del') & _validate.OBJ.PELNE='N' & _result)
      | _validate.ACTION='pdel'
   || B_PERM_U.FULL:='N';
      _result:=B_PERM_U.put()
   ?}
?};

{? _result=0
|| _validate.MSG:='';
   {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do stanowiska kasowego %2 nie powiodło się.'@
                     [_obj.USER,$_obj.STANKAS]
   |? _validate.ACTION='del'
   || _validate.MSG:='Usunięcie uprawnienia użytkownika %1 do stanowiska kasowego %2 nie powiodło się.'@
                     [_obj.USER,$_obj.STANKAS]
   |? _validate.ACTION='padd'
   || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do wszystkich stanowisk kasowych nie powiodło się.'@
                     [_obj.USER]
   ?};
   {? ((_validate.ACTION='add' | _validate.ACTION='del') & _validate.OBJ.PELNE='N') | _validate.ACTION='pdel'
   || {?_validate.MSG<>''
      || _validate.MSG+=' '
      ?};
      _validate.MSG+='Usunięcie uprawnienia użytkownika %1 do wszystkich stanowisk kasowych nie powiodło się.'@
                     [_obj.USER]
   ?}
|| {? _validate.ACTION='padd'
   || _validate.MSG:='Dodano uprawnienie użytkownika %1 do wszystkich stanowisk kasowych.'@[_obj.USER]
   |? ((_validate.ACTION='add' | _validate.ACTION='del') & _validate.OBJ.PELNE='N') | _validate.ACTION='pdel'
   || _validate.MSG:='Usunięto uprawnienie użytkownika %1 do wszystkich stanowisk kasowych.'@[_obj.USER]
   ?};
   _validate.MSG:=''
?};

_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - obj_new - środowisko mechanizmu
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_excel:=_b;

exec('select','xls_users_up',_selected,_excel,'KAS')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 9a7558c29c2499564486cbc783a3292b7ed70b2f053830e846b7efbea698cd50470392830d6b7f38e9d9202486b188172bf1e98c48111dad34627d4810c7b3a81ac96531e5f63dde911a9e9aa381782f227b72f434e553c8516c864e1a46cc89e8761aeb813b8659d1a41d5a52c25c7b336a0b10926553fb3aeaab14d3ac40b0

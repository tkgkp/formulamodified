:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_webhook.fml
:: Utworzony: 21.08.2020
:: Autor: PD
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================
\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='WEBHOOK';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_TEAM';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='MS Teams zespoły=1,1';
_def.NAME:='MS Teams zespoły'@;
_def.DESC:='MS Teams zespoły';

_def.PREFIX:="exec('prefix','xls_webhook')";
_def.TABLE:="exec('table','xls_webhook')";
_def.FIELDS:="exec('fields','xls_webhook',_a)";

_def.BEFORE:="WEBHOOK.cntx_psh()";
_def.AFTER:="WEBHOOK.cntx_pop()";
_def.SELECT:="exec('select','xls_webhook',_a)";

_def.EXPORT:="exec('export','xls_webhook',_a,_b)";
_def.VALIDATE:="exec('validate','xls_webhook',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_webhook',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
WEBHOOK.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
WEBHOOK


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NAZ','Nazwa',1,,'20 znakowa Nazwa');
_def.define('URL','URL',1,,'250 znakowy URL');
_def.define('AKT','Aktywny',1,,'T/N czy zespół aktywny');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
WEBHOOK.prefix();
{? _validate.ACTION='add'
||
   WEBHOOK.blank()
?};

WEBHOOK.NAZ:=_obj.NAZ;
WEBHOOK.URL:=_obj.URL;
WEBHOOK.AKT:=_obj.AKT;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.NAZ.VALUE:=WEBHOOK.NAZ;
_table.URL.VALUE:=WEBHOOK.URL;
_table.AKT.VALUE:=WEBHOOK.AKT;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? (+_obj.NAZ)>20 || _result.msg_length('NAZ',20); _result.RESULT:=0 ?};
{? (+_obj.URL)>250 || _result.msg_length('URL',250); _result.RESULT:=0 ?};
{? (+_obj.AKT)>1 || _result.msg_length('AKT',1); _result.RESULT:=0 ?};
{? _obj.AKT<>'N' & _obj.AKT<>'T' || _result.msg_inset('AKT','N','T');_result.RESULT:=0 ?};
{? _obj.NAZ='' || _result.msg_empty('NAZ'); _result.RESULT:=0 ?};
{? _obj.URL='' || _result.msg_empty('URL'); _result.RESULT:=0 ?};

WEBHOOK.index('NAZ');
WEBHOOK.prefix(_obj.NAZ,);
{? WEBHOOK.first()
||
   {? _mode=0
   || _result.RESULT:=0
   ||
      _result.ACTION:='put';
      exec('record','xls_webhook',_obj,_mode,_result)
   ?}
||
      WEBHOOK.index('URL');
      WEBHOOK.prefix(_obj.URL);
      {? WEBHOOK.first()
      ||
         {? _mode=0
         || _result.RESULT:=0
         ||
            _result.ACTION:='put';
            exec('record','xls_webhook',_obj,_mode,_result)
         ?}
      ||
      _result.ACTION:='add';
      exec('record','xls_webhook',_obj,_mode,_result)
      ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      WEBHOOK.prefix();
      _result:=WEBHOOK.add(1)
   |? _validate.ACTION='put'
   ||
      WEBHOOK.prefix();
      _result:=WEBHOOK.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie zespołu %1 nie powiodło się.'@[_obj.NAZ]
   ||
      _validate.MSG:='Poprawa zespołu %1 nie powiodła się.'@[_obj.NAZ]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2,'NAZ','STRING[20]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'URL','STRING[250]','URL'
   ,'AKT','STRING[1]','Aktywny'
   );

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZ',,);

WEBHOOK.cntx_psh();
WEBHOOK.index('NAZ');
WEBHOOK.prefix();
{? WEBHOOK.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$WEBHOOK.ref();
      _selected.prefix($WEBHOOK.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NAZ:=WEBHOOK.NAZ;
      _tab.URL:=WEBHOOK.URL;
      _tab.AKT:=WEBHOOK.AKT;
      _tab.add();
      WEBHOOK.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NAZ',20,'Wybór zespołów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
WEBHOOK.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2020/10/16 15:26:07 91e205f80baaf6165601044052f5ab206abf879ac2ec3307966f2fdb81068246e283209babe8665da727550a31075702f1ceb9552c5111dae02ec1e3be95fc3a5555d45410b6b1f2f788862f6c48722d3c2d81bacd03fa2c34abc5e8fa08451fc1ddc1c9d924e209bba6edea37868192dc4ab1e1881321996526fcbadc3d10e3

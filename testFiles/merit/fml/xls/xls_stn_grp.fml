:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_stn_grp.fml
:: Utworzony: 14.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu stanowisk w grupie
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='STN_GRP';
_def.FUNPAR:='ZWS_PAR_PGST';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Stanowiska w grupie=1,1';
_def.NAME:='Stanowiska w grupie'@;
_def.DESC:='Stanowiska w grupie';

: źródło danych
_def.TABLE:="STN_GRP";

: prolog
_def.BEFORE:="
   STN.cntx_psh();
   STN.prefix();
   STN_GR.cntx_psh();
   STN_GR.prefix();
   STN_GRP.cntx_psh();
   STN_GRP.f_clear();
   STN_GRP.index('UNIQUE');
   STN_GRP.prefix(REF.FIRMA);
   ~~
";

: epilog
_def.AFTER:="
   STN_GRP.cntx_pop();
   STN_GR.cntx_pop();
   STN.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('STN_GR',MS.name(STN_GRP,'STN_GR'),1,,MS.comment(STN_GRP,'STN_GR'));
   _env.define('STN',   MS.name(STN_GRP,'STN'),   1,,MS.comment(STN_GRP,'STN'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.STN_GR.VALUE:=STN_GRP.STN_GR().GRUPA;
   _buf.STN.VALUE:=STN_GRP.STN().ST;
   _env.write_async('STN_GR',STN_GRP.STN_GR);
   _env.write_async('STN',STN_GRP.STN);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_stn_grp',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_stn_grp',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.STN_GR='' || 'STN_GR'
   |? _buf.STN=''    || 'STN'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(STN_GR,'GRUPA');
{? +_buf.STN_GR>_max
|| _val.msg_length('STN_GR',_max);
   _buf.STN_GR:=_max+_buf.STN_GR
?};
_max:=MS.fld_len(STN,'ST');
{? +_buf.STN>_max
|| _val.msg_length('STN',_max);
   _buf.STN:=_max+_buf.STN
?};

: weryfikacja spójności referencyjnej
{? ~STN_GR.find_tab(,'GRUPA',,'=',_buf.STN_GR)
|| _val.msg_norec('STN_GR');
   _val.RESULT:=0;
   return()
|| _gr:=STN_GR.ref()
?};
{? ~STN.find_tab(,'ST',,'=',_buf.STN)
|| _val.msg_norec('STN');
   _val.RESULT:=0;
   return()
|| _stn:=STN.ref()
?};

_ref:=null;
{? STN_GRP.find_tab(,'STN_GR',,'=',_gr,'STN',,'=',_stn)
|| _ref:=STN_GRP.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('STN_GRP','STN_GR','STN','to_str');
_obj.to_str:="'%1: %2'[_a.STN_GR,_a.STN]";
_obj.STN_GR:=_gr;
_obj.STN:=_stn;
_obj.STN_GRP:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.FIRMA:=REF.FIRMA;
   _a.STN_GR:=_c.STN_GR;
   _a.STN:=_c.STN;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? STN_GRP.seek(_obj.STN_GRP)
   || _set(STN_GRP,_buf,_obj);
      {? ~STN_GRP.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,STN_GRP.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| STN_GRP.blank();
   _set(STN_GRP,_buf,_obj);
   {? ~STN_GRP.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 deec5506a6d87de5c46304c446a9a52726f19207c2d04ee49b15be06a6fbcc1e1b6f5792540a066c80341118f1ed756ddb86ff431df992aae44390f9495f09854460babffb97ddcdf03df65d8049a3d42790a55b16f8e83c821c26c124360885099e47c8bca91eb20a7e29c5579d9bc12280cba474cccea69090460d7b733380

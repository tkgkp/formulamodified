:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_pd_d_zu.fml
:: Utworzony: 25.01.2021
:: Autor: AKUL
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu użytkowników zestawów formuł planu dostaw
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PD_D_ZU';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PLAN_DST';

_def.FILE:='plan_dostaw.xlsx';
_def.SHEET:='Użytkownicy zestawów formuł=1,1';
_def.NAME:='Użytkownicy zestawów formuł'@;
_def.DESC:='Użytkownicy zestawów formuł';
_def.TABLE:="PD_D_ZU";
_def.FIELDS:="exec('fields','xls_pd_d_zu',_a)";
_def.EXPORT:="exec('export','xls_pd_d_zu',_a,_b)";
_def.IMPORT:="exec('import','xls_pd_d_zu',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_pd_d_zu',_a,_b,_c)";
_def.SELECT:="exec('select','xls_pd_d_zu',_a)";
_def.PREFIX:="PD_D_ZU.index('UNIK'); PD_D_ZU.prefix()";
_def.BEFORE:="PD_D_ZU.cntx_psh()";
_def.AFTER:="PD_D_ZU.cntx_pop()";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('PD_D_ZN','Zestaw',0);
_def.define('USER','Użytkownik',0);
_def.define('DEFAULT','Domyślny zestaw');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PD_D_ZU.prefix();
{? _validate.ACTION='add'
|| PD_D_ZU.blank();
   PD_D_ZU.PD_D_ZN:=_validate.OBJ.PD_D_ZN;
   PD_D_ZU.USER:=_validate.OBJ.USER
?};
PD_D_ZU.DEFAULT:=_obj.DEFAULT;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.PD_D_ZN.VALUE:=PD_D_ZU.PD_D_ZN().NAZ; _excel.write_async('PD_D_ZN',PD_D_ZU.PD_D_ZN);
_table.USER.VALUE:=PD_D_ZU.USER().KOD; _excel.write_async('USERS',PD_D_ZU.USER);
_table.DEFAULT.VALUE:=PD_D_ZU.DEFAULT;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('PD_D_ZN','USER');

::Błędy
_result.OBJ.PD_D_ZN:=exec('FindInSet','#table','PD_D_ZN','NAZ',_obj.PD_D_ZN,,,1,,null());
{? _result.OBJ.PD_D_ZN=null()
|| _result.MSG:='Nie znaleziono nagłówka zestawu formuł o nazwie %1.'@[_obj.PD_D_ZN];
   _result.RESULT:=0;
   return(~~)
?};
_result.OBJ.USER:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1,,null());
{? _result.OBJ.USER=null()
|| _result.MSG:='Nie znaleziono użytkownika o kodzie %1.'@[_obj.USER];
   _result.RESULT:=0;
   return(~~)
?};
{? +_obj.DEFAULT=0 | 'TN'*_obj.DEFAULT=0
|| _result.msg_inset('DEFAULT','T','N');
   _result.RESULT:=0
?};

{? _result.RESULT=1
|| PD_D_ZU.index('UNIK');
   PD_D_ZU.prefix(_result.OBJ.PD_D_ZN,_result.OBJ.USER);
   {? PD_D_ZU.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_pd_d_zu',_obj,_mode,_result);
         {? __CHK.record(PD_D_ZU,,'PD_D_ZN','USER')<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_pd_d_zu',_obj,_mode,_result);
      {? __CHK.record(PD_D_ZU,,'PD_D_ZN','USER')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=PD_D_ZU.add(1)
   |? _validate.ACTION='put'
   || _result:=PD_D_ZU.put(1)
   ?}
?};
:: Jeżeli nowy rekord jest zestawym domyślnym, to czyszczę znacznik na dotychczasowym zestawie domyślnym
{? PD_D_ZU.DEFAULT='T'
|| _ref:=PD_D_ZU.ref();
   PD_D_ZU.cntx_psh();
   PD_D_ZU.index('USER');
   PD_D_ZU.prefix(PD_D_ZU.USER,'T');
   {? PD_D_ZU.first() & PD_D_ZU.size()>1
   || _next:=1;
      {? PD_D_ZU.ref()=_ref
      || _next:=PD_D_ZU.next()
      ?};
      {? _next>0
      || PD_D_ZU.prefix();
         PD_D_ZU.DEFAULT:='N';
         PD_D_ZU.put()
      ?}
   ?};
   PD_D_ZU.cntx_pop()
?};
{? _result=0
|| {? _validate.ACTION='add'
   || FUN.info('Dodanie użytkownika %2 do zestawu formuł %1 nie powiodło się.'@[_obj.PD_D_ZN,_obj.USER])
   || FUN.info('Poprawa użytkownika %2 do zestawu formuł %1 nie powiodła się.'@[_obj.PD_D_ZN,_obj.USER])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'ZN','STRING[100]','Zestaw'
   ,'USER','STRING[127]','Użytkownik'
   ,'DEFAULT','STRING[8]','Domyślny zestaw'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'ZN',,,'USER',,);

PD_D_ZU.cntx_psh();
PD_D_ZU.index('UNIK');
PD_D_ZU.prefix();
{? PD_D_ZU.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$PD_D_ZU.ref();
      _selected.prefix($PD_D_ZU.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.ZN:=PD_D_ZU.PD_D_ZN().NAZ;
      _tab.USER:=PD_D_ZU.USER().KOD;
      _tab.DEFAULT:=PD_D_ZU.DEFAULT;
      _tab.add();
      PD_D_ZU.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'ZN,USER,DEFAULT[TN]',20,'Wybór użytkowników zestawów formuł planu dostaw do eksportu'@,1);
{? _result>0
|| _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PD_D_ZU.cntx_pop();

_result

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:15 3980c178dba079bef95a2efb34c031cf3978c93203c6849e9cdf89738d618a62cb1ac80b3ad524558d3fc5fef1fa98595a89889d661a0032f436790d256c2f8ac93289418c33a2abbe7a4870fbae05742b5395b9d2a7e4ec4ba7bf043b463aaf7416be7dccf3905cb6711566e5e59b96321e10bd626b34e3ba6b6829e499e5e7

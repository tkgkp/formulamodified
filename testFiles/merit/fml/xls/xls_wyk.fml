:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_wyk.fml
:: Utworzony: 09.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu opisów wykształceń
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='WYK';
_def.FUNPAR:='ZWS_PAR_PWYK';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Wykształcenie=1,1';
_def.NAME:='Wykształcenie'@;
_def.DESC:='Wykształcenie';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="WYK";

: prolog
_def.BEFORE:="
   WYK.cntx_psh();
   WYK.f_clear();
   WYK.index('WYKSZNAZ');
   WYK.prefix()
";

: epilog
_def.AFTER:="
   WYK.cntx_pop()
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('WK',      MS.name(WYK,'WK'),      1,,MS.comment(WYK,'WK'));
   _env.define('DU',      MS.name(WYK,'DU'),      1,,MS.comment(WYK,'DU'),,2);
   _env.define('KOD',     MS.name(WYK,'KOD'),     1,,MS.comment(WYK,'KOD'));
   _env.define('KODPFRON',MS.name(WYK,'KODPFRON'),1,,MS.comment(WYK,'KODPFRON'));
   _env.define('GUS',     MS.name(WYK,'GUS'),     1,,MS.comment(WYK,'GUS'),,0);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.WK.VALUE:=WYK.WK;
   _buf.DU.VALUE:=WYK.DU;
   _buf.KOD.VALUE:=WYK.KOD;
   _buf.KODPFRON.VALUE:=WYK.KODPFRON;
   _buf.GUS.VALUE:=WYK.GUS;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_wyk',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_wyk',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.WK='' || 'WK'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? _buf.DU<0
|| _val.msg_range('DU',0);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(WYK,'WK');
{? +_buf.WK>_max
|| _val.msg_length('WK',_max);
   _buf.WK:=_max+_buf.WK
?};
_max:=MS.fld_len(WYK,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(WYK,'KODPFRON');
{? +_buf.KODPFRON>_max
|| _val.msg_length('KODPFRON',_max);
   _buf.KODPFRON:=_max+_buf.KODPFRON
?};

_ref:=null;
{? WYK.find_tab(,'WK',,'=',_buf.WK)
|| _ref:=WYK.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('WYK','to_str');
_obj.to_str:="'%1'[_a.WK]";
_obj.WYK:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.WK:=_b.WK;
   _a.DU:=_b.DU;
   _a.KOD:=_b.KOD;
   _a.KODPFRON:=_b.KODPFRON;
   _a.GUS:=_b.GUS;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? WYK.seek(_obj.WYK)
   || _set(WYK,_buf);
      {? ~WYK.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,WYK.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| WYK.blank();
   _set(WYK,_buf);
   {? ~WYK.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 2aee04953300a3fd732365683bdcf2a070ebf262e53991d0abda1334d4266b4fc79f70575b8de7402991dd44a275e8fc83f4a8645a8e7dec16f1fb50966a174a18ef5a8724b49bcece7c557b89fbb910661f01208740aae10603dda07d3cf18635b5d4f692ab960256414568e35830fc3b613b156a163e3bee2ce64c4d10c45b

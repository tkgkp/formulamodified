:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_mkasa_upr.fml
:: Utworzony: 30.08.2019
:: Autor: MW
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu i importu uprawnień do danych - tabela MKASAUPR
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USRUPR';
_def.ID:='UPR_MKASA';

_def.FILE:='uprawnienia.xlsx';
_def.SHEET:='Multikasy=1,1';
_def.NAME:='Uprawnienia do multikas'@;
_def.DESC:='Uprawnienia do multikas';

_def.TABLE:="exec('table','xls_mkasa_upr')";
_def.FIELDS:="exec('fields','xls_mkasa_upr',_a)";

_def.TAB_IMP:="MKASAUPR";

_def.PREFIX:="exec('prefix','xls_mkasa_upr')";
_def.EXPORT:="exec('export','xls_mkasa_upr',_a,_b)";

_def.VALIDATE:="exec('validate','xls_mkasa_upr',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_mkasa_upr',_a,_b,_c)";
_def.BEFORE:="MKASAUPR.cntx_psh()";
_def.AFTER:="MKASAUPR.cntx_pop();
             {? _a.KIND='IMPORT' || exec('usr_upr_napraw','users_upraw','MKAS') ?}";

_def.SELECT:="exec('select','xls_mkasa_upr',_a,_b)";

exec('SelUsersTab','xls_users_up');

~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
__SelUsers.prefix('MKAS');
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('TabMKasaUpr','users_upraw')



\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_tooltip:=MS.name(MKASAUPR,'USER')+' '+'(max. %1 znaków). '@[$MS.fld_len(USERS,'KOD')];
_tooltip+='Pole wymagane'@;
_def.define('USER','Użytkownik'@,1,,_tooltip);

_tooltip:=MS.name(MKASAUPR,'MKASA')+' '+'(max. %1 znaków). '@[$MS.fld_len(MKASA,'NAZ')];
_tooltip+='Pole wymagane'@;
_def.define('MKASA','Multikasa'@,1,,_tooltip);

_tooltip:='Uprawniony (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('DOSTEP','Czy uprawniony?'@,1,,_tooltip);

_tooltip:='Pełne uprawnienia (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('PELNE','Czy pełne uprawnienia?'@,1,,_tooltip);

~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------

_excel:=_a;
_obj:=_b;
_tab:=_excel.table();

{? __SelUsers.first()
|| _result:=__SelUsers.find_key(_tab.USER_REF)
|| _result:=1
?};

{? _result
|| _obj.USER.VALUE:=_tab.USER_KOD;
   _obj.MKASA.VALUE:=_tab.NAZ;
   _obj.DOSTEP.VALUE:=_tab.ACCESS;
   _obj.PELNE.VALUE:=_tab.FULL
?};

_result


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_obj:=_a;
_mode:=_b;
_validate:=_c;

MKASAUPR.prefix();
{? _validate.ACTION='add'
|| MKASAUPR.blank()
?};
MKASAUPR.USER:=_validate.OBJ.USER;
MKASAUPR.MKASA:=_validate.OBJ.MKASA;
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER   - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obj_new() - obiekt z resultem, wynik działania exec('args_valid','#excel_imex').
::                        Obiekt ten jest przekazywany do formuły na IMPORT. Pole RESULT decyduje czy formuła
::                        na import się wykona. Pole MSG służy to odpisania komunikatu
::----------------------------------------------------------------------------------------------------------------------

_obj:=_a;
_mode:=_b;
_result:=_c;

_can_continue:=1;
:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _obj.USER=''
|| _can_continue:=0;
   _result.msg_empty('USER')
?};
{? _obj.MKASA=''
|| _can_continue:=0;
   _result.msg_empty('MKASA')
?};

_user:=null();
{? _can_continue
|| _user:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1);
   {? _user=null()
   || _can_continue:=0;
      _result.msg_norec('USER')
   ?}
?};

_mkasa:=null();
{? _can_continue
|| _mkasa:=exec('FindInSet','#table','MKASA','NAZ',_obj.MKASA,,,1);
   {? _mkasa=null()
   || _can_continue:=0;
      _result.msg_norec('MKASA')
   ?}
?};

{? _obj.DOSTEP<>'T' & _obj.DOSTEP<>'N'
|| _can_continue:=0;
   _result.msg_inset('DOSTEP','T','N')
?};

{? _obj.PELNE<>'T' & _obj.PELNE<>'N'
|| _can_continue:=0;
   _result.msg_inset('PELNE','T','N')
?};

{? _can_continue & _obj.DOSTEP='T'
|| {? ~exec('user_upraw_dostep','users_upraw','MKAS',_user)
   || _can_continue:=0;
      _msg:='Użytkownik nie może mieć uprawnienia, nie ma odpowiedniej roli.'@;
      {? _obj.PELNE='T'
      || _result.msg_value('PELNE',_msg)
      || _result.msg_value('DOSTEP',_msg)
      ?}
   ?}
?};

_bperm_ref:=exec('bperm_ref','users_upraw','MKAS');
B_PERM_U.index('USER');
B_PERM_U.prefix(REF.FIRMA,_user,_bperm_ref);
{? ~B_PERM_U.first()
|| _can_continue:=0
?};

_result.RESULT:=_can_continue;

:: OK
{? _result.RESULT & _obj.PELNE='T'
|| {? B_PERM_U.FULL<>'T'
   || _result.ACTION:='padd';
      _result.OBJ:=obj_new('USER','PELNE');
      _result.OBJ.USER:=_user;
      _result.OBJ.PELNE:='T'
   ?}
|? _result.RESULT & (_mode=1 | _obj.DOSTEP='T')
|| MKASAUPR.index('UNIK');
   MKASAUPR.prefix(_user,_mkasa);
   {? MKASAUPR.first()
   || {? _obj.DOSTEP='N'
      || _result.ACTION:='del'
      |? _mode=1 & B_PERM_U.FULL='T'
      || _result.ACTION:='pdel';
         _result.OBJ:=obj_new('USER','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.PELNE:='N'
      ?}
   || {? _obj.DOSTEP='T'
      || _result.ACTION:='add';
         _result.OBJ:=obj_new('USER','MKASA','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.MKASA:=_mkasa;
         {? _mode=1 & B_PERM_U.FULL='T'
         || _result.OBJ.PELNE:='N'
         || _result.OBJ.PELNE:=''
         ?};
         exec('record','xls_mkasa_upr',_obj,_mode,_result)
      |? _mode=1 & B_PERM_U.FULL='T'
      || _result.ACTION:='pdel';
         _result.OBJ:=obj_new('USER','PELNE');
         _result.OBJ.USER:=_user;
         _result.OBJ.PELNE:='N'
      ?}
   ?}
?};

~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=MKASAUPR.add(1)
   |? _validate.ACTION='del'
   || {? MKASAUPR.count()=0 & MKASAUPR.del(1,1)<>0
      || _result:=1
      ?}
   |? _validate.ACTION='padd'
   || B_PERM_U.FULL:='T';
      B_PERM_U.ACCESS:='T';
      _result:=B_PERM_U.put();
      {? _result
      || exec('user_upraw_ustaw_pelne','users_upraw','MKAS',_validate.OBJ.USER)
      ?}
   ?};
   {? ((_validate.ACTION='add' | _validate.ACTION='del') & _validate.OBJ.PELNE='N' & _result)
      | _validate.ACTION='pdel'
   || B_PERM_U.FULL:='N';
      _result:=B_PERM_U.put()
   ?}
?};

{? _result=0
|| _validate.MSG:='';
   {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do mulitkasy %2 nie powiodło się.'@
                     [_obj.USER,_obj.MKASA]
   |? _validate.ACTION='del'
   || _validate.MSG:='Usunięcie uprawnienia użytkownika %1 do multikasy %2 nie powiodło się.'@
                     [_obj.USER,_obj.MKASA]
   |? _validate.ACTION='padd'
   || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do wszystkich multikas nie powiodło się.'@
                     [_obj.USER]
   ?};
   {? ((_validate.ACTION='add' | _validate.ACTION='del') & _validate.OBJ.PELNE='N') | _validate.ACTION='pdel'
   || {?_validate.MSG<>''
      || _validate.MSG+=' '
      ?};
      _validate.MSG+='Usunięcie uprawnienia użytkownika %1 do wszystkich multikas nie powiodło się.'@
                     [_obj.USER]
   ?}
|| {? _validate.ACTION='padd'
   || _validate.MSG:='Dodano uprawnienie użytkownika %1 do wszystkich multikas.'@[_obj.USER]
   |? ((_validate.ACTION='add' | _validate.ACTION='del') & _validate.OBJ.PELNE='N') | _validate.ACTION='pdel'
   || _validate.MSG:='Usunięto uprawnienie użytkownika %1 do wszystkich multikas.'@[_obj.USER]
   ?};
   _validate.MSG:=''
?};

_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - obj_new - środowisko mechanizmu
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_excel:=_b;

exec('select','xls_users_up',_selected,_excel,'MKAS')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 1d21910977cdca78098230e8bbaadfe93cc32f26cb95eb189ef7f95072f0f1ba294a38f19cca163584d377e9a419761c7e52592e4730c66431ee375707075203c624adec2a407781b6006a669c4ae99c13108b5c1e7b81875eb3da5d8674c5036e62e1f0dedbe4604708cfbba869af7832b42ea06807f4b6df8fddc3d44aabeb

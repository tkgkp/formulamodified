:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kst_def.fml
:: Utworzony: 31.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji stałych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='KST_DEF';
_def.FUNPAR:='ZWS_PAR_KST_ADM';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.NAME:='Stałe systemu - definicje'@;
_def.FILE:=exec('def_kst_xlsx','xls__init');
_def.SHEET:='Definicje stałych=1,1';
_def.DESC:='Definicje stałych';
_def.ADD_ROWS:=0;
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="KST_DEF";

: prolog
_def.BEFORE:="
   KST_ZES.cntx_psh();
   KST_ZES.prefix();
   KST_DEF.cntx_psh();
   KST_DEF.f_clear();
   KST_DEF.index('SYMBOL');
   KST_DEF.prefix();
   ~~
";

: epilog
_def.AFTER:="
   KST_DEF.cntx_pop();
   KST_ZES.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYMBOL',  MS.name(KST_DEF,'SYMBOL'),  0,,MS.comment(KST_DEF,'SYMBOL'));
   _env.define('OPIS',    MS.name(KST_DEF,'OPIS'),    0,,MS.comment(KST_DEF,'OPIS'));
   _env.define('INFO',    MS.name(KST_DEF,'INFO'),    0,,MS.comment(KST_DEF,'INFO'));
   _env.define('ZAPIS',   MS.name(KST_DEF,'ZAPIS'),   0,,MS.comment(KST_DEF,'ZAPIS'));
   _env.define('HISTORIA',MS.name(KST_DEF,'HISTORIA'),0,,MS.comment(KST_DEF,'HISTORIA'));
   _env.define('WSPOLNA', MS.name(KST_DEF,'WSPOLNA'), 0,,MS.comment(KST_DEF,'WSPOLNA'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYMBOL.VALUE:=KST_DEF.SYMBOL;
   _buf.OPIS.VALUE:=KST_DEF.OPIS;
   _buf.INFO.VALUE:=KST_DEF.memo_txt(,1,'INFO');
   _buf.ZAPIS.VALUE:={? KST_DEF.ZAPIS<>'' || KST_DEF.ZAPIS || 'T' ?};
   _buf.HISTORIA.VALUE:={? KST_DEF.HISTORIA<>'' || KST_DEF.HISTORIA || 'N' ?};
   _buf.WSPOLNA.VALUE:={? KST_DEF.WSPOLNA<>'' || KST_DEF.WSPOLNA || 'N' ?};
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_kst_def',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kst_def',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYMBOL=''   || 'SYMBOL'
   |? _buf.ZAPIS=''    || 'ZAPIS'
   |? _buf.HISTORIA='' || 'HISTORIA'
   |? _buf.WSPOLNA=''  || 'WSPOLNA'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('TN'*_buf.ZAPIS)=0
|| _val.msg_inset('ZAPIS','T','N');
   _val.RESULT:=0;
   return()

|? ('TN'*_buf.HISTORIA)=0
|| _val.msg_inset('HISTORIA','T','N');
   _val.RESULT:=0;
   return()

|? ('TN'*_buf.WSPOLNA)=0
|| _val.msg_inset('WSPOLNA','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(KST_DEF,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(KST_DEF,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

_ref:=null;
: odszukaj aktualizowany wiersz
{? KST_DEF.find_tab(,'SYMBOL',,'=',_buf.SYMBOL)
|| _ref:=KST_DEF.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.SYMBOL,_a.OPIS]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,KST_DEF,"
   _a.SYMBOL:=_b.SYMBOL;
   _a.OPIS:=_b.OPIS;
   _a.memo_set(_b.INFO,'INFO');
   _a.ZAPIS:=_b.ZAPIS;
   _a.HISTORIA:=_b.HISTORIA;
   _a.WSPOLNA:=_b.WSPOLNA;
   ~~
",'INFO');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 3040aee93fdc68994d592c5658510e928550e066aa00d510258a16131bc084d1404e4c5483521290396b0613ccb6a7d014ca932daae76071d28a7a7cc0efe31c0f0e70ba1eca221acd00a1f6d5d7c8fbbc7f4521dc02f843e2b32521410be28313a9666b882cf1c63e79484130fbde0120536d3b2e98dd3ab08371d8a2b7a1d6

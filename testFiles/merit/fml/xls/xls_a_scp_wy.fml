:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_a_scp_wy.fml
:: Utworzony: 18.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu wykluczeń weryfikacji norm czasu pracy
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='A_SCP_WY';
_def.FUNPAR:='ZWS_PAR_PSCP';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Wykluczenia weryfikacji norm=1,1';
_def.NAME:='Wykluczenia weryfikacji norm'@;
_def.DESC:='Wykluczenia weryfikacji norm';

: źródło danych
_def.TABLE:="A_SCP_WY";

: prolog
_def.BEFORE:="
   F_ZATR.cntx_psh();
   F_ZATR.prefix();
   A_SCP.cntx_psh();
   A_SCP.prefix();
   A_SCP_WY.cntx_psh();
   A_SCP_WY.index('A_SCP');
   A_SCP_WY.prefix();
   ~~
";

: epilog
_def.AFTER:="
   A_SCP_WY.cntx_pop();
   A_SCP.cntx_pop();
   F_ZATR.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('A_SCP', MS.name(A_SCP_WY,'A_SCP'), 0,,MS.comment(A_SCP_WY,'A_SCP'),type_of(''));
   _env.define('F_ZATR',MS.name(A_SCP_WY,'F_ZATR'),0,,MS.comment(A_SCP_WY,'F_ZATR'),type_of(''));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.A_SCP.VALUE:=A_SCP_WY.A_SCP().NAZ;
   _buf.F_ZATR.VALUE:=A_SCP_WY.F_ZATR().KOD;
   _env.write_async('A_SCP',A_SCP_WY.A_SCP);
   _env.write_async('F_ZATR',A_SCP_WY.F_ZATR);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_a_scp_wy',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_a_scp_wy',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.A_SCP=''  || 'A_SCP'
   |? _buf.F_ZATR='' || 'F_ZATR'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(A_SCP,'NAZ');
{? +_buf.A_SCP>_max
|| _val.msg_length('A_SCP',_max);
   _buf.A_SCP:=_max+_buf.A_SCP
?};
_max:=MS.fld_len(F_ZATR,'KOD');
{? +_buf.F_ZATR>_max
|| _val.msg_length('F_ZATR',_max);
   _buf.F_ZATR:=_max+_buf.F_ZATR
?};

: weryfikacja spójności referencyjnej
{? ~A_SCP.find_tab(,'NAZ',,'=',_buf.A_SCP)
|| _val.msg_norec('A_SCP');
   _val.RESULT:=0;
   return()
|| _a_scp:=A_SCP.ref()
?};
{? ~F_ZATR.find_tab(,'KOD',,'=',_buf.F_ZATR)
|| _val.msg_norec('F_ZATR');
   _val.RESULT:=0;
   return()
|| _f_zatr:=F_ZATR.ref()
?};

_ref:=null;
{? A_SCP_WY.find_tab(,'A_SCP',,'=',_a_scp,'F_ZATR',,'=',_f_zatr)
|| _ref:=A_SCP_WY.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1, %2'[_a.A_SCP,_a.F_ZATR]",
   'A_SCP',_a_scp,
   'F_ZATR',_f_zatr
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,A_SCP_WY,"
   _a.A_SCP:=_c.A_SCP;
   _a.F_ZATR:=_c.F_ZATR;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:46 40a8d49dce9fa6ef7735b26cb5fbbf71ca4d9abf0c5f3f2780cf09a4a6905967a45bc3492d4d2c5c54918e5aef7af2feea6fd7beaf44d9942eb917d21acc99d5836646c1403f0d206108b8b1b785b1dd3446c3cebe155c1724d8f2130a5b38f09af2d9df897271a86a0646f30af35027cbfa17a2f02cc1fc6f42a810dc28eeb5

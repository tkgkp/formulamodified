:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_dezezw.fml
:: Utworzony: 16.08.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu zezwoleń komunikatów edi
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='DEZEZW';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_EDI_DEF';
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Okresy obowiązywania EDI=1,1';
_def.NAME:='Okresy obowiązywania komunikatów EDI'@;
_def.DESC:='Okresy obowiązywania komunikatów EDI';

_def.PREFIX:="DEZEZW.index('ISTDEF'); DEZEZW.prefix('ZWS','E')";
_def.TABLE:="DEZEZW";
_def.FIELDS:="exec('fields','xls_dezezw',_a)";

_def.BEFORE:="
   DEZEZW.cntx_psh();
   __sym_dom:=BPMN.SYM_DOM;
   BPMN.SYM_DOM:='ZWS'

";
_def.AFTER:="
   BPMN.SYM_DOM:=__sym_dom;
   VAR_DEL.delete('__sym_dom');
   DEZEZW.cntx_pop()
";

_def.EXPORT:="exec('export','xls_dezezw',_a,_b)";
_def.VALIDATE:="exec('validate','xls_dezezw',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_dezezw',_a,_b,_c)";
_def.SELECT:="";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KH','Kontrahent',1,,'8 znakowy kod kontrahenta');
_def.define('NIP','NIP kontrahena',1,,'nip kontrahenta');
_def.define('OD','Obowiązuje od',1,,'data od której obowiązuje komunikat EDI',type_of(''));
_def.define('DO','Obowiązuje do',1,,'data do której obowiązuje komunikat EDI',type_of(''));
_def.define('WYD_ZEZW','Wydał zezwolenie',1,,'30 znakowy opis');
_def.define('B','Blokada okresu obowiązywania',1,,'T/N');
_def.define('U','Uwagi',1,,'250 znakowy opis');
_def.define('CRE_USER','Utworzył okres obowiązywania',0,,'30 znakowy opis');
_def.define('CRE_TIME','Czas utworzenia okresu obowiązywania',0,,'czas');
_def.define('MOD_USER','Zmodyfikował okres obowiązywania',0,,'30 znakowy opis');
_def.define('MOD_TIME','Czas modyfikacji okresu obowiązywania',0,,'czas');
_def.define('ISTDEF','Definicja komunikatu EDI',1,,'20 znakowy kod definicji');
_def.define('FILE_PTH','Ścieżka do plików',1,,'250 znakowa ścieżka do plików');
::_def.define('FILE_SIG','Rozszerzenie pliku z podpisem',1,,'8 znakowe rozszerzenie');
_def.define('WR','Dotyczy zapisu do lub odczytu z pliku',1,,'W - zapis/R - odczyt');
::_def.define('S','Zezwolenie na e-podpis',1,,'T/N');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_excel.DIR;

_result:=1;
_table.KH.VALUE:=DEZEZW.KH().KOD;
_table.NIP.VALUE:=DEZEZW.KH().NIP;
_table.OD.VALUE:=DEZEZW.OD$1;
_table.DO.VALUE:={? DEZEZW.DO=date(0,0,0) || '' || DEZEZW.DO$1 ?};
_table.WYD_ZEZW.VALUE:=DEZEZW.WYD_ZEZW;
_table.B.VALUE:=DEZEZW.B;
_table.U.VALUE:=DEZEZW.U;
_table.CRE_USER.VALUE:=DEZEZW.CRE_USER;
_table.CRE_TIME.VALUE:=DEZEZW.CRE_TIME;
_table.MOD_USER.VALUE:=DEZEZW.MOD_USER;
_table.MOD_TIME.VALUE:=DEZEZW.MOD_TIME;
_table.ISTDEF.VALUE:=DEZEZW.ISTDEF().K;
::_excel.write_async('EDI',DEZEZW.ISTDEF);
_table.FILE_PTH.VALUE:=DEZEZW.FILE_PTH;
::_table.FILE_SIG.VALUE:=DEZEZW.FILE_SIG;
_table.WR.VALUE:=DEZEZW.WR;
::_table.S.VALUE:=DEZEZW.S;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

:: wychwyć błędy krytyczne
_fld:=
   {? _obj.ISTDEF='' || 'ISTDEF'
   |? _obj.WR='W' & _obj.KH='' || 'KH'
   || ''
   ?};
{? _fld<>''
::  katastrofa
|| _result.msg_empty(_fld);
   _result.RESULT:=0;
   return()
?};

:: weryfikacja spójności referencyjnej
_istdef:=null();
ISTDEF.cntx_psh();
ISTDEF.index('K');
ISTDEF.prefix('ZWS','E',_obj.ISTDEF,);
_loop:=ISTDEF.first();
{!
|? _loop
|!
   {? ISTDEF.FIRMA='' | ISTDEF.FIRMA=__Firma || _istdef:=ISTDEF.ref() ?};
   _loop:=_istdef=null() & ISTDEF.next()
!};
ISTDEF.cntx_pop();
{? _istdef=null()
|| _result.RESULT:=0;
   _result.MSG:='Nie znaleziono definicji komunikatu EDI o kodzie %1.'@[_obj.ISTDEF];
   return()
?};
_kh:=null();
{? _obj.WR='W'
||
   _kh:=exec('FindInSet','#table','KH','KOD',_obj.KH,2,,1,,null());
   _nip:=exec('FindAndGet','#table',KH,_kh,,"KH.NIP",'');
   _nip:=exec('niptostr','#string',_nip)=exec('niptostr','#string',_obj.NIP);
   {? _kh=null() | _nip
   || _result.RESULT:=0;
      _result.MSG:='Nie znaleziono kontrahenta o kodzie %1 i NIP %2.'@[_obj.KH,_obj.NIP];
      return()
   ?}
?};
_od:=_do:=date(0,0,0);
{? _obj.WR='W'
||
   _date:=_obj.OD;
   _od:=date(0,0,0);
   {? +_date=10 || _od:=date(#(4+_date),#(2+(5-_date)),#(_date)) ?};
   {? _od=date(0,0,0)
   ||
      _result.msg_empty('OD')
   ?};
   _date:=_obj.DO;
   _do:=date(0,0,0);
   {? +_date=10
   || _od:=date(#(4+_date),#(2+(5-_date)),#(_date))
   |? +_date
   || _result.RESULT:=0;
      _result.MSG:='Podana wartość nie jest datą w formacie rrrr/mm/dd.'@;
      return()
   ?}
?};
DEZEZW.index('WR');
DEZEZW.prefix('ZWS',_istdef,_obj.WR,_kh,_od,_do);
{? DEZEZW.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT=1
|| {? _result.ACTION='add' || DEZEZW.blank() ?};

   DEZEZW.SYSTEM:='ZWS';
   DEZEZW.KH:=_kh;
   DEZEZW.OD:=_od;
   DEZEZW.DO:=_do;
   DEZEZW.WYD_ZEZW:=_obj.WYD_ZEZW;
   DEZEZW.B:=_obj.B;
   DEZEZW.U:=_obj.U;
   DEZEZW.CRE_USER:=_obj.CRE_USER;
   DEZEZW.CRE_TIME:=_obj.CRE_TIME;
   DEZEZW.MOD_USER:=_obj.MOD_USER;
   DEZEZW.MOD_TIME:=_obj.MOD_TIME;
   DEZEZW.ISTDEF:=_istdef;
   DEZEZW.FILE_PTH:=_obj.FILE_PTH;
::_table.FILE_SIG.VALUE:=DEZEZW.FILE_SIG;
   DEZEZW.WR:=_obj.WR;
::_table.S.VALUE:=DEZEZW.S;

   {? exec('valid_zezw','edi_def',{? _result.ACTION='add' || 'D' || 'P' ?})<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=DEZEZW.add(1)

   |? _validate.ACTION='put'
   ||
      _result:=DEZEZW.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie okresu obowiązywania definicji EDI nie powiodło się.'@
   ||
      _validate.MSG:='Poprawa okresu obowiązywania definicji EDI nie powiodła się.'@
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 419af0b3567c74815671456f041ac0f5d9a44cc897cb67e40f04a900e4f600221d1b9e52a89e60d67b0bf63087ffb5d96a8576cb5f5397fa1aace5ebdbccba9ea56e06c2a5332ee4fe5fe2fabc50a2b268307b5adbc73981329962f5618231a34cf160bbd0e83816f3c3b2abb3e816d41fb59b647591566b974b4557e4bab3fb

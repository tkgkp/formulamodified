:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_oper.fml
:: Utworzony: 23.07.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typów transakcji kasowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='POZOPER';
_def.DOMAIN:=exec('name','#b_domain','KAS');
_def.FUNPAR:='ZWS_PAR_FKTT';

_def.FILE:='kasa.xlsx';
_def.SHEET:='Typy transakcji kasowych=1,1';
_def.NAME:='Typy transakcji kasowych'@;
_def.DESC:='Typy transakcji kasowych';

_def.PREFIX:="POZOPER.index('KOD'); POZOPER.prefix()";
_def.TABLE:="POZOPER";
_def.FIELDS:="exec('fields','xls_pozoper',_a)";

_def.BEFORE:="POZOPER.cntx_psh()";
_def.AFTER:="POZOPER.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_pozoper',_a,_b)";
_def.VALIDATE:="exec('validate','xls_pozoper',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_pozoper',_a,_b,_c)";
_def.SELECT:="exec('select','xls_pozoper',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('OPER','Typ operacji',1,,'8 znakowy typ operacji');
_def.define('KOD','Typ transakcji',1,,'8 znakowy typ transakcji');
_def.define('NAZWA','Nazwa',1,,'30 znakowa nazwa transakcji');
_def.define('PLUS_MIN','Przychód/rozchód',1,,'Znak + oznacza przychód, znak - oznacza rozchód');

_def.define('ZM_OBR','Transakcja zmienia obroty kasy',1,,'T/N');
_def.define('DOMTYP','Domyślny typ transakcji',1,,'T/N');
_def.define('ZMDOMODD','Dozwolona zmiana jednostki księgowej',1,,'T/N');
_def.define('ZW_BUD','Związany z budżetowaniem',1,,'T/N');

_def.define('MF_TIP','Podpowiedzi z dziedziny Sprzedaż',1,,'T/N');
_def.define('ODDZ','Oddział',1,,'1 znakowy kod oddziału logistycznego');
_def.define('STS','Stanowisko sprzedaży',1,,'8 znakowy kod stanowiska sprzedaży');
_def.define('TYPYSP','Typ dokumentu sprzedaży',1,,'8 znakowy kod typu dokumentu sprzedaży');

_def.define('FIKS_TIP','Podpowiedzi z dziedziny Finanse',1,,'T/N');
_def.define('ANALIT','Analityka',1,,'8 znakowa analityka pomocna w automacie księgującym');
_def.define('DOTYCZY','DOTYCZY',1,,'20 znakowa nazwa słownika użytkownika');

_def.define('VAT','Dotyczy dokumentów VAT',1,,'T/N');

_def.define('REJDOM','Rejestr',1,,'8 znakowy kod domyślnego rejstru księgowego');
_def.define('DMDOKREJ','Rodzaj dokumentu',1,,'30 znakowa nazwa domyślnego rodzaju dokumentu');
_def.define('GRPODDOM','Grupa podatkowa',1,,'8 znakowy kod domyślnej grupy podatkowa');
_def.define('RVATDOM','Rejestr VAT',1,,'8 znakowy domyślny symbol rejestru VAT');
_def.define('STVATDOM','Stawka VAT',1,,'8 znakowy kod domyślnej stawki vat');

_def.define('ZMDOMREJ','Dopuszczalna zmiana domyślnego rejestru',1,,'T/N');
_def.define('ZMDDKREJ','Dopuszczalna zmiana domyślnego rodzaju dokumentu',1,,'T/N');
_def.define('ZMDOMGRU','Dopuszczalna zmiana domyślnej grupy podatkowej',1,,'T/N');
_def.define('ZMDOMRVT','Dopuszczalna zmiana domyślnego rejestru vat',1,,'T/N');

_def.define('PKONTROL','Dopuszczalne tworzenie podziałów dla controllingu',1,,'T/N');

::27   Pakiet TEREN?   TEREN   STRING   1   [ML] TEREN   T
::28   Wartość transakcji   F_WAR   _FORMULA   0   [ML] TEREN   T
::29   Czy pozycja ilościowa   POZ_IL   STRING   1   [ML] Czy pozycja ilościowa?   T
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.OPER.VALUE:=POZOPER.OPER().KOD; _excel.write_async('OPER',POZOPER.OPER);
_table.KOD.VALUE:=POZOPER.KOD;
_table.NAZWA.VALUE:=POZOPER.NAZWA;
_table.PLUS_MIN.VALUE:=POZOPER.PLUS_MIN;

_table.ZM_OBR.VALUE:=POZOPER.ZM_OBR;
_table.DOMTYP.VALUE:=POZOPER.DOMTYP;
_table.ZMDOMODD.VALUE:=POZOPER.ZMDOMODD;
_table.ZW_BUD.VALUE:=POZOPER.ZW_BUD;

_table.MF_TIP.VALUE:=POZOPER.MF_TIP;
_table.ODDZ.VALUE:=POZOPER.ODDZ().KOD; _excel.write_async('ODDZ',POZOPER.ODDZ);
_table.STS.VALUE:=POZOPER.STS().KOD; _excel.write_async('STS',POZOPER.STS);
_table.TYPYSP.VALUE:=POZOPER.TYPYSP().T; _excel.write_async('TYPYSP',POZOPER.TYPYSP);

_table.FIKS_TIP.VALUE:=POZOPER.FIKS_TIP;
_table.ANALIT.VALUE:=POZOPER.ANALIT;
_table.DOTYCZY.VALUE:=POZOPER.DOTYCZY().NAZ; _excel.write_async('SLU',POZOPER.DOTYCZY);

_table.VAT.VALUE:=POZOPER.VAT;

_table.REJDOM.VALUE:=POZOPER.REJDOM;
:: DRO_TODO_AWI: excel - DOK_REJ
::_table.DMDOKREJ.VALUE:=POZOPER.DMDOKREJ().NAZ; _excel.write_async('DOK_REJ',POZOPER.DMDOKREJ);
:: DRO_TODO_AWI: excel - GR_VAT
::_table.GRPODDOM.VALUE:=POZOPER.GRPODDOM().KOD; _excel.write_async('GR_VAT', POZOPER.GRPODDOM);
:: DRO_TODO_AWI: excel - VAT_REJ
::_table.RVATDOM.VALUE:=POZOPER.RVATDOM().SYM; _excel.write_async('VAT_REJ',POZOPER.RVATDOM);
_table.STVATDOM.VALUE:=POZOPER.STVATDOM().KOD; _excel.write_async('SLO',POZOPER.STVATDOM);

_table.ZMDOMREJ.VALUE:=POZOPER.ZMDOMREJ;
_table.ZMDDKREJ.VALUE:=POZOPER.ZMDDKREJ;
_table.ZMDOMGRU.VALUE:=POZOPER.ZMDOMGRU;
_table.ZMDOMRVT.VALUE:=POZOPER.ZMDOMRVT;

_table.PKONTROL.VALUE:=POZOPER.PKONTROL;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
{? _obj.OPER=''
|| _result.RESULT:=0;
   _result.MSG:='Nie podano typu operacji kasowej.'@;
   return(~~)
?};
_oper:=exec('FindInSet','#table','OPER','KOD',_obj.OPER,,,1,,null());
{? _oper=null()
|| _result.RESULT:=0;
   _result.MSG:='Nie znaleziono typu operacji kasowej %1.'@[_obj.OPER];
   return(~~)
?};
{? _obj.KOD=''
|| _result.RESULT:=0;
   _result.MSG:='Nie podano kodu typu transakcji kasowej.'@;
   return(~~)
?};
POZOPER.index('OPER');
POZOPER.prefix(_oper,_obj.KOD,);
{? POZOPER.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT=1
|| {? _result.ACTION='add' || POZOPER.blank() ?};
   POZOPER.OPER:=_oper;
   POZOPER.KOD:=_obj.KOD;
   POZOPER.NAZWA:=_obj.NAZWA;
   POZOPER.PLUS_MIN:=_obj.PLUS_MIN;

   POZOPER.ZM_OBR:=_obj.ZM_OBR;
   POZOPER.DOMTYP:=_obj.DOMTYP;
   POZOPER.ZMDOMODD:=_obj.ZMDOMODD;
   POZOPER.ZW_BUD:=_obj.ZW_BUD;

   POZOPER.MF_TIP:=_obj.MF_TIP;
   {? _obj.ODDZ<>''
   || POZOPER.ODDZ:=exec('FindInSet','#table','ODDZ','KOD2',_obj.ODDZ,,,1,,null());
      {? POZOPER.ODDZ=null()
      || _result.RESULT:=0;
         _result.MSG:='Nie znaleziono oddziału %1.'@[_obj.ODDZ];
         return(~~)
      ?}
   ?};
   {? _obj.STS<>''
   || POZOPER.STS:=exec('FindInSet','#table','STS','K',_obj.STS,,,1,,null());
      {? POZOPER.STS=null()
      || _result.RESULT:=0;
         _result.MSG:='Nie znaleziono stanowiska sprzedaży %1.'@[_obj.STS];
         return(~~)
      ?}
   ?};
   {? _obj.TYPYSP<>''
   || POZOPER.TYPYSP:=exec('FindInSet','#table','TYPYSP','TYPYKOD',_obj.TYPYSP,,,1,,null());
      {? POZOPER.TYPYSP=null()
      || _result.RESULT:=0;
         _result.MSG:='Nie znaleziono typu dokumentu sprzedaży %1.'@[_obj.TYPYSP];
         return(~~)
      ?}
   ?};

   POZOPER.FIKS_TIP:=_obj.FIKS_TIP;
   POZOPER.ANALIT:=_obj.ANALIT;
   {? _obj.DOTYCZY<>''
   || POZOPER.DOTYCZY:=exec('FindInSet','#table','SLU','NAZ',_obj.DOTYCZY,,,1,,null());
      {? POZOPER.DOTYCZY=null()
      || _result.RESULT:=0;
         _result.MSG:='Nie znaleziono słownika użytkownika %1.'@[_obj.DOTYCZY];
         return(~~)
      ?}
   ?};

   POZOPER.VAT:=_obj.VAT;

   POZOPER.REJDOM:=_obj.REJDOM;
:: DRO_TODO_AWI: excel - DOK_REJ
::   _rej:=??? - niejednoznaczny identyfikator rejestru księgowego
::   POZOPER.DMDOKREJ:=exec('FindInSet','#table','DOK_REJ','NAZ',_obj.DMDOKREJ,_rej,,1,,null());
:: DRO_TODO_AWI: excel - GR_VAT
::   POZOPER.GRPODDOM:=exec('FindInSet','#table','GR_VAT','REJ_KOD',_obj.GRPODDOM,_rej,,1,,null());
:: DRO_TODO_AWI: excel - VAT_REJ
::   _rvat:=??? _obj.RVATDOM - niejednoznaczny symbol rejestru vat
::   POZOPER.RVATDOM:=exec('FindInSet','#table','VAT_REJ','REJ_VREF',_rvat,_rej,,1,,null());
   {? _obj.STVATDOM<>''
   || _slu:=exec('FindInSet','#table','SLU','NAZ','~STAWKI VAT PL');
      POZOPER.STVATDOM:=exec('FindInSet','#table','SLO','SL',_obj.STVATDOM,_slu,,1,,null());
      {? POZOPER.STVATDOM=null()
      || _result.RESULT:=0;
         _result.MSG:='Nie znaleziono stawki vat %1.'@[_obj.STVATDOM];
         return(~~)
      ?}
   ?};

   POZOPER.ZMDOMREJ:=_obj.ZMDOMREJ;
   POZOPER.ZMDDKREJ:=_obj.ZMDDKREJ;
   POZOPER.ZMDOMGRU:=_obj.ZMDOMGRU;
   POZOPER.ZMDOMRVT:=_obj.ZMDOMRVT;

   POZOPER.PKONTROL:=_obj.PKONTROL;

   {? exec('poz_wal','kasa_wspolne',_result.ACTION)<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=POZOPER.add(1)

   |? _validate.ACTION='put'
   ||
      _result:=POZOPER.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie typu transakcji kasowej %1 nie powiodło się.'@[_obj.KOD]
   ||
      _validate.MSG:='Poprawa typu transakcji kasowej %1 nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'OPER','STRING[30]','Typ operacji'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZWA','STRING[30]','Nazwa'
   ,'KOD','STRING[8]','Typ transakcji');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZWA',,);

POZOPER.cntx_psh();
POZOPER.index('KOD');
POZOPER.prefix();
{? POZOPER.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$POZOPER.ref();
      _selected.prefix($POZOPER.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.OPER:=POZOPER.OPER().NAZWA;
      _tab.NAZWA:=POZOPER.NAZWA;
      _tab.KOD:=POZOPER.KOD;
      _tab.add();
      POZOPER.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NAZWA,OPER,KOD',20,'Wybór typów transakcji kasowych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
POZOPER.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:49 dda0ef87c0ce3bae14e2808cc3bb97e68e69099f42c2420dda208273d3e5bae6d8db665263ba12a1d91d7bd9fb70a2d0a8b830f7db8029954caadc433339dead05a667e608e28cfa27af95546e19c363bf687b2ecf022c47f8ed77ddfc63b95a56a45dbba8192342e3ded6cbaf736419dded5664e7046a91666c337742b0a598

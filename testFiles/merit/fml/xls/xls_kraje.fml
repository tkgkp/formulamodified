:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kraje.fml
:: Utworzony: 30.08.2018
:: Autor: IFZ
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu tabeli wspólnej: kraje
::======================================================================================================================


\filename
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Nazwa pliku do eksportu struktur księgowych
::  OLD: \filename/xls_kraje.fml
::----------------------------------------------------------------------------------------------------------------------
'kraje.xlsx'

\KRAJE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  OLD: \KRAJE/xls_kraje.fml
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KRAJE';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('filename','xls_kraje');
_def.SHEET:='Kraje=1,1';
_def.NAME:='Kraje';
_def.DESC:='Kraje';
_def.MULTIFIR:='T';

_def.PREFIX:="KRAJE.index('KRAJE');KRAJE.prefix()";
_def.TABLE:="KRAJE";
_def.FIELDS:="exec('KRAJE_fld','xls_kraje',_a)";

_def.BEFORE:="KRAJE.cntx_psh()";
_def.AFTER:="KRAJE.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('KRAJE_exp','xls_kraje',_a,_b)";
_def.VALIDATE:="exec('KRAJE_valid','xls_kraje',_a,_b,_c)";
_def.IMPORT:="exec('KRAJE_imp','xls_kraje',_a,_b,_c)";
~~


\KRAJE_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  OLD: \KRAJE_fld/xls_kraje.fml
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('SYM','Symbol',1,,'Kod kraju zgodnie z klasyfikacją UE');
_def.define('NAZ','Nazwa',1,,'Nazwa kraju');
_def.define('WAL','Waluta',1,,'Wskazanie na walutę kraju Unii Europejskiej');
_def.define('KODISO','Kod ISO kraju',1,,'Kod kraju zgodnie z normą ISO 3166');
_def.define('IBAN','Rachunki bankowe w formacie IBAN',1,,'Czy rachunki bankowe w formacie IBAN?');
_def.define('KODZUS','Kod ZUS',1,,'Kod kraju wg nomenklatury Zakładu Ubezpieczeń Społecznych');
~~


\KRAJE_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::  OLD: \KRAJE_exp/xls_kraje.fml
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_tab:=_excel.table();

_result:=0;
KRAJE.prefix();
_table.SYM.VALUE:=KRAJE.SYM;
_table.NAZ.VALUE:=KRAJE.NAZ;
_table.WAL.VALUE:=KRAJE.WAL().KOD; _excel.write_async('SLO',KRAJE.WAL);
_table.KODISO.VALUE:=KRAJE.KODISO;
_table.IBAN.VALUE:=KRAJE.IBAN;
_table.KODZUS.VALUE:=KRAJE.KODZUS;
_result:=1;
_result


\KRAJE_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  OLD: \KS_ZEST_valid/xls_kraje.fml
::  OLD: \KRAJE_valid/xls_kraje.fml
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;

_slu:=exec('FindInSet','#table','SLU','NAZ','WALUTY',,,1);
_slo:={? _table.WAL='' || null() || exec('FindInSet','#table','SLO','SL',_table.WAL,_slu,,1) ?};
{? _slo=null()
|| _result.MSG:={? _table.WAL=''
                || 'Nie podano kodu waluty '@
                || 'Nie znaleziono waluty o kodzie %1.'@[_table.WAL]
                ?};
   _result.RESULT:=0
?};
_result.RESULT:=1;
KRAJE.index('KRAJE');
KRAJE.prefix(_table.SYM);
{? KRAJE.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
 {? _result.RESULT=1
 || {? _result.ACTION='add' || KRAJE.blank(1) ?};
    KRAJE.SYM:=_table.SYM;
    KRAJE.KODISO:=_table.KODISO;
    KRAJE.NAZ:=_table.NAZ;
    KRAJE.IBAN:=_table.IBAN;
    KRAJE.WAL:=_slo;
    {? exec('rakraje','kraje')<>''
    || _result.RESULT:=0
    ?}
?};
~~


\KRAJE_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukcess
::  OLD: \KRAJE_imp/xls_kraje.fml
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KRAJE.add(1)
   |? _validate.ACTION='put'
   || _result:=KRAJE.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie kraju %1 nie powiodło się.'@[_table.SYM]
   || _validate.MSG:='Poprawa kraju %1 nie powiodła się.'@[_table.SYM]
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 a2cb8a1409b7f1d20129a69f7081edc64b82fc0b11a9913e249823fa822cf704b73d1f762ac61f432cb226200c4dbfe9d0a897c32311211175f7276d4a2a1ff8c82baf7026f2d473755a4397fa40b81d7f5c60bee2cb7892483e9f4d52d473239ffcc7f208f4bad4499f9f136eb062daa0b70575c553905ed619e3b98136542a

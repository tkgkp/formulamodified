:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_inw_lok.fml
:: Utworzony: 15.01.2019
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu lokalizacji inwestycji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_def:=_a;

_def.ID:='INW_LOK';
_def.FUNPAR:='';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_pak_xlsx','xls__init');
_def.SHEET:='Lokalizacje inwestycji=1,1';
_def.NAME:='Lokalizacje inwestycji'@;
_def.DESC:='Lokalizacje inwestycji';
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="INW_LOK";

: prolog
_def.BEFORE:="
   INW_LOK.cntx_psh();
   INW_LOK.f_clear();
   INW_LOK.index('S');
   INW_LOK.prefix();
   ~~
";

: epilog
_def.AFTER:="
   INW_LOK.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('S',MS.name(INW_LOK,'S'),1,,MS.comment(INW_LOK,'S'));
   _env.define('O',MS.name(INW_LOK,'S'),1,,MS.comment(INW_LOK,'O'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.S.VALUE:=INW_LOK.S;
   _buf.O.VALUE:=INW_LOK.O;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_inw_lok',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_inw_lok',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.S='' || 'S'
   |? _buf.O='' || 'O'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(INW_LOK,'S');
{? +_buf.S>_max
|| _val.msg_length('S',_max);
   _buf.S:=_max+_buf.S
?};
_max:=MS.fld_len(INW_LOK,'O');
{? +_buf.O>_max
|| _val.msg_length('O',_max);
   _buf.O:=_max+_buf.O
?};

_ref:=null;
{? INW_LOK.find_tab(,'S',,'=',_buf.S)
|| _ref:=INW_LOK.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.S,_a.O]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,INW_LOK,"
   _a.S:=_b.S;
   _a.O:=_b.O;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 b0a9dbe1afbf7de0973eb7d1e84db6f67a95baf48a91dc285e4e24abcdeb9daecbf17b74b446b165c293971e6b00f17c64c10043eb856bb4041e3e58a86d913407ef4ba084c69c1974eca40b9714d4fffae41e54408edcb07d6682f98e2240eca334d2ce1b2449ec89f2ec77719cad7e8603fdae21ae2ab0131f58399b3ad513

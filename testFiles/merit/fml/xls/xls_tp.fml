:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tp.fml
:: Utworzony: 10.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu tabel podatkowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='TP';
_def.FUNPAR:='ZWS_PAR_PTPO';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:='personel_stale.xlsx';
_def.SHEET:='Tabele podatkowe=1,1';
_def.NAME:='Tabele podatkowe'@;
_def.DESC:='Tabele podatkowe';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="TP";

: prolog
_def.BEFORE:="
   TP.cntx_psh();
   TP.f_clear();
   TP.index('TABPODAT');
   TP.prefix()
";

: epilog
_def.AFTER:="
   TP.cntx_pop()
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('R', MS.name(TP,'R'), 1,,MS.comment(TP,'R'),,0);
   _env.define('K1',MS.name(TP,'K1'),1,,MS.comment(TP,'K1'),,2);
   _env.define('PR',MS.name(TP,'PR'),1,,MS.comment(TP,'PR'),,2);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.R.VALUE:=TP.R;
   _buf.K1.VALUE:=TP.K1;
   _buf.PR.VALUE:=TP.PR;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_tp',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_tp',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.R=0 || 'R'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? _buf.K1<0
|| _val.msg_range('K1',0);
   _val.RESULT:=0;
   return()

|? _buf.PR<0 | _buf.PR>100
|| _val.msg_range('PR',0,100);
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? TP.find_tab(,'R',,'=',_buf.R,'PR',,'=',_buf.PR)
|| _ref:=TP.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('TP','to_str');
_obj.to_str:="'%1: %2'[$_a.R,$_a.PR]";
_obj.TP:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.R:=_b.R;
   _a.K1:=_b.K1;
   _a.PR:=_b.PR;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? TP.seek(_obj.TP)
   || _set(TP,_buf);
      {? ~TP.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,TP.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| TP.blank();
   _set(TP,_buf);
   {? ~TP.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 2145e8f66372f4d47d411287f462945228cadea4c9660810aeb709b9ec375a074122fad3154fc716323bc2b783c3ef833aad422ca2b966f2f1bc8ecc7734e7956c78896fe32e3c9303791180c080af59f7fef803455ee3a885cb468b386010e2ef54282578b8dee3d7c703fed2b1f04f9e910688febeaa03d4297dca27c3fad5

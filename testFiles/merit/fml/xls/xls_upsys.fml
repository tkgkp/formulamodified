:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_upsys.fml
:: Utworzony: 08.01.2019
:: Autor: MW
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu systemów usług (dla umów cykliczne)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='UPSYS';
_def.DOMAIN:=exec('name','#b_domain','LUM');
_def.FUNPAR:='ZWS_PAR_USWY';

_def.FILE:='umowy_cykliczne.xlsx';
_def.SHEET:='Systemy wywozu'@+'=1,1';
_def.NAME:='Systemy wywozu umów cyklicznych'@;
_def.DESC:='Systemy wywozu umów cyklicznych'@;

_def.PREFIX:="exec('prefix','xls_upsys')";
_def.TABLE:="exec('table','xls_upsys')";
_def.FIELDS:="exec('fields','xls_upsys',_a)";

_def.BEFORE:="UPSYS.cntx_psh()";
_def.AFTER:="UPSYS.cntx_pop()";
_def.SELECT:="exec('select','xls_upsys',_a)";

_def.EXPORT:="exec('export','xls_upsys',_a,_b)";
_def.VALIDATE:="exec('validate','xls_upsys',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_upsys',_a,_b,_c)";
~~

\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
UPSYS.index('KOD');
UPSYS.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
UPSYS


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod'@,0,,'Kod (max. 3 znaki)'@);
_def.define('NAZ','Nazwa'@,1,,'Opis (max. 30 znaków)'@);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
UPSYS.prefix();
{? _validate.ACTION='add'
|| UPSYS.blank()
?};
UPSYS.KOD:=_obj.KOD;
UPSYS.NAZ:=_obj.NAZ;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=UPSYS.KOD;
_table.NAZ.VALUE:=UPSYS.NAZ;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? (+_obj.KOD)>2
||
  _result.msg_length('KOD',3);
  _result.RESULT:=0
?};

{? (+_obj.NAZ)>30
||
  _result.msg_length('NAZ',30);
  _result.RESULT:=0
?};


:: OK
{? _result.RESULT>0
|| UPSYS.index('KOD');
   UPSYS.prefix(_obj.KOD,);
   {? UPSYS.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_upsys',_obj,_mode,_result);
         {? exec('chk_upsys_core','umowy',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_upsys',_obj,_mode,_result);
      {? exec('chk_upsys_core','umowy',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=UPSYS.add(1)
   |? _validate.ACTION='put'
   || _result:=UPSYS.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie systemu usług o kodzie %1 nie powiodło się.'@[$_obj.KOD]
   || _validate.MSG:='Poprawa systemu usług o kodzie %1 nie powiodła się.'@[$_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'KOD','STRING[3]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[30]','Nazwa'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

UPSYS.cntx_psh();
UPSYS.index('KOD');
UPSYS.prefix();
{? UPSYS.first()
|| {!
   |?  _tab.blank();
       _tab.REF:=$UPSYS.ref();
       _selected.prefix($UPSYS.ref());
       {? _selected.first()
       || _tab.SELECTED:='T'
       || _tab.SELECTED:='N'
       ?};
       _tab.KOD:=UPSYS.KOD;
       _tab.NAZ:=UPSYS.NAZ;
       _tab.add();
       UPSYS.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'KOD,NAZ',20,'Wybór systemów usług do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
UPSYS.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 0dc6a53598f5b3cc9f6cfc1f180fe2b4a37922b7eec37805a4d2c8ec4096ac2fe8926b87e391a71073774eb9a7a575496cbf753bae7f29cce6eccf238d9e03d84d8bd168b61b3105c03b04f1028bb827a29015592db78662007a53b8491c7d4cfaf75a590796c9172e044552850d23dde7ec94a9aa3811bcda277f8d299050c1

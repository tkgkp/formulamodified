:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_img_blob.fml
:: Utworzony: 22.04.2021
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='IMG_BLOB';
_def.FUNPAR:='ZWS_PAR_FIRMY';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_firmy_xlsx','xls__init');
_def.SHEET:='Obrazy=1,1';
_def.NAME:='Obrazy'@;
_def.DESC:='Obrazy firmy';
_def.ADD_ROWS:=0;
_def.HIDDEN:='N';

: źródło danych
_def.TABLE:="IMG_BLOB";

: prolog
_def.BEFORE:="
   IMG_BLOB.cntx_psh();
   IMG_BLOB.prefix();
   IMG_BLOB.trig_off('add przed','');
   IMG_BLOB.f_set('SLO_KOD',,
      'IMG_BLOB.RODZ=\\'F\\' and IMG_BLOB.USER is null and '
      '(IMG_BLOB.FIRMA is null or IMG_BLOB.FIRMA=:_a)',exec('ref_firma','ustawienia')
   );
   SLO_KOD.cntx_psh();
   SLO_KOD.index('KOD');
   SLO_KOD.prefix();
   SLO_TYP.cntx_psh();
   SLO_TYP.index('SYMBOL');
   SLO_TYP.prefix();
   ~~
";

: epilog
_def.AFTER:="
   SLO_TYP.cntx_pop();
   SLO_KOD.cntx_pop();
   IMG_BLOB.f_clear();
   IMG_BLOB.trig_on('add przed','');
   IMG_BLOB.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SLO_TYP',  MS.name(SLO_TYP,'SYMBOL'),0,,  MS.comment(SLO_TYP,'SYMBOL'));
   _env.define('SLO_KOD',  MS.name(SLO_KOD,'KOD'),0,,     MS.comment(SLO_KOD,'KOD'));
   _env.define('OBRAZ',    MS.name(IMG_BLOB,'OBRAZ'),0,,  MS.comment(IMG_BLOB,'OBRAZ'),type_of(''));
   _env.define('DOMYSLNY', 'Domyślny',0,,                 'Obraz domyślny',type_of(''));
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_img_blob',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_img_blob',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_img_blob',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
:: ~OST: INBLGET
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_buf:=_b;
_buf.SLO_KOD.VALUE:=IMG_BLOB.SLO_KOD().KOD;
_buf.SLO_TYP.VALUE:=SLO_KOD.SLO_TYP().SYMBOL;
_buf.DOMYSLNY.VALUE:={? IMG_BLOB.FIRMA=null() || 'T' || 'N' ?};
_buf.OBRAZ.VALUE:='%1_%2'[$IMG_BLOB.ref(),IMG_BLOB.bl_info('OBRAZ','NAME')];
_at:={? _env.DIR_ON_SERVER || '' || '@' ?};
IMG_BLOB.bl_get('OBRAZ',_at+'%1%2%3'[_env.DIR,_env.SEP,_buf.OBRAZ.VALUE],0);

:: typ (SLO_KOD):
_env.write_async('SLO_KOD',IMG_BLOB.SLO_KOD);

1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SLO_TYP=''  || 'SLO_TYP'
   |? _buf.SLO_KOD=''  || 'SLO_KOD'
   |? _buf.DOMYSLNY='' || 'DOMYSLNY'
   |? _buf.OBRAZ=''    || 'OBRAZ'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.DOMYSLNY))=0
|| _val.msg_inset('DOMYSLNY','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(SLO_TYP,'SYMBOL');
{? +_buf.SLO_TYP>_max
|| _val.msg_length('SLO_TYP',_max);
   _buf.SLO_TYP:=_max+_buf.SLO_TYP
?};
_max:=MS.fld_len(SLO_KOD,'KOD');
{? +_buf.SLO_KOD>_max
|| _val.msg_length('SLO_KOD',_max);
   _buf.SLO_KOD:=_max+_buf.SLO_KOD
?};
_max:=1;
{? +_buf.DOMYSLNY>_max
|| _val.msg_length('DOMYSLNY',_max);
   _buf.DOMYSLNY:=_max+_buf.DOMYSLNY
?};

: weryfikacja spójności referencyjnej
_slo_typ:=null;
{? SLO_TYP.find_key(_buf.SLO_TYP,)
|| _slo_typ:=SLO_TYP.ref()
|| _val.msg_norec('SLO_TYP');
   _val.RESULT:=0;
   return()
?};
_slo_kod:=null;
{? SLO_KOD.find_key(_slo_typ,_buf.SLO_KOD,)
|| _slo_kod:=SLO_KOD.ref()
|| _val.msg_norec('SLO_KOD');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? IMG_BLOB.f_find(_slo_kod)
|| _ref:=IMG_BLOB.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'Plik %1 (%2 - %3) domyślny: %4'[_a.OBRAZ,_a.SLO_KOD,_a.SLO_TYP,_a.DOMYSLNY]",
   'SLO_KOD',_slo_kod
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_env:=_val.env;

exec('write','xls__util',_buf,_mod,_val,IMG_BLOB,"
   _a.FIRMA:={? _b.DOMYSLNY='N' || exec('ref_firma','ustawienia') || null() ?};
   _a.RODZ:='F';
   _a.USER:=null();
   _a.SLO_KOD:=_c.SLO_KOD;
   ~~
");

{? _val.RESULT<>0
:: dołącz do wiersza plik obrazu
|| _at:={? _env.DIR_ON_SERVER || '' || '@' ?};
   _name:=_at+'%1%2%3'[_env.DIR,_env.SEP,_buf.OBRAZ];
   {? fexists(_name,0)
   || IMG_BLOB.bl_put('OBRAZ',_name,0,,17-_buf.OBRAZ)
   || _val.MSG:='Brak pliku "%1" lub nieprawidłowa ścieżka.'[_name]
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 dd18ea7bdcd9e467a8967a73fdfe7694be88690885375e0444576a010cbc7620208b74ace5acd1cac70696031826e2e7e205feeaed2aaefade77f50e4861cdea59498ca95d2f609d4682a5634c1827c45efddbf94d142812068634482a3ad7920801a2db4044f98fdc27ef6499a96864649eaeda1aee5af9740a534b65ca0299

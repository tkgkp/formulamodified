:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ppsfr.fml
:: Utworzony: 19.04.2023
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu rodzajów pracy poza siedzibą firmy
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='PPSFR';
_def.FUNPAR:='ZWS_PAR_PPSR';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Rodzaje pracy poza siedzibą=1,1';
_def.NAME:='Rodzaje pracy poza siedzibą'@;
_def.DESC:='Rodzaje pracy poza siedzibą';

: źródło danych
_def.TABLE:="PPSFR";

: prolog
_def.BEFORE:="
   PPSFR.cntx_psh();
   PPSFR.index('KOD');
   PPSFR.prefix(exec('firma','ustawienia'));
   ~~
";

: epilog
_def.AFTER:="
   PPSFR.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _tStr:=type_of('');
   _env.define('KOD',      MS.name(PPSFR,'KOD'),1,,      MS.comment(PPSFR,'KOD'),_tStr);
   _env.define('NAZWA',    MS.name(PPSFR,'NAZWA'),1,,    MS.comment(PPSFR,'NAZWA'),_tStr);
   _env.define('SKROT',    MS.name(PPSFR,'SKROT'),1,,    MS.comment(PPSFR,'SKROT'),_tStr);
   _env.define('SYSTEM',   MS.name(PPSFR,'SYSTEM'),1,,   MS.comment(PPSFR,'SYSTEM'),_tStr);
   _env.define('LIMITY',   MS.name(PPSFR,'LIMITY'),1,,   MS.comment(PPSFR,'LIMITY'),_tStr);
   _env.define('MODLIM',   MS.name(PPSFR,'MODLIM'),1,,   MS.comment(PPSFR,'MODLIM'),_tStr);
   _env.define('TD_PRAC',  MS.name(PPSFR,'TD_PRAC'),1,,  MS.comment(PPSFR,'TD_PRAC'),_tStr);
   _env.define('WSK_ADR',  MS.name(PPSFR,'WSK_ADR'),1,,  MS.comment(PPSFR,'WSK_ADR'),_tStr);
   _env.define('OCHR_ADR', MS.name(PPSFR,'OCHR_ADR'),1,, MS.comment(PPSFR,'OCHR_ADR'),_tStr);
   _env.define('KAL_ZESP', MS.name(PPSFR,'KAL_ZESP'),1,, MS.comment(PPSFR,'KAL_ZESP'),_tStr);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.KOD.VALUE:=PPSFR.KOD;
   _buf.NAZWA.VALUE:=PPSFR.NAZWA;
   _buf.SKROT.VALUE:=PPSFR.SKROT;
   _buf.SYSTEM.VALUE:=PPSFR.SYSTEM;
   _buf.LIMITY.VALUE:=PPSFR.LIMITY;
   _buf.MODLIM.VALUE:=PPSFR.MODLIM;
   _buf.TD_PRAC.VALUE:=PPSFR.TD_PRAC;
   _buf.WSK_ADR.VALUE:=PPSFR.WSK_ADR;
   _buf.OCHR_ADR.VALUE:=PPSFR.OCHR_ADR;
   _buf.KAL_ZESP.VALUE:=PPSFR.KAL_ZESP;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_ppsfr',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ppsfr',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.KOD=''      || 'KOD'
   |? _buf.NAZWA=''    || 'NAZWA'
   |? _buf.SYSTEM=''   || 'SYSTEM'
   |? _buf.LIMITY=''   || 'LIMITY'
   |? _buf.MODLIM=''   || 'MODLIM'
   |? _buf.TD_PRAC=''  || 'TD_PRAC'
   |? _buf.WSK_ADR=''  || 'WSK_ADR'
   |? _buf.OCHR_ADR='' || 'OCHR_ADR'
   |? _buf.KAL_ZESP='' || 'KAL_ZESP'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('NT'*_buf.SYSTEM)=0
|| _val.msg_inset('SYSTEM','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.LIMITY)=0
|| _val.msg_inset('LIMITY','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.MODLIM)=0
|| _val.msg_inset('MODLIM','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.TD_PRAC)=0
|| _val.msg_inset('TD_PRAC','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.WSK_ADR)=0
|| _val.msg_inset('WSK_ADR','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.OCHR_ADR)=0
|| _val.msg_inset('OCHR_ADR','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.KAL_ZESP)=0
|| _val.msg_inset('KAL_ZESP','N','T');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(PPSFR,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(PPSFR,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};
_max:=MS.fld_len(PPSFR,'SKROT');
{? +_buf.SKROT>_max
|| _val.msg_length('SKROT',_max);
   _buf.SKROT:=_max+_buf.SKROT
?};

: weryfikacja spójności referencyjnej
_ref:=null;
{? PPSFR.find_tab(,
      'KOD',,'=',_buf.KOD,
      'FIRMA',,'=',exec('firma','ustawienia')
   )
|| _ref:=PPSFR.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 %2'[_a.KOD,_a.NAZWA]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,PPSFR,"
   _a.KOD:=_b.KOD;
   _a.NAZWA:=_b.NAZWA;
   _a.SKROT:=_b.SKROT;
   _a.SYSTEM:=_b.SYSTEM;
   _a.LIMITY:=_b.LIMITY;
   _a.MODLIM:=_b.MODLIM;
   _a.TD_PRAC:=_b.TD_PRAC;
   _a.WSK_ADR:=_b.WSK_ADR;
   _a.OCHR_ADR:=_b.OCHR_ADR;
   _a.KAL_ZESP:=_b.KAL_ZESP;
   ~~
");
~~

:Sign Version 2.0 jowisz:1045 2023/04/28 15:59:47 ba03e9192a3691add9dac4fea0dd62f5d9d3ca12e2c96bb656b25728e0234f798d7fe54ed62702a84adfd3418625d957545947819bfe7f69b02afec213b2905a9ed32d19042cf9f8f1e180b65afda2c4f3e36154adf1fc55d426ba88c73085ea7f970f26deaa9a3a689e501511f0ccc627b9fee49821e274b51f6c1cf8f52ffe

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_f_zatrn.fml
:: Utworzony: 09.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu docelowych form współpracy
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='F_ZATRN';
_def.FUNPAR:='ZWS_PKD_FZATR';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Docelowe formy współpracy=1,1';
_def.NAME:='Docelowe formy współpracy'@;
_def.DESC:='Docelowe formy współpracy';

: źródło danych
_def.TABLE:="F_ZATRN";

: prolog
_def.BEFORE:="
   F_ZATR.cntx_psh();
   F_ZATR.prefix();
   F_ZATRN.cntx_psh();
   F_ZATRN.f_clear();
   F_ZATRN.index('KOD_D');
   F_ZATRN.prefix()
";

: epilog
_def.AFTER:="
   F_ZATRN.cntx_pop();
   F_ZATR.cntx_pop()
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('F_ZATR',  MS.name(F_ZATRN,'F_ZATR'), 0,,MS.comment(F_ZATRN,'F_ZATR'));
   _env.define('DOCELOWA',MS.name(F_ZATRN,'DOCELOWA'),0,,MS.comment(F_ZATRN,'DOCELOWA'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.F_ZATR.VALUE:=F_ZATRN.F_ZATR().KOD;
   _buf.DOCELOWA.VALUE:=F_ZATRN.DOCELOWA().KOD;
   _env.write_async('F_ZATR',F_ZATRN.F_ZATR);
   _env.write_async('F_ZATR',F_ZATRN.DOCELOWA);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_f_zatrn',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_f_zatrn',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.F_ZATR=''   || 'F_ZATR'
   |? _buf.DOCELOWA='' || 'DOCELOWA'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(F_ZATR,'KOD');
{? +_buf.F_ZATR>_max
|| _val.msg_length('F_ZATR',_max);
   _buf.F_ZATR:=_max+_buf.F_ZATR
?};
{? +_buf.DOCELOWA>_max
|| _val.msg_length('DOCELOWA',_max);
   _buf.DOCELOWA:=_max+_buf.DOCELOWA
?};

: weryfikacja spójności referencyjnej
{? ~F_ZATR.find_tab(,'KOD',,'=',_buf.F_ZATR)
|| _val.msg_norec('F_ZATR');
   _val.RESULT:=0;
   return()
|| _rT:=F_ZATR.ref()
?};
{? ~F_ZATR.find_tab(,'KOD',,'=',_buf.DOCELOWA)
|| _val.msg_norec('DOCELOWA');
   _val.RESULT:=0;
   return()
|| _rD:=F_ZATR.ref()
?};

_ref:=null;
{? F_ZATRN.find_tab(,'F_ZATR',,'=',_rT,'DOCELOWA',,'=',_rD)
|| _ref:=F_ZATRN.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('F_ZATRN','F_ZATR','DOCELOWA','to_str');
_obj.to_str:="'%1 -> %2'[_a.F_ZATR,_a.DOCELOWA]";
_obj.F_ZATRN:=_ref;
_obj.F_ZATR:=_rT;
_obj.DOCELOWA:=_rD;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.F_ZATR:=_b.F_ZATR;
   _a.DOCELOWA:=_b.DOCELOWA;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? F_ZATRN.seek(_obj.F_ZATRN)
   || _set(F_ZATRN,_obj);
      {? ~F_ZATRN.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,F_ZATRN.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| F_ZATRN.blank();
   _set(F_ZATRN,_obj);
   {? ~F_ZATRN.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:47 15993c5014e302fc4837f078a8bbeea85a47307db91f7a22e3fd3330c457e98412c76ea5021fe1bd389c92381f38fecba8a243c6fb33e5f515260e6bd2727f18ac191b876fb8673d8799d71bdf8914de6e2c8685890ec4a48b0077d03df03275306cb0233d8fcd8b430c18ebd8438da5177f278552acfa70c2b72d8a155c64f2

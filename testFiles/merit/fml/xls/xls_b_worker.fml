:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_b_worker.fml
:: Utworzony: 18.09.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu kolejek
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='B_WORKER';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
::_def.FUNPAR:='';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Kolejki'@+'=1,1';
_def.NAME:='Kolejki'@;
_def.DESC:='Kolejki'@;

_def.PREFIX:="B_WORKER.index('KOD'); B_WORKER.prefix(REF.FIRMA)";
_def.TABLE:="B_WORKER";
_def.FIELDS:="exec('fields','xls_b_worker',_a)";

_def.BEFORE:="B_WORKER.cntx_psh()";
_def.AFTER:="B_WORKER.cntx_pop()";
_def.SELECT:="exec('select','xls_b_worker',_a)";

_def.EXPORT:="exec('export','xls_b_worker',_a,_b)";
_def.VALIDATE:="exec('validate','xls_b_worker',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_b_worker',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD',MS.name(B_WORKER,'KOD'),0,,
   MS.name(B_WORKER,'KOD')+' '+'(max. %1 znaków)'@[$MS.fld_len(B_WORKER,'KOD')]
);
_def.define('OPIS',MS.name(B_WORKER,'OPIS'),1,,
   MS.name(B_WORKER,'OPIS')+' '+'(max. %1 znaków)'@[$MS.fld_len(B_WORKER,'OPIS')]
);
_def.define('PAUZA',MS.name(B_WORKER,'PAUZA'),1,,,,0);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
B_WORKER.prefix();
{? _validate.ACTION='add'
|| B_WORKER.blank();
   B_WORKER.FIRMA:=REF.FIRMA;
   B_WORKER.KOD:=_obj.KOD
?};
B_WORKER.OPIS:=_obj.OPIS;
B_WORKER.PAUZA:=_obj.PAUZA;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=B_WORKER.KOD;
_table.OPIS.VALUE:=B_WORKER.OPIS;
_table.PAUZA.VALUE:=B_WORKER.PAUZA;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_int:=int(_obj.PAUZA);

:: Błędy
{? _int<exec('minint','#math') | _int>exec('maxint','#math')
|| _result.msg_range('PAUZA',exec('minint','#math'),exec('maxint','#math'));
   _result.RESULT:=0
?};

:: Ostrzeżenia
{? +_obj.KOD>MS.fld_len(B_WORKER,'KOD')
|| _result.msg_length('KOD',MS.fld_len(B_WORKER,'KOD'))
?};
{? +_obj.OPIS>MS.fld_len(B_WORKER,'OPIS')
|| _result.msg_length('OPIS',MS.fld_len(B_WORKER,'OPIS'))
?};
{? _int<>_obj.PAUZA
|| _result.msg_int('PAUZA')
?};

:: OK
{? _result.RESULT>0
|| B_WORKER.index('KOD');
   B_WORKER.prefix(REF.FIRMA,_obj.KOD,);
   {? B_WORKER.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_b_worker',_obj,_mode,_result);
         {? exec('chk_b_worker','#b_worker',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_b_worker',_obj,_mode,_result);
      {? exec('chk_b_worker','#b_worker',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=B_WORKER.add(1)
   |? _validate.ACTION='put'
   || _result:=B_WORKER.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie kolejki \'%1\' nie powiodło się.'@[_obj.KOD]
   || _validate.MSG:='Poprawa kolejki \'%1\' nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'KOD','STRING[%1]'[$MS.fld_len(B_WORKER,'KOD')],MS.name(B_WORKER,'KOD')
   ,'OPIS','STRING[%1]'[$MS.fld_len(B_WORKER,'OPIS')],MS.name(B_WORKER,'OPIS')
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

B_WORKER.cntx_psh();
B_WORKER.index('KOD');
B_WORKER.prefix(REF.FIRMA);
{? B_WORKER.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$B_WORKER.ref();
      _selected.prefix($B_WORKER.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=B_WORKER.KOD;
      _tab.OPIS:=B_WORKER.OPIS;
      _tab.add();
      B_WORKER.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'KOD,OPIS',20,'Wybór kolejek do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
B_WORKER.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:47 7d02fc51bf61a420e8c76c833d7b43d50aeccc801ea3d51e17b4dc6361556a81180f5b3a922fb1d534accfa08e9a011722aca93ff54bbee00135823e24cfc3292cf1e1226d9332cc7546530776eb0af3f582a15e90c82176695c050a479aac30bb1fda423280e0040742b6651c51b3e2008d22c802cdf4471d96a79f5b43ff8c

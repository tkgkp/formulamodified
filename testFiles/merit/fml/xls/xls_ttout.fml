:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ttout.fml
:: Utworzony: 25.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu taryfikatora usług
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TTOUT';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTAR';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Taryfikator usług'@+'=1,1';
_def.NAME:='Taryfikator usług'@;
_def.DESC:='Taryfikator usług (operacji wykonywanych w kooperacji)'@;

_def.PREFIX:="TTOUT.index('KOD'); TTOUT.prefix()";
_def.TABLE:="TTOUT";
_def.FIELDS:="exec('fields','xls_ttout',_a)";

_def.BEFORE:="TTOUT.cntx_psh()";
_def.AFTER:="TTOUT.cntx_pop()";
_def.SELECT:="exec('select','xls_ttout',_a)";

_def.EXPORT:="exec('export','xls_ttout',_a,_b)";
_def.VALIDATE:="exec('validate','xls_ttout',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ttout',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod'@,0,,'Kod (max. 10 znaków)'@);
_def.define('NA','Nazwa'@,1,,'Nazwa (max. 50 znaków)'@);
_def.define('CENA','Cena'@,1);
::_def.define('KONTRAH','Kontrahent'@,1);
_def.define('SRW','Sposób rejestracji'@,1);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TTOUT.prefix();
{? _validate.ACTION='add'
|| TTOUT.blank();
   TTOUT.KOD:=_obj.KOD
?};
TTOUT.NA:=_obj.NA;
::TTOUT.KONTRAH:=_validate.OBJ.KONTRAH;
TTOUT.SRW:=_obj.SRW;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=TTOUT.KOD;
_table.NA.VALUE:=TTOUT.NA;
_table.CENA.VALUE:=TTOUT.CENA;
::_table.KONTRAH.VALUE:=TTOUT.KONTRAH().SKR;
_table.SRW.VALUE:=TTOUT.SRW;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
::_result.OBJ:=obj_new('KONTRAH');
::_result.OBJ.KONTRAH:=exec('FindInSet','#table','KH','SKR',_obj.KONTRAH,1,"KH.ref()",1);

::{? _obj.KONTRAH<>'' & _result.OBJ.KONTRAH=null()
::|| _result.msg_norec('KONTRAH');
::   _result.RESULT:=0

:: Ostrzeżenia
{? +_obj.KOD>10
|| _result.msg_length('KOD',10)
?};
{? +_obj.NA>50
|| _result.msg_length('NA',50)
?};

:: OK
TTOUT.index('KOD');
TTOUT.prefix(_obj.KOD,);
{? TTOUT.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put';
      exec('record','xls_ttout',_obj,_mode,_result);
      {? exec('chk_ttout','prod_tar',1)<>''
      || _result.RESULT:=0
      ?}
   ?}
|| _result.ACTION:='add';
   exec('record','xls_ttout',_obj,_mode,_result);
   {? exec('chk_ttout','prod_tar',0)<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TTOUT.add(1)
   |? _validate.ACTION='put'
   || _result:=TTOUT.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie pozycji taryfikatora o kodzie %1 nie powiodło się.'@[_obj.KOD]
   || _validate.MSG:='Poprawa pozycji taryfikatora o kodzie %1 nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'KOD','STRING[10]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NA','STRING[50]','Nazwa'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

TTOUT.cntx_psh();
TTOUT.index('KOD');
TTOUT.prefix();
{? TTOUT.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$TTOUT.ref();
      _selected.prefix($TTOUT.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=TTOUT.KOD;
      _tab.NA:=TTOUT.NA;
      _tab.add();
      TTOUT.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'KOD,NA',20,'Wybór pozycji taryfikatora do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TTOUT.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 07c27781e26d361dcceb200cd66559dc7e034934fa36237b3f03928eac53685cdc6e507da7915fa82a26c5c4ccd1b3f94be4d1ccf653435ccc7d4a71b096200c9723e8eba655bdee08f2598ae2e537bd58dd620da53cc365fb56cd483231994a594777dd30290e73a5bf38898f989ea58487f33967c2dffd5a13d209537b1b9a

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_zpwyd.fml
:: Utworzony: 22.10.2018
:: Autor:
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu podstaw powołania zlecenia
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ZPWYD';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZLE';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Podstawy powołania zlecenia'@+'=1,1';
_def.NAME:='Podstawy powołania zlecenia'@;
_def.DESC:='Podstawy powołania zlecenia'@;

_def.PREFIX:="exec('prefix','xls_zpwyd')";
_def.TABLE:="exec('table','xls_zpwyd')";
_def.FIELDS:="exec('fields','xls_zpwyd',_a)";

_def.BEFORE:="ZPWYD.cntx_psh()";
_def.AFTER:="ZPWYD.cntx_pop()";
_def.SELECT:="exec('select','xls_zpwyd',_a)";

_def.EXPORT:="exec('export','xls_zpwyd',_a,_b)";
_def.VALIDATE:="exec('validate','xls_zpwyd',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_zpwyd',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ZPWYD.index('ZPWYD_PW');
ZPWYD.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ZPWYD


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('PWYD','Nazwa'@,1,,'60 znakowa Podstawa powołania zlecenia'@);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
ZPWYD.prefix();
{? _validate.ACTION='add'
||
   ZPWYD.blank()
?};
ZPWYD.PWYD:=_obj.PWYD;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

{? 1+ZPWYD.PWYD='~'
||
   _result:=0
||
   _result:=1;
   _table.PWYD.VALUE:=ZPWYD.PWYD
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? 1+_obj.PWYD='~'
|| _result.msg_value('PWYD','Zastrzeżona podstawa powołania zlecenia nie może być importowana.');
   _result.RESULT:=0
?};

{? (+_obj.PWYD)>60 ||
  _result.msg_length('PWYD',60);
  _result.RESULT:=0
?};

{? _result.RESULT>0
||
  ZPWYD.index('ZPWYD_PW');
  ZPWYD.prefix(_obj.PWYD,);
  {? ZPWYD.first()
  ||
    {? _mode=0
    || _result.RESULT:=0
    ||
      _result.ACTION:='put';
      exec('record','xls_zpwyd',_obj,_mode,_result);
      {? exec('chk_zpwyd','zl_head',1)<>''
      ||
         _result.RESULT:=0
      ?}
    ?}
  ||
    _result.ACTION:='add';
    exec('record','xls_zpwyd',_obj,_mode,_result);
    {? exec('chk_zpwyd','zl_head',0)<>''
    || _result.RESULT:=0
    ?}
  ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=ZPWYD.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=ZPWYD.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie podstawy powołania zlecenia "%1" nie powiodło się.'@[_obj.PWYD]
   ||
      _validate.MSG:='Poprawa podstawy powołania zlecenia "%1" nie powiodła się.'@[_obj.PWYD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'PWYD','STRING[60]','Podstawa powołania zlecenia');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'PWYD',,);

ZPWYD.cntx_psh();
ZPWYD.index('ZPWYD_PW');
ZPWYD.prefix();
{? ZPWYD.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$ZPWYD.ref();
      _selected.prefix($ZPWYD.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.PWYD:=ZPWYD.PWYD;
      _tab.add();
      ZPWYD.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'PWYD',20,'Wybór podstaw powołania zlecenia do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
ZPWYD.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:52 4643faeb052cd5788b5caa5dbcd5af8d81ea6179402ab0f1b0d16f9af535ce018efaeca1e923a1b069dbefe0a52c92d6f5524d4fdbfe20802a90cc9b6093a3bc6f233610248a8158fac87364a68c6b32a683710a24306bc9bec29b95fd2e44aeba15f1eb9cd62360e1862f912bf56a451ba16319ea443fde337884daf43471ee

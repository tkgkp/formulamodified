:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_arch_ust.fml
:: Utworzony: 20.05.2021
:: Autor: MicKoc [21.37]
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu ustawień archiwizacji załączników
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='ARCH_UST';
_def.FUNPAR:='ZWS_PAR_ARCU';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_xlsx','xls__init');
_def.SHEET:='Ustawienia archiwizacji'@+'=1,1';
_def.NAME:='Ustawienia archiwizacji'@;
_def.DESC:='Ustawienia archiwizacji'@;
_def.ADD_ROWS:=0;

: źródło danych
_def.TABLE:="ARCH_UST";

: prolog
_def.BEFORE:="
   ARCH_UST.cntx_psh();
   ARCH_UST.index('UNIQ');
   ARCH_UST.prefix(exec('ref_firma','ustawienia'));
   ~~
";

: epilog
_def.AFTER:="
   ARCH_UST.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _fl:='MS.name(ARCH_UST,_a)';
   _fc:='MS.comment(ARCH_UST,_a)';
   _env:=_a;
   _env.define(_txt:='TAB',($(_fl))(_txt),0,,($(_fc))(_txt));
   _env.define(_txt:='FLD',($(_fl))(_txt),0,,($(_fc))(_txt));
   _env.define(_txt:='AKT',($(_fl))(_txt),0,,($(_fc))(_txt));
   _env.define(_txt:='LST',($(_fl))(_txt),0,,($(_fc))(_txt));
   _env.define(_txt:='FORM',($(_fl))(_txt),0,,($(_fc))(_txt));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   {? ARCH_UST.AKT='N'
   || return(0)
   ?};
   _buf.TAB.VALUE:=ARCH_UST.TAB;
   _buf.FLD.VALUE:=ARCH_UST.FLD;
   _buf.AKT.VALUE:=ARCH_UST.AKT;
   _buf.LST.VALUE:=ARCH_UST.memo_txt(,1,'LST');
   _buf.FORM.VALUE:=ARCH_UST.FORM;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_arch_ust',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_arch_ust',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

{? var_pres(_buf.TAB)<>type_of(SYSLOG)
|| _val.MSG:='Tabela "%1" nie istnieje.'@[_id[1]];
   _val.RESULT:=0;
   return()
?};

_typ:=var_pres(_buf.FLD,($(_buf.TAB))());
{? ~(_typ=33 | _typ=37)
|| _val.MSG:='Pole "%1" tabeli "%2" nie jest typu BLOB.'@[_buf.FLD,_buf.TAB];
   _val.RESULT:=0;
   return()
?};

{? ~('TN'*_buf.AKT) | ~+|_buf.AKT
|| _val.MSG:='Nieprawidłowa wartość pola "%1".'@[_buf.FLD];
   _val.RESULT:=0;
   return()
?};

:: Wybór akcji
{? ARCH_UST.find_key(_buf.TAB,_buf.FLD,)
|| {? _mod
   || exec('dane','xls_arch_ust','put',_buf);
      _val.ACTION:='put'
   || _val.RESULT:=0
   ?}
|| exec('dane','xls_arch_ust','add',_buf);
   _val.ACTION:='add'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_mod:=_b;
_val:=_c;

_result:=0;
{? _val.RESULT=1
|| {? _val.ACTION='add'
   || _result:=ARCH_UST.add(1)
   |? _val.ACTION='put'
   || _result:=ARCH_UST.put(1)
   ?}
?};
{? ~_result
|| {? _val.ACTION='add'
   || _val.MSG:='Dodanie ustawienia archiwizacji dla pola %1 tabeli %2 nie powiodło się.'@[_buf.FLD,_buf.TAB]
   || _val.MSG:='Poprawa ustawienia archiwizacji dla pola %1 tabeli %2 nie powiodło się.'@[_buf.FLD,_buf.TAB]
   ?}
|| ARCH_UST.memo_set(_buf.LST,'LST');
   _result:=ARCH_UST.memo_put(,'LST');
   {? ~_result
   || {? _val.ACTION='add'
      || _val.MSG:='Dodanie warunku w ustawienich archiwizacji dla pola %1 tabeli %2 nie powiodło się.'@
            [_buf.FLD,_buf.TAB]
      || _val.MSG:='Poprawa warunku w ustawienich archiwizacji dla pola %1 tabeli %2 nie powiodło się.'@
            [_buf.FLD,_buf.TAB]
      ?}
   ?}
?};
_result


\dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Wypełnienie bufora danymi
::   WE: _a [STRING] - identyfikator akcji 'add' lub 'put'
::       _b [OBJEKT] - bufor danych
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a='add' || ARCH_UST.blank() ?};
ARCH_UST.TAB:=_b.TAB;
ARCH_UST.FLD:=_b.FLD;
ARCH_UST.AKT:=_b.AKT;
ARCH_UST.FORM:=_b.FORM

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 a428bd9a69630b438ab0b2f2c5d31577df936d50abe8a9445e0351ddbc4d681721420b00e1b044b75033c6bd4bd8d23761ea00dfd30cbad374c254d189e2496324da94728200c7aa69a20d99f098b0f44ef24956961419aebf63bb05f521d0c2f0023b6c6e24315e100b4994727413d732d3e764920e7e9e07c46ccda6948f22

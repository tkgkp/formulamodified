:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_typyzam.fml
:: Utworzony: 13.07.2018
:: Autor: Mario
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typów zamówień
::======================================================================================================================


\init_z
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TYPYZAMZ';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.FUNPAR:='ZWS_PAR_LTZK';

_def.FILE:='sprzedaz.xlsx';
_def.SHEET:='Typy zamówień sprzedaży=1,1';
_def.NAME:='Typy zamówień sprzedaży'@;
_def.DESC:='Typy zamówień sprzedaży';

_def.PREFIX:="exec('prefix','xls_typyzam','Z')";
_def.TABLE:="TYPYZAM";
_def.FIELDS:="exec('fields','xls_typyzam',_a,'Z')";

_def.BEFORE:="TYPYZAM.cntx_psh()";
_def.AFTER:="TYPYZAM.cntx_pop()";
_def.SELECT:="exec('select','xls_typyzam',_a,'Z')";

_def.EXPORT:="exec('export','xls_typyzam',_a,_b,'Z')";
_def.VALIDATE:="exec('validate','xls_typyzam',_a,_b,_c,'Z')";
_def.IMPORT:="exec('import','xls_typyzam',_a,_b,_c,'Z')";
~~


\init_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TYPYZAMD';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PAR_LTZD';

_def.FILE:='zakupy.xlsx';
_def.SHEET:='Typy zamówień dostaw=1,1';
_def.NAME:='Typy zamówień dostaw'@;
_def.DESC:='Typy zamówień dostaw';

_def.PREFIX:="exec('prefix','xls_typyzam','D')";
_def.TABLE:="TYPYZAM";
_def.FIELDS:="exec('fields','xls_typyzam',_a,'D')";

_def.BEFORE:="TYPYZAM.cntx_psh()";
_def.AFTER:="TYPYZAM.cntx_pop()";
_def.SELECT:="exec('select','xls_typyzam',_a,'D')";

_def.EXPORT:="exec('export','xls_typyzam',_a,_b,'D')";
_def.VALIDATE:="exec('validate','xls_typyzam',_a,_b,_c,'D')";
_def.IMPORT:="exec('import','xls_typyzam',_a,_b,_c,'D')";
~~


\init_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TYPYZAMW';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='ZWS_PAR_LTZW';

_def.FILE:='magazyn.xlsx';
_def.SHEET:='Typy zamówień wewnętrznych=1,1';
_def.NAME:='Typy zamówień wewnętrznych'@;
_def.DESC:='Typy zamówień wewnętrznych';

_def.PREFIX:="exec('prefix','xls_typyzam','W')";
_def.TABLE:="TYPYZAM";
_def.FIELDS:="exec('fields','xls_typyzam',_a,'W')";

_def.BEFORE:="TYPYZAM.cntx_psh()";
_def.AFTER:="TYPYZAM.cntx_pop()";
_def.SELECT:="exec('select','xls_typyzam',_a,'W')";

_def.EXPORT:="exec('export','xls_typyzam',_a,_b,'W')";
_def.VALIDATE:="exec('validate','xls_typyzam',_a,_b,_c,'W')";
_def.IMPORT:="exec('import','xls_typyzam',_a,_b,_c,'W')";
~~


\init_o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TYPYZAMO';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.FUNPAR:='ZWS_PAR_LTOF';

_def.FILE:='sprzedaz.xlsx';
_def.SHEET:='Typy ofert=1,1';
_def.NAME:='Typy ofert'@;
_def.DESC:='Typy ofert';

_def.PREFIX:="exec('prefix','xls_typyzam','O')";
_def.TABLE:="TYPYZAM";
_def.FIELDS:="exec('fields','xls_typyzam',_a,'O')";

_def.BEFORE:="TYPYZAM.cntx_psh()";
_def.AFTER:="TYPYZAM.cntx_pop()";
_def.SELECT:="exec('select','xls_typyzam',_a,'O')";

_def.EXPORT:="exec('export','xls_typyzam',_a,_b,'O')";
_def.VALIDATE:="exec('validate','xls_typyzam',_a,_b,_c,'O')";
_def.IMPORT:="exec('import','xls_typyzam',_a,_b,_c,'O')";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WE: _a - Z,D,W typ zamówienia Zklienta, Dostaw, Wewnętrznych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_rodz:=_a;
TYPYZAM.index('TYP');
TYPYZAM.prefix(_rodz,);
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::       _b -  Z,D,W,O typ zamówienia Zklienta, Dostaw, Wewnętrznych, Ofert
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_rodz:=_b;

{? _rodz<>'O'
|| _def.define('T','Typ',1,,'Kod typu zamówienia');
   _def.define('NAZ','Nazwa',1,,'Nazwa opisowa typu zamówienia')
|| _def.define('T','Typ',1,,'Kod typu oferty');
   _def.define('NAZ','Nazwa',1,,'Nazwa opisowa typu oferty')
?};
_def.define('KOD','Kod numeracji',1,,'Oznaczenie kodu numeracji');
{? _rodz<>'O'
|| _def.define('TYPYDOK','Realizuj dokumentem magazynowym',1,,'Typ dokumentu realizującego dane zamówienie');
   _def.define('MOB','Czy realizacja na urządzeniach mobilnych',1,,'')
?};
_def.define('PROJZAKR','Zakres projektów',1,,'Zakres projektów');
_def.define('PROJWJO','Zakres projektów - jednostka organizacyjna z dokumentu',1,,'');
_def.define('PROJZKH','Zakres projektów - kontrahent z dokumentu',1,,'Zakres projektów - kontrahent z dokumentu');
_def.define('VALIDATE','Dodatkowa formuła walidacji',1,,'Dodatkowa formuła walidacyjna');
_def.define('VALACC','Dodatkowa formuła akceptacyjna',1,,'Dodatkowa formuła akceptacyjna');
_def.define('VALREC','Dodatkowa formuła wycofania',1,,'Dodatkowa formuła wycofania');

{? _rodz='Z' | _rodz='W'
||
   _def.define('ILREA','Domyślna ilość dni na realizację',1,,'Domyślna ilość dni na realizację',,0);
   _def.define('PL_DIR','Domyślny zwrot planowania',1,,'Domyślny zwrot dla planowania strategicznego',,0);
   _def.define('PL_FORCE','Kontynuuj planowanie po przekroczeniu terminu',1,,'');
   _def.define('RPR','Rezerwacje dla produkcji',1,,'Brak rezerwacji dla produkcji - 1, są 0 (domyślnie)',,0)
?};

{? _rodz='Z'
|| _def.define('WAL','Waluta'@,1,,'Waluta - Kod')
?};
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d -  Z,D,W,O typ zamówienia Zklienta, Dostaw, Wewnętrznych, Ofert
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_rodz:=_d;

TYPYZAM.index('TYP');
TYPYZAM.prefix(_rodz,_obj.T,);
{? _validate.ACTION='add'
||
:: UWAGA - pola wybrano gdy dokument jest już wykorzystywany w systemie - czy zrobić rozróżnienie?
   TYPYZAM.blank();
   TYPYZAM.R:=_rodz;
   TYPYZAM.T:=_obj.T;
   {? _rodz='Z' | _rodz='W'
   ||
      TYPYZAM.RPR:=_obj.RPR
   ?}
?};

TYPYZAM.NAZ:=_obj.NAZ;
TYPYZAM.KOD:=_obj.KOD;
{? _rodz<>'O'
|| TYPYZAM.TYPYDOK:=_validate.OBJ.TYPYDOK;
   TYPYZAM.MOB:=_obj.MOB
?};
TYPYZAM.PROJZAKR:=_obj.PROJZAKR;
TYPYZAM.PROJWJO:=_obj.PROJWJO;
TYPYZAM.PROJZKH:=_obj.PROJZKH;

{? _rodz='Z' | _rodz='W'
||
   TYPYZAM.ILREA:=_obj.ILREA;
   TYPYZAM.PL_DIR:=_obj.PL_DIR;
   TYPYZAM.PL_FORCE:=_obj.PL_FORCE
?};
:: UWAGA brak klucza unikalnego po jednym polu dla formula
TYPYZAM.VALIDATE:=_validate.OBJ.VALIDATE;
TYPYZAM.VALACC:=_validate.OBJ.VALACC;
TYPYZAM.VALREC:=_validate.OBJ.VALREC;
{? _rodz='Z'
|| TYPYZAM.WAL:=_validate.OBJ.WAL
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::       _c -  Z,D,W,O typ zamówienia Zklienta, Dostaw, Wewnętrznych, Ofert
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_rodz:=_c;

_result:=1;

_table.T.VALUE:=TYPYZAM.T;
_table.NAZ.VALUE:=TYPYZAM.NAZ;
_table.KOD.VALUE:=TYPYZAM.KOD;
_excel.write_async('NRDOK',exec('FindInSet','#table','NRDOK','NRDOK',TYPYZAM.KOD,,,1,,null()));

{? _rodz<>'O'
|| _table.TYPYDOK.VALUE:=TYPYZAM.TYPYDOK().T;
   _excel.write_async('TYPYDOK',TYPYZAM.TYPYDOK);
   _table.MOB.VALUE:=TYPYZAM.MOB
?};

_table.PROJZAKR.VALUE:=TYPYZAM.PROJZAKR;
_table.PROJWJO.VALUE:=TYPYZAM.PROJWJO;
_table.PROJZKH.VALUE:=TYPYZAM.PROJZKH;

{? _rodz='Z' | _rodz='W'
||
   _table.RPR.VALUE:=TYPYZAM.RPR;
   _table.ILREA.VALUE:=TYPYZAM.ILREA;
   _table.PL_DIR.VALUE:=TYPYZAM.PL_DIR;
   _table.PL_FORCE.VALUE:=TYPYZAM.PL_FORCE
?};
:: UWAGA brak klucza unikalnego po jednym polu dla formula
{? TYPYZAM.VALIDATE<>null()
|| _table.VALIDATE.VALUE:=TYPYZAM.VALIDATE().SKROT
?};
{? TYPYZAM.VALACC<>null()
|| _table.VALACC.VALUE:=TYPYZAM.VALACC().SKROT
?};
{? TYPYZAM.VALREC<>null()
|| _table.VALREC.VALUE:=TYPYZAM.VALREC().SKROT
?};
{? _rodz='Z'
|| _table.WAL.VALUE:=TYPYZAM.WAL().KOD
?};

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::       _d -  Z,D,W,O typ zamówienia Zklienta, Dostaw, Wewnętrznych, Ofert
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;
_rodz:=_d;
_opis:={? _rodz<>'O' || 'zamówień'@ || 'ofert'@ ?};

_result.RESULT:=1;

_result.OBJ:=obj_new('TYPYDOK','VALIDATE','VALACC','VALREC','WAL');

{? _rodz<>'O' & _obj.TYPYDOK<>''
||
   _result.OBJ.TYPYDOK:=exec('FindInSet','#table','TYPYDOK','TYP',_obj.TYPYDOK,,,1);
   {? _result.OBJ.TYPYDOK=null()
   ||
      _result.MSG:='Nie znaleziono typu dokumentu o kodzie %1.'@[_obj.TYPYDOK];
      _result.RESULT:=0
   ?}
||
   _result.OBJ.TYPYDOK:=null
?};

{? _obj.VALIDATE<>''
|| _result.OBJ.VALIDATE:=exec('FindInSet','#table','FORMULA','FORMULA4',_obj.VALIDATE,'v',,1);
   {? _result.OBJ.VALIDATE=null()
   || _result.MSG:='Nie znaleziono formuły walidacji dla typów %1 %2.'@[_opis,_obj.VALIDATE];
      _result.RESULT:=0
   ?}
|| _result.OBJ.VALIDATE:=null()
?};

{? _obj.VALACC<>''
|| _result.OBJ.VALACC:=exec('FindInSet','#table','FORMULA','FORMULA4',_obj.VALACC,'a',,1);
   {? _result.OBJ.VALACC=null()
   || _result.MSG:='Nie znaleziono formuły akceptacji dla typów %1 %2.'@[_opis,_obj.VALACC];
      _result.RESULT:=0
   ?}
|| _result.OBJ.VALACC:=null()
?};

{? _obj.VALREC<>''
|| _result.OBJ.VALREC:=exec('FindInSet','#table','FORMULA','FORMULA4',_obj.VALREC,'r',,1);
   {? _result.OBJ.VALREC=null()
   || _result.MSG:='Nie znaleziono formuły wycofania dla typów %1 %2.'@[_opis,_obj.VALREC];
      _result.RESULT:=0
   ?}
|| _result.OBJ.VALREC:=null()
?};

_result.OBJ.WAL:=null();
{? _rodz='Z' & _obj.WAL<>''
|| _result.OBJ.WAL:=exec('FindInSet','#table','SLO','KOD',_obj.WAL,,,1,,null());
   {? _result.OBJ.WAL=null()
   || _result.MSG:='Nie znaleziono waluty o kodzie %1 dla typu zamówienia %2.'@[_obj.WAL,_obj.T]
   ?}
?};

TYPYZAM.index('TYP');
TYPYZAM.prefix(_rodz,_obj.T,);
{? TYPYZAM.first()
||
   {? _mode=0
   || _result.RESULT:=0
   ||
      _result.ACTION:='put';
      {? TYPYZAM.KOD<>_obj.KOD
      || {? TYPYZAM.count()>0
         || _obj.KOD:=TYPYZAM.KOD;
            {? _result.MSG<>''
            || _result.MSG+=' '
            ?};
            _result.MSG+={? _rodz<>'O'
                         || 'Nie można zmienić numeracji typu zamówienia %1'@[TYPYZAM.T]
                         || 'Nie można zmienić numeracji typu oferty %1'@[TYPYZAM.T]
                         ?}
         ?}
      ?};
      exec('record','xls_typyzam',_obj,_mode,_result,_rodz);
      {? exec('typz_rez','typyzam','put')<>''
      ||
         _result.RESULT:=0
      ?}
   ?}
||
   _result.ACTION:='add';
   exec('record','xls_typyzam',_obj,_mode,_result,_rodz);
   {? exec('typz_rez','typyzam','add')<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d -  Z,D,W,O typ zamówienia Zklienta, Dostaw, Wewnętrznych, Ofert
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_rodz:=_d;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   || _result:=TYPYZAM.add(1)
   |? _validate.ACTION='put'
   || _result:=TYPYZAM.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   || _validate.MSG:={? _rodz<>'O'
                     || 'Dodanie typu zamówienia %1 nie powiodło się.'@[_obj.T]
                     || 'Dodanie typu oferty %1 nie powiodło się.'@[_obj.T]
                     ?}
   || _validate.MSG:={? _rodz<>'O'
                     || 'Modyfikacja typu zamówienia %1 nie powiodła się.'@[_obj.T]
                     || 'Modyfikacja typu oferty %1 nie powiodła się.'@[_obj.T]
                     ?}
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - rodzaj zamówienia
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_rodz:=_b;

_result:=0;

_tab:=tab_tmp(1,'T','STRING[8]','Typ'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[60]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'T',,);

TYPYZAM.cntx_psh();
TYPYZAM.index('TYP');
TYPYZAM.prefix(_rodz);
{? TYPYZAM.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$TYPYZAM.ref();
      _selected.prefix($TYPYZAM.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.T:=TYPYZAM.T;
      _tab.NAZ:=TYPYZAM.NAZ;
      _tab.add();
      TYPYZAM.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'T,NAZ',20,{? _rodz<>'O' || 'Wybór typów zamówień do eksportu'@ || 'Wybór typów ofert do eksportu'@ ?},1);
{? _result>0
|| _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TYPYZAM.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 b18d721c44981485466af7ea9d038191dd686aa684cad68500577050a4041ee437678616ec9e6aafdce69bb5a927d176fe98f110420f7bd3b635ee47e506df30b9e676dbc55ba1cb5b5e961084ca4122ea7d5846b51adbf3adc22f6a24e37362c9ff1ac3a50b0efb7f4275ff717d5459d2a8a2ecb9d722904568d8a8bcbb8bc2

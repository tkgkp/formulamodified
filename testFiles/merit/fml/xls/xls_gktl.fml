:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_gktl.fml
:: Utworzony: 29.09.2022
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu grup kart technologicznych
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='GKTL';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTKT';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Grupy technologii'@+'=1,1';
_def.NAME:='Grupy kart technologicznych'@;
_def.DESC:='Grupy kart technologicznych'@;

_def.PREFIX:="GKTL.index('GR'); GKTL.prefix()";
_def.TABLE:="GKTL";
_def.FIELDS:="exec('fields','xls_gktl',_a)";

_def.BEFORE:="GKTL.cntx_psh()";
_def.AFTER:="GKTL.cntx_pop()";
_def.SELECT:="exec('select','xls_gktl',_a)";

_def.EXPORT:="exec('export','xls_gktl',_a,_b)";
_def.VALIDATE:="exec('validate','xls_gktl',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_gktl',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('GR','Grupa'@,0,,'Grupa (max. 3 znaków)'@);
_def.define('OPIS','Opis'@,1,,'Opis (max. 50 znaków)'@);

~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
GKTL.prefix();
{? _validate.ACTION='add'
|| GKTL.blank();
   GKTL.GR:=_obj.GR
?};
GKTL.OPIS:=_obj.OPIS;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_result:=1;

_table.GR.VALUE:=GKTL.GR;
_table.OPIS.VALUE:=GKTL.OPIS;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

:: OK
{? _result.RESULT>0
|| GKTL.index('GR');
   GKTL.prefix(_obj.GR,);
   {? GKTL.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_gktl',_obj,_mode,_result);
         {? exec('chk_gktl','tech_common',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_gktl',_obj,_mode,_result);
      {? exec('chk_gktl','tech_common',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=GKTL.add(1)
   |? _validate.ACTION='put'
   || _result:=GKTL.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie grupy kart technologicznych %1 nie powiodło się.'@[$_obj.GR]
   || _validate.MSG:='Poprawa grupy kart technologicznych %1 nie powiodła się.'@[$_obj.GR]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'GR','STRING[10]','Grupa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OPIS','STRING[50]','Opis'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'GR',,);

GKTL.cntx_psh();
GKTL.index('GR');
GKTL.prefix();
{? GKTL.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$GKTL.ref();
      _selected.prefix($GKTL.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.GR:=GKTL.GR;
      _tab.OPIS:=GKTL.OPIS;
      _tab.add();
      GKTL.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'GR,OPIS',20,'Wybór grup kart technologicznych do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
GKTL.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 657cd876cd10e37d150a3ada971c4f1e0f295b7008879b90db3f883ec1ae23ae5faab496e0041a3b081cab40690047d22352873d16545cf936558bb8a99cfb9c5b5941dff794df225bc0530d30ae4c8a93664167dccb7786ce1fc8abda9c2b163469e6152f200a84d7d1678cc6c4f3da9be01c12809927b436f5aef612a6d82b

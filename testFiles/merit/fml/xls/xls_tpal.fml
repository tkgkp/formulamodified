:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tpal.fml
:: Utworzony: 13.07.2018
:: Autor: Mario
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typów palet
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TPAL';
_def.DOMAIN:=exec('name','#b_domain','LMO');
_def.FUNPAR:='ZWS_PAR_LMAG';

_def.FILE:='magazyn_mobilny.xlsx';
_def.SHEET:='Typy palet=1,1';
_def.NAME:='Typy palet'@;
_def.DESC:='Typy palet';

_def.PREFIX:="exec('prefix','xls_tpal')";
_def.TABLE:="exec('table','xls_tpal')";
_def.FIELDS:="exec('fields','xls_tpal',_a)";

_def.BEFORE:="TPAL.cntx_psh()";
_def.AFTER:="TPAL.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_tpal',_a,_b)";
_def.VALIDATE:="exec('validate','xls_tpal',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_tpal',_a,_b,_c)";
_def.SELECT:="exec('select','xls_tpal',_a)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TPAL.index('TYP');
TPAL.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TPAL


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TYP','Typ palety',1,,'20 znakowy');
_def.define('STD','Czy paleta jest standardowa',1,,'Wartości T/N');
_def.define('SZER','Szerokość',1,,'',,3);
_def.define('DLUG','Długość',1,,'',,3);
_def.define('WYS','Wysokość',1,,'',,3);
_def.define('OBJ','Objętość',1,,'',,3);
_def.define('MASA','Ładowność',1,,'w kg',,3);
_def.define('KONT','Czy jest to kontener?',1,,'Wartości T/N');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TPAL.prefix();
{? _validate.ACTION='add'
||
   TPAL.blank();
   TPAL.TYP:=_obj.TYP
?};

TPAL.STD:=_obj.STD;
TPAL.SZER:=_obj.SZER;
TPAL.DLUG:=_obj.DLUG;
TPAL.WYS:=_obj.WYS;
TPAL.OBJ:=_obj.OBJ;
TPAL.MASA:=_obj.MASA;
TPAL.KONT:=_obj.KONT;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.TYP.VALUE:=TPAL.TYP;
_table.STD.VALUE:=TPAL.STD;
_table.SZER.VALUE:=TPAL.SZER;
_table.DLUG.VALUE:=TPAL.DLUG;
_table.WYS.VALUE:=TPAL.WYS;
_table.OBJ.VALUE:=TPAL.OBJ;
_table.MASA.VALUE:=TPAL.MASA;
_table.KONT.VALUE:=TPAL.KONT;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
TPAL.index('TYP');
TPAL.prefix(_obj.TYP,);
{? TPAL.first()
||
   {? _mode=0
   || _result.RESULT:=0
   ||
      _result.ACTION:='put';
      exec('record','xls_tpal',_obj,_mode,_result);
      {? exec('chk_tpal','magdok_wspolne','put')<>''
      ||
         _result.RESULT:=0
      ?}
   ?}
||
   _result.ACTION:='add';
   exec('record','xls_tpal',_obj,_mode,_result);
   {? exec('chk_tpal','magdok_wspolne','add')<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=TPAL.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=TPAL.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie typu palety o kodzie %1 nie powiodło się.'@[_obj.TYP]
   ||
      _validate.MSG:='Poprawa typu palety o kodzie %1 nie powiodło się.'@[_obj.TYP]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'TYP','STRING[20]','Typ palety'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'TYP',,);

TPAL.cntx_psh();
TPAL.index('TYP');
TPAL.prefix();
{? TPAL.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$TPAL.ref();
      _selected.prefix($TPAL.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.TYP:=TPAL.TYP;
      _tab.add();
      TPAL.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'TYP',20,'Wybór typów palet do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TPAL.cntx_pop();

_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:43 08661447cc04b52d02c0717b63b8970ec22954fb2e148f8097c400d6b1a6629790f032f282576516a0867825176114d840aeda300d80e9b91eca9a6d1484065f6adf419fbb1e8ef6d1867eb2fbc83850e76001e053947e00b1175742a3b1da53207a7f7eb87ccd41ce8cbbd34a1762a060ffd101209a8bc4566613e65af98318

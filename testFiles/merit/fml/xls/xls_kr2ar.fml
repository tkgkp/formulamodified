:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku:  xls_kr2ar.fml
:: Utworzony: 13.01.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu powiązań rubryk kalkulacji technologii z rubrykami kalkulacji/analizy zlecenia
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KR2AR';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZLE';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Powiązania rubryk'@+'=1,1';
_def.NAME:='Powiązania rubryk kalkulacji technolgiii z rubrykami kalkulacji/analizy zlecenia'@;
_def.DESC:='Powiązania rubryk kalkulacji technologii z rubrykami kalkulacji/analizy zlecenia'@;

_def.PREFIX:="KR2AR.index('KRUB'); KR2AR.prefix()";
_def.TABLE:="KR2AR";
_def.FIELDS:="exec('fields','xls_kr2ar',_a)";

_def.BEFORE:="KR2AR.cntx_psh()";
_def.AFTER:="KR2AR.cntx_pop()";
_def.SELECT:="exec('select','xls_kr2ar',_a)";

_def.EXPORT:="exec('export','xls_kr2ar',_a,_b)";
_def.VALIDATE:="exec('validate','xls_kr2ar',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kr2ar',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NR_TKW','Rubryka technologii'@,0,,'Wskazanie na numer rubryki kalkulacji karty technologicznej (liczba naturalna)'@,1,0);
::_def.define('OPIS_TKW','Opis'@,0,,'Opis wskazanej rubryki kalkulacji karty technologicznej (max. 50 znaków)'@);
_def.define('NR_ZLE','Rubryka zlecenia'@,0,,'Wskazanie na numer rubryki kalkulacji/analizy zlecenia (liczba naturalna)'@,1,0);
::_def.define('OPIS_ZLE','Opis'@,0,,'Opis wskazanej rubryki kalkulacji/analizy zlecenia (max. 50 znaków)'@);
_def.define('DSP','Czy wyświetlać w analizie porównawczej'@,1,,'[T]/[N]'@);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
KR2AR.prefix();
{? _validate.ACTION='add'
|| KR2AR.blank()
?};
KR2AR.KRUB:=_validate.OBJ.KRUB;
KR2AR.ANRUB:=_validate.OBJ.ANRUB;
KR2AR.DSP:=_obj.DSP;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.NR_TKW.VALUE:=KR2AR.KRUB().NR; _excel.write_async('KRUB',KR2AR.KRUB);
::_table.OPIS_TKW.VALUE:=KR2AR.KRUB().OPIS;
_table.NR_ZLE.VALUE:=KR2AR.ANRUB().NR; _excel.write_async('ANRUB',KR2AR.ANRUB);
::_table.OPIS_ZLE.VALUE:=KR2AR.ANRUB().OPIS;
_table.DSP.VALUE:=KR2AR.DSP;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('KRUB','ANRUB');
_result.OBJ.KRUB:=exec('FindInSet','#table','KRUB','NR',_obj.NR_TKW,,"KRUB.ref()",,,null);
_result.OBJ.ANRUB:=exec('FindInSet','#table','ANRUB','RUBR',_obj.NR_ZLE,,"ANRUB.ref()",,,null);

_int:=int(_obj.NR_TKW);
_int2:=int(_obj.NR_ZLE);
:: Błędy
{? _result.OBJ.KRUB=null()
|| _result.msg_norec('NR_TKW');
   _result.RESULT:=0
?};
{? _result.OBJ.ANRUB=null()
|| _result.msg_norec('NR_ZLE');
   _result.RESULT:=0
?};
{? +_obj.DSP=0 | 'TN'*_obj.DSP=0
|| _result.msg_inset('DSP','T','N');
   _result.RESULT:=0
?};
{? _int<exec('minint','#math') | _int>exec('maxint','#math')
|| _result.msg_range('NR_TKW',exec('minint','#math'),exec('maxint','#math'));
   _result.RESULT:=0
?};
{? _int2<exec('minint','#math') | _int2>exec('maxint','#math')
|| _result.msg_range('NR_ZLE',exec('minint','#math'),exec('maxint','#math'));
   _result.RESULT:=0
?};

:: Ostrzeżenia
{? _int<>_obj.NR_TKW
|| _result.msg_int('NR_TKW')
?};
{? _int2<>_obj.NR_ZLE
|| _result.msg_int('NR_ZLE')
?};

:: OK
{? _result.RESULT>0
|| {? (KR2AR.index('KRUB'); KR2AR.prefix(_obj.NR_TKW); KR2AR.first()) |
      (KR2AR.index('ANRUB'); KR2AR.prefix(_obj.NR_ZLE); KR2AR.first())
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_kr2ar',_obj,_mode,_result);
         {? (_wyn:=exec('chk_kr2ar','zl_kalk',1))<>''
         || {? _wyn='NDX'
            ||
::             Jeżeli naruszenie unikalmości indeksu a wybrano opcję z nadpisywaniem to usuwa blokujące unikalność
::             powiązania i dodaje rekord z Excela
                KR2AR.index('KRUB');
                KR2AR.prefix(_obj.NR_TKW);
                {? KR2AR.first()
                || KR2AR.del()
                ?};
                KR2AR.index('ANRUB');
                KR2AR.prefix(_obj.NR_ZLE);
                {? KR2AR.first()
                || KR2AR.del()
                ?};
                _result.ACTION:='add';
                exec('record','xls_kr2ar',_obj,_mode,_result)
            || _result.RESULT:=0
            ?}
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_kr2ar',_obj,_mode,_result);
      {? exec('chk_kr2ar','zl_kalk',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KR2AR.add(1)
   |? _validate.ACTION='put'
   || _result:=KR2AR.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie powiązania rubryki kalkulacji technologii o numerze %2 z rubryką kalkulacji/analizy zlecenia o numerze %1 nie powiodło się.'@[$_obj.NR_TKW,$_obj.NR_ZLE]
   || _validate.MSG:='Modyfikacja powiązania rubryki kalkulacji technologii o numerze %2 z rubryką kalkulacji/analizy zlecenia o numerze %1 nie powiodła się.'@[$_obj.NR_TKW,$_obj.NR_ZLE]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
    'NR_TKW','INTEGER','Rubryka TKW'
   ,'OPIS_TKW','STRING[50]','Opis'
   ,'NR_ZLE','INTEGER','Rubryka zlecenia'
   ,'OPIS_ZLE','STRING[50]','Opis'
   ,'DSP','STRING[1]','Wyświetlić w analizie porównawczej?'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NR_TKW',,);

KR2AR.cntx_psh();
KR2AR.index('KRUB');
KR2AR.prefix();
{? KR2AR.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$KR2AR.ref();
      _selected.prefix($KR2AR.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NR_TKW:=KR2AR.KRUB().NR;
      _tab.OPIS_TKW:=KR2AR.KRUB().OPIS;
      _tab.NR_ZLE:=KR2AR.ANRUB().NR;
      _tab.OPIS_ZLE:=KR2AR.ANRUB().OPIS;
      _tab.DSP:=KR2AR.DSP;
      _tab.add();
      KR2AR.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'NR_TKW,OPIS_TKW,NR_ZLE,OPIS_ZLE,DSP',20,'Wybór powiązań rubryk kalkulacji technologii z rubrykami kalkulacji/analizy zlecenia do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KR2AR.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2020/04/03 17:14:08 2de3ac4038494d37a524db7de010fd113c8b89020cf42c3c82fb3fc43cd970559d93bfb76d0fe441e48aa8d7f95701ae959e065b80a21d5d15e1e382abb91aa19a87b67bfaeb5370f1df1dfd0ed01b09a9ead960da67769c1257617eedcec9ae457b3592dafc2dec4bc98a1a900ecf391f98f5b87bd9c32e25784ff898b6f46a

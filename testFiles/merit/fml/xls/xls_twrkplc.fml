:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_twrkplc.fml
:: Utworzony: 26.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu stanowisk
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TWRKPLC';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZAS';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Stanowiska produkcyjne'@+'=1,1';
_def.NAME:='Stanowiska produkcyjne'@;
_def.DESC:='Stanowiska produkcyjne'@;

_def.PREFIX:="TWRKPLC.index('KOD'); TWRKPLC.prefix()";
_def.TABLE:="TWRKPLC";
_def.FIELDS:="exec('fields','xls_twrkplc',_a)";

_def.BEFORE:="TWRKPLC.cntx_psh()";
_def.AFTER:="TWRKPLC.cntx_pop()";
_def.SELECT:="exec('select','xls_twrkplc',_a)";

_def.EXPORT:="exec('export','xls_twrkplc',_a,_b)";
_def.VALIDATE:="exec('validate','xls_twrkplc',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_twrkplc',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_define:=$(
   '_a.define(_b,'
   '{? var_pres(\'_d\')=type_of(\'\') || _d || exec(\'fldLabel\',\'#field\',TWRKPLC,_b) ?},_c,,'
   '{? var_pres(\'_e\')=type_of(\'\') || _e || exec(\'fldComment\',\'#field\',TWRKPLC,_b) ?})'
);
_define(_def,'KOD',0,,'Kod (max. 20 znaków)'@);
_define(_def,'NA',1,,'Nazwa (max. 60 znaków)'@);
_define(_def,'KK',1);
_define(_def,'KH',1);
_define(_def,'LGBP',1);
_define(_def,'SKS',1);
_define(_def,'NI',1,,'Numer inwentarzowy (max. 30 znaków)'@);
_define(_def,'WYD',1);
_define(_def,'PL_GRP',1);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TWRKPLC.prefix();
{? _validate.ACTION='add'
|| TWRKPLC.blank();
   TWRKPLC.KOD:=_obj.KOD
?};
TWRKPLC.NA:=_obj.NA;
TWRKPLC.KK:=_validate.OBJ.KK;
TWRKPLC.KH:=_obj.KH;
TWRKPLC.LGBP:=_obj.LGBP;
TWRKPLC.SKS:=_obj.SKS;
TWRKPLC.NI:=_obj.NI;
TWRKPLC.WYD:=_validate.OBJ.WYD;
TWRKPLC.PL_GRP:=_obj.PL_GRP;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=TWRKPLC.KOD;
_table.NA.VALUE:=TWRKPLC.NA;
_table.KK.VALUE:=TWRKPLC.KK().SYM;
_table.KH.VALUE:=TWRKPLC.KH;
_table.LGBP.VALUE:=TWRKPLC.LGBP;
_table.SKS.VALUE:=TWRKPLC.SKS;
_table.NI.VALUE:=TWRKPLC.NI;
_table.WYD.VALUE:=TWRKPLC.WYD().KOD; _excel.write_async('SLO',TWRKPLC.WYD);
_table.PL_GRP.VALUE:=TWRKPLC.PL_GRP;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('KK','WYD');
_result.OBJ.KK:=exec('FindInSet','#table','KK','KONTAKOD',_obj.KK,REF.FIRMA,"KK.ref()",1);
_result.OBJ.WYD:=exec('FindInSet','#table','SLO','SL',_obj.WYD,XINFO.SLWYDZIA,"SLO.ref()",1);

:: Ostrzeżenia
{? +_obj.NA>60
|| _result.msg_length('NA',60)
?};
{? +_obj.KOD>20
|| _result.msg_length('KOD',20)
?};

:: Błędy
{? _obj.KK<>'' & _result.OBJ.KK=null()
|| _result.msg_norec('KK');
   _result.RESULT:=0
?};
{? _obj.WYD<>'' & _result.OBJ.WYD=null()
|| _result.msg_norec('WYD');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| TWRKPLC.index('KOD');
   TWRKPLC.prefix(_obj.KOD,);
   {? TWRKPLC.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_twrkplc',_obj,_mode,_result);
         {? exec('chk_twrkplc','zasoby',1,0)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_twrkplc',_obj,_mode,_result);
      {? exec('chk_twrkplc','zasoby',0,0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TWRKPLC.add(1);
::    Przypisuje kalendarz cząstkowy
      _nazwa:=%255+$TWRKPLC.ref();
      KAL_NAZW.cntx_psh();
      KAL_NAZW.index('KAL_NAZW');
      KAL_NAZW.prefix(exec('ref_firma','ustawienia'));
      {? KAL_NAZW.find_key(_nazwa,_nazwa)
      || TWRKPLC.KAL:=KAL_NAZW.ref();
         TWRKPLC.put()
      || KAL_NAZW.blank();
         KAL_NAZW.NAZWA:=_nazwa;
         KAL_NAZW.CZESC:='T';
         {? KAL_NAZW.add(1)
         || TWRKPLC.KAL:=KAL_NAZW.ref();
            TWRKPLC.put()
         ?}
      ?};
      KAL_NAZW.cntx_pop()
   |? _validate.ACTION='put'
   || _result:=TWRKPLC.put(1)
   ?};
   {? _result>0 & TWRKPLC.KAL=null()
   || R_WZCZ.index('R_WZWND');
      R_WZCZ.prefix(TWRKPLC.name(),TWRKPLC.ref(),'N','N');
      {? ~R_WZCZ.size()
      || exec('dod_kal_czesc','kaledit',TWRKPLC)
      ?}
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie stanowiska o kodzie %1 nie powiodło się.'@[_obj.KOD]
   || _validate.MSG:='Poprawa stanowiska o kodzie %1 nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'KOD','STRING[20]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NA','STRING[60]','Nazwa'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

TWRKPLC.cntx_psh();
TWRKPLC.index('KOD');
TWRKPLC.prefix();
{? TWRKPLC.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$TWRKPLC.ref();
      _selected.prefix($TWRKPLC.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=TWRKPLC.KOD;
      _tab.NA:=TWRKPLC.NA;
      _tab.add();
      TWRKPLC.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'KOD,NA',20,'Wybór stanowisk do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TWRKPLC.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2021/04/09 15:28:03 340a23c7ecee039730946ebe60f2a27e297e364660d668edd1f9064733fbead2e2f6f725ffdfd79439786bb8874aad942617f42c152b7bd2622aee2252950681e2e4ab57eb8f1cd5a414dff412c61a561b0558658fc28a659672b2fa2533d894b6d04e9378da7739e51476287946beb0cf0b5c306f9e22bfebff7853d35993b5

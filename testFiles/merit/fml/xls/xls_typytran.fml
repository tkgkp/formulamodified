:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_urz.fml
:: Utworzony: 03.09.2018
:: Autor: AF
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu urządzeń
::======================================================================================================================


\filename
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Nazwa pliku do eksportu
::----------------------------------------------------------------------------------------------------------------------
'typytransakcji.xlsx'


\TYPYTRAN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TYPYTRAN';
_def.FUNPAR:='ZWS_PAR_SPCBANK';
_def.DOMAIN:=exec('name','#b_domain','ZWS');

_def.FILE:=exec('filename','xls_typytran');
_def.SHEET:='Lista typów transakcji'@+'=1,1';
_def.NAME:='Typ transakcji'@;
_def.DESC:='Typ transakcji'@;

_def.PREFIX:="TYPYTRAN.index('TYP'); TYPYTRAN.prefix()";
_def.TABLE:="TYPYTRAN";
_def.FIELDS:="exec('TYPYTRAN_fld','xls_typytran',_a)";

_def.BEFORE:="TYPYTRAN.cntx_psh()";
_def.AFTER:="TYPYTRAN.cntx_pop()";
_def.SELECT:="exec('TYPYTRAN_select','xls_typytran',_a)";

_def.EXPORT:="exec('TYPYTRAN_exp','xls_typytran',_a,_b)";
_def.VALIDATE:="exec('TYPYTRAN_valid','xls_typytran',_a,_b,_c)";
_def.IMPORT:="exec('TYPYTRAN_imp','xls_typytran',_a,_b,_c)";
~~


\TYPYTRAN_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('RODZSL','Rodzaj słownika',1,,'20 znakowa nazwa słownika np typy transakcji, kody statystyczne');
_def.define('KOD','Kod',1,,'20 znakowy kod pozycji w słowniku');
_def.define('TYPTRAN','Opis',1,,'100 znakowy opis pozycji w słowniku');
_def.define('KONTO','Konto',1,,'255 znakowa formuła konta do dekretacji');
_def.define('ROZR','Rozrachunek',1,,'1 znakowy czy twprzyć rozrachunek T - Tak, N - Nie');
_def.define('BANK','Bank',1,,'złączeniowy wskazanie na bank prowadzący transakcję');
_def.define('NUMER','Numer',1,,'8 znakowy numer rozliczeniowy oddziału banku');
_def.define('RODZ','Rodzaj płatności',1,,'1 znakowy rodzaj płatności Z - Zus, U - US, K - pozostałe');
_def.define('TYP','Typ płatności ZUS lub US',1,,'złączeniowy wskazanie na typ płatności ZUS lub US');
_def.define('R_KRS','Rodzaj kursu',1,,'1 znakowy rodzaj kursu do przeliczenia operacji walutowych');
_def.define('SLU','Słownik użytkownika',1,,'20 znakowa nazwa słownika');
~~


\TYPYTRAN_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_result:=0;

_tab:=tab_tmp(1,'RODZSL','STRING[30]','Rodzaj słownika'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'KOD','STRING[100]','Kod'
   ,'TYPTRAN','STRING[30]','Opis'
   ,'KONTO','STRING[30]','Konto');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'RODZSL',,,'KOD',,,'TYPTRAN',,,'KONTO',,);

TYPYTRAN.cntx_psh();
TYPYTRAN.index('TYP');
TYPYTRAN.prefix();
{? TYPYTRAN.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$TYPYTRAN.ref();
      _selected.prefix($TYPYTRAN.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.RODZSL:=TYPYTRAN.RODZSL;
      _tab.KOD:=TYPYTRAN.KOD;
      _tab.TYPTRAN:=TYPYTRAN.TYPTRAN;
      _tab.KONTO:=TYPYTRAN.KONTO;
      _tab.add();

      TYPYTRAN.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'RODZSL,KOD,TYPTRAN,KONTO',40,'Wybór typów transakcji do eksportu'@,1);
{? _result>0
|| _selected.erase();
   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TYPYTRAN.cntx_pop();
_result


\TYPYTRAN_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.RODZSL.VALUE:=TYPYTRAN.RODZSL;
_table.KOD.VALUE:=TYPYTRAN.KOD;
_table.TYPTRAN.VALUE:=TYPYTRAN.TYPTRAN;
_table.KONTO.VALUE:=TYPYTRAN.KONTO;
_table.ROZR.VALUE:=TYPYTRAN.ROZR;
_table.BANK.VALUE:=TYPYTRAN.BANK().NB;
_table.NUMER.VALUE:=TYPYTRAN.BANK().NUMER;
_table.RODZ.VALUE:=TYPYTRAN.RODZ;

_table.SLU.VALUE:=TYPYTRAN.TYP().SLU().NAZ; _excel.write_async('SLU',TYPYTRAN.TYP().SLU);
_table.TYP.VALUE:=TYPYTRAN.TYP().KOD; _excel.write_async('SLO',TYPYTRAN.TYP);

_table.R_KRS.VALUE:=TYPYTRAN.R_KRS;
_excel.write_async('B',TYPYTRAN.BANK);

_result


\TYPYTRAN_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1;

{? _table.SLU <> ''
|| _slu:=exec('FindInSet','#table','SLU','NAZ',_table.SLU,,,1);
   {? _slu=null
   || _result.MSG:='Nie odnaleziono słownika użytkownika'@;
      _result.RESULT:=0
   ?};
   {? _table.TYP <> ''
   || _slo:=exec('FindInSet','#table','SLO','SL',_table.TYP,_slu,,1);
      {? _slo=null
      || _result.MSG:='Nie odnaleziono pozycji w słowniku użytkownika'@;
         _result.RESULT:=0
      ?}
   ?}
|| _slu:=null;
   _slo:=null
?};

{? (_bank:=exec('FindInSet','#table','B','BANK',_table.NUMER,_table.BANK,,1))=null
|| _result.MSG:='Nie znaleziono banku: %1.'@[_table.BANK];_result.RESULT:=0
|| TYPYTRAN.index('TYP');
   TYPYTRAN.prefix(_table.RODZSL,_bank,_table.TYPTRAN,_table.TYPTRAN,_table.RODZ,_slo);
   {? TYPYTRAN.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || TYPYTRAN.blank() ?};
      TYPYTRAN.RODZSL:=_table.RODZSL;
      TYPYTRAN.KOD:=_table.KOD;
      TYPYTRAN.TYPTRAN:=_table.TYPTRAN;
      TYPYTRAN.KONTO:=_table.KONTO;
      TYPYTRAN.ROZR:=_table.ROZR;
      TYPYTRAN.BANK:=_bank;
      TYPYTRAN.RODZ:=_table.RODZ;
      TYPYTRAN.TYP:=_slo;
      TYPYTRAN.TYP().SLU():=_slu;
      TYPYTRAN.R_KRS:=_table.R_KRS;
      {? _table.RODZSL='IMP_WYC_BAN'
      || {? __CHK.record(TYPYTRAN,,'TYPTRAN','KONTO','ROZR')<>'' || _result.RESULT:=0 ?}
      || {? __CHK.record(TYPYTRAN,,'KOD','TYPTRAN')<>'' || _result.RESULT:=0 ?}
      ?}
   ?}
?};
~~


\TYPYTRAN_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;

{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TYPYTRAN.add(1)
   |? _validate.ACTION='put'
   || _result:=TYPYTRAN.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie typu transakcji o nazwie %1 nie powiodło się.'@[_table.TYPTRAN]
   || _validate.MSG:='Poprawa typu transakcji o nazwie %1 nie powiodło się.'@[_table.TYPTRAN]
   ?}
?};
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 81288151a78119c0940736bdb241ae81b921b82ff8a15f4dd87f424acd654cdde30f28db2bbdf1514fc6b8db95ace78ff0201c0173f7aa32ce0c95b331dfb7bf3f49ca0fe17c65332ac15a904ad5f9a67ceea3ba31cad69940ffff45d8ccaac367f1f46ab760c7a3db5c17768f84808a90770b787b2035d71f2901f78d04c6b6

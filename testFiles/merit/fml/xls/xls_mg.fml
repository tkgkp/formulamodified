:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_mg.fml
:: Utworzony: 25.07.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji magazynów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='MG';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='ZWS_PAR_LMAG';

_def.FILE:='magazyn.xlsx';
_def.SHEET:='Magazyny=1,1';
_def.NAME:='Definicja magazynów'@;
_def.DESC:='Definicja magazynów';

_def.PREFIX:="exec('prefix','xls_mg')";
_def.TABLE:="exec('table','xls_mg')";
_def.FIELDS:="exec('fields','xls_mg',_a)";

_def.BEFORE:="MG.cntx_psh()";
_def.AFTER:="MG.cntx_pop()";
_def.SELECT:="exec('select','xls_mg',_a)";

_def.EXPORT:="exec('export','xls_mg',_a,_b)";
_def.IMPORT:="exec('import','xls_mg',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_mg',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
MG.index('MAGAZYNY');
MG.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
MG


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('SYM','Symbol',0,,'Symbol magazynu: 8 znakowy kod - duże litery.');
_def.define('NAZ','Nazwa',1,,'Nazwa magazynu');
_def.define('TYP','Typ',1,,
'Typ magazynu (sposób wyceny):
(DOSTAWY) - Rzeczywiste ceny dostaw
(DOSTEWI) - Ceny ewidencyjne z obsługą dostaw
(EWIDEN) - Ceny ewidencyjne
(FIFO) - Ceny typu FIFO
(LIFO) - Ceny typu LIFO
(ŚREDNIE) - Ceny średnie bieżące
');
_def.define('ODDZ','Oddział',1,,'1-znakowy kod oddziału'@);
_def.define('IL','Magazyn ilościowy',1,,'Czy magazyn tylko ilościowy (typ DOSTAWY, bez księgowania)? (T/N)'@);
_def.define('DEFSTATS','Domyślny status dostawy',1,,
'Magazyn typu DOSTAWY lub DOSTEWI - Domyślny status dostawy dla pozycji dokumentów przychodowych'@);
_def.define('MGR','Redagowanie grupy materiałowej',1,,
'Czy redagować grupę materiałową podczas edycji pozycji dokumentu? (T/N)'@);
_def.define('U','Usługi dla rozchodu',1,,
'Czy można wprowadzać usługi dla dokumentów rozchodowych? (T/N)'@);
_def.define('KSG','Księgować?',1,,'Księgowanie dokumentów w dziedzinie Finansów? (T/N)'@);
_def.define('MG_OPAK','Magazyn opakowań',1,,'Symbol magazynu dla ewidencji opakowań.'@);
_def.define('SKL','Skład celny?',1,,'Czy magazyn typu skład celny? (T/N)'@);
_def.define('SKL_WAL','Waluta składu celnego',1,,'Domyślna waluta dla składu celnego - 3 znakowy kod.'@);
_def.define('W','Wydziałowy?',1,,'Czy magazyn wydziałowy? (T/N)'@);
_def.define('WYD','Wydział',1,,'Kod wydziału w przypadku magazynu wydziałowego.'@);
_def.define('SL','Obsługa wymiarów',1,,'Magazyn z obsługą wymiarów - lokalizacje, terminy ważności? (T/N)'@);
_def.define('KOD_EANL','Domyślna lokalizacja',1,,'Dla magazynu z obsługą wymiarów 20-znakowy kod domyślnej lokalizacji.'@);
_def.define('SPR_EANL','Kontrola lokalizacji',1,,
'Dla magazynu z obsługą wymiarów - czy kontrolować podanie lokalizacji na pozycjach dokumentów? (T/N)'@);
_def.define('SP_REANL','Automatyczne podpowiadanie lokalizacji dla wydań',1,,
'Sposób automatycznego określenia lokalizacji dla wydań:
(S) - Wg kodu lokalizacji
(A) - Wg stanu rosnąco
(D) - Wg stanu malejąco
(T) - Wg terminu ważności rosnąco
(W) - Wg terminu ważności malejąco
(N) - Brak
'@);
_def.define('PAL','Obsługa palet',1,,'Magazyn z obsługą wymiarów - czy magazyn paletowy? (T/N)'@);
_def.define('MWS','Magazyn wysokiego składowania',1,,'Magazyn z obsługą wymiarów - czy magazyn typu MWS? (T/N)'@);
_def.define('WGMGGR','Czy rozchód wg stref magazynowania',1,,
'Magazyn wysokiego składowania - czy rozchód wg stref magazynowania? (T/N)'@);
_def.define('SPADD','Sposób dołączenia palety',1,,
'Magazyn z obsługą palet - sposób dołaczenia palety:
(N) - Nowa
(T) - Kopia
(A) - Kopia z akceptacją
(J) - Jednopozycyjna
(B) - Jednopozycyjna z akceptacją
'@);
_def.define('TPAL','Domyślny typ palety',1,,'Magazyn z obsługą palet - kod domyślnego typu palety'@);
_def.define('DOPAS','Dopasowywać ilość palet?',1,,
'Magazyn z obsługą palet - Czy ilość palet będzie dopasowywana na podstawie ilości na palecie'@);
_def.define('FPAL','Formuła automatycznego rozpisania palet',1,,
'Magazyn z obsługą palet - kod formuły automatycznego rozpisania palet'@);
_def.define('FROZ','Formuła automatycznego rozpisania rozpakowania',1,,
'Magazyn z obsługą palet - kod formuły automatycznego rozpisania rozpakowania palet'@);
_def.define('SP_IDMOB','Podpowiadanie identyfikacji dostaw',1,,
'Magazyn z obsługą wymiarów - sposób podpowiadania identyfikacji dostaw:
(F) - Wg kolejki FIFO
(L) - Wg kolejki LIFO
'@);
_def.define('KOOP','Magazyn kooperacji',1,,'Czy magazyn kooperacji? (T/N)'@);
_def.define('COS','Magazyn konsygnacyjny',1,,'Obsługa maagzynu typu call-off stock? (T/N)'@);
_def.define('P_ALL','Przyjęcia MWS',1,,'Przyjęcia w MWS z dowolnej lokalizacji (1/0)'@,,0);
_def.define('W_ALL','Wydania MWS',1,,'Wydania w MWS z dowolnej lokalizacji (1/0)'@,,0);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

MG.index('MAGAZYNY');
MG.prefix();
{? _validate.ACTION='add'
||
   MG.blank();
   MG.SYM:=_obj.SYM
?};

MG.NAZ:=_obj.NAZ;
MG.TYP:=_obj.TYP;
MG.ODDZ:=_obj.ODDZ;
MG.IL:=~-_obj.IL;
MG.DEFSTATS:=_validate.OBJ.DEFSTATS;
MG.MGR:=~-_obj.MGR;
MG.U:=~-_obj.U;
MG.KSG:=~-_obj.KSG;
MG.MG_OPAK:=_obj.MG_OPAK;
MG.SKL:=~-_obj.SKL;
MG.SKL_WAL:=_validate.OBJ.SKL_WAL;
MG.W:=~-_obj.W;
MG.WYD:=_validate.OBJ.WYD;
MG.SL:=~-_obj.SL;
MG.KOD_EANL:=_obj.KOD_EANL;
MG.SPR_EANL:=~-_obj.SPR_EANL;
MG.SP_REANL:=~-_obj.SP_REANL;
MG.PAL:=~-_obj.PAL;
MG.MWS:=~-_obj.MWS;
MG.WGMGGR:=~-_obj.WGMGGR;
MG.SPADD:=~-_obj.SPADD;
MG.TPAL:=_validate.OBJ.TPAL;
MG.DOPAS:=~-_obj.DOPAS;
MG.FPAL:=_validate.OBJ.FPAL;
MG.FROZ:=_validate.OBJ.FROZ;
MG.SP_IDMOB:=~-_obj.SP_IDMOB;
MG.KOOP:=~-_obj.KOOP;
MG.COS:=~-_obj.COS;
MG.P_ALL:=_obj.P_ALL;
MG.W_ALL:=_obj.W_ALL;

{? MG.KOD_EANL<>''
|| _eanl:=exec('FindInSet','#table','EANL','KOD',MG.KOD_EANL,,,1);
   {? _eanl<>null() & exec('FindInSet','#table','EANL','KOD',MG.KOD_EANL,,"EANL.MG",1)=MG.ref()
   || MG.EANL:=_eanl;
      MG.KOD_EANL:=''
   ?}
|? MG.EANL<>null()
|| MG.EANL:=null()
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.SYM.VALUE:=MG.SYM;
_table.NAZ.VALUE:=MG.NAZ;
_table.TYP.VALUE:=MG.TYP;
_table.ODDZ.VALUE:=MG.ODDZ; _excel.write_async('ODDZ',exec('FindInSet','#table','ODDZ','KOD',MG.ODDZ,,,1));
_table.IL.VALUE:=MG.IL;
_table.DEFSTATS.VALUE:=MG.DEFSTATS().N; _excel.write_async('STATS',MG.DEFSTATS);
_table.MGR.VALUE:=MG.MGR;
_table.U.VALUE:=MG.U;
_table.KSG.VALUE:=MG.KSG;
_table.MG_OPAK.VALUE:=MG.MG_OPAK;
_table.SKL.VALUE:=MG.SKL;
_table.SKL_WAL.VALUE:=MG.SKL_WAL().SLU().NAZ+'/'+MG.SKL_WAL().KOD; _excel.write_async('SLO',MG.SKL_WAL);
_table.W.VALUE:=MG.W;
_table.WYD.VALUE:=MG.WYD().SLU().NAZ+'/'+MG.WYD().KOD; _excel.write_async('SLO',MG.WYD);
_table.SL.VALUE:=MG.SL;
_table.KOD_EANL.VALUE:=MG.EANL().KOD;
_table.SPR_EANL.VALUE:=MG.SPR_EANL;
_table.SP_REANL.VALUE:=MG.SP_REANL;
_table.PAL.VALUE:=MG.PAL;
_table.MWS.VALUE:=MG.MWS;
_table.WGMGGR.VALUE:=MG.WGMGGR;
_table.SPADD.VALUE:=MG.SPADD;
_table.TPAL.VALUE:=MG.TPAL().TYP;
_table.DOPAS.VALUE:=MG.DOPAS;
_table.FPAL.VALUE:=MG.FPAL().SKROT; _excel.write_async('FORMULA',MG.FPAL);
_table.FROZ.VALUE:=MG.FROZ().SKROT; _excel.write_async('FORMULA',MG.FROZ);
_table.SP_IDMOB.VALUE:=MG.SP_IDMOB;
_table.KOOP.VALUE:=MG.KOOP;
_table.COS.VALUE:=MG.COS;
_table.P_ALL.VALUE:=MG.P_ALL;
_table.W_ALL.VALUE:=MG.W_ALL;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('WYD','SKL_WAL','DEFSTATS','TPAL','FPAL','FROZ');

{? _obj.W='T' & _obj.WYD<>''
|| _wsk:=_obj.WYD*'/';
   {? ~_wsk
   || _result.MSG:='Niepoprawny zapis wydziału %1. Wymaga użycia separatora \"/\".'@[_obj.WYD];
      _result.RESULT:=0
   || _slu:=form((_wsk-1)+_obj.WYD);
      _kod:=form(_wsk-_obj.WYD);
      _slu:={? _slu<>'' || exec('FindInSet','#table','SLU','NAZ',_slu,,,1) || null() ?};
      {? _slu<>null()
      || {? _kod<>''
         || _result.OBJ.WYD:=exec('FindInSet','#table','SLO','SL',_kod,_slu,,1);
            {? _result.OBJ.WYD=null()
            || _result.MSG:='Nie znaleziono wydziału %1.'@[_obj.WYD];
               _result.RESULT:=0
            ?}
         || _result.MSG:='Nie podano kodu wydziału %1.'@[_obj.WYD];
            _result.RESULT:=0
         ?}
      || _result.MSG:='Nie znaleziono wydziału %1.'@[_obj.WYD];
         _result.RESULT:=0
      ?}
   ?}
|| _result.OBJ.WYD:=null()
?};

{? _obj.SKL='T' & _obj.SKL_WAL<>''
|| _wsk:=_obj.SKL_WAL*'/';
   {? ~_wsk
   || _result.MSG:='Niepoprawny zapis waluty dla składu celnego %1. Wymaga użycia separatora \"/\".'@[_obj.SKL_WAL];
      _result.RESULT:=0
   || _slu:=form((_wsk-1)+_obj.SKL_WAL);
      _kod:=form(_wsk-_obj.SKL_WAL);
      _slu:={? _slu<>'' || exec('FindInSet','#table','SLU','NAZ',_slu,,,1) || null() ?};
      {? _slu<>null()
      || {? _kod<>''
         || _result.OBJ.SKL_WAL:=exec('FindInSet','#table','SLO','SL',_kod,_slu,,1);
            {? _result.OBJ.SKL_WAL=null()
            || _result.MSG:='Nie znaleziono waluty dla składu celnego %1.'@[_obj.SKL_WAL];
               _result.RESULT:=0
            ?}
         || _result.MSG:='Nie podano kodu waluty dla składu celnego %1.'@[_obj.SKL_WAL];
            _result.RESULT:=0
         ?}
      || _result.MSG:='Nie znaleziono waluty dla składu celnego %1.'@[_obj.SKL_WAL];
         _result.RESULT:=0
      ?}
   ?}
|| _result.OBJ.SKL_WAL:=null()
?};

{? (1+_obj.TYP)='D' & _obj.DEFSTATS<>''
|| _result.OBJ.DEFSTATS:=exec('FindInSet','#table','STATS','NN',_obj.DEFSTATS,,,1);
   {? _result.OBJ.DEFSTATS=null()
   || _result.MSG:='Nie znaleziono domyślnego statusu dostawy %1.'@[_obj.DEFSTATS];
      _result.RESULT:=0
   ?}
|| _result.OBJ.DEFSTATS:={? (1+_obj.TYP)='D' || exec('STATS_N_2_ref','statexam','~Zgodny') || null() ?}
?};

{? _obj.PAL='T' & _obj.TPAL<>''
|| _result.OBJ.TPAL:=exec('FindInSet','#table','TPAL','TYP',_obj.TPAL,,,1);
   {? _result.OBJ.TPAL=null()
   || _result.MSG:='Nie znaleziono typu palety %1.'@[_obj.TPAL];
      _result.RESULT:=0
   ?}
|| _result.OBJ.TPAL:=null()
?};

{? _obj.PAL='T' & _obj.FPAL<>''
|| _result.OBJ.FPAL:=exec('FindInSet','#table','FORMULA','FORMULA4',_obj.FPAL,'^',,1);
   {? _result.OBJ.FPAL=null()
   || _result.MSG:='Nie znaleziono formuły automatycznego podpowiadania palet %1.'@[_obj.FPAL];
      _result.RESULT:=0
   ?}
|| _result.OBJ.FPAL:=null()
?};

{? _obj.PAL='T' & _obj.FROZ<>''
|| _result.OBJ.FROZ:=exec('FindInSet','#table','FORMULA','FORMULA4',_obj.FROZ,'^',,1);
   {? _result.OBJ.FROZ=null()
   || _result.MSG:='Nie znaleziono formuły autoamtycznego rozpakowania palet %1.'@[_obj.FROZ];
      _result.RESULT:=0
   ?}
|| _result.OBJ.FROZ:=null()
?};

{? _obj.MWS<>'T'
|| _obj.P_ALL:=0;
   _obj.W_ALL:=0
?};

{? _result.RESULT=1
||
   MG.index('MAGAZYNY');
   MG.prefix(_obj.SYM,);
   {? MG.first()
   || {? _mode=0
      || _result.RESULT:=0
      || POM.POP_MG:='T'+MG.MWS+MG.PAL;
         _result.ACTION:='put';
         exec('record','xls_mg',_obj,_mode,_result);
         {? exec('chk_mg','magdok_wspolne')<>'' || _result.RESULT:=0 ?}
      ?}
   || POM.POP_MG:='NXX';
      _result.ACTION:='add';
      exec('record','xls_mg',_obj,_mode,_result);
      {? exec('chk_mg','magdok_wspolne')<>'' || _result.RESULT:=0 ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=MG.add(1)
   |? _validate.ACTION='put'
   || _result:=MG.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:='Dodanie definicji magazynu %1 nie powiodło się.'@[MG.SYM]
|? _result & MG.SL='T' & MG.EANL=null() & MG.KOD_EANL<>''
|| EANL.cntx_psh();
   EANL.prefix();
   EANL.blank();
   EANL.MG:=MG.ref();
   EANL.DOK:='T';
   EANL.KOD:=MG.KOD_EANL;
   _ref:={? EANL.add(1) || EANL.ref() || null() ?};
   EANL.cntx_pop();
   {? _ref<>null() || MG.EANL:=_ref; MG.put(1) ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'SYM','STRING[20]','Symbol'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[60]','Nazwa'
   ,'ODDZ','STRING[1]','Nazwa'
   ,'TYP','STRING[8]','Typ'
   ,'MG_OPAK','STRING[8]','Magazyn opakowań'
   ,'SL','STRING[1]','Obsługa wymiarów'
   ,'PAL','STRING[1]','Paletowy'
   ,'MWS','STRING[1]','Typu MWS'
   ,'SKL','STRING[1]','Skład celny'
   ,'W','STRING[1]','Wydziałowy'
   ,'KOOP','STRING[20]','Kooperacyjny');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'SYM',,);

MG.cntx_psh();
MG.index('MAGAZYNY');
MG.prefix();
{? MG.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$MG.ref();
      _selected.prefix($MG.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.SYM:=MG.SYM;
      _tab.NAZ:=MG.NAZ;
      _tab.ODDZ:=MG.ODDZ;
      _tab.TYP:=MG.TYP;
      _tab.MG_OPAK:=MG.MG_OPAK;
      _tab.SL:=MG.SL;
      _tab.PAL:=MG.PAL;
      _tab.MWS:=MG.MWS;
      _tab.SKL:=MG.SKL;
      _tab.W:=MG.W;
      _tab.KOOP:=MG.KOOP;
      _tab.add();
      MG.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'SYM,NAZ,ODDZ,TYP,MG_OPAK,SL,PAL,MWS,SKL,W,KOOP',10,'Wybór definicji magazynów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
MG.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 6e1fd37d7639f01f78715a92d372abe0ebdac7915b6b89ef3924c42a7c206818ff324f51f61f396e7a3ae4eb5989a13754065c0b8aad47cd83b3d1b468c10a2e6c6e1f4853853c625eba5e390f03936fec8c0950539fd7fba3f7d7e4f1c79d166284e075efb8271c076a9da6f592c82db18c7d1c25faa5f413ef8872a170b685

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_rem_ztp.fml
:: Utworzony: 31.01.2019
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi typów zasobów remontowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='REM_ZTP';
_def.DOMAIN:=exec('name','#b_domain','TRE');
_def.FUNPAR:='ZWS_PAR_TRTZ';

_def.ADD_ROWS:=0;
_def.FILE:='remonty.xlsx';
_def.SHEET:='Typy zasobów'@+'=1,1';
_def.NAME:='Typy zasobów'@;
_def.DESC:='Typy zasobów remontowych'@;

_def.PREFIX:="REM_ZTP.index('TYP'); REM_ZTP.prefix()";
_def.TABLE:="REM_ZTP";
_def.FIELDS:="exec('fields','xls_rem_ztp',_a)";

_def.BEFORE:="{? _a.is_import() || exec('init_ztp','tre') ?}; REM_ZTP.cntx_psh()";
_def.AFTER:="REM_ZTP.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_rem_ztp',_a,_b)";
_def.VALIDATE:="exec('validate','xls_rem_ztp',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rem_ztp',_a,_b,_c)";
_def.SELECT:="exec('select','xls_rem_ztp',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TYP','Typ'@,0);
_def.define('ACTIVE','Aktywny'@,1);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
REM_ZTP.prefix();
REM_ZTP.ACTIVE:=_obj.ACTIVE;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.TYP.VALUE:=REM_ZTP.TYP;
_table.ACTIVE.VALUE:=REM_ZTP.ACTIVE;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

:: Błędy
{? 'TN'*_obj.ACTIVE=0
|| _result.msg_inset('ACTIVE','T','N');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| REM_ZTP.index('TYP');
   REM_ZTP.prefix(_obj.TYP,);
   {? REM_ZTP.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         {? _obj.ACTIVE='N' & REM_ZTP.count()>0
         || _result.MSG+='Dezaktywacja nie jest możliwa — typ zasobu remontowego ''%1'' jest już użyty w systemie.'@
                         [REM_ZTP.TYP];
            _result.RESULT:=0
         || exec('record','xls_rem_ztp',_obj,_mode,_result)
         ?}
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='put'
   || _result:=REM_ZTP.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:='Poprawa typu zasobu remontowego ''%1'' nie powiodła się.'@[_obj.TYP]
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'TYP','STRING[25]','Typ'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'ACTIVE','STRING[1]','Aktywny'
);

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'TYP',,);

REM_ZTP.cntx_psh();
REM_ZTP.index('TYP');
REM_ZTP.prefix();
{? REM_ZTP.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$REM_ZTP.ref();
      _selected.prefix($REM_ZTP.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.TYP:=REM_ZTP.TYP;
      _tab.ACTIVE:=REM_ZTP.ACTIVE;
      _tab.add();
      REM_ZTP.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'TYP[25],ACTIVE[TN]',,'Wybór typów zasobów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
REM_ZTP.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/10/14 09:22:01 93a5c68b62b1a81e19b10ee93d937fe21248a7e7779279fce2400b139d6f39473ab88a187073dd5b1a7e3b071ca0b93aec39c090bbd8a190a02a5bd7c4a508cc4afc23e30a5fca943665d3b432facb9a5331301d75e496d6258b7439ff189a8038a32d90f60b2e03c40bf278ccb277ba4c289b840545cd41425debf9fd432bfb

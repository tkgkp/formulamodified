:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_formula.fml
:: Utworzony: 12.07.2018
:: Autor: Mario
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu formuł systemu
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.MULTIFIR:='T';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Formuły uniwersalne=1,1';
_def.NAME:='Formuły uniwersalne'@;
_def.DESC:='Formuły uniwersalne';

_def.PREFIX:="exec('prefix','xls_formula')";
_def.TABLE:="FORMULA";
_def.FIELDS:="exec('fields','xls_formula',_a)";

_def.BEFORE:="FORMULA.cntx_psh()";
_def.AFTER:="FORMULA.cntx_pop(); FORMULA.f_clear()";
_def.SELECT:="exec('select','xls_formula',_a)";

_def.EXPORT:="exec('export','xls_formula',_a,_b)";
_def.VALIDATE:="exec('validate','xls_formula',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_formula',_a,_b,_c)";
~~


\init_pd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA_pd';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PLAN_DST';
_def.MULTIFIR:='T';

_def.FILE:='plan_dostaw.xlsx';
_def.SHEET:='Formuły uniwersalne=1,1';
_def.NAME:='Formuły uniwersalne'@;
_def.DESC:='Formuły uniwersalne';

_def.PREFIX:="exec('prefix_pd','xls_formula')";
_def.TABLE:="FORMULA";
_def.FIELDS:="exec('fields','xls_formula',_a)";

_def.BEFORE:="FORMULA.cntx_psh()";
_def.AFTER:="FORMULA.cntx_pop(); FORMULA.f_clear()";
_def.SELECT:="exec('select','xls_formula',_a)";

_def.EXPORT:="exec('export','xls_formula',_a,_b)";
_def.VALIDATE:="exec('validate','xls_formula',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_formula',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
FORMULA.index('FORMULA4');
FORMULA.prefix();
FORMULA.f_clear();
FORMULA.f_set(,,'RODZAJ NOT IN (\'a\', \'r\', \'v\', \'2\', \'3\')');
~~


\prefix_pd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
FORMULA.index('FORMULA4');
FORMULA.prefix();
FORMULA.f_clear();
:FORMULA.f_set(,'join PD_D using(FORMULA.REFERENCE,PD_D.FORMULA)');
FORMULA.f_set(,,'RODZAJ BETWEEN \'2\' and \'3\'');
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('RODZAJ','Rodzaj',0,,'Pole 1 znakowe');
_def.define('SKROT','Skrót',0,,'');
_def.define('NAZWA','Nazwa',0,,'');
_def.define('FORMULA','Treść formuły',0,,'');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
FORMULA.prefix();
{? _validate.ACTION='add'
||
   FORMULA.blank();
   FORMULA.RODZAJ:=_obj.RODZAJ;
   FORMULA.SKROT:=_obj.SKROT
?};
FORMULA.NAZWA:=_obj.NAZWA;
FORMULA.FORMULA:=_obj.FORMULA;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.RODZAJ.VALUE:=FORMULA.RODZAJ;
_table.SKROT.VALUE:=FORMULA.SKROT;
_table.NAZWA.VALUE:=FORMULA.NAZWA;
_table.FORMULA.VALUE:=FORMULA.FORMULA;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? (+form(_obj.RODZAJ))<>1 || _result.msg_range('RODZAJ','długości 1 znaku','długości 1 znaku'); _result.RESULT:=0 ?};
{? (+form(_obj.SKROT))=0 || _result.msg_empty('SKROT'); _result.RESULT:=0 ?};
{? (+form(_obj.NAZWA))=0 || _result.msg_empty('NAZWA'); _result.RESULT:=0 ?};

{? _result.RESULT=1
|| FORMULA.index('FORMULA4');
   FORMULA.prefix(_obj.RODZAJ,_obj.SKROT,);
   {? FORMULA.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_formula',_obj,_mode,_result)
      ?}
   || _result.ACTION:='add';
      exec('record','xls_formula',_obj,_mode,_result)
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=FORMULA.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=FORMULA.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie formuły o rodzaju %1 i skrócie %2 nie powiodło się.'@[_obj.RODZAJ, _obj.SKROT]
   ||
      _validate.MSG:='Modyfikacja formuły o rodzaju %1 i skrócie %2 nie powiodło się.'@[_obj.RODZAJ, _obj.SKROT]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'RODZAJ','STRING[1]','Rodzaj'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'SKROT','STRING[8]','Skrót'
   ,'NAZWA','STRING[127]','Nazwa'
   ,'FORMULA','STRING[255]','Treść formuły');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'RODZAJ',,,'SKROT',,);

FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix();
{? FORMULA.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$FORMULA.ref();
      _selected.prefix($FORMULA.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.RODZAJ:=FORMULA.RODZAJ;
      _tab.SKROT:=FORMULA.SKROT;
      _tab.NAZWA:=FORMULA.NAZWA;
      _tab.FORMULA:=FORMULA.FORMULA;
      _tab.add();
      FORMULA.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'RODZAJ,SKROT,NAZWA,FORMULA',20,'Wybór formuł uniwersalnych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
FORMULA.cntx_pop();
_result


\init_sp_zk_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - formuły dla typów dokumentów i zamówień
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.PREFIX:="exec('prefix_sp_zk_mg','xls_formula')";
_def.TABLE:="FORMULA";
_def.FIELDS:="exec('fields','xls_formula',_a)";

_def.BEFORE:="FORMULA.cntx_psh()";
_def.AFTER:="FORMULA.cntx_pop(); FORMULA.f_clear()";
_def.SELECT:="exec('select','xls_formula',_a)";

_def.EXPORT:="exec('export','xls_formula',_a,_b)";
_def.VALIDATE:="exec('validate','xls_formula',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_formula',_a,_b,_c)";
~~


\init_sp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - formuły dla typów dokumentów sprzedaży
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA_sp';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.SHEET:='Formuły uniwersalne dok. spr.=1,1';
_def.NAME:='Formuły uniwersalne (dokumenty sprzedaży)'@;
_def.DESC:='Formuły uniwersalne (dokumenty sprzedaży)';
_def.FUNPAR:='ZWS_PAR_LTDS';
_def.MULTIFIR:='T';

_def.FILE:='sprzedaz.xlsx';

exec('init_sp_zk_mg','xls_formula',_def);
~~


\init_zk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - formuły dla typów dokumentów zakupu
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA_zk';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.SHEET:='Formuły uniwersalne dok. zak.=1,1';
_def.NAME:='Formuły uniwersalne (dokumenty zakupu)'@;
_def.DESC:='Formuły uniwersalne (dokumenty zakupu)';
_def.FUNPAR:='ZWS_PAR_LTDZ';
_def.MULTIFIR:='T';

_def.FILE:='zakupy.xlsx';

exec('init_sp_zk_mg','xls_formula',_def);
~~


\init_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - formuły dla typów dokumentów magazynowych
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA_mg';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.SHEET:='Formuły uniwersalne dok. mag.=1,1';
_def.NAME:='Formuły uniwersalne (dokumenty magazynowe)'@;
_def.DESC:='Formuły uniwersalne (dokumenty magazynowe)';
_def.FUNPAR:='ZWS_PAR_LTDM';
_def.MULTIFIR:='T';

_def.FILE:='magazyn.xlsx';

exec('init_sp_zk_mg','xls_formula',_def);
~~


\prefix_sp_zk_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Określa zakres eksportowanych danych - formuły dla typów dokumentów i zamówień
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
FORMULA.index('FORMULA4');
FORMULA.prefix();
FORMULA.f_clear();
FORMULA.f_set(,,'RODZAJ IN (\'a\', \'r\', \'v\')');
~~


\init_sp_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - formuły dla typów zamówień sprzedaży
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA_sp_zam';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.SHEET:='Formuły uniwersalne zam. spr.=1,1';
_def.NAME:='Formuły uniwersalne (zamówienia sprzedaży)'@;
_def.DESC:='Formuły uniwersalne (zamówienia sprzedaży)';
_def.FUNPAR:='ZWS_PAR_LTZK';
_def.MULTIFIR:='T';

_def.FILE:='sprzedaz.xlsx';

exec('init_sp_zk_mg','xls_formula',_def);
~~


\init_zk_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - formuły dla typów zamówień dostaw
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA_zk_zam';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.SHEET:='Formuły uniwersalne zam. dos.=1,1';
_def.NAME:='Formuły uniwersalne (zamówienia dostaw)'@;
_def.DESC:='Formuły uniwersalne (zamówienia dostaw)';
_def.FUNPAR:='ZWS_PAR_LTZD';
_def.MULTIFIR:='T';

_def.FILE:='zakupy.xlsx';

exec('init_sp_zk_mg','xls_formula',_def);
~~


\init_mg_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - formuły dla typów zamówień wewnętrznych
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA_mg_zam';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.SHEET:='Formuły uniwersalne zam. wew.=1,1';
_def.NAME:='Formuły uniwersalne (zamówienia wewnętrzne)'@;
_def.DESC:='Formuły uniwersalne (zamówienia wewnętrzne)';
_def.FUNPAR:='ZWS_PAR_LTZW';
_def.MULTIFIR:='T';

_def.FILE:='magazyn.xlsx';

exec('init_sp_zk_mg','xls_formula',_def);
~~


\init_sp_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - formuły dla typów ofert sprzedaży
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FORMULA_sp_ofe';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.SHEET:='Formuły uniwersalne ofe. spr.=1,1';
_def.NAME:='Formuły uniwersalne (oferty sprzedaży)'@;
_def.DESC:='Formuły uniwersalne (oferty sprzedaży)';
_def.FUNPAR:='ZWS_PAR_LTOF';
_def.MULTIFIR:='T';

_def.FILE:='sprzedaz.xlsx';

exec('init_sp_zk_mg','xls_formula',_def);
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 2583b37a5ac842eed03dcd21a61634be072fe362a23e0f270c569a8580a9181b0b6e3bb3c127bec59deace747f3ee5bfa4bae6c33db309fe23affc8483ce5a73963242a7ea4ef508a50ef883a9bc5ba90f9b82c156dc954b6698fd496944466b5380bd4d88557d86c4807a51267c502668502c380643aed6e46b17916b2a3213

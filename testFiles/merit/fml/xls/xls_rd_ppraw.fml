:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_rd_ppraw.fml
:: Utworzony: 22.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu słownika podstaw prawnych do urlopu rodzicielskiego
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='RD_PPRAW';
_def.FUNPAR:='ZWS_PAR_PPRA';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Podstawy prawne do świadectwa=1,1';
_def.NAME:='Podstawy prawne do świadectwa pracy'@;
_def.DESC:='Podstawy prawne do świadectwa pracy';

: źródło danych
_def.TABLE:="RD_PPRAW";

: prolog
_def.BEFORE:="
   RD_PPRAW.cntx_psh();
   RD_PPRAW.f_clear();
   RD_PPRAW.index('P_PRAWNA');
   RD_PPRAW.prefix();
   ~~
";

: epilog
_def.AFTER:="
   RD_PPRAW.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('LP',      MS.name(RD_PPRAW,'LP'),      1,,MS.comment(RD_PPRAW,'LP'));
   _env.define('ARTYKUL', MS.name(RD_PPRAW,'ARTYKUL'), 1,,MS.comment(RD_PPRAW,'ARTYKUL'));
   _env.define('ART_NDX', MS.name(RD_PPRAW,'ART_NDX'), 1,,MS.comment(RD_PPRAW,'ART_NDX'));
   _env.define('PARAGRAF',MS.name(RD_PPRAW,'PARAGRAF'),1,,MS.comment(RD_PPRAW,'PARAGRAF'));
   _env.define('PUNKT',   MS.name(RD_PPRAW,'PUNKT'),   1,,MS.comment(RD_PPRAW,'PUNKT'));
   _env.define('TRESC',   MS.name(RD_PPRAW,'TRESC'),   1,,MS.comment(RD_PPRAW,'TRESC'));
   _env.define('OPIS',    MS.name(RD_PPRAW,'OPIS'),    1,,MS.comment(RD_PPRAW,'OPIS'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.LP.VALUE:=RD_PPRAW.LP;
   _buf.ARTYKUL.VALUE:=RD_PPRAW.ARTYKUL;
   _buf.ART_NDX.VALUE:=RD_PPRAW.ART_NDX;
   _buf.PARAGRAF.VALUE:=RD_PPRAW.PARAGRAF;
   _buf.PUNKT.VALUE:=RD_PPRAW.PUNKT;
   _buf.TRESC.VALUE:=RD_PPRAW.TRESC;
   _buf.OPIS.VALUE:=RD_PPRAW.memo_txt(,1,'OPIS');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_rd_ppraw',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rd_ppraw',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

_ref:=null;
{? RD_PPRAW.find_tab(,
      'ARTYKUL',,'=',_buf.ARTYKUL,
      'ART_NDX',,'=',_buf.ART_NDX,
      'PARAGRAF',,'=',_buf.PARAGRAF,
      'PUNKT',,'=',_buf.PUNKT
   )
|| _ref:=RD_PPRAW.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('RD_PPRAW','to_str');
_obj.to_str:="'%1, %2, %3, %4'[_a.ARTYKUL,_a.ART_NDX,_a.PARAGRAF,_a.PUNKT]";
_obj.RD_PPRAW:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.LP:=_b.LP;
   _a.ARTYKUL:=_b.ARTYKUL;
   _a.ART_NDX:=_b.ART_NDX;
   _a.PARAGRAF:=_b.PARAGRAF;
   _a.PUNKT:=_b.PUNKT;
   _a.TRESC:=_b.TRESC;
   RD_PPRAW.memo_set(_b.OPIS,'OPIS');
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? RD_PPRAW.seek(_obj.RD_PPRAW)
   || _set(RD_PPRAW,_buf,_obj);
      {? ~RD_PPRAW.put(1) | ~RD_PPRAW.memo_put(,'OPIS')
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,RD_PPRAW.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| RD_PPRAW.blank();
   _set(RD_PPRAW,_buf,_obj);
   {? ~RD_PPRAW.add(1) | ~RD_PPRAW.memo_put(,'OPIS')
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:49 90a71abe61519e0d1df20c573e8c0a87d9abe5a0dcd967498971da1674965f58229ed729e64faae21f279dba287c7a30c988f1eda7a97a0c96418e3dce2d0dede982f469287513d20902993f0dcd6c39c171a0ebec964b66d166f5ae40dfcaf4d5a33b32204819a9da06fa66193e7520292088bb9b387f6b8fd2ee794cc3d351

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_anrub.fml
:: Utworzony: 20.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu rubryk kalkulacji/analizy zleceń
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ANRUB';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZLE';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Rubryki zleceń'@+'=1,1';
_def.NAME:='Rubryki kalkulacji/analizy zleceń'@;
_def.DESC:='Rubryki do wyliczania kalkulacji/analizy zleceń'@;

_def.PREFIX:="ANRUB.index('RUBR'); ANRUB.prefix()";
_def.TABLE:="ANRUB";
_def.FIELDS:="exec('fields','xls_anrub',_a)";

_def.BEFORE:="ANRUB.cntx_psh()";
_def.AFTER:="ANRUB.cntx_pop()";
_def.SELECT:="exec('select','xls_anrub',_a)";

_def.EXPORT:="exec('export','xls_anrub',_a,_b)";
_def.VALIDATE:="exec('validate','xls_anrub',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_anrub',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NR','Numer'@,0,,'Numer rubryki (liczba naturalna)'@,,0);
_def.define('OPIS','Opis'@,1,,'Opis (max. 50 znaków)'@);
_def.define('PRINTED','Na wydruku'@,1,,'[T]/[N]'@);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
ANRUB.prefix();
{? _validate.ACTION='add'
|| ANRUB.blank();
   ANRUB.NR:=_obj.NR
?};
ANRUB.OPIS:=_obj.OPIS;
ANRUB.PRINTED:=_obj.PRINTED;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.NR.VALUE:=ANRUB.NR;
_table.OPIS.VALUE:=ANRUB.OPIS;
_table.PRINTED.VALUE:=ANRUB.PRINTED;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_int:=int(_obj.NR);

:: Błędy
{? +_obj.PRINTED=0 | 'TN'*_obj.PRINTED=0
|| _result.msg_inset('PRINTED','T','N');
   _result.RESULT:=0
?};
{? _int<exec('minint','#math') | _int>exec('maxint','#math')
|| _result.msg_range('NR',exec('minint','#math'),exec('maxint','#math'));
   _result.RESULT:=0
?};

:: Ostrzeżenia
{? _int<>_obj.NR
|| _result.msg_int('NR')
?};

:: OK
{? _result.RESULT>0
|| ANRUB.index('RUBR');
   ANRUB.prefix(_obj.NR);
   {? ANRUB.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_anrub',_obj,_mode,_result);
         {? exec('chk_anrub','zl_kalk',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_anrub',_obj,_mode,_result);
      {? exec('chk_anrub','zl_kalk',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=ANRUB.add(1)
   |? _validate.ACTION='put'
   || _result:=ANRUB.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie rubryki kalkulacji/analizy zlecenia o numerze %1 nie powiodło się.'@[$_obj.NR]
   || _validate.MSG:='Poprawa rubryki kalkulacji/analizy zlecenia o numerze %1 nie powiodła się.'@[$_obj.NR]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'NR','INTEGER','Numer'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OPIS','STRING[50]','Opis'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NR',,);

ANRUB.cntx_psh();
ANRUB.index('RUBR');
ANRUB.prefix();
{? ANRUB.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$ANRUB.ref();
      _selected.prefix($ANRUB.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NR:=ANRUB.NR;
      _tab.OPIS:=ANRUB.OPIS;
      _tab.add();
      ANRUB.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'NR,OPIS',20,'Wybór rubryk kalkulacji/analizy do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
ANRUB.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2020/04/03 17:14:07 cf16d572033d736403d1f2d437794893f37b1f291b83ea0f02970684079f72219a5b317d36df54f65b1e9e754182facec1c875d17a23f98ecebce883221e0b7f743452a3887193487311be7bfe50fb037c16fa63bf5d3511142b0d6c705a9f4b31d2ebfd95ff36b5f4e52487e40751c8a670dba4268a18836ae2da235cff7307

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ttg.fml
:: Utworzony: 12.08.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu grup technologicznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TTG';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='TKTL';
_def.HIDDEN:='T';
_def.SKIP:='T';
_def.ADD_ROWS:=0;

_def.FILE:='produkcja_technologie.xlsx';
_def.SHEET:='Grupy technologiczne'@+'=1,1';
_def.NAME:='Grupy technologiczne'@;
_def.DESC:='Grupy technologiczne'@;

_def.PREFIX:="TTG.index('GR'); TTG.prefix()";
_def.TABLE:="TTG";
_def.FIELDS:="exec('fields','xls_ttg',_a)";

_def.BEFORE:="TTG.cntx_psh()";
_def.AFTER:="TTG.cntx_pop()";

_def.EXPORT:="exec('export','xls_ttg',_a,_b)";
_def.VALIDATE:="exec('validate','xls_ttg',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ttg',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_edit:=0;
_def.define('GR','Grupa'@,_edit);
_def.define('OPIS','Opis'@,_edit);
_def.define('ZAMIENN','Zamienniki z grupy'@,_edit,,
            'Czy elementy grupy mają być podpowiadane jako zamienniki? - [T]/[N]');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TTG.prefix();
{? _validate.ACTION='add'
|| TTG.blank();
   TTG.GR:=_obj.GR
?};
TTG.OPIS:=_obj.OPIS;
TTG.ZAMIENN:=_obj.ZAMIENN;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.GR.VALUE:=TTG.GR;
_table.OPIS.VALUE:=TTG.OPIS;
_table.ZAMIENN.VALUE:=TTG.ZAMIENN;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

:: Błędy
{? +_obj.ZAMIENN=0 | 'TN'*_obj.ZAMIENN=0
|| _result.msg_inset('ZAMIENN','T','N');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| TTG.index('GR');
   TTG.prefix(_obj.GR,);
   {? TTG.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_ttg',_obj,_mode,_result);
         {? exec('ttg_arec','tech_mater')<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_ttg',_obj,_mode,_result);
      {? exec('ttg_arec','tech_mater')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TTG.add(1)
   |? _validate.ACTION='put'
   || _result:=TTG.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie grupy technologicznej \'%1\' nie powiodło się.'@[_obj.GR]
   || _validate.MSG:='Poprawa grupy technologicznej \'%1\' nie powiodła się.'@[_obj.GR]
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2020/10/16 15:26:06 808cd186266f4873f271b82882e40e462fe32c47caec64878517ff5cede7201a8971f7340a75317b8b5e3e6f0c5ec0ec845e2f48ee1d53bd7136c187005ec32302e1cde975fa901f040305d1f112c9143efe899f81b39f5ee3c95a989ed66ba34ef576b57fe6b1e952959bb14c90042c2cfa8372c34a61b1de9884989e5daae7

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_translat.fml
:: Utworzony: 11.07.2018
:: Autor: Mario
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu tłumaczeń
::======================================================================================================================


\init_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_excel.ID:='TRANSLAT_SPR';
_excel.DOMAIN:=exec('name','#b_domain','ZWS');
_excel.FUNPAR:='ZWS_PAR_TRANS';

_excel.FILE:='sprzedaz.xlsx';
_excel.SHEET:='Tłumacz. wydruków dok. spr.=1,1';
_excel.NAME:='Tłumaczenia wydruków dokumentów sprzedaży i korekty'@;
_excel.DESC:='Tłumaczenia wydruków dokumentów sprzedaży i korekty';
_excel.TABLE:="exec('table','xls_translat','S')";
_excel.TAB_IMP:="TRANSLAT";
_excel.FIELDS:="exec('fields','xls_translat',_a)";
_excel.EXPORT:="exec('export','xls_translat',_a,_b)";
_excel.BEFORE:="exec('before','xls_translat',_a,'S')";
_excel.VALIDATE:="exec('validate','xls_translat',_a,_b,_c)";
_excel.IMPORT:="exec('import','xls_translat',_a,_b,_c)";
~~


\init_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_excel.ID:='TRANSLAT_MAG';
_excel.DOMAIN:=exec('name','#b_domain','ZWS');
_excel.FUNPAR:='ZWS_PAR_TRANS';

_excel.FILE:='magazyn.xlsx';
_excel.SHEET:='Tłumacz. wydruków magazyn.=1,1';
_excel.NAME:='Tłumaczenia wydruków dokumentów magazynowych'@;
_excel.DESC:='Tłumaczenia wydruków dokumentów magazynowych';
_excel.TABLE:="exec('table','xls_translat','M')";
_excel.TAB_IMP:="TRANSLAT";
_excel.FIELDS:="exec('fields','xls_translat',_a)";
_excel.EXPORT:="exec('export','xls_translat',_a,_b)";
_excel.BEFORE:="exec('before','xls_translat',_a,'M')";
_excel.VALIDATE:="exec('validate','xls_translat',_a,_b,_c)";
_excel.IMPORT:="exec('import','xls_translat',_a,_b,_c)";
~~


\init_spd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_excel.ID:='TRANSLAT_SPD';
_excel.DOMAIN:=exec('name','#b_domain','ZWS');
_excel.FUNPAR:='ZWS_PAR_TRANS';

_excel.FILE:='sprawozdania_finansowe.xlsx';
_excel.SHEET:='Tłumaczenia sprawozdań=1,1';
_excel.NAME:='Tłumaczenia wydruków sprawozdań definiowalnych'@;
_excel.DESC:='Tłumaczenia wydruków sprawozdań definiowalnych';
_excel.TABLE:="exec('table','xls_translat','D')";
_excel.TAB_IMP:="TRANSLAT";
_excel.FIELDS:="exec('fields','xls_translat',_a)";
_excel.EXPORT:="exec('export','xls_translat',_a,_b)";
_excel.BEFORE:="exec('before','xls_translat',_a,'D')";
_excel.VALIDATE:="exec('validate','xls_translat',_a,_b,_c)";
_excel.IMPORT:="exec('import','xls_translat',_a,_b,_c)";
~~


\init_jm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TRANSLAT_JM';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_KDJM';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Tłumaczenia j.miary=1,1';
_def.NAME:='Tłumaczenia dla jednostek miary'@;
_def.DESC:='Tłumaczenia dla jednostek miary';

_def.PREFIX:="exec('prefixT','xls_translat','J')";
_def.TABLE:="exec('tableT','xls_translat','J')";
_def.TAB_IMP:="TRANSLAT";
_def.FIELDS:="exec('fieldsT','xls_translat',_a,'J')";

_def.BEFORE:="exec('czytaj','#stalesys',,XINFO,'SLJEZYK');TRANSLAT.cntx_psh()";
_def.AFTER:="TRANSLAT.cntx_pop(); TRANSLAT.f_clear()";
_def.SELECT:="exec('selectT','xls_translat',_a,'J')";

_def.EXPORT:="exec('exportT','xls_translat',_a,_b,'J')";
_def.IMPORT:="exec('importT','xls_translat',_a,_b,_c,'J')";
_def.VALIDATE:="exec('validateT','xls_translat',_a,_b,_c,'J')";
~~


\init_tsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TRANSLAT_TSP';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.FUNPAR:='ZWS_PAR_LTDS';

_def.FILE:='sprzedaz.xlsx';
_def.SHEET:='Tłumaczenia t.dok.sprz.=1,1';
_def.NAME:='Tłumaczenia dla typów dokumentów sprzedaży'@;
_def.DESC:='Tłumaczenia dla typów dokumentów sprzedaży';

_def.PREFIX:="exec('prefixT','xls_translat','S')";
_def.TABLE:="exec('tableT','xls_translat','S')";
_def.TAB_IMP:="TRANSLAT";
_def.FIELDS:="exec('fieldsT','xls_translat',_a,'S')";

_def.BEFORE:="exec('czytaj','#stalesys',,XINFO,'SLJEZYK');TRANSLAT.cntx_psh()";
_def.AFTER:="TRANSLAT.cntx_pop(); TRANSLAT.f_clear()";
_def.SELECT:="exec('selectT','xls_translat',_a,'S')";

_def.EXPORT:="exec('exportT','xls_translat',_a,_b,'S')";
_def.IMPORT:="exec('importT','xls_translat',_a,_b,_c,'S')";
_def.VALIDATE:="exec('validateT','xls_translat',_a,_b,_c,'S')";
~~


\init_tzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TRANSLAT_TZK';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PAR_LTDZ';

_def.FILE:='zakupy.xlsx';
_def.SHEET:='Tłumaczenia t.dok.zakupu=1,1';
_def.NAME:='Tłumaczenia dla typów dokumentów zakupu'@;
_def.DESC:='Tłumaczenia dla typów dokumentów zakupu';

_def.PREFIX:="exec('prefixT','xls_translat','Z')";
_def.TABLE:="exec('tableT','xls_translat','Z')";
_def.FIELDS:="exec('fieldsT','xls_translat',_a,'Z')";

_def.BEFORE:="exec('czytaj','#stalesys',,XINFO,'SLJEZYK');TRANSLAT.cntx_psh()";
_def.AFTER:="TRANSLAT.cntx_pop(); TRANSLAT.f_clear()";
_def.SELECT:="exec('selectT','xls_translat',_a,'Z')";

_def.EXPORT:="exec('exportT','xls_translat',_a,_b,'Z')";
_def.IMPORT:="exec('importT','xls_translat',_a,_b,_c,'Z')";
_def.VALIDATE:="exec('validateT','xls_translat',_a,_b,_c,'Z')";
~~


\init_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TRANSLAT_SLO';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_KSLO';

_def.FILE:='slowniki_uzytkownika.xlsx';
_def.SHEET:='Tłumaczenia poz.słowników=1,1';
_def.NAME:='Tłumaczenia dla pozycji słowników'@;
_def.DESC:='Tłumaczenia dla pozycji słowników';

_def.PREFIX:="exec('prefixT','xls_translat','D')";
_def.TABLE:="exec('tableT','xls_translat','D')";
_def.TAB_IMP:="TRANSLAT";
_def.FIELDS:="exec('fieldsT','xls_translat',_a,'D')";

_def.BEFORE:="exec('czytaj','#stalesys',,XINFO,'SLJEZYK');TRANSLAT.cntx_psh()";
_def.AFTER:="TRANSLAT.cntx_pop(); TRANSLAT.f_clear()";
_def.SELECT:="exec('selectT','xls_translat',_a,'D')";

_def.EXPORT:="exec('exportT','xls_translat',_a,_b,'D')";
_def.IMPORT:="exec('importT','xls_translat',_a,_b,_c,'D')";
_def.VALIDATE:="exec('validateT','xls_translat',_a,_b,_c,'D')";
~~


\init_wdr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_excel.ID:='TRANSLAT_WDR';
_excel.DOMAIN:=exec('name','#b_domain','ZWS');
_excel.FUNPAR:='ZWS_PAR_TRANS';

_excel.FILE:='wdrozeniowe.xlsx';
_excel.SHEET:='Tłumaczenia wdrożeniowe=1,1';
_excel.NAME:='Tłumaczenia wdrożeniowe'@;
_excel.DESC:='Tłumaczenia wdrożeniowe';
_excel.TABLE:="exec('table','xls_translat','W')";
_excel.TAB_IMP:="TRANSLAT";
_excel.FIELDS:="exec('fields','xls_translat',_a)";
_excel.EXPORT:="exec('export','xls_translat',_a,_b)";
_excel.BEFORE:="exec('before','xls_translat',_a,'W')";
_excel.VALIDATE:="exec('validate','xls_translat',_a,_b,_c)";
_excel.IMPORT:="exec('import','xls_translat',_a,_b,_c)";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WE: _a - rodzaj tłumaczenia
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_rodz:=_a;
_kod_tlum:=exec('kod_tlumacz','xls_translat',_rodz);
_table:=sql('
     select TRANS_PL.KOD as KOD, TRANS_PL.ORG as NAZ, SLO.KOD as JEZYK, TRANSLAT.T as TLUMACZ, (\':\' || TRANSLAT.IDADD || TRANSLAT.REFERENCE) as UIDREF
     from translat left join trans_pl join trans_gr join SLO using(TRANSLAT.JEZYK, SLO.reference)
     where trans_gr.kod=\':_a\'
     order by KOD ',_kod_tlum);
{? _rodz='S' & ~_table.size()
|| obj_del(_table);
   _kod_tlum+='y';
   _table:=sql('
        select TRANS_PL.KOD as KOD, TRANS_PL.ORG as NAZ, SLO.KOD as JEZYK, TRANSLAT.T as TLUMACZ, (\':\' || TRANSLAT.IDADD || TRANSLAT.REFERENCE) as UIDREF
        from translat left join trans_pl join trans_gr join SLO using(TRANSLAT.JEZYK, SLO.reference)
        where trans_gr.kod=\':_a\'
        order by KOD ',_kod_tlum)
?};
_table


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::   WY: 0 - porażka, wczytywanie definicji imex się nie powiodło
::       1 - sukces
::       UWAGA - metoda _def.add() może się nie powieść, dlatego należy jej wyniki
::               przekazać na wyjście
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod',0,,'Systemowe oznaczenie tłumaczenia');
_def.define('NAZ','Oryginalna Nazwa',0,,'Nazwa używana gdy nie zdefiniowano tłumaczenia');
_def.define('JEZYK','Język',0,,'Kod języka');
_def.define('TLUMACZ','Tłumaczenie w danym języku',1,,'');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_obj:=_b;
_table:=_excel.table();

_result:=1;

_obj.KOD.VALUE:=_table.KOD;
_obj.NAZ.VALUE:=_table.NAZ;
_obj.JEZYK.VALUE:=_table.JEZYK;
'tu docelowo obsluga SLO dla słownika jezykow';
_obj.TLUMACZ.VALUE:=_table.TLUMACZ;
_result


\before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wykonuje sie przed rozpoczęciem importu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - rodzaj tłumaczenia
::   WY: 1/0
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_result:=1;
_rodz:=_b;
exec('czytaj','#stalesys',,XINFO,'SLJEZYK');
_trans_gr:=exec('FindInSet','#table','TRANS_GR','KOD',_rodz,,,1);
{? _trans_gr=null & _rodz='S' || exec('init','tlumaczenia','01')
|? _trans_gr=null & _rodz='M' || exec('init','tlumaczenia','03')
|? _trans_gr=null & _rodz='D' || exec('init','tlumaczenia','02')
|? _trans_gr=null & _rodz='W' || exec('init','tlumaczenia','04')
?};
exec('czytaj','#stalesys',,XINFO,'SLJEZYK');
{? XINFO.SLJEZYK<>null
||
::   _result.MSG:='Nie zdefiniowano "słownika języków do tłumaczeń" w parametrach pracy systemu.';
   _result:=0
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('TRANS_PL','JEZYK');

_result.OBJ.TRANS_PL:=exec('FindInSet','#table','TRANS_PL','KOD',_obj.KOD,,,1);
{? _result.OBJ.TRANS_PL=null
|| _result.MSG:='Brak kodu %1 w słowniku tekstów tłumaczeń.'@[_obj.KOD];
   _result.RESULT:=0
?};

_result.OBJ.JEZYK:=exec('FindInSet','#table','SLO','SL',_obj.JEZYK,XINFO.SLJEZYK,,1);
{? _result.OBJ.JEZYK=null
|| _result.MSG:='Brak języka %1 w słowniku języków dla tłumaczeń.'@[_obj.JEZYK];
   _result.RESULT:=0
?};

TRANSLAT.index('GR');
{? _mode=0
||
   TRANSLAT.prefix(_result.OBJ.TRANS_PL,_obj.JEZYK);
   {? TRANSLAT.first()
   || _result.RESULT:=0;
      _result.MSG:='Istnieje już tłumaczenie %1 dla języka  %2 o kodzie %3.'@[_obj.TLUMACZ,_obj.JEZYK,_obj.KOD]
   || _result.ACTION:='add'
   ?}
||
   'pozwalamy zawsze poprawiać JM';
   TRANSLAT.prefix(_result.OBJ.TRANS_PL,_obj.JEZYK);
   {? TRANSLAT.first()
   || _result.ACTION:='put'
   || _result.ACTION:='add'
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

TRANSLAT.index('GR');
{? _validate.ACTION='add'
||
   TRANSLAT.blank()
?};
TRANSLAT.TRANS_PL:=_validate.OBJ.TRANS_PL;
TRANSLAT.JEZYK:=_validate.OBJ.JEZYK;
TRANSLAT.T:=_obj.TLUMACZ;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=TRANSLAT.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=TRANSLAT.put(1)
   ?}
?};
{? _result=0
||
   _validate.MSG:='Dodanie tłumaczenia %1 dla języka  %2 o kodzie %3 nie powiodło się.'@[_obj.TLUMACZ,_obj.JEZYK,_obj.KOD]
?};
_result


\kod_tlumacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: zwraca kod tłumaczenia
::   WE: _a - S/M/D sprzedaż, magazyn, spr definowalne
::   WY: kod
::----------------------------------------------------------------------------------------------------------------------
_result:='';
{? _a='S'
||
   _result:='Wydruki dokumentów sprzedaży i korekt'
|? _a='M'
||
   _result:='Wydruki dokumentów magazynowych'
|? _a='D'
||
   _result:='Wydruki sprawozdań'
|? _a='W'
||
   _result:='Wdrożeniowe'
?};
_result


\prefixT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WE: _a - 'J'-jednostka miary, 'S'-typ dokumentu sprzedaży, 'Z'-typ dokumentu zakupu, 'D'-słownik
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TRANSLAT.prefix();
TRANSLAT.f_clear();
{? _a='J' || TRANSLAT.f_set('T,JM','join JM','TRANS_PL is null')
|? _a='D' || TRANSLAT.f_set('T,SLO','join SLO using(TRANSLAT.SLO,SLO.REFERENCE)','TRANS_PL is null')
|? _a='S' || TRANSLAT.f_set('T,TYPYSP','join TYPYSP','TRANS_PL is null and TYPYSP.ZAK=\'N\'')
|? _a='Z' || TRANSLAT.f_set('T,TYPYSP','join TYPYSP','TRANS_PL is null and TYPYSP.ZAK=\'T\'')
?};
~~


\tableT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TRANSLAT


\fieldsT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::       _b - 'J'-jednostka miary, 'S'-typ dokumentu sprzedaży, 'Z'-typ dokumentu zakupu, 'D'-słownik
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

{? _b='J'
|| _def.define('JM','Jednostka miary',0,,'Kod jednostki miary - 10 znaków');
   _def.define('JEZYK','Język',0,,'Kod języka');
   _def.define('T','Tłumaczenie w danym języku',1,,'')
|? _b='D'
|| _def.define('SLU','Słownik',0,,'Nazwa słownika - 10 znaków');
   _def.define('SLO','Wartość ze słownika',0,,'Kod wartości słownika - 10 znaków');
   _def.define('JEZYK','Język',0,,'Kod języka');
   _def.define('T','Tłumaczenie w danym języku',1,,'')
|| _def.define('TYPYSP','Typ dokumentu',0,,'Kod typu dokumentu - 8 znaków');
   _def.define('JEZYK','Język',0,,'Kod języka');
   _def.define('T','Tłumaczenie w danym języku',1,,'')
?};
~~


\recordT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - 'J'-jednostka miary, 'S'-typ dokumentu sprzedaży, 'Z'-typ dokumentu zakupu, 'D'-słownik
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

{? _d='J'
|| TRANSLAT.index('JM');
   TRANSLAT.prefix();
   {? _validate.ACTION='add'
   ||
      TRANSLAT.blank();
      TRANSLAT.JM:=_validate.OBJ.JM
   ?}
|? _d='D'
|| TRANSLAT.index('SLO');
   TRANSLAT.prefix();
   {? _validate.ACTION='add'
   ||
      TRANSLAT.blank();
      TRANSLAT.SLO:=_validate.OBJ.SLO
   ?}
|| TRANSLAT.index('TYPYSP');
   TRANSLAT.prefix();
   {? _validate.ACTION='add'
   ||
      TRANSLAT.blank();
      TRANSLAT.TYPYSP:=_validate.OBJ.TYPYSP
   ?}
?};

TRANSLAT.JEZYK:=_validate.OBJ.JEZYK;
TRANSLAT.T:=_obj.T;
~~


\exportT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::       _c - 'J'-jednostka miary, 'S'-typ dokumentu sprzedaży, 'Z'-typ dokumentu zakupu, 'D'-słownik
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
{? _c='J' & TRANSLAT.JM<>null()
|| _table.JM.VALUE:=TRANSLAT.JM().KOD; _excel.write_async('JM',TRANSLAT.JM);
   _table.JEZYK.VALUE:=TRANSLAT.JEZYK().KOD; _excel.write_async('SLO',TRANSLAT.JEZYK);
   _table.T.VALUE:=TRANSLAT.T
|? _c='D' & TRANSLAT.SLO<>null()
|| _table.SLU.VALUE:=TRANSLAT.SLO().SLU().NAZ; _excel.write_async('SLU',TRANSLAT.SLO().SLU);
   _table.SLO.VALUE:=TRANSLAT.SLO().KOD; _excel.write_async('SLO',TRANSLAT.SLO);
   _table.JEZYK.VALUE:=TRANSLAT.JEZYK().KOD; _excel.write_async('SLO',TRANSLAT.JEZYK);
   _table.T.VALUE:=TRANSLAT.T
|? _c='S' & TRANSLAT.TYPYSP<>null() & TRANSLAT.TYPYSP().ZAK='N'
|| _table.TYPYSP.VALUE:=TRANSLAT.TYPYSP().T; _excel.write_async('TYPYSP',TRANSLAT.TYPYSP);
   _table.JEZYK.VALUE:=TRANSLAT.JEZYK().KOD; _excel.write_async('SLO',TRANSLAT.JEZYK);
   _table.T.VALUE:=TRANSLAT.T
|? _c='Z' & TRANSLAT.TYPYSP<>null() & TRANSLAT.TYPYSP().ZAK='T'
|| _table.TYPYSP.VALUE:=TRANSLAT.TYPYSP().T; _excel.write_async('TYPYZAK',TRANSLAT.TYPYSP);
   _table.JEZYK.VALUE:=TRANSLAT.JEZYK().KOD; _excel.write_async('SLO',TRANSLAT.JEZYK);
   _table.T.VALUE:=TRANSLAT.T
|| _result:=0
?};
_result


\validateT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::       _d - 'J'-jednostka miary, 'S'-typ dokumentu sprzedaży, 'Z'-typ dokumentu zakupu, 'D'-słownik
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? _d='J'
|| _result.OBJ:=obj_new('JM','JEZYK');

   _result.OBJ.JM:={? _obj.JM='' || null() || exec('FindInSet','#table','JM','KOD',_obj.JM,,,1) ?};
   {? _result.OBJ.JM=null()
   || _result.MSG:='Nie znaleziono jednostki miary %1.'@[_obj.JM];
      _result.RESULT:=0
   ?}
|? _d='D'
|| _result.OBJ:=obj_new('SLO','JEZYK');

   _slu:={? _obj.SLU='' || null() || exec('FindInSet','#table','SLU','NAZ',_obj.SLU,,,1) ?};
   {? _slu=null()
   || _result.MSG:={? _obj.SLU=''
                   || 'Nie podano nazwy słownika'@
                   || 'Nie znaleziono słownika %1.'@[_obj.SLU]
                   ?};
      _result.RESULT:=0
   || _result.OBJ.SLO:={? _obj.SLO='' || null() || exec('FindInSet','#table','SLO','SL',_obj.SLO,_slu,,1) ?};
      {? _result.OBJ.SLO=null()
      || _result.MSG:={? _obj.SLO=''
                      || 'Nie podano elementu słownika'@
                      || 'Nie znaleziono elementu słownika o kodzie %1.'@[_obj.SLO]
                      ?};
         _result.RESULT:=0
      ?}
   ?}
|? (';SZ'*_d)>1
|| _result.OBJ:=obj_new('TYPYSP','JEZYK');

   _result.OBJ.TYPYSP:={? _obj.TYPYSP='' || null() || exec('FindInSet','#table','TYPYSP','TYPYKOD',_obj.TYPYSP,,,1) ?};
   {? _result.OBJ.TYPYSP=null()
   || _result.MSG:='Nie znaleziono typu dokumentu %1.'@[_obj.TYPYSP];
      _result.RESULT:=0
   |? _result.OBJ.TYPYSP<>null() & exec('FindAndGet','#table',TYPYSP,_result.OBJ.TYPYSP,,"ZAK",'')<>{? _d='S' || 'N' || 'T' ?}
   || _result.MSG:='Podany typ dokumentu %1 nie jest dokumentem %2.'@[_obj.TYPYSP,{? _d='S' || 'sprzedaży'@ || 'zakupu'@ ?}];
      _result.RESULT:=0
   ?}
?};

{? XINFO.SLJEZYK=null()
|| exec('czytaj','#stalesys',,XINFO,'SLJEZYK');
   {? XINFO.SLJEZYK=null()
   || _result.MSG:='Brak zdefiniowanego słownika języków.';
      _result.RESULT:=0
   ?}
?};

{? (+_obj.JEZYK)<>3
|| _result.msg_range('JEZYK','długości 3 znaków','długości 3 znaków');
   _result.OBJ.JEZYK:=null();
   _result.RESULT:=0
|| _res:=0;
   _result.OBJ.JEZYK:={? XINFO.SLJEZYK<>null() & +(_obj.JEZYK)>0
                      || _res:=1;
                         exec('FindInSet','#table','SLO','SL',_obj.JEZYK,XINFO.SLJEZYK,,1)
                      || null()
                      ?};
   {? _result.OBJ.JEZYK=null()
   || _result.MSG:='Nie znaleziono podanego języka %1.'@[_obj.JEZYK];
      _result.RESULT:=0
   ?}
?};
{? (+_obj.T)=0
|| _result.MSG:='Nie podano treści tłumaczenia %1.'@[_obj.JM];
   _result.RESULT:=0
?};

{? _result.RESULT=1
||
   {? _d='J'
   || TRANSLAT.index('JMT');
      TRANSLAT.prefix(_result.OBJ.JM,_result.OBJ.JEZYK,_obj.T,)
   |? _d='D'
   || TRANSLAT.index('SLOT');
      TRANSLAT.prefix(_result.OBJ.SLO,_result.OBJ.JEZYK,_obj.T,)
   |? (';SZ'*_d)>1
   || TRANSLAT.index('TYPYSPT');
      TRANSLAT.prefix(_result.OBJ.TYPYSP,_result.OBJ.JEZYK,_obj.T,)
   ?};
   {? TRANSLAT.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('recordT','xls_translat',_obj,_mode,_result,_d)
      ?}
   || _result.ACTION:='add';
      exec('recordT','xls_translat',_obj,_mode,_result,_d)
   ?}
?};
~~


\importT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - 'J'-jednostka miary, 'S'-typ dokumentu sprzedaży, 'Z'-typ dokumentu zakupu, 'D'-słownik
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TRANSLAT.add(1)
   |? _validate.ACTION='put'
   || _result:=TRANSLAT.put(1)
   ?}
?};
{? _result=0
|| {? _d='J'
   || _validate.MSG:={? _validate.ACTION='add'
                     || 'Dodanie tłumaczenia jednostek miar %1 nie powiodło się.'@[TRANSLAT.JM().KOD]
                     || 'Modyfikacja tłumaczenia jednostek miar %1 nie powiodła się.'@[TRANSLAT.JM().KOD]
                     ?}
   |? _d='S'
   || _validate.MSG:={? _validate.ACTION='add'
                     || 'Dodanie tłumaczenia wartości ze słownika %1 nie powiodło się.'@[TRANSLAT.SLO().KOD]
                     || 'Modyfikacja tłumaczenia wartości ze słownika %1 nie powiodła się.'@[TRANSLAT.SLO().KOD]
                     ?}
   |? (';SZ'*_d)>1
   || _validate.MSG:={? _validate.ACTION='add'
                     || 'Dodanie tłumaczenia typu dokumentu %1 nie powiodło się.'@[TRANSLAT.TYPYSP().T]
                     || 'Modyfikacja tłumaczenia typu dokumentu %1 nie powiodła się.'@[TRANSLAT.TYPYSP().T]
                     ?}
   ?}
?};
_result


\selectT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - 'J'-jednostka miary, 'S'-typ dokumentu sprzedaży, 'Z'-typ dokumentu zakupu, 'D'-słownik
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

{? _b='J'
|| _tab:=tab_tmp(1,'KOD','STRING[10]','Jednostka miary'
     ,'REF','STRING[16]','SQL ref'
     ,'SELECTED','STRING[1]','Czy wybrano'
     ,'JEZYK','STRING[10]','Język'
     ,'T','STRING[60]','Tłumaczenie')
|? _b='D'
|| _tab:=tab_tmp(1,'KOD','STRING[10]','Wartość ze słownika'
     ,'REF','STRING[16]','SQL ref'
     ,'SELECTED','STRING[1]','Czy wybrano'
     ,'SLU','STRING[20]','Słownik'
     ,'JEZYK','STRING[10]','Język'
     ,'T','STRING[60]','Tłumaczenie')
|? (';SZ'*_b)>1
|| _tab:=tab_tmp(1,'KOD','STRING[10]','Typ dokumentu'
     ,'REF','STRING[16]','SQL ref'
     ,'SELECTED','STRING[1]','Czy wybrano'
     ,'JEZYK','STRING[10]','Język'
     ,'T','STRING[60]','Tłumaczenie')
?};

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

JM.cntx_psh();
TYPYSP.cntx_psh();
SLO.cntx_psh();
TRANSLAT.cntx_psh();
TRANSLAT.index('GR');
TRANSLAT.prefix(null());
{? TRANSLAT.first()
|| {!
   |?
      {? (_b='J' & TRANSLAT.JM<>null())
       | (_b='D' & TRANSLAT.SLO<>null())
       | (_b='S' & TRANSLAT.TYPYSP<>null() & TRANSLAT.TYPYSP().ZAK='N')
       | (_b='Z' & TRANSLAT.TYPYSP<>null() & TRANSLAT.TYPYSP().ZAK='T')
      || _tab.blank();
         _tab.REF:=$TRANSLAT.ref();
         _selected.prefix($TRANSLAT.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.KOD:={? _b='J' || TRANSLAT.JM().KOD
                   |? _b='D' || TRANSLAT.SLO().KOD
                   || TRANSLAT.TYPYSP().T
                   ?};
         {? _b='D' || _tab.SLU:=TRANSLAT.SLO().SLU().NAZ ?};
         _tab.JEZYK:=TRANSLAT.JEZYK().KOD;
         _tab.T:=TRANSLAT.T;
         _tab.add()
      ?};
      TRANSLAT.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,{? _b='D' || 'SLU,KOD,JEZYK,T' || 'KOD,JEZYK,T' ?},20
 ,'Wybór tłumaczeń %1 do eksportu'@[{? _b='J' || 'jednostek miary'@
                                    |? _b='D' || 'wartości ze słowników'@
                                    || 'typów dokumentu'@ ?}],1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
JM.cntx_pop();
TYPYSP.cntx_pop();
SLO.cntx_pop();
TRANSLAT.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2023/10/18 14:04:24 6b2c87c92592f7bcce77eda8b6e1eb2c8401f677fcf5aae4db6bd6c7c8903550189decaf61decde515efe2f6e9f52c57b05303664d85ea0cc4b088f0caa914ae090ad8f865bf180358e004851fcbae4cd0b4748a527fe6aaed22a16bf373be14c1937f6477e4ca9ea394a8d0ef8106d6623230b6416308c9d0d8fd351a29e0f4

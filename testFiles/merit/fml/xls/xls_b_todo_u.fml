:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_b_todo_u.fml
:: Utworzony: 17.03.2022
:: Autor: MW
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu personalizacji zadań użytkowników
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='B_TODO_U';
_def.DOMAIN:=exec('name','#b_domain','ZPR');
_def.FUNPAR:='';
_def.MULTIFIR:='T';

_def.FILE:='procesy.xlsx';
_def.SHEET:='Personalizacja zadań użytkow.'@+'=1,1';
_def.NAME:='Personalizacja zadań użytkowników'@;
_def.DESC:='Personalizacja zadań użytkowników'@;

_def.PREFIX:="exec('prefix','xls_b_todo_u')";
_def.TABLE:="exec('table','xls_b_todo_u')";
_def.FIELDS:="exec('fields','xls_b_todo_u',_a)";

_def.BEFORE:="B_TODO_U.cntx_psh()";
_def.AFTER:="B_TODO_U.cntx_pop()";
_def.SELECT:="exec('select','xls_b_todo_u',_a)";

_def.EXPORT:="exec('export','xls_b_todo_u',_a,_b)";
_def.VALIDATE:="exec('validate','xls_b_todo_u',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_b_todo_u',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
B_TODO_U.index('UNIK');
B_TODO_U.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
B_TODO_U


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;


_tooltip:='Użytkownik (max. %1 znaków). '@[$MS.fld_len(USERS,'KOD')];
_tooltip+='Pole wymagane'@;
_def.define('USER','Użytkownik'@,1,,_tooltip);

_tooltip:='UID elementu (max. %1 znaków). '@[$MS.fld_len(B_PREL,'UID')];
_tooltip+='Pole wymagane'@;
_def.define('BPRELUID','UID elementu',1,,_tooltip);

_tooltip:='Symbol elementu (max. %1 znaków).'@[$MS.fld_len(B_PREL,'SYMBOL')];
_def.define('BPRELSYM','Symbol elementu',1,,_tooltip);

_tooltip:='Symbol procesu (max. %1 znaków). '@[$MS.fld_len(B_PROC,'SYMBOL')];
_tooltip+='Pole wymagane'@;
_def.define('BPROCSYM','Symbol procesu',1,,_tooltip);

_tooltip:='Wersja procesu (max. %1 znaków). '@[$MS.fld_len(B_PROC,'VER')];
_tooltip+='Pole wymagane'@;
_def.define('BPROCVER','Wersja procesu',1,,_tooltip);

_tooltip:=MS.name(B_TODO_U,'VISIBLE')+' (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('VISIBLE','Zadanie widoczne',1,,_tooltip);

_tooltip:=MS.name(B_TODO_U,'NOTIFY_1')+' (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('NOTIFY_1','Powiadomienie e-mail',1,,_tooltip);

_tooltip:=MS.name(B_TODO_U,'NOTIFY_2')+' (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('NOTIFY_2','Powiadomienie SMS',1,,_tooltip);

_tooltip:=MS.name(B_TODO_U,'NOTIFY_3')+' (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('NOTIFY_3','Powiadomienie systemowe',1,,_tooltip);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_bprel_manual:=exec('bprel_manual','xls_b_todo_u',_validate.OBJ.B_PREL);
_bprel_visible:=exec('FindAndGet','#table','B_PREL',_validate.OBJ.B_PREL,,"B_PREL.VISIBLE",'T');
B_TODO_U.prefix();
{? _validate.ACTION='add'
|| B_TODO_U.blank()
?};
B_TODO_U.USER:=_validate.OBJ.USER;
B_TODO_U.B_PREL:=_validate.OBJ.B_PREL;
{? _bprel_manual
|| {? _validate.ACTION='add'
   || B_TODO_U.VISIBLE:=_bprel_visible
   ?}
|| B_TODO_U.VISIBLE:=_obj.VISIBLE
?};
B_TODO_U.NOTIFY_1:=_obj.NOTIFY_1;
B_TODO_U.NOTIFY_2:=_obj.NOTIFY_2;
B_TODO_U.NOTIFY_3:=_obj.NOTIFY_3;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=0;

{?B_TODO_U.B_PREL().B_PROC().FIRMA=REF.FIRMA
|| _user:=B_TODO_U.USER().KOD;
   _bprel_uid:=B_TODO_U.B_PREL().UID;
   _bprel_sym:=B_TODO_U.B_PREL().SYMBOL;
   _bproc_sym:=B_TODO_U.B_PREL().B_PROC().SYMBOL;
   _bproc_ver:=B_TODO_U.B_PREL().B_PROC().VER;
   {? _user<>'' & _bprel_uid<>'' & _bproc_sym<>'' & _bproc_ver<>''
   || _result:=1;
      _table.USER.VALUE:=_user;
      _table.BPRELUID.VALUE:=_bprel_uid;
      _table.BPRELSYM.VALUE:=_bprel_sym;
      _table.BPROCSYM.VALUE:=_bproc_sym;
      _table.BPROCVER.VALUE:=_bproc_ver;
      _table.VISIBLE.VALUE:=B_TODO_U.VISIBLE;
      _table.NOTIFY_1.VALUE:=B_TODO_U.NOTIFY_1;
      _table.NOTIFY_2.VALUE:=B_TODO_U.NOTIFY_2;
      _table.NOTIFY_3.VALUE:=B_TODO_U.NOTIFY_3
   ?}
?};

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.OBJ:=obj_new('USER','B_PREL');

_can_continue:=1;
:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _obj.USER=''
|| _can_continue:=0;
   _result.msg_empty('USER')
?};
{? _obj.BPRELUID=''
|| _can_continue:=0;
   _result.msg_empty('BPRELUID')
?};
{? _obj.BPROCSYM=''
|| _can_continue:=0;
   _result.msg_empty('BPROCSYM')
?};
{? _obj.BPROCVER=''
|| _can_continue:=0;
   _result.msg_empty('BPROCSYM')
?};

_user:=null();
{? _can_continue
|| _user:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1);
   {? _user=null()
   || _can_continue:=0;
      _result.msg_norec('USER')
   || _result.OBJ.USER:=_user
   ?}
?};

_bprel_manual:=0;
B_PROC.cntx_psh();
B_PROC.index('SYMVER');
B_PROC.prefix(REF.FIRMA,_obj.BPROCSYM,_obj.BPROCVER);
{? B_PROC.first()
|| B_PREL.cntx_psh();
   B_PREL.index('UID');
   B_PREL.prefix(B_PROC.ref(),_obj.BPRELUID);
   {? B_PREL.first()
   || _result.OBJ.B_PREL:=B_PREL.ref();
      _bprel_manual:=exec('bprel_manual','xls_b_todo_u',B_PREL.ref());
      {? B_PREL.PERSONAL<>'T'
      || _can_continue:=0;
         _result.MSG:='Personalizacja czynności %1 w procesie %2, %3 jest niedostępna.'@
                      [_obj.BPRELSYM,_obj.BPROCSYM,_obj.BPROCVER]
      ?}
   || _can_continue:=0;
      _result.MSG:='Nie odnaleziono czynności %1 (identyfikator %2) w procesie %3, %4.'@
                   [_obj.BPRELSYM,_obj.BPRELUID,_obj.BPROCSYM,_obj.BPROCVER]
   ?};
   B_PREL.cntx_pop()
|| _result.MSG:='Nie odnaleziono procesu %1, %2.'@[_obj.BPROCSYM,_obj.BPROCVER];
   _can_continue:=0
?};
B_PROC.cntx_pop();

{? _can_continue

|| {? ~_bprel_manual & _obj.VISIBLE<>'T' & _obj.VISIBLE<>'N'
   || _can_continue:=0;
      _result.msg_inset('VISIBLE','T','N')
   ?};

   {? _obj.NOTIFY_1<>'T' & _obj.NOTIFY_1<>'N'
   || _can_continue:=0;
      _result.msg_inset('NOTIFY_1','T','N')
   ?};

   {? _obj.NOTIFY_2<>'T' & _obj.NOTIFY_2<>'N'
   || _can_continue:=0;
      _result.msg_inset('NOTIFY_2','T','N')
   ?};

   {? _obj.NOTIFY_3<>'T' & _obj.NOTIFY_3<>'N'
   || _can_continue:=0;
      _result.msg_inset('NOTIFY_3','T','N')
   ?}
?};

_result.RESULT:=_can_continue;

{? _result.RESULT
|| B_TODO_U.index('UNIK');
   B_TODO_U.prefix(_result.OBJ.B_PREL,_result.OBJ.USER);
   {? B_TODO_U.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_b_todo_u',_obj,_mode,_result)
      ?}
   || _result.ACTION:='add';
      exec('record','xls_b_todo_u',_obj,_mode,_result)
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=B_TODO_U.add(1)
   |? _validate.ACTION='put'
   || _result:=B_TODO_U.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   ||  _validate.MSG:='Dodanie personalizacji listy zadań nie powiodło się'@
   ||  _validate.MSG:='Poprawa personalizacji listy zadań nie powiodła się.'@
   ?}
|| exec('bi_todo_set_visible','xls_b_todo_u')
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'USER','STRING[10]','Użytkownik'
   ,'PROCES','STRING[20]','Proces'
   ,'ELEMENT','STRING[30]','Czynność');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'USER',,,'PROCES',,,'ELEMENT',,);

B_TODO_U.cntx_psh();
B_TODO_U.index('UNIK');
B_TODO_U.prefix();
{? B_TODO_U.first()
|| {!
   |? {? B_TODO_U.B_PREL().B_PROC().FIRMA=REF.FIRMA
      || _tab.blank();
         _tab.REF:=$B_TODO_U.ref();
         _selected.prefix($B_TODO_U.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.USER:=B_TODO_U.USER().KOD;
         _tab.PROCES:=B_TODO_U.B_PREL().B_PROC().SYMBOL+' '+B_TODO_U.B_PREL().B_PROC().VER;
         _tab.ELEMENT:=B_TODO_U.B_PREL().SYMBOL;
         _tab.add()
      ?};
      B_TODO_U.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'USER,PROCES,ELEMENT',20,'Wybór pozycji do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
B_TODO_U.cntx_pop();

_result


\bi_todo_set_visible
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Ustawia widoczność zadań w tabeli BI_TODO zgodnie z zaimportowanym rekordem tablie BI_TODO_U
::----------------------------------------------------------------------------------------------------------------------
BI_TODO.cntx_psh();
BI_TODO.index('VISIBLE');
BI_TODO.prefix(REF.FIRMA,B_TODO_U.B_PREL,B_TODO_U.USER);
{? BI_TODO.first()
|| {!
   |? {? ~exec('bprel_manual','xls_b_todo_u',BI_TODO.B_PREL) & BI_TODO.VISIBLE<>B_TODO_U.VISIBLE
      || BI_TODO.VISIBLE:=B_TODO_U.VISIBLE;
         BI_TODO.put()
      ?};
      BI_TODO.next()
   !}
?};
BI_TODO.cntx_pop();
~~


\bprel_manual
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Sprawdza, czy B_PREL jest zadaniem manualnym
::   WE: _a B_PREL.ref()
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_b_prel:=_a;
_manual:=0;
_b_ele:=exec('FindAndGet','#table','B_PREL',_b_prel,,"B_PREL.B_ELE",null());
{? _b_ele<>null()
|| {? exec('FindInSet','#table','B_ACTION','B_ELE',_b_ele,,"B_ACTION.MANUAL",,,'')='T'
   || _manual:=1
   ?}
?};
_manual

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 105b24c78efecfb360023245478634b83951ca40e1f366918d79ab8b54da03e2e04697ea4f2efb5ab07b72cc4cd956996008bd9215b693663e7c2186f809c854d4b36d659c142e90e270573ed5aa48db6d4a07f42c6a68d212d4fbcb4df946bfb5ac83b5e26fdd7f682739d18aa0a47b62b941a439eef14dc804a797bceb8629

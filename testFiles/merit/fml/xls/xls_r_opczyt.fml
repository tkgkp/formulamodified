:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_r_opczyt.fml
:: Utworzony: 17.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji źródeł danych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='R_OPCZYT';
_def.FUNPAR:='ZWS_PAR_PZRO';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Definicja źródeł danych=1,1';
_def.NAME:='Definicja źródeł danych'@;
_def.DESC:='Definicja źródeł danych';

: źródło danych
_def.TABLE:="R_OPCZYT";

: prolog
_def.BEFORE:="
   R_KDEF.cntx_psh();
   R_KDEF.prefix();
   R_OPCZYT.cntx_psh();
   R_OPCZYT.index('R_OPCZYT');
   R_OPCZYT.prefix();
   ~~
";

: epilog
_def.AFTER:="
   R_OPCZYT.cntx_pop();
   R_KDEF.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('K',     MS.name(R_OPCZYT,'K'),     1,,MS.comment(R_OPCZYT,'K'));
   _env.define('O',     MS.name(R_OPCZYT,'O'),     1,,MS.comment(R_OPCZYT,'O'));
   _env.define('SYMBOL',MS.name(R_OPCZYT,'SYMBOL'),1,,MS.comment(R_OPCZYT,'SYMBOL'));
   _env.define('PORTAL',MS.name(R_OPCZYT,'PORTAL'),1,,MS.comment(R_OPCZYT,'PORTAL'));
   _env.define('SL',    MS.name(R_OPCZYT,'SL'),    1,,MS.comment(R_OPCZYT,'SL'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.K.VALUE:=R_OPCZYT.K;
   _buf.O.VALUE:=R_OPCZYT.O;
   _buf.SYMBOL.VALUE:=R_OPCZYT.SYMBOL;
   _buf.PORTAL.VALUE:=R_OPCZYT.PORTAL;
   _buf.SL.VALUE:=R_OPCZYT.SL;

   {? '<WEJWYJ><SYSTEM>'*R_OPCZYT.SYMBOL=0
   || {? R_KDEF.find_tab(,'SYMBOL',,'=',R_OPCZYT.SYMBOL)
      || _env.write_async('R_KDEF',R_KDEF.ref())
      ?}
   ?};
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_r_opczyt',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_r_opczyt',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.K=''      || 'K'
   |? _buf.O=''      || 'O'
   |? _buf.SYMBOL='' || 'SYMBOL'
   |? _buf.PORTAL='' || 'PORTAL'
   |? _buf.SL=''     || 'SL'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? '<WEJWYJ><SYSTEM>'*_buf.SYMBOL=0
|| {? ~R_KDEF.find_tab(,'SYMBOL',,'=',_buf.SYMBOL)
   || _val.msg_inset('SYMBOL','<WEJWYJ>','<SYSTEM>');
      _val.RESULT:=0;
      return()
   ?}
?};
{? 'TN'*_buf.PORTAL=0
|| _val.msg_inset('PORTAL','T','N');
   _val.RESULT:=0;
   return()
?};
{? 'TN'*_buf.SL=0
|| _val.msg_inset('SL','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_fld:=spli_str('K,O,SYMBOL,PORTAL,SL',',');
{! _ii:=1..obj_len(_fld)
|! _acr:=_fld[_ii];
   _max:=MS.fld_len(R_OPCZYT,_acr);
   {? ($('+_a.%1>_b'[_acr]))(_buf,_max)
   || _val.msg_length(_acr,_max);
      ($('_a.%1:=_b+_a.%1'[_acr]))(_buf,_max)
   ?}
!};
obj_del(_fld);

_ref:=null;
{? R_OPCZYT.find_tab(,'K',,'=',_buf.K)
|| _ref:=R_OPCZYT.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.K,_a.O]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,R_OPCZYT,"
   _a.K:=_b.K;
   _a.O:=_b.O;
   _a.SYMBOL:=_b.SYMBOL;
   _a.PORTAL:=_b.PORTAL;
   _a.SL:=_b.SL;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:50 af1a27c8ea009c0e9098fbb211541102e4bfd59f28aa2e75af64f1bd2c48f95985e58f153c3f0155312e45710e189a8c66234b6f576e0f7e266e66e7d6dd208b356eeb3a619f0386301f5a38b64cb67519c6d0808a6f8e5433bbd51ec5b7c948016b14cf3508dc2dcb0f0244c40410762bd39467191ecbee69f55cb189b46c1c

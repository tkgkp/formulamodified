:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_mso.fml
:: Utworzony: 23.04.2021
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='MSO';
_def.FUNPAR:='ZWS_PAR_FIRMY';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_firmy_xlsx','xls__init');
_def.SHEET:='Outlook=1,1';
_def.NAME:='Outlook'@;
_def.DESC:='Dodatek do MS Outlook';
_def.ADD_ROWS:=0;
_def.HIDDEN:='N';

: źródło danych
_def.TABLE:="MSO";

: prolog
_def.BEFORE:="
   MSO.cntx_psh();
   MSO.prefix();
   MSO.f_set('TYP,MSO_W',,'MSO.FIRMA=:_a',exec('ref_firma','ustawienia'));
   ~~
";

: epilog
_def.AFTER:="
   MSO.f_clear();
   MSO.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('MSO_W',   MS.name(MSO,'MSO_W'),0,,  MS.comment(MSO,'MSO_W'));
   _env.define('TYP',     MS.name(MSO,'TYP'),0,,    MS.comment(MSO,'TYP'));
   _env.define('MSO_P',   MS.name(MSO,'MSO_P'),0,,  MS.comment(MSO,'MSO_P'),type_of(''));
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_mso',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_mso',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_mso',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
:: ~OST: INBLGET
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_buf:=_b;
_buf.MSO_W.VALUE:=MSO.MSO_W;
_buf.TYP.VALUE:=MSO.TYP;
_buf.MSO_P.VALUE:='%1_%2'[$MSO.ref(),MSO.bl_info('MSO_P','NAME')];
_at:={? _env.DIR_ON_SERVER || '' || '@' ?};
MSO.bl_get('MSO_P',_at+'%1%2%3'[_env.DIR,_env.SEP,_buf.MSO_P.VALUE],0);

1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.MSO_W=''    || 'MSO_W'
   |? _buf.TYP=''      || 'TYP'
   |? _buf.MSO_P=''    || 'MSO_P'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.TYP))=0
|| _val.msg_inset('TYP','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(MSO,'MSO_W');
{? +_buf.MSO_W>_max
|| _val.msg_length('MSO_W',_max);
   _buf.MSO_W:=_max+_buf.MSO_W
?};
_max:=MS.fld_len(MSO,'TYP');
{? +_buf.TYP>_max
|| _val.msg_length('TYP',_max);
   _buf.TYP:=_max+_buf.TYP
?};

_ref:=null;
{? MSO.f_find(_buf.TYP,_buf.MSO_W)
|| _ref:=MSO.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'Plik %1 (%2) domyślny: %3'[_a.MSO_P,_a.MSO_W,_a.TYP]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_env:=_val.env;

exec('write','xls__util',_buf,_mod,_val,MSO,"
   _a.FIRMA:=exec('ref_firma','ustawienia');
   _a.MSO_W:=_b.MSO_W;
   _a.TYP:=_b.TYP;
   ~~
");

{? _val.RESULT<>0
:: dołącz do wiersza plik
|| _at:={? _env.DIR_ON_SERVER || '' || '@' ?};
   _name:=_at+'%1%2%3'[_env.DIR,_env.SEP,_buf.MSO_P];
   {? fexists(_name,0)
   || MSO.bl_put('MSO_P',_name,0,,17-_buf.MSO_P)
   || _val.MSG:='Brak pliku "%1" lub nieprawidłowa ścieżka.'[_name]
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 66766d6d92ebe3d20908d8ae1d502c84cefe323274441bc8d1ff23a3cd82e8270079cc7aef71607e8983879294fa3af8d533978d55a99a1311aa9985d25b044ecd53706377fef68962453db652759d3ae8dac523eaa8fefe534d2c75bb39c68679b60c92ca20883003c2bd13738941e2a9c54a0820813ed99bdedcabb3ccd169

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_r_karty.fml
:: Utworzony: 18.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu kart pracy
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='R_KARTY';
_def.FUNPAR:='ZWS_PAR_PKAP';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Karta pracy=1,1';
_def.NAME:='Karta pracy'@;
_def.DESC:='Karta pracy';

: źródło danych
_def.TABLE:="R_KARTY";

: prolog
_def.BEFORE:="
   R_KARTY.cntx_psh();
   R_KARTY.index('R_KARTY');
   R_KARTY.prefix();
   ~~
";

: epilog
_def.AFTER:="
   R_KARTY.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('N',MS.name(R_KARTY,'N'),1,,MS.comment(R_KARTY,'N'));
   _env.define('O',MS.name(R_KARTY,'O'),1,,MS.comment(R_KARTY,'O'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.N.VALUE:=R_KARTY.N;
   _buf.O.VALUE:=R_KARTY.O;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_r_karty',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_r_karty',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.N='' || 'K'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_fld:=spli_str('N,O',',');
{! _ii:=1..obj_len(_fld)
|! _acr:=_fld[_ii];
   _max:=MS.fld_len(R_KARTY,_acr);
   {? ($('+_a.%1>_b'[_acr]))(_buf,_max)
   || _val.msg_length(_acr,_max);
      ($('_a.%1:=_b+_a.%1'[_acr]))(_buf,_max)
   ?}
!};
obj_del(_fld);

_ref:=null;
{? R_KARTY.find_tab(,'N',,'=',_buf.N)
|| _ref:=R_KARTY.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.N,_a.O]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,R_KARTY,"
   _a.N:=_b.N;
   _a.O:=_b.O;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:50 deaf1aaed9e64acf36770c8f7a79ef58640d5c82d3b05a0754e2f9165ca627d981b6ee95b829e04a9e9d3db97be02fc716bfb89107a2e8636acc35ca69fb195aab643d4a7ad8d86e960bcf26f6f825f334d7fb74412797dce877c53d60c30f15effab1d9c6caa188902d8bd767197edfa5b8af90b55f8491faf479738726dc02

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_stn_gr.fml
:: Utworzony: 14.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu grupy stanowisk
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='STN_GR';
_def.FUNPAR:='ZWS_PAR_PGST';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Grupy stanowisk=1,1';
_def.NAME:='Grupy stanowisk'@;
_def.DESC:='Grupy stanowisk';

: źródło danych
_def.TABLE:="STN_GR";

: prolog
_def.BEFORE:="
   STN_GR.cntx_psh();
   STN_GR.f_clear();
   STN_GR.index('GRUPA');
   STN_GR.prefix(REF.FIRMA);
   ~~
";

: epilog
_def.AFTER:="
   STN_GR.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('GRUPA',MS.name(STN_GR,'GRUPA'),1,,MS.comment(STN_GR,'GRUPA'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.GRUPA.VALUE:=STN_GR.GRUPA;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_stn_gr',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_stn_gr',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.GRUPA='' || 'GRUPA'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(STN_GR,'GRUPA');
{? +_buf.GRUPA>_max
|| _val.msg_length('GRUPA',_max);
   _buf.GRUPA:=_max+_buf.GRUPA
?};

_ref:=null;
{? STN_GR.find_tab(,'GRUPA',,'=',_buf.GRUPA)
|| _ref:=STN_GR.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('STN_GR','to_str');
_obj.to_str:="'%1'[_a.GRUPA]";
_obj.STN_GR:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.GRUPA:=_b.GRUPA;
   _a.FIRMA:=REF.FIRMA;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? STN_GR.seek(_obj.STN_GR)
   || _set(STN_GR,_buf);
      {? ~STN_GR.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,STN_GR.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| STN_GR.blank();
   _set(STN_GR,_buf);
   {? ~STN_GR.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 b9092f2be52fd1ce2da144e049bcaaa9b68d31797b87c7f4e87d1a357db4f69353d3ccca415192ee3e903b23a0011a11b3f2c57595f62ac8cf351882bcc99e050ebd012a7898ce2cbc229c672cdc5dfd6d5bd1201d20b52138aab0d917d806dcd824feb6510dc824012929a0f7b73cdf798452b031d3c0b39902d94ecb3b18b9

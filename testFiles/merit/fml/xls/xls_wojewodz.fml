:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_wojewodz.fml
:: Utworzony: 15.01.2019
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu województw
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_def:=_a;

_def.ID:='WOJEWODZ';
_def.FUNPAR:='';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_pak_xlsx','xls__init');
_def.SHEET:='Województwa=1,1';
_def.NAME:='Województwa'@;
_def.DESC:='Województwa';
_def.HIDDEN:='N';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="WOJEWODZ";

: prolog
_def.BEFORE:="
   WOJEWODZ.cntx_psh();
   WOJEWODZ.f_clear();
   WOJEWODZ.index('WOJNAZWA');
   WOJEWODZ.prefix();
   ~~
";

: epilog
_def.AFTER:="
   WOJEWODZ.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('NAZWA',MS.name(WOJEWODZ,'NAZWA'),1,,MS.comment(WOJEWODZ,'NAZWA'));
   _env.define('KOD',MS.name(WOJEWODZ,'KOD'),1,,MS.comment(WOJEWODZ,'KOD'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.NAZWA.VALUE:=WOJEWODZ.NAZWA;
   _buf.KOD.VALUE:=WOJEWODZ.KOD;
:  powiaty województwa
   POWIATY.cntx_psh();
   POWIATY.index('WOJEWODZ');
   POWIATY.prefix(WOJEWODZ.ref());
   _loop:=POWIATY.first();
   {!
   |? _loop
   |! _env.write_async('POWIATY',POWIATY.ref());
      _loop:=POWIATY.next()
   !};
   POWIATY.cntx_pop();
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_wojewodz',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_wojewodz',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.NAZWA='' || 'NAZWA'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(WOJEWODZ,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};
_max:=MS.fld_len(WOJEWODZ,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};

_ref:=null;
{? WOJEWODZ.find_tab(,'NAZWA',,'=',_buf.NAZWA)
|| _ref:=WOJEWODZ.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 (%2)'[_a.NAZWA,_a.KOD]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,WOJEWODZ,"
   _a.NAZWA:=_b.NAZWA;
   _a.KOD:=_b.KOD;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 086dd940b66cbd0cbc2b23fabccd5d28979d96d198af6692167fc29b42ff23eb84c7d2fb270c7c02c5c5d4e10db18a19a64ea6aed7ff2ef4f489d43fd4fe789ccc44696336155ddb226bd6bedb647979234a7c784a2a1447146d3b1cb8d73566611b8ded458a2f0f9fd421012f11ee178e7841e7b140973762815c37cf4349b8

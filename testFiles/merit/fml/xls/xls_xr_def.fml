:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_xr_def.fml
:: Utworzony: 23.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji raportów zewnętrznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='XR_DEF';
_def.FUNPAR:='ZWS_PKD_XRDEF';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_xlsx','xls__init');
_def.SHEET:='Definicje raportów=1,1';
_def.NAME:='Definicje raportów'@;
_def.DESC:='Definicje raportów';

: źródło danych
_def.TABLE:="XR_DEF";

: prolog
_def.BEFORE:="
   XR_DEF.cntx_psh();
   XR_DEF.f_clear();
   XR_DEF.index('SYMBOL');
   XR_DEF.prefix();
   ~~
";

: epilog
_def.AFTER:="
   XR_DEF.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYMBOL',MS.name(XR_DEF,'SYMBOL'),0,,MS.comment(XR_DEF,'SYMBOL'));
   _env.define('OPIS',  MS.name(XR_DEF,'OPIS'),  1,,MS.comment(XR_DEF,'OPIS'));
   _env.define('SYSTEM',MS.name(XR_DEF,'SYSTEM'),1,,MS.comment(XR_DEF,'SYSTEM'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYMBOL.VALUE:=XR_DEF.SYMBOL;
   _buf.OPIS.VALUE:=XR_DEF.OPIS;
   _buf.SYSTEM.VALUE:=XR_DEF.SYSTEM;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_xr_def',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_xr_def',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYMBOL='' || 'SYMBOL'
   |? _buf.OPIS=''   || 'OPIS'
   |? _buf.SYSTEM='' || 'SYSTEM'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(XR_DEF,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(XR_DEF,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};
_max:=MS.fld_len(XR_DEF,'SYSTEM');
{? +_buf.SYSTEM>_max
|| _val.msg_length('SYSTEM',_max);
   _buf.SYSTEM:=_max+_buf.SYSTEM
?};

{? exec('domain_ref','#b_domain',_buf.SYSTEM)=null
|| _val.msg_norec('SYSTEM');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? XR_DEF.find_tab(,'SYMBOL',,'=',_buf.SYMBOL)
|| _ref:=XR_DEF.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.SYMBOL,_a.OPIS]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,XR_DEF,"
   _a.SYMBOL:=_b.SYMBOL;
   _a.OPIS:=_b.OPIS;
   _a.SYSTEM:=_b.SYSTEM;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:52 812bffe5b1a4bfee9a56311660047fb9c8cb17e81a3750e7ae7ae70afe27e180f1b95799f7302031ccd536f04ecbddece379999860266164ed43f7d129ec216c0e2965710dfb72d5c3a2f45c0aecd141d93c79b2d0219733aa2a66b3c448311bac49f49bdabd9439068ed380907efc605f8e1d039c3d5126c46223dac1d57c0d

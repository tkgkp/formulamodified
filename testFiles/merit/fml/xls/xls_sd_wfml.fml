:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_sd_wfml.fml
:: Utworzony: 13.03.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='SD_WFML';
_def.FUNPAR:='ZWS_PAR_ASDZ';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_sd_zes_xlsx','xls__init');
_def.SHEET:='Formuły parametrów=1,1';
_def.NAME:='Formuły parametrów zestawów danych'@;
_def.DESC:='Formuły parametrów zestawów danych';
_def.ADD_ROWS:=0;
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="SD_WFML";

: prolog
_def.BEFORE:="
   SD_WFML.cntx_psh();
   SD_WFML.f_clear();
   SD_WFML.index('KOD');
   SD_WFML.prefix();
   SD_WPAR.cntx_psh();
   SD_WPAR.index('KOD');
   SD_WPAR.prefix();
   SD_XSLO.cntx_psh();
   SD_XSLO.index('KOD');
   SD_XSLO.prefix();
   ~~
";

: epilog
_def.AFTER:="
   SD_XSLO.cntx_pop();
   SD_WPAR.cntx_pop();
   SD_WFML.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYSTEM', MS.name(SD_WFML,'SYSTEM'),0,, MS.comment(SD_WFML,'SYSTEM'));
   _env.define('KOD',    MS.name(SD_WFML,'KOD'),0,,    MS.comment(SD_WFML,'KOD'));
   _env.define('SD_WPAR',MS.name(SD_WFML,'SD_WPAR'),0,,MS.comment(SD_WFML,'SD_WPAR'),type_of(''));
   _env.define('SD_XSLO',MS.name(SD_WFML,'SD_XSLO'),0,,MS.comment(SD_WFML,'SD_XSLO'),type_of(''));
   _env.define('TRESC',  MS.name(SD_WFML,'TRESC'),0,,  MS.comment(SD_WFML,'TRESC'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYSTEM.VALUE:=SD_WFML.SYSTEM;
   _buf.KOD.VALUE:=SD_WFML.KOD;
   _buf.SD_WPAR.VALUE:=SD_WFML.SD_WPAR().KOD;
   _buf.SD_XSLO.VALUE:=SD_WFML.SD_XSLO().KOD;
   _buf.TRESC.VALUE:=SD_WFML.memo_txt(,1,'TRESC');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_sd_wfml',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_sd_wfml',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYSTEM=''  || 'SYSTEM'
   |? _buf.KOD=''     || 'KOD'
   |? _buf.SD_WPAR='' || 'SD_WPAR'
   |? _buf.SD_XSLO='' || 'SD_XSLO'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.SYSTEM))=0
|| _val.msg_inset('SYSTEM','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(SD_FML,'SYSTEM');
{? +_buf.SYSTEM>_max
|| _val.msg_length('SYSTEM',_max);
   _buf.SYSTEM:=_max+_buf.SYSTEM
?};
_max:=MS.fld_len(SD_FML,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(SD_WPAR,'KOD');
{? +_buf.SD_WPAR>_max
|| _val.msg_length('SD_WPAR',_max);
   _buf.SD_WPAR:=_max+_buf.SD_WPAR
?};
_max:=MS.fld_len(SD_XSLO,'KOD');
{? +_buf.SD_XSLO>_max
|| _val.msg_length('SD_XSLO',_max);
   _buf.SD_XSLO:=_max+_buf.SD_XSLO
?};

: weryfikacja spójności referencyjnej
_par:=null;
{? SD_WPAR.find_key(_buf.SD_WPAR,)
|| _par:=SD_WPAR.ref()
|| _val.msg_norec('SD_WPAR');
   _val.RESULT:=0;
   return()
?};
_slo:=null;
{? SD_XSLO.find_key(_buf.SD_XSLO,)
|| _slo:=SD_XSLO.ref()
|| _val.msg_norec('SD_XSLO');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? (SD_WFML.index('KOD'); SD_WFML.find_key(_buf.KOD,)) |
   (SD_WFML.index('UNIQUE'); SD_WFML.find_key(_slo,_par))
|| _ref:=SD_WFML.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[_a.KOD]",
   'SD_WPAR',_par,
   'SD_XSLO',_slo
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,SD_WFML,"
   _a.SYSTEM:=_b.SYSTEM;
   _a.KOD:=_b.KOD;
   _a.SD_WPAR:=_c.SD_WPAR;
   _a.SD_XSLO:=_c.SD_XSLO;
   _a.memo_set(_b.TRESC,'TRESC');
   ~~
",'TRESC');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 4aa97a8738aa78617262029aba58705274fca0021b46c6ac83038ff14277658da227c7929bd402c6529b662cc938b44f1d5f93863150a640a76f38fa5ba3a5467f6a99e961ff70bf0124485a4c58904e357bb6652f646396a644ca9b9f6ba435145eed8ef757595eb11c08b26c7f9b00b92a7254fc60bb398af808fb0ce245d7

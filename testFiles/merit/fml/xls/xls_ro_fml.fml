:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ro_fml.fml
:: Utworzony: 04.12.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu formuł
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='RO_FML';
_def.FUNPAR:='ZWS_PAR_ARDK';
_def.DOMAIN:=exec('name','#b_domain','ROD');
_def.FILE:=exec('def_zdo_xlsx','xls__init');
_def.SHEET:='Formuły=1,1';
_def.NAME:='Formuły dla zarządzania danymi osobowymi'@;
_def.DESC:='Formuły dla zarządzania danymi osobowymi';
_def.ADD_ROWS:=0;
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="RO_FML";

: prolog
_def.BEFORE:="
   RO_FML.cntx_psh();
   RO_FML.f_clear();
   RO_FML.index('SYMBOL');
   RO_FML.prefix();
   ~~
";

: epilog
_def.AFTER:="
   RO_FML.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('RODZAJ', MS.name(RO_FML,'RODZAJ'),0,, MS.comment(RO_FML,'RODZAJ'));
   _env.define('SYMBOL', MS.name(RO_FML,'SYMBOL'),0,, MS.comment(RO_FML,'SYMBOL'));
   _env.define('OPIS',   MS.name(RO_FML,'OPIS'),0,,   MS.comment(RO_FML,'OPIS'));
   _env.define('UWAGI',  MS.name(RO_FML,'UWAGI'),0,,  MS.comment(RO_FML,'UWAGI'));
   _env.define('FORMULA',MS.name(RO_FML,'FORMULA'),0,,MS.comment(RO_FML,'FORMULA'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.RODZAJ.VALUE:=RO_FML.RODZAJ;
   _buf.SYMBOL.VALUE:=RO_FML.SYMBOL;
   _buf.OPIS.VALUE:=RO_FML.OPIS;
   _buf.UWAGI.VALUE:=RO_FML.memo_txt(,1,'UWAGI');
   _buf.FORMULA.VALUE:=RO_FML.memo_txt(,1,'FORMULA');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_ro_fml',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ro_fml',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.RODZAJ='' || 'RODZAJ'
   |? _buf.SYMBOL='' || 'SYMBOL'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(RO_FML,'RODZAJ');
{? +_buf.RODZAJ>_max
|| _val.msg_length('RODZAJ',_max);
   _buf.RODZAJ:=_max+_buf.RODZAJ
?};
_max:=MS.fld_len(RO_FML,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(RO_FML,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

_ref:=null;
{? RO_FML.find_tab(,
      'RODZAJ',,'=',_buf.RODZAJ,
      'SYMBOL',,'=',_buf.SYMBOL
   )
|| _ref:=RO_FML.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.RODZAJ,_a.SYMBOL]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.02]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,RO_FML,"
   _a.RODZAJ:=_b.RODZAJ;
   _a.SYMBOL:=_b.SYMBOL;
   _a.OPIS:=_b.OPIS;
   _a.memo_set(_b.UWAGI,'UWAGI');
   _a.memo_set(_b.FORMULA,'FORMULA');
   ~~
",'UWAGI,FORMULA');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 19ff6da87feb2d330d91160a1e927826b9487d2339ddedc9afac783255957f7bc65c6c9676758b42064032ce7e04645ca8db06096256611c10a3111a1dec98aad7b2eb6554774ff668258bad84278366891123a09c2de8e61da804275d72af4163c6eff37c485c7ef8f25550ea023dbfdfc793850b0d4adb23f63858fa076d98

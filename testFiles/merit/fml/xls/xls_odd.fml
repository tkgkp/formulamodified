:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_odd.fml
:: Utworzony: 23.07.2018
:: Autor: MB
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu jednostek księgowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ODD';
_def.DOMAIN:=exec('name','#b_domain','FKS');
_def.FUNPAR:='ZWS_DEF_ODD';
_def.FILE:='struktury_księgowe.xlsx';
_def.SHEET:='Jednostki księgowe=1,1';
_def.NAME:='Jednostki księgowe'@;
_def.DESC:='Jednostki księgowe';

_def.PREFIX:="ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA)";
_def.TABLE:="ODD";
_def.FIELDS:="exec('fld','xls_odd',_a)";

_def.BEFORE:="ODD.cntx_psh()";
_def.AFTER:="ODD.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_odd',_a,_b)";
_def.VALIDATE:="exec('validate','xls_odd',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_odd',_a,_b,_c)";
_def.SELECT:="exec('select','xls_odd',_a)";
~~


\fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('OD','Skrót',1,,'8 znakowy skrót');
_def.define('N','Nazwa',1,,'40 znakowa nazwa');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.OD.VALUE:=ODD.OD;
_table.N.VALUE:=ODD.N;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
ODD.index('ODDZIALY');
ODD.prefix(REF.FIRMA,_obj.OD,);
{? ODD.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT=1
|| {? _result.ACTION='add' || ODD.blank() ?};
   ODD.FIRMA:=REF.FIRMA;
   ODD.OD:=_obj.OD;
   ODD.N:=_obj.N;
   {? exec('ar_odd','jksieg',_result.ACTION='add')<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=ODD.add(1)
   |? _validate.ACTION='put'
   || _result:=ODD.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie jednostki księgowej %1 nie powiodło się.'@[_obj.OD]
   || _validate.MSG:='Poprawa jednostki księgoerj %1 nie powiodła się.'@[_obj.OD]
   ?}
|? _validate.ACTION='add'
|| exec('aa_odd','jksieg')
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'OD','STRING[8]','Skrót'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'N','STRING[40]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'N',,);

ODD.cntx_psh();
ODD.index('ODDZIALY');
ODD.prefix();
{? ODD.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$ODD.ref();
      _selected.prefix($ODD.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.N:=ODD.N;
      _tab.OD:=ODD.OD;
      _tab.add();
      ODD.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'OD,N',20,'Wybór jednostek księgowych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
ODD.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:49 6c3554a1f479ec4958e4abd2567499730745eb8242f2c94ed66f30f983496d36cfcb566e9538b9337b50985c81e73f5b39f590d28421a10c2f8c780bb97c96efccdf86440406a8c3eede02e4be138a93ffab83af0081dd3b5932bf923cf98d3806dc7f84b0406d4cc4465481f12dbe400dad0cca6aeba521fa02ee418f345ca8

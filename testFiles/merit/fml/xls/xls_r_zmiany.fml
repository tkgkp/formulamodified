:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_r_zmiany.fml
:: Utworzony: 25.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji zmian w pracy zmianowej
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='R_ZMIANY';
_def.FUNPAR:='ZWS_PAR_PPZM';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Praca zmianowa=1,1';
_def.NAME:='Praca zmianowa'@;
_def.DESC:='Praca zmianowa';

: źródło danych
_def.TABLE:="R_ZMIANY";

: prolog
_def.BEFORE:="
   R_ZMIANY.cntx_psh();
   R_ZMIANY.index('R_ZMIANY');
   R_ZMIANY.prefix();
   ~~
";

: epilog
_def.AFTER:="
   R_ZMIANY.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('OPIS',   MS.name(R_ZMIANY,'OPIS'),   1,,MS.comment(R_ZMIANY,'OPIS'));
   _env.define('GODZ_OD',MS.name(R_ZMIANY,'GODZ_OD'),1,,MS.comment(R_ZMIANY,'GODZ_OD'));
   _env.define('MIN_OD', MS.name(R_ZMIANY,'MIN_OD'), 1,,MS.comment(R_ZMIANY,'MIN_OD'));
   _env.define('MAX_OD', MS.name(R_ZMIANY,'MAX_OD'), 1,,MS.comment(R_ZMIANY,'MAX_OD'));
   _env.define('GODZ_DO',MS.name(R_ZMIANY,'GODZ_DO'),1,,MS.comment(R_ZMIANY,'GODZ_DO'));
   _env.define('WYJ_ND', MS.name(R_ZMIANY,'WYJ_ND'), 1,,MS.comment(R_ZMIANY,'WYJ_ND'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.OPIS.VALUE:=R_ZMIANY.OPIS;
   _buf.GODZ_OD.VALUE:=R_ZMIANY.GODZ_OD;
   _buf.MIN_OD.VALUE:=R_ZMIANY.MIN_OD;
   _buf.MAX_OD.VALUE:=R_ZMIANY.MAX_OD;
   _buf.GODZ_DO.VALUE:=R_ZMIANY.GODZ_DO;
   _buf.WYJ_ND.VALUE:=R_ZMIANY.WYJ_ND;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_r_zmiany',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_r_zmiany',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.OPIS=''   || 'OPIS'
   |? _buf.WYJ_ND='' || 'WYJ_ND'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? 'TN'*_buf.WYJ_ND=0
|| _val.msg_inset('WYJ_ND','T','N');
   _val.RESULT:=0;
   return()
?};
_czas:=(_buf.GODZ_DO-_buf.GODZ_OD)+*((_buf.WYJ_ND*'T')*(*time(24,0,0)));

{? _czas<=*0
|| _val.MSG:='Czas trwania zmiany musi być większy od zera.'@;
   _val.RESULT:=0;
   return()
?}
;

_ref:=null;
{? R_ZMIANY.find_tab(,'OPIS',,'=',_buf.OPIS)
|| _ref:=R_ZMIANY.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[_a.OPIS]",
   'CZAS',_czas
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,R_ZMIANY,"
   _a.OPIS:=_b.OPIS;
   _a.GODZ_OD:=_b.GODZ_OD;
   _a.MIN_OD:=_b.MIN_OD;
   _a.MAX_OD:=_b.MAX_OD;
   _a.GODZ_DO:=_b.GODZ_DO;
   _a.WYJ_ND:=_b.WYJ_ND;
   _a.CZAS:=_c.CZAS;

   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:50 768f5c2836ad704c66430d9f5ede0e0c6881b0e78fb8e13409c45aa0d6be9ca6f5c45ca5445427b49f929842e049f338a3566624b1fc60fcd0d2770ee73a0c9c4759061d724aaffcfd22c37501c1b8ef0e00df0090eec0c2fe1de7c3a659cdefdb3111c6d7d157523dcb72813d00162a40137dc5a6e9ad535062e1862655315a

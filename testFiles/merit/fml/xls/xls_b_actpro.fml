:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_b_actpro.fml
:: Utworzony: 22.11.2018
:: Autor: MW
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu faz produkcji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='B_ACTION_K2';
_def.DOMAIN:=exec('name','#b_domain','ZPR');
_def.FUNPAR:='';

_def.FILE:='procesy.xlsx';
_def.SHEET:='Czynności - konfig. wg firm'@+'=1,1';
_def.NAME:='Czynności - konfiguracja wg firm'@;
_def.DESC:='Czynności - konfiguracja wg firm'@;

_def.PREFIX:="exec('prefix','xls_b_actpro')";
_def.TABLE:="exec('table','xls_b_actpro')";
_def.FIELDS:="exec('fields','xls_b_actpro',_a)";

_def.BEFORE:="__TASK:=app_info('task');B_ACTPRO.cntx_psh()";
_def.AFTER:="VAR_DEL.delete('__TASK');B_ACTPRO.cntx_pop()";
_def.SELECT:="exec('select','xls_b_actpro',_a)";

_def.EXPORT:="exec('export','xls_b_actpro',_a,_b)";
_def.VALIDATE:="exec('validate','xls_b_actpro',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_b_actpro',_a,_b,_c)";
~~

\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
B_ACTPRO.index('FIRMA');
B_ACTPRO.prefix(REF.FIRMA);
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
B_ACTPRO


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('B_ACTION','Czynność'@,1);
_def.define('RUNMICRO','Czynność może być uruchomiona nieprocesowo'@,1,,'[T]/[N]');
_def.define('TAKEOVER','Czynność może być przejmowana'@,1,,'[T]/[N]');
_def.define('RELEASE','Czynność może być zwolniona'@,1,,'[T]/[N]');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
B_ACTPRO.prefix();
{? _validate.ACTION='add'
|| B_ACTPRO.blank()
?};
B_ACTPRO.B_ACTION:=_validate.OBJ.B_ACTION;
B_ACTPRO.FIRMA:=REF.FIRMA;
B_ACTPRO.RUNMICRO:=_obj.RUNMICRO;
B_ACTPRO.TAKEOVER:=_obj.TAKEOVER;
B_ACTPRO.RELEASE:=_obj.RELEASE;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=0;
_uid:=B_ACTPRO.B_ACTION().UID;

{? exec('is_def','#b_action',_uid,__TASK)>0
|| _result:=1;
   _table.B_ACTION.VALUE:=_uid;
   {? B_ACTPRO.RUNMICRO='T' | B_ACTPRO.RUNMICRO='N'
   || _table.RUNMICRO.VALUE:=B_ACTPRO.RUNMICRO
   || _table.RUNMICRO.VALUE:='N'
   ?};
   {? B_ACTPRO.TAKEOVER='T' | B_ACTPRO.TAKEOVER='N'
   ||_table.TAKEOVER.VALUE:=B_ACTPRO.TAKEOVER
   ||_table.TAKEOVER.VALUE:='N'
   ?};
   {? B_ACTPRO.RELEASE='T' | B_ACTPRO.RELEASE='N'
   || _table.RELEASE.VALUE:=B_ACTPRO.RELEASE
   || _table.RELEASE.VALUE:='N'
   ?}
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('B_ACTION');

:: Błędy
{? _obj.B_ACTION=''
|| _result.msg_empty('B_ACTION');
   _result.RESULT:=0
?};

:: Szukanie, gdy nie było błędu
{? _result.RESULT>0
||
  _result.OBJ.B_ACTION := exec('FindInSet','#table','B_ACTION','UNIK',_obj.B_ACTION,,,1)
?};

:: Błędy
{? _result.OBJ.B_ACTION=null()
|| _result.msg_norec('B_ACTION');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| B_ACTPRO.index('UNIK');
   B_ACTPRO.prefix(_result.OBJ.B_ACTION,REF.FIRMA);
   {? B_ACTPRO.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_b_actpro',_obj,_mode,_result)
      ?}
   || _result.ACTION:='add';
      exec('record','xls_b_actpro',_obj,_mode,_result)
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=B_ACTPRO.add(1)
   |? _validate.ACTION='put'
   || _result:=B_ACTPRO.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie konfiguracji czynności %1 dla firmy %2 nie powiodło się.'@[_obj.B_ACTION,_obj.FIRMA]
   || _validate.MSG:='Poprawa konfiguracji czynności %1 dla firmy %2 nie powiodła się.'@[_obj.B_ACTION,_obj.FIRMA]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'@
   ,'B_ACTION','STRING[12]','Czynność'@
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'B_ACTION',,);

B_ACTPRO.cntx_psh();
B_ACTPRO.index('FIRMA');
B_ACTPRO.prefix(REF.FIRMA);
{? B_ACTPRO.first()
|| {!
   |?  _tab.blank();
       _tab.REF:=$B_ACTPRO.ref();
       _selected.prefix($B_ACTPRO.ref());
       {? _selected.first()
       || _tab.SELECTED:='T'
       || _tab.SELECTED:='N'
       ?};
       _tab.B_ACTION:=B_ACTPRO.B_ACTION().UID;
       _tab.add();
       B_ACTPRO.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'B_ACTION',20,'Wybór czynności do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
B_ACTPRO.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:41 23ae32098e94630055c857edd99144f4fcb5193ba7c302e4ce196dde4e3b0d005e56005667b7ebca201f75b23abc16edc96eedc0df9a59d64055d7b8e3749f9ff08b2c4cafd0efdcbd82ad4b798c64cf334c8da41d4f2d82cddc59c2f5f086018819e91467ec20090683e4f2795dafb1e7b68dc8540bf51f00c137efe03d0431

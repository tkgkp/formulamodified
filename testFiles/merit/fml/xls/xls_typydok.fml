:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_typydok.fml
:: Utworzony: 13.07.2018
:: Autor: Mario
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typów dokumentów magazynowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TYPYDOK';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='ZWS_PAR_LTDM';

_def.FILE:='magazyn.xlsx';
_def.SHEET:='Typy dokumentów magazynowych=1,1';
_def.NAME:='Typy dokumentów magazynowych'@;
_def.DESC:='Typy dokumentów magazynowych';

_def.PREFIX:="exec('prefix','xls_typydok')";
_def.TABLE:="exec('table','xls_typydok')";
_def.FIELDS:="exec('fields','xls_typydok',_a)";

_def.BEFORE:="exec('before','xls_typydok',_a)";
_def.AFTER:="exec('after','xls_typydok',_a)";
_def.SELECT:="exec('select','xls_typydok',_a)";

_def.EXPORT:="exec('export','xls_typydok',_a,_b)";
_def.IMPORT:="exec('import','xls_typydok',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_typydok',_a,_b,_c)";
~~


\before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: formuła przed importem
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__buftd');
__buftd:=tab_tmp(2,'MODE','INTEGER',''
          ,'T','STRING[8]',''
          ,'NAZ','STRING[60]',''
          ,'Z','STRING[1]',''
          ,'P','STRING[1]',''
          ,'TD','STRING[8]',''
          ,'DK','STRING[1]',''
          ,'DS','STRING[1]',''
          ,'WZ','STRING[31]',''
          ,'WAL','STRING[1]',''
          ,'KK','STRING[35]',''
          ,'WK','STRING[1]',''
          ,'UPRW','STRING[1]',''
          ,'DN','STRING[1]',''
          ,'KOD','STRING[3]',''
          ,'UE','STRING[1]',''
          ,'CS','STRING[1]',''
          ,'AFIFO','STRING[1]',''
          ,'RTRANSAK','STRING[2]',''
          ,'ZLEC','STRING[1]',''
          ,'WYR','STRING[1]',''
          ,'SYS','STRING[1]',''
          ,'NRT','INTEGER',''
          ,'STATS_Z','STRING[1]',''
          ,'STATS_O','STRING[1]',''
          ,'STATS_N','STRING[1]',''
          ,'KOOP','STRING[1]',''
          ,'JPK','STRING[8]',''
          ,'PROJZAKR','STRING[20]',''
          ,'PROJWJO','STRING[1]',''
          ,'PROJZKH','STRING[1]',''
          ,'VALIDATE','STRING[8]',''
          ,'VALACC','STRING[8]',''
          ,'VALREC','STRING[8]',''
          ,'INW','STRING[1]','');
TYPYDOK.cntx_psh()


\after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: formuła po imporcie
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
__buftd.prefix();
{? __buftd.first()
|| {!
   |? _action:='';
      _mode:=__buftd.MODE;
      BEER.NRDOK_AB:='ND';
      TYPYDOK.index('TYP');
      TYPYDOK.prefix(__buftd.T,);
      {? TYPYDOK.first()
      || {? _mode=0
         || _action:='no'
         || _action:='put'
         ?}
      || _action:='add'
      ?};
      {? ';add;put'*_action
      || _len:=__buftd.fld_num();
         {? _action='add'
         || TYPYDOK.prefix();
            TYPYDOK.blank()
         ?};
         _kk:='';
         _kk_r:='';
         {! _i:=2.._len
         |! {? __buftd.fld_acr(_i)='KK'
            || _kk:=__buftd[_i]
            |? __buftd.fld_acr(_i)='KK_R'
            || _kk_r:=__buftd[_i]
            |? __buftd.fld_acr(_i)='VALIDATE'
            || {? __buftd[_i]<>''
               || TYPYDOK.VALIDATE:=exec('FindInSet','#table','FORMULA','FORMULA4',__buftd[_i],'v',,1)
               ?}
            |? __buftd.fld_acr(_i)='VALACC'
            || {? __buftd[_i]<>''
               || TYPYDOK.VALACC:=exec('FindInSet','#table','FORMULA','FORMULA4',__buftd[_i],'a',,1)
               ?}
            |? __buftd.fld_acr(_i)='VALREC'
            || {? __buftd[_i]<>''
               || TYPYDOK.VALREC:=exec('FindInSet','#table','FORMULA','FORMULA4',__buftd[_i],'r',,1)
               ?}
            || ($('TYPYDOK.'+__buftd.fld_acr(_i)))():=__buftd[_i]
            ?}
         !};
         {? _kk_r<>'' & _kk<>''
         || _rok_f:=exec('FindInSet','#table','ROK_F','NAZWA',_rkk,REF.FIRMA,,1);
            {? _rok_f<>null()
            || KK.cntx_psh();
               KK.index('UNIK');
               KK.prefix(REF.FIRMA,'',_rok_f,_kk);
               {? KK.first() || TYPYDOK.KK:=KK.ref() ?};
               KK.cntx_pop()
            ?}
         ?};
         {? exec('chk_typd','typydok',_action)='' || {? _action='add' || TYPYDOK.add(1) || TYPYDOK.put(1) ?} ?}
      ?};
      __buftd.next()
   !}
?};
VAR_DEL.delete('__buftd');
TYPYDOK.cntx_pop()


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TYPYDOK.index('TYP');
TYPYDOK.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TYPYDOK


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('T','Typ dokumentu',1,,'');
_def.define('NAZ','Nazwa dokumentu',1,,'');
_def.define('Z','Zewnętrzny',1,,'[T]/[N]');
_def.define('P','Przychodowy',1,,'[T]/[N]');
_def.define('TD','Tworzy dokument',1,,'');
_def.define('DK','Dokument korygujący',1,,'[T]/[N]');
_def.define('DS','Tworzy dok. sprzed.',1,,'[T]/[N]');
_def.define('WZ','Wzorzec wydruku',1,,'');
_def.define('WAL','Walutowy',1,,'');
_def.define('KK','Domyślne konto kosztów',1,,'35 znakowy symbol konta kosztowego');
_def.define('KK_R','Rok konta kosztów',1,,'Nazwa roku dla konta kosztowego');
_def.define('WK','Wymagane konto',1,,'[T]/[N]');
_def.define('UPRW','Uproszczone wystawianie',1,,'');
_def.define('DN','Zwrot',1,,'');
_def.define('KOD','Kod numeracji',1,,'');
_def.define('UE','Unijny',1,,'');
_def.define('CS','Redakcja cen sprzed.',1,,'[T]/[N]');
_def.define('AFIFO','Auto FIFO/LIFO',1,,'');
_def.define('RTRANSAK','Rodzaj transakcji',1,,'');
_def.define('ZLEC','Czy dokument powiazany ze zleceniem?',1,,'');
_def.define('WYR','Wyrób ze zlecenia',1,,'');
_def.define('SYS','Typ systemowy',1,,'');
_def.define('NRT','Numeracja tymczasowa',1,,'');
_def.define('STATS_Z','Wydania dla zgodnych',1,,'');
_def.define('STATS_O','Wydania dla nieokreślonych',1,,'');
_def.define('STATS_N','Wydania dla niezgodnych',1,,'');
_def.define('KOOP','Dotyczy kooperacji',1,,'');
_def.define('JPK','Typ JPK',1,,'');
_def.define('PROJZAKR','Zakres projektów',1,,'');
_def.define('PROJWJO','Zakres projektów - jednostka organizacyjna z dokumentu',1,,'');
_def.define('PROJZKH','Zakres projektów - kontrahent z dokumentu',1,,'');
_def.define('VALIDATE','Dodatkowa formuła walidacji',1,,'');
_def.define('VALACC','Dodatkowa formuła akceptacyjna',1,,'');
_def.define('VALREC','Dodatkowa formuła wycofania',1,,'');
_def.define('INW','Inwentayzacja - przecena',1,,'[N]-brak/[I]-inwentaryzacja/[E]-przecena');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

TYPYDOK.index('TYP');
TYPYDOK.prefix();
{? _validate.ACTION='add'
||
   TYPYDOK.blank();
   TYPYDOK.T:=_obj.T
?};

TYPYDOK.Z:=_obj.Z;
TYPYDOK.P:=_obj.P;
TYPYDOK.TD:=_obj.TD;
TYPYDOK.DK:=_obj.DK;
TYPYDOK.DS:=_obj.DS;
TYPYDOK.WAL:=_obj.WAL;
TYPYDOK.UPRW:=_obj.UPRW;
TYPYDOK.DN:=_obj.DN;
TYPYDOK.KOD:=_obj.KOD;
TYPYDOK.CS:=_obj.CS;
TYPYDOK.AFIFO:=_obj.AFIFO;
TYPYDOK.ZLEC:=_obj.ZLEC;
TYPYDOK.WYR:=_obj.WYR;
TYPYDOK.SYS:=_obj.SYS;
TYPYDOK.NRT:=_obj.NRT;
TYPYDOK.STATS_Z:=_obj.STATS_Z;
TYPYDOK.STATS_O:=_obj.STATS_O;
TYPYDOK.STATS_N:=_obj.STATS_N;
TYPYDOK.KOOP:=_obj.KOOP;
TYPYDOK.PROJZAKR:=_obj.PROJZAKR;
TYPYDOK.PROJWJO:=_obj.PROJWJO;
TYPYDOK.PROJZKH:=_obj.PROJZKH;
TYPYDOK.NAZ:=_obj.NAZ;
TYPYDOK.UE:=_obj.UE;
TYPYDOK.RTRANSAK:=_obj.RTRANSAK;
TYPYDOK.WZ:=_obj.WZ;
TYPYDOK.WK:=_obj.WK;
TYPYDOK.JPK:=_obj.JPK;
TYPYDOK.VALIDATE:=_validate.OBJ.VALIDATE;
TYPYDOK.VALACC:=_validate.OBJ.VALACC;
TYPYDOK.VALREC:=_validate.OBJ.VALREC;
TYPYDOK.KK:=_validate.OBJ.KK;
TYPYDOK.INW:=_obj.INW;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.T.VALUE:=TYPYDOK.T;
_table.NAZ.VALUE:=TYPYDOK.NAZ;
_table.Z.VALUE:=TYPYDOK.Z;
_table.P.VALUE:=TYPYDOK.P;
_table.TD.VALUE:=TYPYDOK.TD;
_table.DK.VALUE:=TYPYDOK.DK;
_table.DS.VALUE:=TYPYDOK.DS;
_table.WZ.VALUE:=TYPYDOK.WZ;
_table.WAL.VALUE:=TYPYDOK.WAL;
_table.KK.VALUE:=TYPYDOK.KK().SYM;
_excel.write_async('KK',TYPYDOK.KK);
_table.KK_R.VALUE:=TYPYDOK.KK().ROK_F().NAZ;
_table.WK.VALUE:=TYPYDOK.WK;
_table.UPRW.VALUE:=TYPYDOK.UPRW;
_table.DN.VALUE:=TYPYDOK.DN;
_table.KOD.VALUE:=TYPYDOK.KOD;
_excel.write_async('NRDOK',exec('FindInSet','#table','NRDOK','NRDOK',TYPYDOK.KOD,,,1,,null()));
_table.UE.VALUE:=TYPYDOK.UE;
_table.CS.VALUE:=TYPYDOK.CS;
_table.AFIFO.VALUE:=TYPYDOK.AFIFO;
_table.RTRANSAK.VALUE:=TYPYDOK.RTRANSAK;
_table.ZLEC.VALUE:=TYPYDOK.ZLEC;
_table.WYR.VALUE:=TYPYDOK.WYR;
_table.SYS.VALUE:=TYPYDOK.SYS;
_table.NRT.VALUE:=TYPYDOK.NRT;
_table.STATS_Z.VALUE:=TYPYDOK.STATS_Z;
_table.STATS_O.VALUE:=TYPYDOK.STATS_O;
_table.STATS_N.VALUE:=TYPYDOK.STATS_N;
_table.KOOP.VALUE:=TYPYDOK.KOOP;
_table.JPK.VALUE:=TYPYDOK.JPK;
_table.PROJZAKR.VALUE:=TYPYDOK.PROJZAKR;
_table.PROJWJO.VALUE:=TYPYDOK.PROJWJO;
_table.PROJZKH.VALUE:=TYPYDOK.PROJZKH;
_table.VALIDATE.VALUE:=TYPYDOK.VALIDATE().SKROT;
_table.VALACC.VALUE:=TYPYDOK.VALACC().SKROT;
_table.VALREC.VALUE:=TYPYDOK.VALREC().SKROT;
_table.INW.VALUE:=TYPYDOK.INW;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1;

_result.OBJ:=obj_new('VALIDATE','VALACC','VALREC','KK');

{? _obj.VALIDATE<>''
|| _result.OBJ.VALIDATE:=exec('FindInSet','#table','FORMULA','FORMULA4',_obj.VALIDATE,'v',,1);
   {? _result.OBJ.VALIDATE=null()
   || _result.MSG:='Nie znaleziono formuły walidacji dla typów dokumentów magazynowych %1.'@[_obj.VALIDATE];
      _result.RESULT:=0
   ?}
|| _result.OBJ.VALIDATE:=null()
?};

{? _obj.VALACC<>''
|| _result.OBJ.VALACC:=exec('FindInSet','#table','FORMULA','FORMULA4',_obj.VALACC,'a',,1);
   {? _result.OBJ.VALACC=null()
   || _result.MSG:='Nie znaleziono formuły akceptacji dla typów dokumentów magazynowych %1.'@[_obj.VALACC];
      _result.RESULT:=0
   ?}
|| _result.OBJ.VALACC:=null()
?};

{? _obj.VALREC<>''
|| _result.OBJ.VALREC:=exec('FindInSet','#table','FORMULA','FORMULA4',_obj.VALREC,'r',,1);
   {? _result.OBJ.VALREC=null()
   || _result.MSG:='Nie znaleziono formuły wycofania dla typów dokumentów magazynowych %1.'@[_obj.VALREC];
      _result.RESULT:=0
   ?}
|| _result.OBJ.VALREC:=null()
?};

_result.OBJ.KK:=null();
{? _obj.KK_R<>'' & _obj.KK<>''
|| _rok_f:=exec('FindInSet','#table','ROK_F','NAZWA',_obj.KK_R,REF.FIRMA,,1);
   {? _rok_f<>null()
   || KK.cntx_psh();
      KK.index('UNIK');
      KK.prefix(REF.FIRMA,'',_rok_f,_obj.KK);
      {? KK.first() || _result.OBJ.KK:=KK.ref() ?};
      KK.cntx_pop()
   ?}
?};
{? _obj.INW<>'N' & _obj.INW<>'I' & _obj.INW<>'E' || _result.msg_inset('INW','N','I','E');_result.RESULT:=0 ?};
:: zapisanie danych do bufora
{? _result.RESULT=1 & _obj.TD<>''
|| VAR_DEL.delete('__bufobj');
   __bufobj:=_a;
   _len:=__buftd.fld_num();
   __buftd.prefix();
   __buftd.blank();
   __buftd.MODE:=_mode;
   {! _i:=2.._len |! __buftd[_i]:=($('__bufobj.'+__buftd.fld_acr(_i)))() !};
   __buftd.add(1);
   VAR_DEL.delete('__bufobj');
   _result.RESULT:=0
?};

{? _result.RESULT=1
|| BEER.NRDOK_AB:='ND';
   TYPYDOK.index('TYP');
   TYPYDOK.prefix(_obj.T,);
   {? TYPYDOK.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         {? TYPYDOK.KOD<>_obj.KOD
         || {? TYPYDOK.count()>0
            || _obj.KOD:=TYPYDOK.KOD;
               {? _result.MSG<>''
               || _result.MSG+=' '
               ?};
               _result.MSG+='Nie można zmienić numeracji typu dokumentu %1'@[TYPYDOK.T]
            ?}
         ?};
         exec('record','xls_typydok',_obj,_mode,_result);
         {? exec('chk_typd','typydok',_result.ACTION)<>'' || _result.RESULT:=0 ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_typydok',_obj,_mode,_result);
      {? exec('chk_typd','typydok',_result.ACTION)<>'' || _result.RESULT:=0 ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TYPYDOK.add(1)
   |? _validate.ACTION='put'
   || _result:=TYPYDOK.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:={? _validate.ACTION='add'
                  || 'Dodanie typu dokumentu %1 nie powiodła się.'@[TYPYDOK.T]
                  || 'Modyfikacja typu dokumentu %1 nie powiodła się.'@[TYPYDOK.T]
                  ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'T','STRING[8]','Typ'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[60]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'T',,);

TYPYDOK.cntx_psh();
TYPYDOK.index('TYP');
TYPYDOK.prefix();
{? TYPYDOK.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$TYPYDOK.ref();
      _selected.prefix($TYPYDOK.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.T:=TYPYDOK.T;
      _tab.NAZ:=TYPYDOK.NAZ;
      _tab.add();
      TYPYDOK.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'T,NAZ',20,'Wybór typów dokumentów do eksportu'@,1);
{? _result>0
|| _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TYPYDOK.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 b2d87932a6711a5da7018e2c6d80e559e0bb29bbbc99e8dac02918ec8a64be4717d49ee8b19cb1c8302b15b39fc3d8da4c7f933b555b513ab1d6a01a625e914ecc0db58df3476c45933bbe2cf3c5c1f0dcd514a73e9ba6d8a527caeabfc43b961a18db0dfef797932bc57c7da6b7a9e79c6779522fc0537cda5461beef1d929c

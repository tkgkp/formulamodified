:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_wal.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu walut
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='WAL';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_KRRS';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Waluty=1,1';
_def.NAME:='Waluty'@;
_def.DESC:='Waluty';
_def.TABLE:="exec('table','xls_wal')";
_def.FIELDS:="exec('fields','xls_wal',_a)";
_def.EXPORT:="exec('export','xls_wal',_a,_b)";
_def.IMPORT:="exec('import','xls_wal',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_wal',_a,_b,_c)";
_def.SELECT:="exec('select','xls_wal',_a)";
_def.PREFIX:="WAL.index('WAL_KOD'); WAL.prefix()";
_def.BEFORE:="WAL.cntx_psh()";
_def.AFTER:="WAL.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
WAL.index('WAL_KOD');
WAL.prefix();
WAL


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod',0,,'Trzycyfrowe oznaczenie waluty');
_def.define('SYM','Symbol',1,,'Trzyliterowy symbol waluty');
_def.define('J','Jednostka',1,,'Przelicznik dla waluty');
_def.define('NAZ','Nazwa',1,,'20 znakowa Nazwa waluty');
_def.define('KRAJ','Kraj',1,,'20 znakowy Kraj związany z walutą');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
WAL.prefix();
{? _validate.ACTION='add'
||
   WAL.blank()
?};

WAL.KOD:=_obj.KOD;
WAL.SYM:=_obj.SYM;
WAL.J:=_obj.J;
WAL.NAZ:=_obj.NAZ;
WAL.KRAJ:=_obj.KRAJ;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=WAL.KOD;
_table.SYM.VALUE:=WAL.SYM;
_table.J.VALUE:=WAL.J;
_table.NAZ.VALUE:=WAL.NAZ;
_table.KRAJ.VALUE:=WAL.KRAJ;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
WAL.index('WAL_SYM');
WAL.prefix(_obj.SYM,);
{? WAL.first()
||
   {? _mode=0
   || _result.RESULT:=0
   ||
      _result.ACTION:='put';
      exec('record','xls_wal',_obj,_mode,_result);
      {? params_exec('chk_wal','slo_slu')<>''
      ||
         _result.RESULT:=0
      ?}
   ?}
||
   _result.ACTION:='add';
   exec('record','xls_wal',_obj,_mode,_result);
   {? params_exec('chk_wal','slo_slu')<>''
   || _result.RESULT:=0
   ?}
?};



_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=WAL.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=WAL.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie waluty o symbolu %1 nie powiodło się.'@[_obj.SYM])
   ||
      FUN.info('Poprawa waluty o symbolu %1 nie powiodła się.'@[_obj.SYM])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[20]','Nazwa'
   ,'SYM','STRING[3]','Symbol'
   ,'KRAJ','STRING[20]','Kraj');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZ',,);

WAL.cntx_psh();
WAL.index('WAL_KOD');
WAL.prefix();
{? WAL.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$WAL.ref();
      _selected.prefix($WAL.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.SYM:=WAL.SYM;
      _tab.NAZ:=WAL.NAZ;
      _tab.KRAJ:=WAL.KRAJ;
      _tab.add();
      WAL.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NAZ,SYM,KRAJ',20,'Wybór waluty do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
WAL.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 a01e422a5bfc11cdff10bf473c9d960ec9d460c615c78c2a5fb3feaa636c162d24888fbfb3a3212e030e225c048c10cce97304589b04e112b03d660b31bebc78bea9470df719ff258f339804853b2f0ec0b0eec084c31933fdc9d6e79d92ce4e8e0dae08b76d95db0d853f104dc8f42cd4361dd6cba889bedb83595b8207991d

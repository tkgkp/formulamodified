:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ppsf_sn.fml
:: Utworzony: 19.04.2023
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu nagłówków sposobów rozliczania pracy zdalnej
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='PPSF_SN';
_def.FUNPAR:='ZWS_PAR_PPSS';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:=exec('def_per_ppl_xlsx','xls__init');
_def.SHEET:='Naglówki spos. roz. pracy zd.=1,1';
_def.NAME:='Naglówki sposobów rozl. pracy zd.'@;
_def.DESC:='Naglówki sposobów rozliczania pracy zdalnej';

: źródło danych
_def.TABLE:="PPSF_SN";

: prolog
_def.BEFORE:="
   PPSF_SN.cntx_psh();
   PPSF_SN.index('KOD');
   PPSF_SN.prefix(exec('firma','ustawienia'));
   ~~
";

: epilog
_def.AFTER:="
   PPSF_SN.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _tStr:=type_of('');
   _tInt:=type_of(0);
   _env.define('KOD',    MS.name(PPSF_SN,'KOD'),1,,    MS.comment(PPSF_SN,'KOD'),_tStr);
   _env.define('NAZWA',  MS.name(PPSF_SN,'NAZWA'),1,,  MS.comment(PPSF_SN,'NAZWA'),_tStr);
   _env.define('SYSTEM', MS.name(PPSF_SN,'SYSTEM'),1,, MS.comment(PPSF_SN,'SYSTEM'),_tStr);
   _env.define('DOM',    MS.name(PPSF_SN,'DOM'),1,,    MS.comment(PPSF_SN,'DOM'),_tStr);
   _env.define('OPIS',   MS.name(PPSF_SN,'OPIS'),1,,   MS.comment(PPSF_SN,'OPIS'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.KOD.VALUE:=PPSF_SN.KOD;
   _buf.NAZWA.VALUE:=PPSF_SN.NAZWA;
   _buf.SYSTEM.VALUE:=PPSF_SN.SYSTEM;
   _buf.DOM.VALUE:=PPSF_SN.DOM;
   _buf.OPIS.VALUE:=PPSF_SN.memo_txt(0,1,'OPIS');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_ppsf_sn',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ppsf_sn',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.KOD=''    || 'KOD'
   |? _buf.NAZWA=''  || 'NAZWA'
   |? _buf.SYSTEM='' || 'SYSTEM'
   |? _buf.DOM=''    || 'DOM'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('NT'*_buf.SYSTEM)=0
|| _val.msg_inset('SYSTEM','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.DOM)=0
|| _val.msg_inset('DOM','N','T');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(PPSF_SN,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(PPSF_SN,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};

: weryfikacja spójności referencyjnej
_ref:=null;
{? PPSF_SN.find_tab(,
      'KOD',,'=',_buf.KOD,
      'FIRMA',,'=',exec('firma','ustawienia')
   )
|| _ref:=PPSF_SN.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 %2'[_a.KOD,_a.NAZWA]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,PPSF_SN,"
   _a.KOD:=_b.KOD;
   _a.NAZWA:=_b.NAZWA;
   _a.SYSTEM:=_b.SYSTEM;
   _a.DOM:=_b.DOM;
   _a.memo_set(_b.OPIS,'OPIS');
   ~~
",'OPIS');
~~

:Sign Version 2.0 jowisz:1045 2023/04/28 15:59:47 030aa77c7d42ce65397fb0340404d06490f9705b3fd1d2a62a45b9e6b159d912d409c326b9ea535e67ca1ebae37cd854433a21e33f10909178b108526511dbe4f3ba244032b1ca5e6da71e4d7f0745261df6b95e3cf290566d28369b7c40a0e3af9cf0821d550c96ec9f5d6792dcd6a8349be8a45c9d10c131bc9851225a29e7

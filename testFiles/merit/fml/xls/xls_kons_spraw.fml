:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_urz.fml
:: Utworzony: 03.09.2018
:: Autor: AF
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu urządzeń
::======================================================================================================================


\filename
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Nazwa pliku do eksportu
::----------------------------------------------------------------------------------------------------------------------
'kons_sprawozdan.xlsx'


\KON_NRAP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KON_NRAP';
_def.FUNPAR:='ZWS_PAR_KRAP';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('filename','xls_kons_spraw');
_def.SHEET:='Lista kontroli wyłączeń'@+'=1,1';
_def.NAME:='Kontrola wyłączeń'@;
_def.DESC:='Kontrola wyłączeń'@;
_def.MULTIFIR:='T';

_def.PREFIX:="KON_NRAP.index('SYM'); KON_NRAP.prefix()";
_def.TABLE:="KON_NRAP";
_def.FIELDS:="exec('KON_NRAP_fld','xls_kons_spraw',_a)";

_def.BEFORE:="KON_NRAP.cntx_psh()";
_def.AFTER:="KON_NRAP.cntx_pop()";
_def.SELECT:="exec('KON_NRAP_select','xls_kons_spraw',_a)";

_def.EXPORT:="exec('KON_NRAP_exp','xls_kons_spraw',_a,_b)";
_def.VALIDATE:="exec('KON_NRAP_valid','xls_kons_spraw',_a,_b,_c)";
_def.IMPORT:="exec('KON_NRAP_imp','xls_kons_spraw',_a,_b,_c)";
~~


\KON_NRAP_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('SYM','Symbol',1,,'20 znakowy symbol');
_def.define('OPIS','Opis',1,,'255 znakowy opis');
~~


\KON_NRAP_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_result:=0;

_tab:=tab_tmp(1,'SYM','STRING[20]','Symbol'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OPIS','STRING[255]','Opis');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'SYM',,,'OPIS',,);

KON_NRAP.cntx_psh();
KON_NRAP.index('SYM');
KON_NRAP.prefix();
{? KON_NRAP.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$KON_NRAP.ref();
      _selected.prefix($KON_NRAP.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.SYM:=KON_NRAP.SYM;
      _tab.OPIS:=KON_NRAP.OPIS;
      _tab.add();

      KON_NRAP.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'SYM,OPIS',40,'Wybór definicjii do eksportu'@,1);
{? _result>0
|| _selected.erase();
   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KON_NRAP.cntx_pop();
_result


\KON_NRAP_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.SYM.VALUE:=KON_NRAP.SYM;
_table.OPIS.VALUE:=KON_NRAP.OPIS;

_result


\KON_NRAP_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1;

KON_NRAP.index('SYM');
KON_NRAP.prefix(_table.SYM,);
{? KON_NRAP.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT=1
|| {? _result.ACTION='add' || KON_NRAP.blank() ?};
   KON_NRAP.SYM:=_table.SYM;
   KON_NRAP.OPIS:=_table.OPIS
?};
~~


\KON_NRAP_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;

{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KON_NRAP.add(1)
   |? _validate.ACTION='put'
   || _result:=KON_NRAP.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie definicji ontroli wyłączeń %1 nie powiodło się.'@[_table.SYM]
   || _validate.MSG:='Poprawa definicji ontroli wyłączeń %1 nie powiodła się.'@[_table.SYM]
   ?}
?};
_result


\KON_PRAP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KON_PRAP';
_def.FUNPAR:='';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('filename','xls_kons_spraw');
_def.SHEET:='Lista pozycji kontroli wyłączeń'@+'=1,1';
_def.NAME:='Pozycja kontroli wyłączeń'@;
_def.DESC:='Pozycja kontroli wyłączeń'@;

_def.PREFIX:="KON_PRAP.index('KON_PRAP'); KON_PRAP.prefix()";
_def.TABLE:="KON_PRAP";
_def.FIELDS:="exec('KON_PRAP_fld','xls_kons_spraw',_a)";

_def.BEFORE:="KON_PRAP.cntx_psh()";
_def.AFTER:="KON_PRAP.cntx_pop()";
_def.SELECT:="exec('KON_PRAP_select','xls_kons_spraw',_a)";

_def.EXPORT:="exec('KON_PRAP_exp','xls_kons_spraw',_a,_b)";
_def.VALIDATE:="exec('KON_PRAP_valid','xls_kons_spraw',_a,_b,_c)";
_def.IMPORT:="exec('KON_PRAP_imp','xls_kons_spraw',_a,_b,_c)";
~~


\KON_PRAP_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('KON_NRAP','Nagłówek',1,,'Wskazanie na nagłówek');
_def.define('TYP','Typ',1,,'1 znakowy znacznik typu pozycji definicji raportu dla kontroli N - Pionowa Z - Pozioma');
_def.define('TYPOPIS','Opis typu',1,,'10 znakowy opis typu');
_def.define('GR_SIK','Sprawozdanie',1,,'Wskazanie na sprawozdanie');
_def.define('GR_KOL','Kolumna',1,,'Wskazanie na kolumnę sprawozdania',type_of(0),0);
_def.define('DEFW','Wiersz',1,,'Wskazanie na wiersz sprawozdania');
_def.define('WSP','Współczynnik',1,,'27 znakowu współczynnik');
_def.define('FIRMA','Firma',1,,'Wskazanie na firmę');
~~


\KON_PRAP_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_result:=0;

_tab:=tab_tmp(1,'KON_NRAP','STRING[20]','Nagłowek'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'TYP','STRING[1]','Typ'
   ,'TYPOPIS','STRING[10]','Opis typu'
   ,'GR_SIK','STRING[10]','Sprawozdanie');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KON_NRAP',,,'TYP',,,'TYPOPIS',,,'GR_SIK',,);

KON_PRAP.cntx_psh();
KON_PRAP.index('KON_PRAP');
KON_PRAP.prefix();
{? KON_PRAP.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$KON_PRAP.ref();
      _selected.prefix($KON_PRAP.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KON_NRAP:=KON_PRAP.KON_NRAP().SYM;
      _tab.TYP:=KON_PRAP.TYP;
      _tab.TYPOPIS:=KON_PRAP.TYPOPIS;
      _tab.GR_SIK:=KON_PRAP.GR_SIK().NAZWA;
      _tab.add();

      KON_PRAP.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'KON_NRAP,TYP,TYPOPIS,GR_SIK',40,'Wybór pozycji do eksportu'@,1);
{? _result>0
|| _selected.erase();
   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KON_PRAP.cntx_pop();
_result


\KON_PRAP_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::  OLD: \KART_ZAS_exp/xls_zasoby.fml
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.FIRMA.VALUE:=KON_PRAP.GR_SIK().FIRMA().SYMBOL;
_table.KON_NRAP.VALUE:=KON_PRAP.KON_NRAP().SYM; _excel.write_async('KON_NRAP',KON_PRAP.KON_NRAP);
_table.TYP.VALUE:=KON_PRAP.TYP;
_table.TYPOPIS.VALUE:=KON_PRAP.TYPOPIS;
_table.GR_SIK.VALUE:=KON_PRAP.GR_SIK().SKROT;

_excel.write_async('GR_SIK',KON_PRAP.GR_SIK);

_table.GR_KOL.VALUE:=KON_PRAP.GR_KOL().LP; _excel.write_async('GR_KOL',KON_PRAP.GR_KOL);
_table.DEFW.VALUE:=KON_PRAP.DEFW().KOD; _excel.write_async('DEFW',KON_PRAP.DEFW);
_table.WSP.VALUE:=KON_PRAP.WSP;

_result


\KON_PRAP_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1;

{? _table.FIRMA<>''
|| _firma:=exec('FindInSet','#table','FIRMA','SYMBOL',_table.FIRMA,,,1)
|| _firma:=null
?};

_nagspraw:=null;
_nagdef:=null;
_kolspraw:=null;
_wierspraw:=null;

{? _table.KON_NRAP<>'' & (_nagdef:=exec('FindInSet','#table','KON_NRAP','SYM',_table.KON_NRAP,,,1))=null
|| _result.MSG:='Nie znaleziono nagłówka definicji: %1'@[_table.KON_NRAP];
   _result.RESULT:=0
|? _table.GR_SIK<>'' &  (_nagspraw:=exec('FindInSet','#table','GR_SIK','SKROT',_table.GR_SIK,_firma,,1))=null
|| _result.MSG:='Nie znaleziono sprawozdania definiowalnego: %1'@[_table.GR_SIK];
   _result.RESULT:=0
|? $_table.GR_KOL<>'' &  (_kolspraw:=exec('FindInSet','#table','GR_KOL','LP',_table.GR_KOL,_nagspraw,,0))=null
|| _result.MSG:='Nie znaleziono kolumny sprawozdania definiowalnego: %1'@[_table.GR_SIK];
   _result.RESULT:=0
|? _table.DEFW<>'' &  (_wierspraw:=exec('FindInSet','#table','DEFW','GRUPA',_table.DEFW,_nagspraw,,1))=null
|| _result.MSG:='Nie znaleziono wiersza sprawozdania definiowalnego: %1'@[_table.DEFW];
   _result.RESULT:=0

|| KON_PRAP.index('KON_PRAP');
   KON_PRAP.prefix(_nagdef,_table.TYP,_nagspraw,_kolspraw,_wierspraw,);
   {? KON_PRAP.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || KON_PRAP.blank() ?};
      KON_PRAP.KON_NRAP:=_nagdef;
      KON_PRAP.TYP:=_table.TYP;
      KON_PRAP.TYPOPIS:=_table.TYPOPIS;
      KON_PRAP.GR_SIK:=_nagspraw;
      KON_PRAP.GR_KOL:=_kolspraw;
      KON_PRAP.DEFW:=_wierspraw;
      KON_PRAP.WSP:=_table.WSP

::      {? exec('spr_an_zas','zasoby_wspolne')<>''
::      || _result.RESULT:=0
::      ?}
   ?}
?};
~~


\KON_PRAP_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;

{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KON_PRAP.add(1)
   |? _validate.ACTION='put'
   || _result:=KON_PRAP.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie pozycji raportu kontrolnego do wyłączeń %1 nie powiodło się.'@[_table.KON_NRAP]
   || _validate.MSG:='Poprawa pozycji raportu kontrolnego do wyłączeń %1 nie powiodła się.'@[_table.KON_NRAP]
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 99000dcf5aef9698da4bb1da182281b0b738dfa25d78cfcc6df67e8039bff29c82f2b26c9e6c708f281c584682b35ab5aa658ee48a778eb39224734de45f2e5d5c31e48dc8dd8c1a7f276ecd1e4543a80b45d8e1171433cacc5aa54d83b18a6fa7e21f76b6996dd700d91c5601e4a31935ed1f1309757281e53e9d0dc56ebb7f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ppsf_sp.fml
:: Utworzony: 19.04.2023
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu pozycji sposobów rozliczania pracy zdalnej
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='PPSF_SP';
_def.FUNPAR:='ZWS_PAR_PPSS';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:=exec('def_per_ppl_xlsx','xls__init');
_def.SHEET:='Pozycje sposobów roz. pracy zd.=1,1';
_def.NAME:='Pozycje sposobów roz. pracy zd.'@;
_def.DESC:='Pozycje sposobów rozliczeń pracy zdalnej';

: źródło danych
_def.TABLE:="PPSF_SP";

: prolog
_def.BEFORE:="
   ROK_F.cntx_psh();
   ROK_F.prefix();
   KK.cntx_psh();
   KK.prefix();
   R.cntx_psh();
   R.prefix();
   PPSF_SN.cntx_psh();
   PPSF_SN.prefix();
   PPSF_SP.cntx_psh();
   PPSF_SP.index('FNR');
   PPSF_SP.prefix(exec('firma','ustawienia'));
   ~~
";

: epilog
_def.AFTER:="
   PPSF_SP.cntx_pop();
   PPSF_SN.cntx_pop();
   R.cntx_pop();
   KK.cntx_pop();
   ROK_F.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _tStr:=type_of('');
   _tInt:=type_of(0);
   _env.define('PPSF_SN',  MS.name(PPSF_SN,'KOD'),1,,      MS.comment(PPSF_SN,'KOD'),     _tStr);
   _env.define('R',        MS.name(PPSF_SP,'R'),1,,        MS.comment(PPSF_SP,'R'),       _tInt);
   _env.define('SP_ROZL',  MS.name(PPSF_SP,'SP_ROZL'),1,,  MS.comment(PPSF_SP,'SP_ROZL'), _tStr);
   _env.define('KW',       MS.name(PPSF_SP,'KW'),1,,       MS.comment(PPSF_SP,'KW'),      _tInt);
   _env.define('FORMULA',  MS.name(PPSF_SP,'FORMULA'),1,,  MS.comment(PPSF_SP,'FORMULA'), _tStr);
   _env.define('INDYW',    MS.name(PPSF_SP,'INDYW'),1,,    MS.comment(PPSF_SP,'INDYW'),   _tStr);
   _env.define('POMN',     MS.name(PPSF_SP,'POMN'),1,,     MS.comment(PPSF_SP,'POMN'),    _tStr);
   _env.define('OKR_ROZL', MS.name(PPSF_SP,'OKR_ROZL'),1,, MS.comment(PPSF_SP,'OKR_ROZL'),_tStr);
   _env.define('WYM',      MS.name(PPSF_SP,'WYM'),1,,      MS.comment(PPSF_SP,'WYM'),     _tStr);
   _env.define('ROK_F',    MS.name(KK,'ROK_F'),1,,         MS.comment(KK,'ROK_F'),        _tStr);
   _env.define('SYSTEM',   MS.name(KK,'SYSTEM'),1,,        MS.comment(KK,'SYSTEM'),       _tStr);
   _env.define('KK',       MS.name(PPSF_SP,'KK'),1,,       MS.comment(PPSF_SP,'KK'),      _tStr);
   _env.define('R_LG',     MS.name(PPSF_SP,'R_LG'),1,,     MS.comment(PPSF_SP,'R_LG'),    _tInt);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.PPSF_SN.VALUE:=PPSF_SP.PPSF_SN().KOD;
   _buf.R.VALUE:=PPSF_SP.R().RN;
   _buf.SP_ROZL.VALUE:=PPSF_SP.SP_ROZL;
   _buf.KW.VALUE:=PPSF_SP.KW;
   _buf.FORMULA.VALUE:=PPSF_SP.FORMULA;
   _buf.INDYW.VALUE:=PPSF_SP.INDYW;
   _buf.POMN.VALUE:=PPSF_SP.POMN;
   _buf.OKR_ROZL.VALUE:=PPSF_SP.OKR_ROZL;
   _buf.WYM.VALUE:=PPSF_SP.WYM;
   _buf.ROK_F.VALUE:=PPSF_SP.KK().ROK_F().NAZ;
   _buf.SYSTEM.VALUE:=PPSF_SP.KK().SYSTEM;
   _buf.KK.VALUE:=PPSF_SP.KK().SYM;
   _buf.R_LG.VALUE:=PPSF_SP.R_LG().RN;
   _env.write_async('PPSF_SN',PPSF_SP.PPSF_SN);
   _env.write_async('RUBRYKI',exec('r','xls__util'));
   _env.write_async('KK',PPSF_SP.KK);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_ppsf_sp',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ppsf_sp',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.PPSF_SN=''                 || 'PPSF_SN'
   |? _buf.R=0                        || 'R'
   |? _buf.SP_ROZL=''                 || 'SP_ROZL'
   |? _buf.KW=0 & _buf.INDYW='N'      || 'KW'
   |? _buf.INDYW=''                   || 'INDYW'
   |? _buf.POMN='' & _buf.SP_ROZL='M' || 'POMN'
   |? _buf.OKR_ROZL=''                || 'OKR_ROZL'
   |? _buf.WYM=''                     || 'WYM'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('GDM'*_buf.SP_ROZL)=0
|| _val.msg_inset('SP_ROZL','G','D','M');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.INDYW)=0
|| _val.msg_inset('INDYW','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.POMN)=0 & _buf.SP_ROZL='M'
|| _val.msg_inset('POMN','N','T');
   _val.RESULT:=0;
   return()
|? ('BP'*_buf.OKR_ROZL)=0
|| _val.msg_inset('OKR_ROZL','B','P');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.WYM)=0
|| _val.msg_inset('WYM','N','T');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(PPSF_SP,'FORMULA');
{? +_buf.FORMULA>_max
|| _val.msg_length('FORMULA',_max);
   _buf.FORMULA:=_max+_buf.FORMULA
?};

_firma:=exec('firma','ustawienia');
_rf:=null;
{? _buf.ROK_F<>''
|| {? ~ROK_F.find_tab(,
         'FIRMA',,'=',_firma,
         'NAZ',,'=',_buf.ROK_F
      )
   || _val.msg_norec('ROK_F');
      _val.RESULT:=0;
      return()
   || _rf:=ROK_F.ref()
   ?}
?};
_kk:=null;
{? _buf.KK<>''
|| {? ~KK.find_tab(,
         'FIRMA',,'=',_firma,
         'SYSTEM',,'=',_buf.SYSTEM,
         'ROK_F',,'=',_rf,
         'SYM',,'=',_buf.KK
      )
   || _val.msg_norec('KK');
      _val.RESULT:=0;
      return()
   || _kk:=KK.ref()
   ?}
?};
_ppsfSn:=null();
{? _buf.PPSF_SN<>''
|| {? ~PPSF_SN.find_tab(,
         'FIRMA',,'=',_firma,
         'KOD',,'=',_buf.PPSF_SN
      )
   || _val.msg_norec('PPSF_SN');
      _val.RESULT:=0;
      return()
   || _ppsfSn:=PPSF_SN.ref()
   ?}
?};
_rub:=null();
{? _buf.R<>0
|| {? ~R.find_tab(,
         'RN',,'=',_buf.R
      )
   || _val.msg_norec('R');
      _val.RESULT:=0;
      return()
   || _rub:=R.ref()
   ?}
?};
_rLg:=null();
{? _buf.R_LG<>0
|| {? ~R.find_tab(,
         'RN',,'=',_buf.R_LG
      )
   || _val.msg_norec('R_LG');
      _val.RESULT:=0;
      return()
   || _rLg:=R.ref()
   ?}
?};

: weryfikacja spójności referencyjnej
_ref:=null;
{? PPSF_SP.find_tab(,
      'PPSF_SN','KOD','=',_buf.PPSF_SN,
      'PPSF_SN','FIRMA','=',_firma,
      'R',,'=',_rub
   )
|| _ref:=PPSF_SP.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 %2'[_a.PPSF_SN,_a.R]",
   'KK',_kk,
   'PPSF_SN',_ppsfSn,
   'R',_rub,
   'R_LG',_rLg
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,PPSF_SP,"
   _a.PPSF_SN:=_c.PPSF_SN;
   _a.R:=_c.R;
   _a.SP_ROZL:=_b.SP_ROZL;
   _a.KW:=_b.KW;
   _a.FORMULA:=_b.FORMULA;
   _a.INDYW:=_b.INDYW;
   _a.POMN:=_b.POMN;
   _a.OKR_ROZL:=_b.OKR_ROZL;
   _a.WYM:=_b.WYM;
   _a.KK:=_c.KK;
   _a.R_LG:=_c.R_LG;
   ~~
");
~~

:Sign Version 2.0 jowisz:1045 2023/04/28 15:59:47 4bc48c8b0e2b35af3a91628ca2d521a704067c827503b42bf936e93bbf8e2082a3e7984f4361b000bf3f87883373e41d147bf5d8c7218a165c5f5159387bfa15d895ee090d9c11b7ba5c636e10c2a0a42998d1a1289a2f4e54f6ad1698124ecc6a141fcb5d5b36a549b89faa2af1251adba7fb1cf7f3accbb81131daa6a50fe3

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_s_zus.fml
:: Utworzony: 10.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu kodów ZUS
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='S_ZUS';
_def.FUNPAR:='ZWS_PAR_PZUS';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Kody ZUS=1,1';
_def.NAME:='Kody ZUS'@;
_def.DESC:='Kody ZUS';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="S_ZUS";

: prolog
_def.BEFORE:="
   S_ZUS.cntx_psh();
   S_ZUS.f_clear();
   S_ZUS.index('S_ZUS');
   S_ZUS.prefix()
";

: epilog
_def.AFTER:="
   S_ZUS.cntx_pop()
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('RODZAJ',MS.name(S_ZUS,'RODZAJ'),1,,MS.comment(S_ZUS,'RODZAJ'));
   _env.define('KOD',   MS.name(S_ZUS,'KOD'),   1,,MS.comment(S_ZUS,'KOD'));
   _env.define('OPIS',  MS.name(S_ZUS,'OPIS'),  1,,MS.comment(S_ZUS,'OPIS'),type_of(''));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.RODZAJ.VALUE:=S_ZUS.RODZAJ;
   _buf.KOD.VALUE:=S_ZUS.KOD;
   _buf.OPIS.VALUE:=S_ZUS.memo_txt(,1,'OPIS');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_s_zus',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_s_zus',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.RODZAJ='' || 'RODZAJ'
   |? _buf.KOD=''    || 'KOD'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(S_ZUS,'RODZAJ');
{? +_buf.RODZAJ>_max
|| _val.msg_length('RODZAJ',_max);
   _buf.RODZAJ:=_max+_buf.RODZAJ
?};
_max:=MS.fld_len(S_ZUS,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};

_ref:=null;
{? S_ZUS.find_tab(,'RODZAJ',,'=',_buf.RODZAJ,'KOD',,'=',_buf.KOD)
|| _ref:=S_ZUS.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('S_ZUS','to_str');
_obj.to_str:="'%1: %2'[_a.RODZAJ,_a.KOD]";
_obj.S_ZUS:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.RODZAJ:=_b.RODZAJ;
   _a.KOD:=_b.KOD;
   _a.memo_set(_b.OPIS,'OPIS');
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? S_ZUS.seek(_obj.S_ZUS)
   || _set(S_ZUS,_buf);
      {? ~S_ZUS.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      || S_ZUS.memo_put(,'OPIS')
      ?}
   || _val.msg_norec(,S_ZUS.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| S_ZUS.blank();
   _set(S_ZUS,_buf);
   {? ~S_ZUS.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   || S_ZUS.memo_put(,'OPIS')
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 b8c7e4ddb602f790ba170a80e52d7dafc1f0c9e387e0a22681e40acb2db85c2d17407ab19627bf0bfcae55383813b59a3fe5123b644e928b9a53b57b7e195f9e28ecc573cd8de301236f4494b9deecbfff5d5d57ead3f50ea69e723292e4f534dc7b33886c6fd5ff72d1ed980af808f5b1941e93fd3a5133ccc100dd5240228f

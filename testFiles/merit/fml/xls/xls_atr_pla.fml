:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_atr_pla.fml
:: Utworzony: 07.02.2021
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu atrybutów płacowych.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Inicjowanie mechanizmu.
::   WE: _a [OBJECT] - Środowisko mechanizmu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='RA_DEF';
_def.FUNPAR:='ZWS_PAR_PSKT';
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.NAME:='Atrybuty płacowe'@;
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.SHEET:='Atrybuty płacowe'@+'=1,1';
_def.DESC:='Definicje atrybutów płacowych'@;
_def.MULTIFIR:='T';

_def.TABLE:="RA_DEF";
_def.BEFORE:="
   RA_DEF.f_clear();
   RA_DEF.index('RA_DEF');
   RA_DEF.prefix(0,1,'S',)
";
_def.AFTER:="RA_DEF.prefix()";
_def.PREFIX:="";

_def.FIELDS:="
   _fld:=_a;

   _fld.define('FILE','Nazwa pliku',0,,'Nazwa pliku zawierającego definicje atrybutów płacowych',type_of(''));
   ~~
";

_def.SELECT:="";

_def.EXPORT:="
   _excel:=_a;
   _buf:=_b;

   _buf.FILE.VALUE:='atrybuty_placowe.txt';

   exec('atrplaimex_decl','rubatr');
   _atr:=obj_new(@.CLASS.AtrPlaImEx);
   _ret:=_atr.export({? _excel.DIR_ON_SERVER || '' || '@' ?}
                     +_excel.DIR+exec('sep','#file', {? _excel.DIR_ON_SERVER || 2 || 0 ?})+_buf.FILE.VALUE);

   _excel.write_async('RUBRYKI',exec('r','xls__util'));

   _ret=''
";

_def.VALIDATE:="exec('validate','xls_atr_pla',_a,_b,_c)";
_def.IMPORT:="1";
::_def.IMPORT:="exec('import','xls_atr_pla',_a,_b,_c)";

~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Formuła sprawdza, czy można poprawić/dodać rekord.
::   WE: _a [OBJECT] - Obiekt z polami odczytami z Excela.
::       _b [NUMBER] - Tryb aktualizacji: 0 - nie (dodawane są tylko nowe rekordy) / 1 - tak.
::       _c [OBJECT] - Obiekt (RESULT, ACTION, OBJ) - wynik walidacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_update:=_b;
_validate:=_c;

_validate.OBJ:=obj_new('FN');

{? _buf.FILE=''
|| _validate.msg_empty('FILE');
   _validate.RESULT:=0
|? _fn:={? _validate.env.DIR_ON_SERVER || '' || '@' ?}+_validate.env.DIR+exec('sep','#file')+_buf.FILE;
   fexists(_fn)<>1
|| _validate.msg_value('FILE','Brak dostępu do pliku %1.'[1-_fn]);
   _validate.RESULT:=0
|? exec('atrplaimex_decl','rubatr');
   _atr:=obj_new(@.CLASS.AtrPlaImEx);
   _ret:=_atr.add_attr(_fn,'S*,U*',_update);
   _ret<>''
|| _validate.MSG:=_ret;
   _validate.RESULT:=0
?};

~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 1f0e6e7d5fcf8a96db96dc59d4b49dc60c4fc293de7dd41d111a567277aaddee37a844ac53b3a720be5411bd671e2525b2b9705880540eeced83f2ec8ab1735f8f26bb46e458286c298cca620ac67c2e58f72935d6d26ea2600e299b3050fbabba02f6b89221baad0f63692dd7148a51cfa68f540f13b3d9c6d1de93b5ad3727

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ppk_xel.fml
:: Utworzony: 08.07.2020
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu opisów danych PPK
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='PPK_XEL';
_def.FUNPAR:='ZWS_PAR_PXWE';
_def.DOMAIN:=exec('name','#b_domain','PPK');
_def.FILE:=exec('def_per_ppk_xlsx','xls__init');
_def.SHEET:='Opis danych PPK'@+'=1,1';
_def.NAME:='Opis danych PPK'@;
_def.DESC:='Opis danych PPK'@;
_def.ADD_ROWS:=0;
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="PPK_XEL";

: prolog
_def.BEFORE:="
   PPK_XEL.cntx_psh();
   PPK_XEL.index('SYMBOL');
   PPK_XEL.prefix();
   ~~
";

: epilog
_def.AFTER:="
   PPK_XEL.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYMBOL',MS.name(PPK_XEL,'SYMBOL'),0,,MS.comment(PPK_XEL,'SYMBOL'));
   _env.define('OPIS',  MS.name(PPK_XEL,'OPIS'),0,,  MS.comment(PPK_XEL,'OPIS'));
   _env.define('UWAGI', MS.name(PPK_XEL,'UWAGI'),0,, MS.comment(PPK_XEL,'UWAGI'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYMBOL.VALUE:=PPK_XEL.SYMBOL;
   _buf.OPIS.VALUE:=PPK_XEL.OPIS;
   _buf.UWAGI.VALUE:=PPK_XEL.memo_txt(,1,'UWAGI');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_ppk_xel',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ppk_xel',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYMBOL=''
   || 'SYMBOL'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(PPK_XEL,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};

_max:=MS.fld_len(PPK_XEL,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

: wybór akcji
_ref:=null;
{? PPK_XEL.find_key(_buf.SYMBOL)
|| _ref:=PPK_XEL.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[_a.SYMBOL]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,PPK_XEL,"
   _a.SYMBOL:=_b.SYMBOL;
   _a.OPIS:=_b.OPIS;
   _a.memo_set(_b.UWAGI,'UWAGI');
   ~~
",'UWAGI');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 5f2f01bdff52ac69b3d25315f9491d6e5e0b4673ec918835099d7e795fc611f8482ce7e3f7560d0b256fbee1b5160997c58c3a36d5defcb68981f9dd845014d30797b8e1c7bbe224024ee04b93b275339f5d02787d5482b35bbfa2eb96d88055e03738225d5b38530774db3a0586c3cd15e8be97d1ee0b0f79c38e70ccc6a8c3

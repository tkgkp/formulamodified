:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_blink.fml
:: Utworzony: 02.10.2020
:: Autor: AMK
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi parametryzacji Businesslink
::======================================================================================================================


\filename
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Nazwa pliku do eksportu parametryzacji Businesslink
::----------------------------------------------------------------------------------------------------------------------
'parametryzacja_BLMT.xlsx'


\BL_TYPYSP_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - S - typy dokumentów sprzedaży (domyślnie), Z - typy dokumentów zakupu
::----------------------------------------------------------------------------------------------------------------------
_rodz:='S';
{? var_pres('_b')=type_of('') & (_b='S' | _b='Z')
|| _rodz:=_b
?};

_def:=_a;
{? _rodz='Z'
|| _def.ID:='BL_TD_Z';
   _def.FUNPAR:='ZWS_PAR_BLDZ'
|| _def.ID:='BL_TD_S';
   _def.FUNPAR:='ZWS_PAR_BLDS'
?};
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('filename','xls_blink');

{? _rodz='Z'
|| _def.SHEET:='Typy dokumentów zakupu BL=1,1';
   _def.NAME:='Typy dokumentów zakupu Businesslink';
   _def.DESC:='Typy dokumentów zakupu Businesslink'
|| _def.SHEET:='Typy dokumentów sprzedaży BL=1,1';
   _def.NAME:='Typy dokumentów sprzedaży Businesslink';
   _def.DESC:='Typy dokumentów sprzedaży Businesslink'
?};

_def.TABLE:="TYPYSP";
_def.FIELDS:="exec('BL_TYPYSP_fld','xls_blink',_a)";

_def.BEFORE:=$('exec(\'BL_TYPYSP_before\',\'xls_blink\',_a,\''+_rodz+'\')');
_def.AFTER:=$('exec(\'BL_TYPYSP_after\',\'xls_blink\',_a,\''+_rodz+'\')');

{? _rodz='Z'
|| _def.PREFIX:="TYPYSP.index('BL1'); TYPYSP.prefix('T','T')"
|| _def.PREFIX:="TYPYSP.index('BL1'); TYPYSP.prefix('T','N')"
?};
_def.SELECT:="";
_def.EXPORT:="exec('BL_TYPYSP_exp','xls_blink',_a,_b)";

_def.VALIDATE:=$('exec(\'BL_TYPYSP_valid\',\'xls_blink\',_a,_b,_c,\''+_rodz+'\')');
_def.IMPORT:=$('exec(\'BL_TYPYSP_imp\',\'xls_blink\',_a,_b,_c,\''+_rodz+'\')');
~~


\BL_TYPYSP_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('T','Typ',1,,'');
_def.define('NAZ','Nazwa dokumentu',1,,'');
~~


\BL_TYPYSP_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.T.VALUE:=TYPYSP.T;
_table.NAZ.VALUE:=TYPYSP.NAZ;
_result


\BL_TYPYSP_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::       _d - S - typy dokumentów sprzedaży (domyślnie), Z - typy dokumentów zakupu
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_rodz:='S';
{? var_pres('_d')=type_of('') & (_d='S' | _d='Z')
|| _rodz:=_d
?};

_result.RESULT:=1;

{? _table.T=''
|| _result.MSG:='Nie podano typu dokumentu.'@;
   _result.RESULT:=0
?};

{? _result.RESULT
|| TYPYSP.index('SPRZAK');
   {? _rodz='Z'
   || TYPYSP.prefix('T',_table.T,)
   || TYPYSP.prefix('N',_table.T,)
   ?};
   {? TYPYSP.first()
   || BLINK_IMP_TYPY+=TYPYSP.T+' ';
      {? TYPYSP.BL<>'T'
      || _result.ACTION:='put'
      ?}
   || _result.MSG:='Brak typu dokumentu %1.'@[_table.T];
      _result.RESULT:=0
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='put'
      || TYPYSP.BL:='T'
      ?}
   ?}
?};
~~


\BL_TYPYSP_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - S - typy dokumentów sprzedaży (domyślnie), Z - typy dokumentów zakupu
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_rodz:='S';
{? var_pres('_d')=type_of('') & (_d='S' | _d='Z')
|| _rodz:=_d
?};
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='put'
   || _result:=TYPYSP.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='put'
   || _validate.MSG:='Poprawa typu %1 nie powiodła się.'@[_table.T]
   ?}
?};
_result


\BL_TYPYSP_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Przed importem z Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
{? _def.KIND='IMPORT'
|| VAR_DEL.delete('BLINK_IMP_TYPY');
   BLINK_IMP_TYPY:=''
?};
TYPYSP.cntx_psh()


\BL_TYPYSP_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Po imporcie z Excel
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - S - typy dokumentów sprzedaży (domyślnie), Z - typy dokumentów zakupu
::----------------------------------------------------------------------------------------------------------------------
_rodz:='S';
{? var_pres('_b')=type_of('') & (_b='S' | _b='Z')
|| _rodz:=_b
?};
_def:=_a;

{? _def.KIND='IMPORT'
|| {?_def.excel.IMP_MODE
   || TYPYSP.index('SPRZAK');
      {? _rodz='Z'
      || TYPYSP.prefix('T')
      || TYPYSP.prefix('N')
      ?};
      {? TYPYSP.first()
      || _vv:=exec('args_valid','#excel_imex',_def.excel);
         {!
         |? {? TYPYSP.BL='T' & (BLINK_IMP_TYPY+' ')*(TYPYSP.T+' ')=0
            || _msg:='Typ dokumentu %1'@[TYPYSP.T];
               TYPYSP.BL:='N';
               _vv.blank();
               {?_vv.RESULT:=TYPYSP.put(1)
               || _msg+=' - usunięto powiązanie z Businesslink.'@
               || _msg+=' - nie udało się usunąć powiązania z Businesslink.'@
               ?};
               _vv.MSG:=_msg;
               exec('history_add_imp','#excel_imex',_vv,_def.excel)
            ?};
            TYPYSP.next()
         !};
         obj_del(_vv)
      ?}
   ?};
   VAR_DEL.delete('BLINK_IMP_TYPY')
?};

TYPYSP.cntx_pop()


\KSEF_TYPYSP_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KSEF_TD_S';
_def.FUNPAR:='ZWS_PAR_BKDS';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('filename','xls_blink');

_def.SHEET:='Typy dokumentów sprzedaży KSeF=1,1';
_def.NAME:='Typy dokumentów sprzedaży KSeF';
_def.DESC:='Typy dokumentów sprzedaży KSeF';

_def.TABLE:="TYPYSP";
_def.FIELDS:="exec('KSEF_TYPYSP_fld','xls_blink',_a)";

_def.BEFORE:="exec('KSEF_TYPYSP_before','xls_blink',_a)";
_def.AFTER:="exec('KSEF_TYPYSP_after','xls_blink',_a)";

_def.PREFIX:="TYPYSP.index('BL_KSEF1'); TYPYSP.prefix('T','N')";

_def.SELECT:="";
_def.EXPORT:="exec('KSEF_TYPYSP_exp','xls_blink',_a,_b)";

_def.VALIDATE:="exec('KSEF_TYPYSP_valid','xls_blink',_a,_b,_c)";
_def.IMPORT:="exec('KSEF_TYPYSP_imp','xls_blink',_a,_b,_c)";
~~


\KSEF_TYPYSP_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('T','Typ',1,,'');
_def.define('NAZ','Nazwa dokumentu',1,,'');
_def.define('KSEFZDOK','E-dokument tworzony z dokumentu księgowego',1,,'');
{? exec('upgrade2325_blbc1','zbl')
|| _def.define('KSEF_BC','Zakres kontroli Businesscheck',1,,'',2)
?};
~~


\KSEF_TYPYSP_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.T.VALUE:=TYPYSP.T;
_table.NAZ.VALUE:=TYPYSP.NAZ;
_table.KSEFZDOK.VALUE:=TYPYSP.KSEFZDOK;
{? exec('upgrade2325_blbc1','zbl')
|| _table.KSEF_BC.VALUE:=form(TYPYSP.KSEF_BC,,0,'9')
?};
_result


\KSEF_TYPYSP_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? _table.T=''
|| _result.MSG:='Nie podano typu dokumentu.'@;
   _result.RESULT:=0
?};

{? _table.KSEFZDOK=''
|| _table.KSEFZDOK:='N'
|? _table.KSEFZDOK<>'T' & _table.KSEFZDOK<>'N'
|| _result.msg_inset('KSEFZDOK','T','N');
   _result.RESULT:=0
?};

{? exec('upgrade2325_blbc1','zbl')
|| {? _table.KSEF_BC=''
   || _table.KSEF_BC:='0'
   |? _table.KSEF_BC<>'0' & _table.KSEF_BC<>'1' & _table.KSEF_BC<>'2'
   || _result.msg_inset('KSEF_BC','0','1','2');
      _result.RESULT:=0
   ?}
?};

{? _result.RESULT
|| TYPYSP.index('SPRZAK');
   TYPYSP.prefix('N',_table.T,);
   {? TYPYSP.first()
   || KSEF_IMP_TYPY+=TYPYSP.T+' ';
      {? TYPYSP.KSEF<>'T' | TYPYSP.KSEFZDOK<>_table.KSEFZDOK | (exec('upgrade2325_blbc1','zbl') & TYPYSP.KSEF_BC<>#_table.KSEF_BC)
      || _result.ACTION:='put'
      ?}
   || _result.MSG:='Brak typu dokumentu %1.'@[_table.T];
      _result.RESULT:=0
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='put'
      || TYPYSP.KSEF:='T';
         TYPYSP.KSEFZDOK:=_table.KSEFZDOK;
         {? exec('upgrade2325_blbc1','zbl')
         || TYPYSP.KSEF_BC:=#_table.KSEF_BC
         ?}
      ?}
   ?}
?};
~~


\KSEF_TYPYSP_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='put'
   || _result:=TYPYSP.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='put'
   || _validate.MSG:='Poprawa typu %1 nie powiodła się.'@[_table.T]
   ?}
?};
_result


\KSEF_TYPYSP_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Przed importem z Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
{? _def.KIND='IMPORT'
|| VAR_DEL.delete('KSEF_IMP_TYPY');
   KSEF_IMP_TYPY:=''
?};
TYPYSP.cntx_psh()


\KSEF_TYPYSP_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Po imporcie z Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

{? _def.KIND='IMPORT'
|| {?_def.excel.IMP_MODE
   || TYPYSP.index('SPRZAK');
      TYPYSP.prefix('N');
      {? TYPYSP.first()
      || _vv:=exec('args_valid','#excel_imex',_def.excel);
         {!
         |? {? TYPYSP.KSEF='T' & (KSEF_IMP_TYPY+' ')*(TYPYSP.T+' ')=0
            || _msg:='Typ dokumentu %1'@[TYPYSP.T];
               TYPYSP.KSEF:='N';
               TYPYSP.KSEFZDOK:='N';
               {? exec('upgrade2325_blbc1','zbl')
               || TYPYSP.KSEF_BC:=0
               ?};
               _vv.blank();
               {?_vv.RESULT:=TYPYSP.put(1)
               || _msg+=' - usunięto powiązanie z KSeF.'@
               || _msg+=' - nie udało się usunąć powiązania z KSeF.'@
               ?};
               _vv.MSG:=_msg;
               exec('history_add_imp','#excel_imex',_vv,_def.excel)
            ?};
            TYPYSP.next()
         !};
         obj_del(_vv)
      ?}
   ?};
   VAR_DEL.delete('KSEF_IMP_TYPY')
?};

TYPYSP.cntx_pop()


\BL_TYPYZAM_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - D - zamówienie dostawy (domyślnie), Z - zamówienie sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_rodz:='D';
{? var_pres('_b')=type_of('') & (_b='D' | _b='Z')
|| _rodz:=_b
?};

_def:=_a;
{? _rodz='Z'
|| _def.ID:='BL_TD_ZK';
   _def.FUNPAR:='ZWS_PAR_BLZK'
|| _def.ID:='BL_TD_ZD';
   _def.FUNPAR:='ZWS_PAR_BLZD'
?};
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('filename','xls_blink');

{? _rodz='Z'
|| _def.SHEET:='Typy zamówień sprzedaży BL=1,1';
   _def.NAME:='Typy zamówień sprzedaży Businesslink';
   _def.DESC:='Typy zamówień sprzedaży Businesslink'
|| _def.SHEET:='Typy zamówień dostaw BL=1,1';
   _def.NAME:='Typy zamówień dostaw Businesslink';
   _def.DESC:='Typy zamówień dostaw Businesslink'
?};

_def.TABLE:="TYPYZAM";
_def.FIELDS:="exec('BL_TYPYZAM_fld','xls_blink',_a)";

_def.BEFORE:=$('exec(\'BL_TYPYZAM_before\',\'xls_blink\',_a,\''+_rodz+'\')');
_def.AFTER:=$('exec(\'BL_TYPYZAM_after\',\'xls_blink\',_a,\''+_rodz+'\')');

_def.PREFIX:=$('TYPYZAM.index(\'BL1\'); TYPYZAM.prefix(\'T\',\''+_rodz+'\')');
_def.SELECT:="";
_def.EXPORT:="exec('BL_TYPYZAM_exp','xls_blink',_a,_b)";

_def.VALIDATE:=$('exec(\'BL_TYPYZAM_valid\',\'xls_blink\',_a,_b,_c,\''+_rodz+'\')');
_def.IMPORT:=$('exec(\'BL_TYPYZAM_imp\',\'xls_blink\',_a,_b,_c,\''+_rodz+'\')');
~~


\BL_TYPYZAM_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('T','Typ',1,,'');
_def.define('NAZ','Nazwa dokumentu',1,,'');
~~


\BL_TYPYZAM_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.T.VALUE:=TYPYZAM.T;
_table.NAZ.VALUE:=TYPYZAM.NAZ;
_result


\BL_TYPYZAM_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::       _d - S - typy dokumentów sprzedaży (domyślnie), Z - typy dokumentów zakupu
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_rodz:='D';
{? var_pres('_d')=type_of('') & (_d='D' | _d='Z')
|| _rodz:=_d
?};

_result.RESULT:=1;

{? _table.T=''
|| _result.MSG:='Nie podano typu dokumentu.'@;
   _result.RESULT:=0
?};

{? _result.RESULT
|| TYPYZAM.index('TYP');
   TYPYZAM.prefix(_rodz,_table.T,);
   {? TYPYZAM.first()
   || BLINK_IMP_TYPY+=TYPYZAM.T+' ';
      {? TYPYZAM.BL<>'T'
      || _result.ACTION:='put'
      ?}
   || _result.MSG:='Brak typu zamówienia %1.'@[_table.T];
      _result.RESULT:=0
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='put'
      || TYPYZAM.BL:='T'
      ?}
   ?}
?};
~~


\BL_TYPYZAM_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - S - typy dokumentów sprzedaży (domyślnie), Z - typy dokumentów zakupu
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_rodz:='D';
{? var_pres('_d')=type_of('') & (_d='D' | _d='Z')
|| _rodz:=_d
?};
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='put'
   || _result:=TYPYZAM.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='put'
   || _validate.MSG:='Poprawa typu %1 nie powiodła się.'@[_table.T]
   ?}
?};
_result


\BL_TYPYZAM_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Przed importem z Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
{? _def.KIND='IMPORT'
|| VAR_DEL.delete('BLINK_IMP_TYPY');
   BLINK_IMP_TYPY:=''
?};
TYPYZAM.cntx_psh()


\BL_TYPYZAM_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Po imporcie z Excel
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - D - zamówienie dostawy (domyślnie), Z - zamówienie sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_rodz:='D';
{? var_pres('_b')=type_of('') & (_b='D' | _b='Z')
|| _rodz:=_b
?};
_def:=_a;

{? _def.KIND='IMPORT'
|| {?_def.excel.IMP_MODE
   || TYPYZAM.index('TYP');
      TYPYZAM.prefix(_rodz);
      {? TYPYZAM.first()
      || _vv:=exec('args_valid','#excel_imex',_def.excel);
         {!
         |? {? TYPYZAM.BL='T' & (BLINK_IMP_TYPY+' ')*(TYPYZAM.T+' ')=0
            || _msg:='Typ zamówienia %1'@[TYPYZAM.T];
               TYPYZAM.BL:='N';
               _vv.blank();
               {?_vv.RESULT:=TYPYZAM.put(1)
               || _msg+=' - usunięto powiązanie z Businesslink.'@
               || _msg+=' - nie udało się usunąć powiązania z Businesslink.'@
               ?};
               _vv.MSG:=_msg;
               exec('history_add_imp','#excel_imex',_vv,_def.excel)
            ?};
            TYPYZAM.next()
         !};
         obj_del(_vv)
      ?}
   ?};
   VAR_DEL.delete('BLINK_IMP_TYPY')
?};

TYPYZAM.cntx_pop()

:Sign Version 2.0 jowisz:1045 2023/09/27 10:38:14 1d9adbe767ff7daaa7d8eaae8bacfd9e105fb01392e61752a52e3ec03a32df077980eac46bc801ae3e808e493ecfcf789cfe4933a728cd0a9de2538b8463c1d0c5ec8f0c8b920dd7e514084325af65f35386ba2d7cfaa2d0df137e0519e99456de9b01fadb835003edf2e8b53cebde0ed6b38e16ccbe1603fd797a8fdb3b49e6

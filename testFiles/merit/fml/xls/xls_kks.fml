:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kks.fml
:: Utworzony: 20.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu stałych kalkulacji kart technologicznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KKS';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTKT';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Stałe kalkulacji'@+'=1,1';
_def.NAME:='Stałe kalkulacji'@;
_def.DESC:='Stałe do wyliczania TKW na podstawie kart technologicznych'@;

_def.PREFIX:="KKS.index('WR'); KKS.prefix(); ~~";
_def.TABLE:="KKS";
_def.FIELDS:="exec('fields','xls_kks',_a)";

_def.BEFORE:="KKS.cntx_psh()";
_def.AFTER:="KKS.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_kks',_a,_b)";
_def.VALIDATE:="exec('validate','xls_kks',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kks',_a,_b,_c)";
_def.SELECT:="exec('select','xls_kks',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('OPC','Wariant kalkulacji'@,0,,MS.comment(KKS,'OPC')+' '+'(max. %1 znaków)'@[$MS.fld_len(KWAR,'NAZ')]);
_def.define('RUBR','Rubryka'@,0,,MS.comment(KKS,'RUBR')+' '+'(liczba naturalna)'@,type_of(0),0);
_def.define('WAR','Wartość'@,1,,MS.comment(KKS,'WAR'));
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
KKS.prefix();
{? _validate.ACTION='add'
|| KKS.blank();
   KKS.OPC:=_validate.OBJ.KWAR;
   KKS.RUBR:=_validate.OBJ.KRUB
?};
KKS.WAR:=_obj.WAR;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.OPC.VALUE:=KKS.OPC().NAZ; _excel.write_async('KWAR',KKS.OPC);
_table.RUBR.VALUE:=KKS.RUBR().NR; _excel.write_async('KRUB',KKS.RUBR);
_table.WAR.VALUE:=KKS.WAR;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('KWAR','KRUB');
_result.OBJ.KWAR:=exec('FindInSet','#table','KWAR','NA',_obj.OPC,,"KWAR.ref()",1);
_result.OBJ.KRUB:=exec('FindInSet','#table','KRUB','NR',_obj.RUBR,,"KRUB.ref()");

:: Błędy
{? _result.OBJ.KWAR=null()
|| _result.msg_norec('OPC');
   _result.RESULT:=0
?};
{? _result.OBJ.KRUB=null()
|| _result.msg_norec('RUBR');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| KKS.index('WR');
   KKS.prefix(_result.OBJ.KWAR,_obj.RUBR);
   {? KKS.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_kks',_obj,_mode,_result);
         {? exec('chk_kks','tech_kalk',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_kks',_obj,_mode,_result);
      {? exec('chk_kks','tech_kalk',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KKS.add(1)
   |? _validate.ACTION='put'
   || _result:=KKS.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie stałej kalkulacji dla rubryki %1 wariantu %2 nie powiodło się.'@[_obj.RUBR,_obj.OPC]
   || _validate.MSG:='Poprawa stałej kalkulacji dla rubryki %1 wariantu %2 nie powiodła się.'@[_obj.RUBR,_obj.OPC]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'OPC','STRING[40]','Wariant kalkulacji'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'RUBR','INTEGER','Rubryka');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'OPC',,);

KKS.cntx_psh();
KKS.index('WR');
KKS.prefix();
{? KKS.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$KKS.ref();
      _selected.prefix($KKS.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.OPC:=KKS.OPC().NAZ;
      _tab.RUBR:=KKS.RUBR().NR;
      _tab.add();
      KKS.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'OPC,RUBR',20,'Wybór stałych kalkulacji do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KKS.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:48 d0ce1a931274895eeccc3035d12dd355abb99cfed854189e3014732451aa0c214c4418cda61ae5260e9e66b277f9c84d99ce50c7929f0255ade8e1ebabe7bd2d6411bad275a317695fe4edb64d75d3212aa945bcc16c5d9e52142ce3f3d5f4ba2b49455901f8dc9eb9b3d9cf722eebd9ccc60a4c4d95433b20742e4663a94de2

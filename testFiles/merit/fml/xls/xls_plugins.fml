:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_plugins.fml
:: Utworzony: 18.09.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu wtyczek wdrożeniowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PLUGINS';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
::_def.FUNPAR:='';
_def.ADD_ROWS:=0;
_def.MULTIFIR:='T';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Wtyczki wdrożeniowe'@+'=1,1';
_def.NAME:='Wtyczki wdrożeniowe'@;
_def.DESC:='Wtyczki wdrożeniowe'@;

_def.PREFIX:="PLUGINS.index('KOD'); PLUGINS.prefix()";
_def.TABLE:="PLUGINS";
_def.FIELDS:="exec('fields','xls_plugins',_a)";

_def.BEFORE:="PLUGINS.cntx_psh()";
_def.AFTER:="PLUGINS.cntx_pop()";
_def.SELECT:="exec('select','xls_plugins',_a)";

_def.EXPORT:="exec('export','xls_plugins',_a,_b)";
_def.VALIDATE:="exec('validate','xls_plugins',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_plugins',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD',MS.name(PLUGINS,'KOD'),0,,
   MS.name(PLUGINS,'KOD')+' '+'(max. %1 znaków)'@[$MS.fld_len(PLUGINS,'KOD')]
);
_def.define('FORMULA',MS.name(PLUGINS,'FORMULA'),1,,
   MS.name(PLUGINS,'FORMULA')+' '+'(max. %1 znaków)'@[$MS.fld_len(PLUGINS,'FORMULA')]
);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PLUGINS.prefix();
PLUGINS.FORMULA:=_obj.FORMULA;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=PLUGINS.KOD;
_table.FORMULA.VALUE:=PLUGINS.FORMULA;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

:: Błędy
{? +_obj.FORMULA>MS.fld_len(PLUGINS,'FORMULA')
|| _result.msg_length('FORMULA',MS.fld_len(PLUGINS,'FORMULA'));
   _result.RESULT:=0
?};

:: Ostrzeżenia
{? +_obj.KOD>MS.fld_len(PLUGINS,'KOD')
|| _result.msg_length('KOD',MS.fld_len(PLUGINS,'KOD'))
?};

:: OK
{? _result.RESULT>0
||
   {? _mode=0
   ||
::    Tryb pracy: dodaj brakujące - nadpisywanie treści formuł, jeżeli jest niepusta w arkuszu
      _result.ACTION:='put';
      PLUGINS.index('KOD');
      PLUGINS.prefix(_obj.KOD,);
      {? PLUGINS.size()>0 & PLUGINS.first()
      || {? _obj.FORMULA='' | _obj.FORMULA=PLUGINS.FORMULA
         || _result.RESULT:=0
         || exec('record','xls_plugins',_obj,_mode,_result);
            {? exec('valid_formula','#field',_obj.FORMULA)=0
            || _result.RESULT:=0
            ?}
         ?}
      ?}
   ||
::    Tryb pracy: nadpisuj istniejące - nadpisywanie treści formuł zawsze, o ile w arkuszu jest inna (także pusta)
      _result.ACTION:='put';
      PLUGINS.index('KOD');
      PLUGINS.prefix(_obj.KOD,);
      {? PLUGINS.size()>0 & PLUGINS.first()
      || {? _obj.FORMULA=PLUGINS.FORMULA
         || _result.RESULT:=0
         || exec('record','xls_plugins',_obj,_mode,_result);
            {? exec('valid_formula','#field',_obj.FORMULA)=0
            || _result.RESULT:=0
            ?}
         ?}
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='put'
   || _result:=PLUGINS.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='put'
   || _validate.MSG:='Poprawa wtyczki wdrożeniowej \'%1\' nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'KOD','STRING[%1]'[$MS.fld_len(PLUGINS,'KOD')],MS.name(PLUGINS,'KOD')
   ,'NAME','STRING[%1]'[$MS.fld_len(PLUGINS,'NAME')],MS.name(PLUGINS,'NAME')
   ,'FORMULA','STRING[%1]'[$MS.fld_len(PLUGINS,'FORMULA')],MS.name(PLUGINS,'FORMULA')
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

PLUGINS.cntx_psh();
PLUGINS.index('KOD');
PLUGINS.prefix();
{? PLUGINS.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$PLUGINS.ref();
      _selected.prefix($PLUGINS.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=PLUGINS.KOD;
      _tab.NAME:=PLUGINS.NAME;
      _tab.FORMULA:=PLUGINS.FORMULA;
      _tab.add();
      PLUGINS.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'KOD,NAME,FORMULA',30,'Wybór wtyczek do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PLUGINS.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 ac25a97562259c1f42b8b757021507082bba4d2c9d38ec2217b6baed8dce955406b6d5212cab26ab9065d74f8813f8866126f748ed153153549551475a205d8c48bc29f339d77a95a3ae08fdf2129b03a3afddcf30dac97f94689afdbc0afb2f28e70ae684a960c78aea01b7a97211ff75b77bf6d4d0e8347b13e922b8933a19

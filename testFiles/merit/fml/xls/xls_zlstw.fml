:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_zlstw.fml
:: Utworzony: 22.10.2018
:: Autor: Mario
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu przyczyn korekt limitów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW:  MW [19.02]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ZLSTW';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZLE';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Przyczyny korekt limitów'@+'=1,1';
_def.NAME:='Przyczyny korekt limitów'@;
_def.DESC:='Przyczyny korekt limitów'@;

_def.PREFIX:="exec('prefix','xls_zlstw')";
_def.TABLE:="exec('table','xls_zlstw')";
_def.FIELDS:="exec('fields','xls_zlstw',_a)";

_def.BEFORE:="ZLSTW.cntx_psh()";
_def.AFTER:="ZLSTW.cntx_pop()";
_def.SELECT:="exec('select','xls_zlstw',_a)";

_def.EXPORT:="exec('export','xls_zlstw',_a,_b)";
_def.VALIDATE:="exec('validate','xls_zlstw',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_zlstw',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ZLSTW.index('K');
ZLSTW.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ZLSTW


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod',1,,'10 znakowy kod');
_def.define('NAZ','Nazwa',1,,'60 znakowa Nazwa');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
ZLSTW.prefix();
{? _validate.ACTION='add'
||
   ZLSTW.blank()
?};
ZLSTW.KOD:=_obj.KOD;
ZLSTW.NAZ:=_obj.NAZ;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

{? 1+ZLSTW.KOD='~'
||
   _result:=0
||
  _result:=1;
  _table.KOD.VALUE:=ZLSTW.KOD;
  _table.NAZ.VALUE:=ZLSTW.NAZ
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? 1+_obj.KOD='~'
|| _result.msg_value('KOD','Zastrzeżona przyczyna korekty limitów nie może być importowana.');
   _result.RESULT:=0
?};

{? (+_obj.KOD)>10
||
  _result.msg_length('KOD',10);
  _result.RESULT:=0
?};

{? _result.RESULT>0
||
  ZLSTW.index('K');
  ZLSTW.prefix(_obj.KOD,);
  {? ZLSTW.first()
  ||
    {? _mode=0
    || _result.RESULT:=0
    ||
      _result.ACTION:='put';
      exec('record','xls_zlstw',_obj,_mode,_result);
      {? exec('chk_zlstw','zl_limit',1)<>''
      ||
         _result.RESULT:=0
      ?}
    ?}
  ||
    _result.ACTION:='add';
    exec('record','xls_zlstw',_obj,_mode,_result);
    {? exec('chk_zlstw','zl_limit',0)<>''
    || _result.RESULT:=0
    ?}
  ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=ZLSTW.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=ZLSTW.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie przyczyny korekty limitów o kodzie %1 nie powiodło się.'@[_obj.KOD]
   ||
      _validate.MSG:='Poprawa przyczyny korekty limitów o kodzie %1 nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2,'KOD','STRING[10]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[60]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

ZLSTW.cntx_psh();
ZLSTW.index('K');
ZLSTW.prefix();
{? ZLSTW.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$ZLSTW.ref();
      _selected.prefix($ZLSTW.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=ZLSTW.KOD;
      _tab.NAZ:=ZLSTW.NAZ;
      _tab.add();
      ZLSTW.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'KOD,NAZ',20,'Wybór przyczyn korekt limitów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
ZLSTW.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:52 3fb4df067ec46a7941f464876e452fa026b5e7c848beef240634b09f6db1a21f32d48acb9c5036ad923bff5af9bdca73202fcfad45dd50bd71a5f817e3a729533415041530328bed4c9d779c4707cec9becf792578118c7f274bcab39a7668ea7b5c92bc335a9b910ab014e6cb1da8cf701b9fffad3c9889688975c757870664

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_slo_kod.fml
:: Utworzony: 02.08.2018
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu słowników kadrowych - kody.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Inicjowanie mechanizmu.
::   WE:  _a [OBJECT]  - Środowisko mechanizmu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='SLO_KOD';
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.NAME:='Słowniki kodów'@;
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FUNPAR:='ZWS_PAR_PTSL';
_def.SHEET:='Słowniki kodów'@+'=1,1';
_def.DESC:='Słowniki kodów'@;
_def.MULTIFIR:='T';

_def.TABLE:="SLO_KOD";
_def.BEFORE:="
   SLO_KOD.cntx_psh();
   SLO_KOD.f_clear();
   SLO_KOD.index('KOD');
   SLO_KOD.prefix();
   SLO_TYP.cntx_psh();
   SLO_TYP.index('SYMBOL');
   SLO_TYP.prefix()
";
_def.AFTER:="
   SLO_TYP.cntx_pop();
   SLO_KOD.cntx_pop()
";

_def.FIELDS:="
   _fld:=_a;
   _fl:=""MS.name(SLO_KOD,_a)"";
   _fc:=""MS.comment(SLO_KOD,_a)"";

   _fld.define('SLO_TYP',_fl('SLO_TYP'),0,,_fc('SLO_TYP')+' (max. %1 zn.)' [$MS.fld_len(SLO_TYP,'SYMBOL')]);
   _fld.define('KOD',_fl('KOD'),1,,_fc('KOD')+' (max. %1 zn.)' [$MS.fld_len(SLO_KOD,'KOD')]);
   _fld.define('NAZWA',_fl('NAZWA'),1,,_fc('NAZWA')+' (max. %1 zn.)' [$MS.fld_len(SLO_KOD,'NAZWA')]);
   _fld.define('SYSTEM',_fl('SYSTEM'),0,,_fc('SYSTEM'));

   ~~
";

_def.EXPORT:="
   _excel:=_a;
   _buf:=_b;

   _buf.SLO_TYP.VALUE:=SLO_KOD.SLO_TYP().SYMBOL;   _excel.write_async('SLO_TYP',SLO_KOD.SLO_TYP);
   _buf.KOD.VALUE:=SLO_KOD.KOD;
   _buf.NAZWA.VALUE:=SLO_KOD.NAZWA;
   _buf.SYSTEM.VALUE:=SLO_KOD.SYSTEM;

   1
";

_def.VALIDATE:="exec('validate','xls_slo_kod',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_slo_kod',_a,_b,_c)";

~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła sprawdza, czy można poprawić/dodać rekord.
::   WE: _a [OBJECT] - Obiekt z polami odczytami z Excela.
::       _b [NUMBER] - Tryb aktualizacji: 0 - nie (dodawane są tylko nowe rekordy) / 1 - tak.
::       _c [OBJECT] - Obiekt (RESULT, ACTION, OBJ) - wynik walidacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_update:=_b;
_validate:=_c;

_validate.OBJ:=obj_new('SLO_TYP','DL_KOD');
_validate.OBJ.SLO_TYP:=null();
_validate.OBJ.DL_KOD:=0;

:: Błędy krytyczne
{? _buf.SLO_TYP=''
|| _validate.msg_empty('SLO_TYP');
   _validate.RESULT:=0;
   return()
?};
{? SLO_TYP.find_key(_buf.SLO_TYP,)
|| _validate.OBJ.SLO_TYP:=SLO_TYP.ref();
   _validate.OBJ.DL_KOD:=SLO_TYP.DL_KOD
|| _validate.msg_norec('SLO_TYP');
   _validate.RESULT:=0;
   return()
?};

_fld:=
   {? _buf.KOD=''
   || 'KOD'
   |? _buf.NAZWA=''
   || 'NAZWA'
   |? _buf.SYSTEM=''
   || 'SYSTEM'
   || ''
   ?};
{? _fld<>''
|| _validate.msg_empty(_fld);
   _validate.RESULT:=0;
   return()
?};

{? +_buf.SYSTEM<>1 | 'TN'*_buf.SYSTEM=0
|| _validate.msg_inset('SYSTEM','T','N');
   _validate.RESULT:=0;
   return()
?};

:: Ostrzeżenia
_dl:={? _validate.OBJ.DL_KOD>0 || _validate.OBJ.DL_KOD || MS.fld_len(SLO_KOD,'KOD') ?};
{? +_buf.KOD>_dl
|| _validate.msg_length('KOD',_dl);
   _buf.KOD:=_dl+_buf.KOD
?};
_dl:=MS.fld_len(SLO_KOD,'NAZWA');
{? +_buf.NAZWA>_dl
|| _validate.msg_length('NAZWA',_dl)
?};

:: Wybór akcji
{? SLO_KOD.find_key(_validate.OBJ.SLO_TYP,_buf.KOD,)
|| {? _update
   || {? SLO_KOD.NAZWA<>_buf.NAZWA | SLO_KOD.SYSTEM<>_buf.SYSTEM
      || _validate.ACTION:='put'
      || _validate.ACTION:='~put'
      ?}
   || _validate.RESULT:=0
   ?}
|| _validate.ACTION:='add'
?};

~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła odpowiedzialna za utworzenie lub aktualizację rekordu.
::   WE: _a [OBJECT] - Obiekt z polami odczytami z Excela.
::       _b [NUMBER] - Tryb aktualizacji: 0 - nie (dodawane są tylko nowe rekordy) / 1 - tak.
::       _c [OBJECT] - Obiekt (RESULT, ACTION, OBJ) - wynik walidacji.
::   WY: Status operacji: 0/1
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_update:=_b;
_validate:=_c;

_przypisz:="
   _buf:=_a;
   _update:=_b;
   _validate:=_c;

   SLO_KOD.SLO_TYP:=_validate.OBJ.SLO_TYP;
   SLO_KOD.KOD:=_buf.KOD;
   SLO_KOD.NAZWA:=_buf.NAZWA;
   SLO_KOD.SYSTEM:=_buf.SYSTEM;

   ~~
";

_key:=_buf.SLO_TYP+'/'+_buf.KOD;

_ret:=1;

{? _validate.ACTION='add'
|| SLO_KOD.blank();
   _przypisz(_buf,_update,_validate);
   {? ~SLO_KOD.add(1)
   || _ret:=0;
      _validate.msg_insert(_key)
   ?}
|? _validate.ACTION='put'
|| _przypisz(_buf,_update,_validate);
   {? ~SLO_KOD.put(1)
   || _ret:=0;
      _validate.msg_update(_key)
   ?}
?};

_ret

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 23b9b93bc0125c18a31d33652c7a618dcb706388e5044231f2065ca1d6e7094e59a8c5482bfaed8a2fe9a2225ba29d14e4ca7bf4592a207cdd193eb26c5f5fa523719c2268020874d4e3ba6f11c3306c297c894f79882f6dec880f1ae8e6317ab3378191610a6860c91146bf6d89bb05a2eb7053fdc73583aae576dea8840afc

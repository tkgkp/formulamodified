:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tpktl.fml
:: Utworzony: 20.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typów kart technologicznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TPKTL';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTKT';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Typy technologii'@+'=1,1';
_def.NAME:='Typy kart technologicznych'@;
_def.DESC:='Typy kart technologicznych'@;

_def.PREFIX:="TPKTL.index('TYP'); TPKTL.prefix()";
_def.TABLE:="TPKTL";
_def.FIELDS:="exec('fields','xls_tpktl',_a)";

_def.BEFORE:="TPKTL.cntx_psh()";
_def.AFTER:="TPKTL.cntx_pop()";
_def.SELECT:="exec('select','xls_tpktl',_a)";

_def.EXPORT:="exec('export','xls_tpktl',_a,_b)";
_def.VALIDATE:="exec('validate','xls_tpktl',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_tpktl',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TYP','Typ'@,0,,'Typ (max. 3 znaki)'@);
_def.define('OPIS','Opis'@,1,,'Opis (max. 60 znaków)'@);
_def.define('XJM','Karta na XJM'@,1);
_def.define('OPER','Operacje do karty'@,1,,'[T]/[N]');
::_def.define('SUR','Surowce do karty'@,1,,'Surowce do karty [K]\nSurowce do operacji [O]');
_def.define('UTIL','Narzędzia do karty'@,1,,'[T]/[N]');
_def.define('DEF_OPCK','Domyślny wariant kalkulacji'@,1);
_def.define('CRUB','Nr rubryki ceny'@,1,,,,0);
_def.define('CRUBMAT','Nr rubryki materiałów'@,1,,,,0);
_def.define('CRUBROB','Nr rubryki robocizny'@,1,,,,0);
_def.define('CRUBIL','Nr rubryki ilości'@,1,,,,0);
_def.define('PAR','Parametryzowana'@,1,,'[T]/[N]');
_def.define('DJ','Domyślna jednostka miary'@,1,,'Podstawowa [P]\nDodatkowa [D]');
_def.define('CZAPLA','Czy tworzyć przepisy planistyczne'@,1,,'[T]/[N]');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TPKTL.prefix();
{? _validate.ACTION='add'
|| TPKTL.blank();
   TPKTL.TYP:=_obj.TYP
?};
TPKTL.OPIS:=_obj.OPIS;
TPKTL.XJM:=_obj.XJM;
TPKTL.OPER:=_obj.OPER;
::TPKTL.SUR:=_obj.SUR;
TPKTL.UTIL:={? _obj.UTIL='N' || 'N' || 'O' ?};
TPKTL.DEF_OPCK:=_validate.OBJ.DEF_OPCK;
TPKTL.CRUB:=_obj.CRUB;
TPKTL.CRUBMAT:=_obj.CRUBMAT;
TPKTL.CRUBROB:=_obj.CRUBROB;
TPKTL.CRUBIL:=_obj.CRUBIL;
TPKTL.PAR:=_obj.PAR;
TPKTL.DJ:=_obj.DJ;
TPKTL.CZAPLA:=_obj.CZAPLA;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

{? 1+TPKTL.TYP='~'
|| _result:=0
|| _result:=1;
   _table.TYP.VALUE:=TPKTL.TYP;
   _table.OPIS.VALUE:=TPKTL.OPIS;
   _table.XJM.VALUE:=TPKTL.XJM;
   _table.OPER.VALUE:=TPKTL.OPER;
::   _table.SUR.VALUE:=TPKTL.SUR;
   _table.UTIL.VALUE:={? TPKTL.UTIL='N' || 'N' || 'T' ?};
   _table.DEF_OPCK.VALUE:=TPKTL.DEF_OPCK().NAZ; _excel.write_async('KWAR',TPKTL.DEF_OPCK);
   _table.CRUB.VALUE:=TPKTL.CRUB;
   _table.CRUBMAT.VALUE:=TPKTL.CRUBMAT;
   _table.CRUBROB.VALUE:=TPKTL.CRUBROB;
   _table.CRUBIL.VALUE:=TPKTL.CRUBIL;
   _table.PAR.VALUE:=TPKTL.PAR;
   _table.DJ.VALUE:=TPKTL.DJ;
   _table.CZAPLA.VALUE:=TPKTL.CZAPLA;

:: Dostępne warianty kalkulacji dla typu karty
::TPKTLF.cntx_psh();
::TPKTLF.index('TO');
::TPKTLF.prefix(TPKTL.ref());
::{? TPKTLF.first()
::|| {!
::   |? _excel.write_async('TPKTLF',TPKTLF.ref());
::      TPKTLF.next()
::   !}
::?};
::TPKTLF.cntx_pop();
   ~~
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('DEF_OPCK');
_result.OBJ.DEF_OPCK:=exec('FindInSet','#table','KWAR','NA',_obj.DEF_OPCK,,"KWAR.ref()",1);

:: Błędy
{? 1+_obj.TYP='~'
|| _result.msg_value('TYP','Zastrzeżony typ karty technologiczne nie może być importowany.');
   _result.RESULT:=0
?};
{? _obj.DEF_OPCK<>'' & _result.OBJ.DEF_OPCK=null()
|| _result.msg_norec('DEF_OPCK');
   _result.RESULT:=0
?};
{? +_obj.OPER=0 | 'TN'*_obj.OPER=0
|| _result.msg_inset('OPER','T','N');
   _result.RESULT:=0
?};
::{? +_obj.SUR=0 | 'KO'*_obj.SUR=0
::|| _result.msg_inset('SUR','K','O');
::   _result.RESULT:=0
::?};
{? +_obj.UTIL=0 | 'TKON'*_obj.UTIL=0
|| _result.msg_inset('UTIL','K','O','N','T');
   _result.RESULT:=0
?};
{? +_obj.PAR=0 | 'TN'*_obj.PAR=0
|| _result.msg_inset('PAR','T','N');
   _result.RESULT:=0
?};
{? +_obj.DJ=0 | 'PD'*_obj.DJ=0
|| _result.msg_inset('DJ','P','D');
   _result.RESULT:=0
?};
{? +_obj.CZAPLA=0 | 'TN'*_obj.CZAPLA=0
|| _result.msg_inset('CZAPLA','T','N');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| TPKTL.index('TYP');
   TPKTL.prefix(_obj.TYP,);
   {? TPKTL.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_tpktl',_obj,_mode,_result);
         {? exec('chk_tpktl','tech_common',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_tpktl',_obj,_mode,_result);
      {? exec('chk_tpktl','tech_common',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TPKTL.add(1)
   |? _validate.ACTION='put'
   || _result:=TPKTL.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie typu karty %1 nie powiodło się.'@[$_obj.TYP]
   || _validate.MSG:='Poprawa typu karty %1 nie powiodła się.'@[$_obj.TYP]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'TYP','STRING[3]','Typ'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OPIS','STRING[60]','Opis'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'TYP',,);

TPKTL.cntx_psh();
TPKTL.index('TYP');
TPKTL.prefix();
{? TPKTL.first()
|| {!
   |? {? 1+TPKTL.TYP<>'~'
      || _tab.blank();
         _tab.REF:=$TPKTL.ref();
         _selected.prefix($TPKTL.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.TYP:=TPKTL.TYP;
         _tab.OPIS:=TPKTL.OPIS;
         _tab.add()
      ?};
      TPKTL.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'TYP,OPIS',20,'Wybór typów kart technologicznych do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TPKTL.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 150354d2e9b6e86eebecc3f878f376fe12c0fce83e18df8ceba0a20cb1adf0dbcbc1c9596ffbf39ecc3854b5e2892228fb0a37b3b99da3cc1193d437c961d5c11e63371984c10b0af726043439e69b165c07c991c6e1ef0ec262d01cb6cbbae3484f37435f44a52fe16514ca9da0904d0e9a9236c09c3627c2c7e813b09c38dc

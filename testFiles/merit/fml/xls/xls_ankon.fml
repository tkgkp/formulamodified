:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ankon.fml
:: Utworzony: 23.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu stałych kalkulacji/anali zleceń
::======================================================================================================================


\init_0
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ANKON0';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZLE';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Stałe kalkulacji zleceń'@+'=1,1';
_def.NAME:='Stałe kalkulacji zleceń'@;
_def.DESC:='Stałe do wyliczania kalkulacji na podstawie zleceń'@;

_def.PREFIX:="ANKON.index('AOR'); ANKON.prefix('0')";
_def.TABLE:="ANKON";
_def.FIELDS:="exec('fields','xls_ankon',_a,'0')";

_def.BEFORE:="ANKON.cntx_psh()";
_def.AFTER:="ANKON.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_ankon',_a,_b,'0')";
_def.VALIDATE:="exec('validate','xls_ankon',_a,_b,_c,'0')";
_def.IMPORT:="exec('import','xls_ankon',_a,_b,_c,'0')";
~~


\init_1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ANKON1';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZLE';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Stałe analiz zleceń'@+'=1,1';
_def.NAME:='Stałe analiz zleceń'@;
_def.DESC:='Stałe do wyliczania analizy na podstawie zleceń'@;

_def.PREFIX:="ANKON.index('AOR'); ANKON.prefix('1')";
_def.TABLE:="ANKON";
_def.FIELDS:="exec('fields','xls_ankon',_a,'1')";

_def.BEFORE:="ANKON.cntx_psh()";
_def.AFTER:="ANKON.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_ankon',_a,_b,'1')";
_def.VALIDATE:="exec('validate','xls_ankon',_a,_b,_c,'1')";
_def.IMPORT:="exec('import','xls_ankon',_a,_b,_c,'1')";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::       _b - '0'/'1' - kalkulacja/analiza
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_a01:=_b;

_def.define('OPC',{? _a01='0' || 'Wariant kalkulacji'@ || 'Wariant analizy'@ ?},0,,
   MS.comment(ANKON,'OPC')+' '+'(max. %1 znaków)'@[$MS.fld_len(ANWAR,'NA')]
);
_def.define('RUBR','Rubryka'@,0,,MS.comment(ANKON,'RUBR')+' '+'(liczba naturalna)'@,type_of(0),0);
_def.define('WAR','Wartość'@,1,,MS.comment(ANKON,'WAR'));
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

ANKON.prefix();
{? _validate.ACTION='add'
|| ANKON.blank();
   ANKON.OPC:=_validate.OBJ.ANWAR;
   ANKON.RUBR:=_validate.OBJ.ANRUB
?};
ANKON.WAR:=_obj.WAR;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::       _c - '0'/'1' - kalkulacja/analiza
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_a01:=_c;

_result:=1;
_table.OPC.VALUE:=ANKON.OPC().NA; _excel.write_async('ANWAR'+_a01,ANKON.OPC);
_table.RUBR.VALUE:=ANKON.RUBR().NR; _excel.write_async('ANRUB',ANKON.RUBR);
_table.WAR.VALUE:=ANKON.WAR;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::       _d - '0'/'1' - kalkulacja/analiza
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;
_a01:=_d;

_result.RESULT:=1;
_result.OBJ:=obj_new('ANWAR','ANRUB');
_result.OBJ.ANWAR:=exec('FindInSet','#table','ANWAR','NA',_obj.OPC,_a01,"ANWAR.ref()",1);
_result.OBJ.ANRUB:=exec('FindInSet','#table','ANRUB','RUBR',_obj.RUBR,,"ANRUB.ref()");

:: Błędy
{? _result.OBJ.ANWAR=null()
|| _result.msg_norec('OPC');
   _result.RESULT:=0
?};
{? _result.OBJ.ANRUB=null()
|| _result.msg_norec('RUBR');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| ANKON.index('OR');
   ANKON.prefix(_result.OBJ.ANWAR,_result.OBJ.ANRUB);
   {? ANKON.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_ankon',_obj,_mode,_result);
         {? exec('chk_ankon','zl_kalk',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_ankon',_obj,_mode,_result);
      {? exec('chk_ankon','zl_kalk',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - '0'/'1' - kalkulacja/analiza
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_a01:=_d;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=ANKON.add(1)
   |? _validate.ACTION='put'
   || _result:=ANKON.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || {? _a01='0'
      || _validate.MSG:='Dodanie stałej kalkulacji dla rubryki %1 wariantu %2 nie powiodło się.'@[_obj.RUBR,_obj.OPC]
      || _validate.MSG:='Dodanie stałej analizy dla rubryki %1 wariantu %2 nie powiodło się.'@[_obj.RUBR,_obj.OPC]
      ?}
   || {? _a01='0'
      || _validate.MSG:='Poprawa stałej kalkulacji dla rubryki %1 wariantu %2 nie powiodła się.'@[_obj.RUBR,_obj.OPC]
      || _validate.MSG:='Poprawa stałej analizy dla rubryki %1 wariantu %2 nie powiodła się.'@[_obj.RUBR,_obj.OPC]
      ?}
   ?}
?};
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:46 60a6be21eae2c062e954bf7459e1fd6d3558102d2b2fd189eaff5556730ba6a4fda6eb06b24b094836534f0eab610f16231e530bebfd7ee9139feef43affa7c53e2acad7aaaf925109e51e47284dbc38e4337a7aeed60e2ea54204bb52b9aa56afdea0f66ae82c9ae6501562ed12e598a779818c79f8b3846acdadceafe67132

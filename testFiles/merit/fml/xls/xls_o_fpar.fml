:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_o_fpar.fml
:: Utworzony: 04.11.2020
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_def:=_a;

_def.ID:='O_FPAR';
_def.FUNPAR:='ZWS_PAR_PALS';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_o_par','xls__init');
_def.SHEET:='Parametry w firmie=1,1';
_def.NAME:='Parametry w firmie'@;
_def.DESC:='Parametry w firmie';
_def.ADD_ROWS:=0;
_def.HIDDEN:='N';

: źródło danych
_def.TABLE:="O_FPAR";

: prolog
_def.BEFORE:="
   O_DPAR.cntx_psh();
   O_DPAR.index('KOD');
   O_DPAR.prefix();
   O_FPAR.cntx_psh();
   O_FPAR.index('SYMBOL');
   _ref:=exec('ref_firma','ustawienia');
   {? _a.is_export() & exec('runtime_dro','#system')
   || _ref:=null
   ?};
   O_FPAR.prefix(_ref);
   ~~
";

: epilog
_def.AFTER:="
   O_FPAR.cntx_pop();
   O_DPAR.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('O_DPAR',  MS.name(O_FPAR,'O_DPAR'),0,,  MS.comment(O_FPAR,'O_DPAR'));
   _env.define('SYMBOL',  MS.name(O_DPAR,'SYMBOL'),0,,  MS.comment(O_DPAR,'SYMBOL'));
   _env.define('NAZWA',   MS.name(O_DPAR,'NAZWA'),0,,   MS.comment(O_DPAR,'NAZWA'));
   _env.define('WLACZONY',MS.name(O_FPAR,'WLACZONY'),0,,MS.comment(O_FPAR,'WLACZONY'));
   _env.define('WYM_JEST',MS.name(O_FPAR,'WYM_JEST'),0,,MS.comment(O_FPAR,'WYM_JEST'));
   _env.define('MOD_BL',  MS.name(O_FPAR,'MOD_BL'),0,,  MS.comment(O_FPAR,'MOD_BL'));
   _env.define('FML_BL',  MS.name(O_FPAR,'FML_BL'),0,,  MS.comment(O_FPAR,'FML_BL'));
   _env.define('MOD_AE',  MS.name(O_FPAR,'MOD_AE'),0,,  MS.comment(O_FPAR,'MOD_AE'));
   _env.define('FML_AE',  MS.name(O_FPAR,'FML_AE'),0,,  MS.comment(O_FPAR,'FML_AE'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_o_fpar',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_o_fpar',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_o_fpar',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_env:=_a;
_buf:=_b;

O_FPAR.O_DPAR();
_buf.O_DPAR.VALUE:=O_DPAR.KOD;
_buf.SYMBOL.VALUE:=O_DPAR.SYMBOL;
_buf.NAZWA.VALUE:=O_DPAR.NAZWA;
_buf.WLACZONY.VALUE:=O_FPAR.WLACZONY;
_buf.WYM_JEST.VALUE:=O_FPAR.WYM_JEST;
_buf.MOD_BL.VALUE:=O_FPAR.MOD_BL;
_buf.FML_BL.VALUE:=O_FPAR.memo_txt(,1,'FML_BL');
_buf.MOD_AE.VALUE:=O_FPAR.MOD_AE;
_buf.FML_AE.VALUE:=O_FPAR.memo_txt(,1,'FML_AE');

1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.O_DPAR=''   || 'O_DPAR'
   |? _buf.WLACZONY='' || 'TYP'
   |? _buf.WYM_JEST='' || 'WYM_JEST'
   |? _buf.MOD_BL=''   || 'MOD_BL'
   |? _buf.MOD_AE=''   || 'MOD_AE'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.WLACZONY))=0
|| _val.msg_inset('WLACZONY','T','N');
   _val.RESULT:=0;
   return()
|? ('TN'*(1+_buf.WYM_JEST))=0
|| _val.msg_inset('WYM_JEST','T','N');
   _val.RESULT:=0;
   return()
|? ('TN'*(1+_buf.MOD_BL))=0
|| _val.msg_inset('MOD_BL','T','N');
   _val.RESULT:=0;
   return()
|? ('TN'*(1+_buf.MOD_AE))=0
|| _val.msg_inset('MOD_AE','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(O_DPAR,'KOD');
{? +_buf.O_DPAR>_max
|| _val.msg_length('O_DPAR',_max);
   _buf.SYSTEM:=_max+_buf.SYSTEM
?};

_def:=null;
{? O_DPAR.find_key(_buf.O_DPAR,)
|| _def:=O_DPAR.ref()
|| _val.msg_norec('O_DPAR');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? O_FPAR.find_tab(,'O_DPAR',,'=',_def)
|| _ref:=O_FPAR.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[_a.O_DPAR]",
   'O_DPAR',_def
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,O_FPAR,"
   _a.FIRMA:=exec('ref_firma','ustawienia');
   _a.O_DPAR:=_c.O_DPAR;
   _a.WLACZONY:=_b.WLACZONY;
   _a.WYM_JEST:=_b.WYM_JEST;
   _a.MOD_BL:=_b.MOD_BL;
   _a.memo_set(_b.FML_BL,'FML_BL');
   _a.MOD_AE:=_b.MOD_AE;
   _a.memo_set(_b.FML_AE,'FML_AE');
   ~~
",'FML_BL,FML_AE');
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 4dd2ba14feb24e55632a8e21648d2b2c3e454dbcd605941f8b610804ff13a1cd9894ab2756e79fc3848268a4302526d715800791670a65ad0d70963f4821122af8d27bedf0c7708ca0ffba91e57a5832b46912ec95fc4ed6928fda114c38025a67d9b074543563f362511be18d62c310ea9b21536e20d4ec7f6134d7abf929ae

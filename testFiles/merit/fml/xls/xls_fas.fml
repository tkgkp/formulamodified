:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_fas.fml
:: Utworzony: 08.01.2019
:: Autor:MW
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu sposobów fakturowania (dla umów cykliczne)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='FAS';
_def.DOMAIN:=exec('name','#b_domain','LUM');
_def.FUNPAR:='ZWS_PAR_UFAS';

_def.FILE:='umowy_cykliczne.xlsx';
_def.SHEET:='Sposoby fakturowania'@+'=1,1';
_def.NAME:='Sposoby fakturowania umów cyklicznych'@;
_def.DESC:='Sposoby fakturowania umów cyklicznych'@;

_def.PREFIX:="exec('prefix','xls_fas')";
_def.TABLE:="exec('table','xls_fas')";
_def.FIELDS:="exec('fields','xls_fas',_a)";

_def.BEFORE:="FAS.cntx_psh()";
_def.AFTER:="FAS.cntx_pop()";
_def.SELECT:="exec('select','xls_fas',_a)";

_def.EXPORT:="exec('export','xls_fas',_a,_b)";
_def.VALIDATE:="exec('validate','xls_fas',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_fas',_a,_b,_c)";
~~

\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
FAS.index('KOD');
FAS.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
FAS


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod'@,0,,'Kod (max. 2 znaki)'@);
_def.define('NAZ','Nazwa'@,1,,'Opis (max. 40 znaków)'@);
_def.define('WYS','Wyświetlać'@,1,,'T/N',type_of(''));
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
FAS.prefix();
{? _validate.ACTION='add'
|| FAS.blank()
?};
FAS.KOD:=_obj.KOD;
FAS.NAZ:=_obj.NAZ;
FAS.WYS:=_obj.WYS;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=FAS.KOD;
_table.NAZ.VALUE:=FAS.NAZ;
_table.WYS.VALUE:=FAS.WYS;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? (+_obj.KOD)>2
||
  _result.msg_length('KOD',2);
  _result.RESULT:=0
?};

{? (+_obj.NAZ)>40
||
  _result.msg_length('NAZ',40);
  _result.RESULT:=0
?};

{? _obj.WYS<>'T' & _obj.WYS<>'N'
|| _result.msg_inset('WYS','T','N');
   _result.RESULT:=0
?};


:: OK
{? _result.RESULT>0
|| FAS.index('KOD');
   FAS.prefix(_obj.KOD,);
   {? FAS.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_fas',_obj,_mode,_result);
         {? exec('chk_fas_core','umowy',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_fas',_obj,_mode,_result);
      {? exec('chk_fas_core','umowy',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=FAS.add(1)
   |? _validate.ACTION='put'
   || _result:=FAS.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie sposobu fakturowania o kodzie %1 nie powiodło się.'@[$_obj.KOD]
   || _validate.MSG:='Poprawa sposobu fakturowania o kodzie %1 nie powiodła się.'@[$_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'KOD','STRING[2]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[40]','Nazwa'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

FAS.cntx_psh();
FAS.index('KOD');
FAS.prefix();
{? FAS.first()
|| {!
   |?  _tab.blank();
       _tab.REF:=$FAS.ref();
       _selected.prefix($FAS.ref());
       {? _selected.first()
       || _tab.SELECTED:='T'
       || _tab.SELECTED:='N'
       ?};
       _tab.KOD:=FAS.KOD;
       _tab.NAZ:=FAS.NAZ;
       _tab.add();
       FAS.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'KOD,NAZ',20,'Wybór sposobów fakturowania do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
FAS.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:47 7d803fe50f10169c2d304820843b3266a36ff9292d5389c6cee1d42301b56fb4c00cf86dea483211c325258831dff1b6b9a1ef66b301b1af88dc94b3e7392abde112f9c737624fe673891b69a4e0818805547322d0d8cc024fa279582911bc6ecbb4140c8ffb78dbb6b702a160bb88098a1acb9a135f1d5f12e698827011d1ea

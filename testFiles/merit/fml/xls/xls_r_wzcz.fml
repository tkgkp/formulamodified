:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_r_wzcz.fml
:: Utworzony: 27.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu wzorców czasu pracy
::======================================================================================================================


\init_zas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='R_WZCZ_ZAS';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZAS';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Wzorce czasu pracy zasobów'@+'=1,1';
_def.NAME:='Wzorce czasu pracy zasobów'@;
_def.DESC:='Wzorce czasu pracy zasobów produkcyjnych'@;

_def.PREFIX:="R_WZCZ.index('R_WZ_BR'); R_WZCZ.prefix(null())";
_def.TABLE:="R_WZCZ";
_def.FIELDS:="exec('fields','xls_r_wzcz',_a)";

_def.BEFORE:="R_WZCZ.cntx_psh()";
_def.AFTER:="R_WZCZ.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_r_wzcz',_a,_b)";
_def.VALIDATE:="exec('validate','xls_r_wzcz',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_r_wzcz',_a,_b,_c)";
_def.SELECT:="exec('select','xls_r_wzcz',_a,'ZAS')";
~~


\init_bryg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='R_WZCZ_BRYG';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TBRG';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Wzorce czasu pracy brygad'@+'=1,1';
_def.NAME:='Wzorce czasu pracy brygad'@;
_def.DESC:='Wzorce czasu pracy brygad'@;

_def.PREFIX:="R_WZCZ.index('R_WZ_ZAS'); R_WZCZ.prefix(null(),null())";
_def.TABLE:="R_WZCZ";
_def.FIELDS:="exec('fields','xls_r_wzcz',_a)";

_def.BEFORE:="R_WZCZ.cntx_psh()";
_def.AFTER:="R_WZCZ.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_r_wzcz',_a,_b)";
_def.VALIDATE:="exec('validate','xls_r_wzcz',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_r_wzcz',_a,_b,_c)";
_def.SELECT:="exec('select','xls_r_wzcz',_a,'BRYG')";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TYPE','Typ zasobu'@,0,,
   'Dopuszczalne wartości:\n''%1''\n''%2''\n''%3''.'@['Brygada','Stanowisko','Gniazdo']
);
_def.define('RESOURCE','Zasób'@,0);
_def.define('KAL','Kalendarz'@,0);
_def.define('OD','Od daty'@,0);
_def.define('KIN','Wzorzec indywidualny'@,1,,'[T]/[N]');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
R_WZCZ.prefix();
{? _validate.ACTION='add'
|| R_WZCZ.blank()
?};
R_WZCZ.ZLBR:=_validate.OBJ.ZLBR;
R_WZCZ.TWRKPLC:=_validate.OBJ.TWRKPLC;
R_WZCZ.TWRKZBR:=_validate.OBJ.TWRKZBR;
R_WZCZ.MASKA:=_validate.OBJ.MASKA;
R_WZCZ.REKORD:=_validate.OBJ.REKORD;
R_WZCZ.KAL:=_validate.OBJ.KAL;
R_WZCZ.CZESC:=_validate.OBJ.CZESC;
R_WZCZ.OD:=_obj.OD;
R_WZCZ.KIN:=_obj.KIN;
R_WZCZ.GRAFIK:='N';
R_WZCZ.UKRYTY:='N';
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
{? R_WZCZ.UKRYTY='T' | R_WZCZ.GRAFIK='T' | 1+R_WZCZ.KAL().NAZWA=%255 || _result:=0 ?};

{? _result>0
|| {? R_WZCZ.ZLBR<>null()
   || _tab:=ZLBR;
      _type:='Brygada';
      _resource:=exec('FindAndGet','#table',_tab,R_WZCZ.REKORD,R_WZCZ.MASKA,"ZLBR.KOD",'')
   |? R_WZCZ.TWRKPLC<>null()
   || _tab:=TWRKPLC;
      _type:='Stanowisko';
      _resource:=exec('FindAndGet','#table',_tab,R_WZCZ.REKORD,R_WZCZ.MASKA,"TWRKPLC.KOD",'')
   |? R_WZCZ.TWRKZBR<>null()
   || _tab:=TWRKZBR;
      _type:='Gniazdo';
      _resource:=exec('FindAndGet','#table',_tab,R_WZCZ.REKORD,R_WZCZ.MASKA,"TWRKZBR.SYMBOL",'')
   || _result:=0
   ?}
?};

{? _result>0
|| _table.TYPE.VALUE:=_type;
   _table.RESOURCE.VALUE:=_resource;
   _table.KAL.VALUE:=R_WZCZ.KAL().NAZWA; _excel.write_async('KAL_NAZW',R_WZCZ.KAL);
   _table.OD.VALUE:=R_WZCZ.OD;
   _table.KIN.VALUE:=R_WZCZ.KIN
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('KAL','ZLBR','TWRKPLC','TWRKZBR','MASKA','REKORD','CZESC');
_result.OBJ.ZLBR:=_result.OBJ.TWRKPLC:=_result.OBJ.TWRKZBR:=_result.OBJ.CZESC:=null();
_result.OBJ.KAL:=exec('FindInSet','#table','KAL_NAZW','KAL_NAZW',_obj.KAL,REF.FIRMA,"KAL_NAZW.ref()",1);

:: Błędy
{? _result.OBJ.KAL=null()
|| _result.msg_norec(,'Kalendarz'@);
   _result.RESULT:=0
?};
{? _obj.TYPE='Brygada'
|| _result.OBJ.ZLBR:=exec('FindInSet','#table','ZLBR','KOD',_obj.RESOURCE,,"ZLBR.ref()",1);
   {? _result.OBJ.ZLBR=null()
   || _result.msg_norec(,'Zasób'@);
      _result.RESULT:=0
   || _result.OBJ.MASKA:=ref_name(_result.OBJ.ZLBR);
      _result.OBJ.REKORD:=#_result.OBJ.ZLBR;
      _result.OBJ.CZESC:=exec('FindAndGet','#table',ZLBR,_result.OBJ.ZLBR,,"KAL",null())
   ?}
|? _obj.TYPE='Stanowisko'
|| _result.OBJ.TWRKPLC:=exec('FindInSet','#table','TWRKPLC','KOD',_obj.RESOURCE,,"TWRKPLC.ref()",1);
   {? _result.OBJ.TWRKPLC=null()
   || _result.msg_norec(,'Zasób'@);
      _result.RESULT:=0
   || _result.OBJ.MASKA:=ref_name(_result.OBJ.TWRKPLC);
      _result.OBJ.REKORD:=#_result.OBJ.TWRKPLC;
      _result.OBJ.CZESC:=exec('FindAndGet','#table',TWRKPLC,_result.OBJ.TWRKPLC,,"KAL",null())
   ?}
|? _obj.TYPE='Gniazdo'
|| _result.OBJ.TWRKZBR:=exec('FindInSet','#table','TWRKZBR','SYM',_obj.RESOURCE,,"TWRKZBR.ref()",1);
   {? _result.OBJ.TWRKZBR=null()
   || _result.msg_norec(,'Zasób'@);
      _result.RESULT:=0
   || _result.OBJ.MASKA:=ref_name(_result.OBJ.TWRKZBR);
      _result.OBJ.REKORD:=#_result.OBJ.TWRKZBR;
      _result.OBJ.CZESC:=exec('FindAndGet','#table',TWRKZBR,_result.OBJ.TWRKZBR,,"KAL",null())
   ?}
|| _result.MSG:='Błędny typ zasobu (dopuszczalne: ''%1'', ''%2'', ''%3'').'@['Brygada','Stanowisko','Gniazdo'];
   _result.RESULT:=0
?};

_chk:="__CHK.record(R_WZCZ,,'OD')";

:: OK
{? _result.RESULT>0
|| R_WZCZ.index('R_WZCZ');
   R_WZCZ.prefix(_result.OBJ.MASKA,_result.OBJ.REKORD,_obj.OD);
   {? R_WZCZ.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_r_wzcz',_obj,_mode,_result);
         {? _chk(1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_r_wzcz',_obj,_mode,_result);
      {? _chk(0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=R_WZCZ.add(1)
   |? _validate.ACTION='put'
   || _result:=R_WZCZ.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie wzorca kalendarza %1 dla %2 nie powiodło się.'@[_obj.KAL,_obj.RESOURCE]
   || _validate.MSG:='Poprawa wzorca kalendarza %1 dla %2 nie powiodła się.'@[_obj.KAL,_obj.RESOURCE]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - STRING - kontekst wywołania: 'BRYG' - brygady, 'ZAS' - zasoby produkcyjne
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_type:=_b;

_result:=0;

_tab:=tab_tmp(4
   ,'TYPE','STRING[15]','Typ zasobu'@
   ,'RESOURCE','STRING[60]','Zasób'@
   ,'OD','DATE','Od daty'@
   ,'KAL','STRING[20]','Kalendarz'@
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'@
);

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KAL',,);

R_WZCZ.cntx_psh();
{? _type='BRYG'
|| R_WZCZ.index('R_WZ_ZAS');
   R_WZCZ.prefix(null(),null())
|? _type='ZAS'
|| R_WZCZ.index('R_WZ_BR');
   R_WZCZ.prefix(null())
::|| R_WZCZ.index('R_WZCZ');
::   R_WZCZ.prefix()
?};
{? R_WZCZ.first()
|| {!
   |?
      {? R_WZCZ.UKRYTY='T' | R_WZCZ.GRAFIK='T' | 1+R_WZCZ.KAL().NAZWA=%255
      || ~~
      || _tab.blank();
         _tab.REF:=$R_WZCZ.ref();
         _selected.prefix($R_WZCZ.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.KAL:=R_WZCZ.KAL().NAZWA;
         {? R_WZCZ.ZLBR<>null()
         || _type:='Brygada';
            _resource:=exec('FindAndGet','#table',ZLBR,R_WZCZ.REKORD,R_WZCZ.MASKA,"ZLBR.KOD",'')
         |? R_WZCZ.TWRKPLC<>null()
         || _type:='Stanowisko';
            _resource:=exec('FindAndGet','#table',TWRKPLC,R_WZCZ.REKORD,R_WZCZ.MASKA,"TWRKPLC.KOD",'')
         |? R_WZCZ.TWRKZBR<>null()
         || _type:='Gniazdo';
            _resource:=exec('FindAndGet','#table',TWRKZBR,R_WZCZ.REKORD,R_WZCZ.MASKA,"TWRKZBR.SYMBOL",'')
         || _type:='';
            _resource:=''
         ?};
         _tab.TYPE:=_type;
         _tab.RESOURCE:=_resource;
         _tab.OD:=R_WZCZ.OD;

         {? _tab.TYPE<>'' || _tab.add() ?}
      ?};
      R_WZCZ.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'TYPE,RESOURCE,OD,KAL',15,'Wybór wzorców czasu pracy do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
R_WZCZ.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2021/04/09 15:28:03 47d8ea23896f2a2b5966c720e3ecccb9d416234ca02021ddacad3ccb18cb709f514b07cb315f29cd84a9138ba4c6a65d090bad7595a8e842c19c35bdcd5c5a2f10c08c6b168c6a1a12345e23d27f1e908a37983370e2021098cebbac799c0bf3cfa2c9a9ea79b47c2b6343fc787c3a049b1e4cb496ea13ae968b9d4ba434b58a

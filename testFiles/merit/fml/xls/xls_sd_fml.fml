:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_sd_fml.fml
:: Utworzony: 13.03.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='SD_FML';
_def.FUNPAR:='ZWS_PAR_ASDD';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_sd_def_xlsx','xls__init');
_def.SHEET:='Formuły parametrów=1,1';
_def.NAME:='Formuły parametrów szablonów'@;
_def.DESC:='Formuły parametrów szablonów';
_def.ADD_ROWS:=0;
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="SD_FML";

: prolog
_def.BEFORE:="
   SD_FML.cntx_psh();
   SD_FML.f_clear();
   SD_FML.index('KOD');
   SD_FML.prefix();
   SD_PAR.cntx_psh();
   SD_PAR.index('KOD');
   SD_PAR.prefix();
   SD_XSLO.cntx_psh();
   SD_XSLO.index('KOD');
   SD_XSLO.prefix();
   ~~
";

: epilog
_def.AFTER:="
   SD_XSLO.cntx_pop();
   SD_PAR.cntx_pop();
   SD_FML.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYSTEM', MS.name(SD_FML,'SYSTEM'),0,, MS.comment(SD_FML,'SYSTEM'));
   _env.define('KOD',    MS.name(SD_FML,'KOD'),0,,    MS.comment(SD_FML,'KOD'));
   _env.define('SD_PAR', MS.name(SD_FML,'SD_PAR'),0,, MS.comment(SD_FML,'SD_PAR'),type_of(''));
   _env.define('SD_XSLO',MS.name(SD_FML,'SD_XSLO'),0,,MS.comment(SD_FML,'SD_XSLO'),type_of(''));
   _env.define('TRESC',  MS.name(SD_FML,'TRESC'),0,,  MS.comment(SD_FML,'TRESC'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYSTEM.VALUE:=SD_FML.SYSTEM;
   _buf.KOD.VALUE:=SD_FML.KOD;
   _buf.SD_PAR.VALUE:=SD_FML.SD_PAR().KOD;
   _buf.SD_XSLO.VALUE:=SD_FML.SD_XSLO().KOD;
   _buf.TRESC.VALUE:=SD_FML.memo_txt(,1,'TRESC');
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_sd_fml',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_sd_fml',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYSTEM=''  || 'SYSTEM'
   |? _buf.KOD=''     || 'KOD'
   |? _buf.SD_PAR=''  || 'SD_PAR'
   |? _buf.SD_XSLO='' || 'SD_XSLO'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.SYSTEM))=0
|| _val.msg_inset('SYSTEM','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(SD_FML,'SYSTEM');
{? +_buf.SYSTEM>_max
|| _val.msg_length('SYSTEM',_max);
   _buf.SYSTEM:=_max+_buf.SYSTEM
?};
_max:=MS.fld_len(SD_FML,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(SD_PAR,'KOD');
{? +_buf.SD_PAR>_max
|| _val.msg_length('SD_PAR',_max);
   _buf.SD_PAR:=_max+_buf.SD_PAR
?};
_max:=MS.fld_len(SD_XSLO,'KOD');
{? +_buf.SD_XSLO>_max
|| _val.msg_length('SD_XSLO',_max);
   _buf.SD_XSLO:=_max+_buf.SD_XSLO
?};

: weryfikacja spójności referencyjnej
_par:=null;
{? SD_PAR.find_key(_buf.SD_PAR,)
|| _par:=SD_PAR.ref()
|| _val.msg_norec('SD_PAR');
   _val.RESULT:=0;
   return()
?};
_slo:=null;
{? SD_XSLO.find_key(_buf.SD_XSLO,)
|| _slo:=SD_XSLO.ref()
|| _val.msg_norec('SD_XSLO');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? (SD_FML.index('KOD'); SD_FML.find_key(_buf.KOD,)) |
   (SD_FML.index('UNIQUE'); SD_FML.find_key(_slo,_par))
|| _ref:=SD_FML.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[_a.KOD]",
   'SD_PAR',_par,
   'SD_XSLO',_slo
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,SD_FML,"
   _a.SYSTEM:=_b.SYSTEM;
   _a.KOD:=_b.KOD;
   _a.SD_PAR:=_c.SD_PAR;
   _a.SD_XSLO:=_c.SD_XSLO;
   _a.memo_set(_b.TRESC,'TRESC');
   ~~
",'TRESC');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 82dc5928565e50bffaa306b96807ccb5472ec33094c223c16fd7618ec05fc7764df2aa974517a16eba864a9707e53140dc252b27856c52d43feef2a6ef72415dfd53b4dbe7ff05677abe28dcf72e56ab7be9520b8c2fbe5bedf5248725224dcdcb38b044d4fea2de5069a568dad765021d37498e80fc7788e8dac9473005531b

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_slo_typ.fml
:: Utworzony: 02.08.2018
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu słowników kadrowych - typy.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Inicjowanie mechanizmu.
::   WE:  _a [OBJECT]  - Środowisko mechanizmu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='SLO_TYP';
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.NAME:='Typy słowników kadrowych'@;
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FUNPAR:='ZWS_PAR_PTSL';
_def.SHEET:='Typy słowników kadrowych'@+'=1,1';
_def.DESC:='Definicja typów słowników kadrowych'@;
_def.MULTIFIR:='T';

_def.TABLE:="SLO_TYP";
_def.BEFORE:="
   SLO_TYP.cntx_psh();
   SLO_TYP.f_clear();
   SLO_TYP.index('SYMBOL');
   SLO_TYP.prefix()
";
_def.AFTER:="SLO_TYP.cntx_pop()";

_def.FIELDS:="
   _fld:=_a;
   _fl:=""MS.name(SLO_TYP,_a)"";
   _fc:=""MS.comment(SLO_TYP,_a)"";

   _fld.define('SYMBOL',_fl('SYMBOL'),0,,_fc('SYMBOL')+' (max. %1 zn.)' [$MS.fld_len(SLO_TYP,'SYMBOL')]);
   _fld.define('OPIS',_fl('OPIS'),1,,_fc('OPIS')+' (max. %1 zn.)' [$MS.fld_len(SLO_TYP,'OPIS')]);
   _fld.define('DL_KOD',_fl('DL_KOD'),1,,_fc('DL_KOD')+' [0;%1]' [$MS.fld_len(SLO_KOD,'KOD')],,0);
   _fld.define('DL_NAZ',_fl('DL_NAZ'),1,,_fc('DL_NAZ')+' [0;%1]' [$MS.fld_len(SLO_NAZ,'NAZWA')],,0);

   ~~
";

_def.EXPORT:="
   _excel:=_a;
   _buf:=_b;

   _buf.SYMBOL.VALUE:=SLO_TYP.SYMBOL;
   _buf.OPIS.VALUE:=SLO_TYP.OPIS;
   _buf.DL_KOD.VALUE:=SLO_TYP.DL_KOD;
   _buf.DL_NAZ.VALUE:=SLO_TYP.DL_NAZ;

   1
";

_def.VALIDATE:="exec('validate','xls_slo_typ',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_slo_typ',_a,_b,_c)";

~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła sprawdza, czy można poprawić/dodać rekord.
::   WE: _a [OBJECT] - Obiekt z polami odczytami z Excela.
::       _b [NUMBER] - Tryb aktualizacji: 0 - nie (dodawane są tylko nowe rekordy) / 1 - tak.
::       _c [OBJECT] - Obiekt (RESULT, ACTION, OBJ) - wynik walidacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_update:=_b;
_validate:=_c;

:: Błędy krytyczne
_fld:=
   {? _buf.SYMBOL=''
   || 'SYMBOL'
   |? _buf.OPIS=''
   || 'OPIS'
   || ''
   ?};
{? _fld<>''
|| _validate.msg_empty(_fld);
   _validate.RESULT:=0;
   return()
?};

_mkod:=MS.fld_len(SLO_KOD,'KOD');
{? _buf.DL_KOD<0 | _mkod<_buf.DL_KOD
|| _validate.msg_range('DL_KOD',0,_mkod);
   _validate.RESULT:=0;
   return()
?};

_mnazwa:=MS.fld_len(SLO_NAZ,'NAZWA');
{? _buf.DL_NAZ<0 | _mnazwa<_buf.DL_NAZ
|| _validate.msg_range('DL_NAZ',0,_mnazwa);
   _validate.RESULT:=0;
   return()
?};

:: Ostrzeżenia
_dl:=MS.fld_len(SLO_TYP,'SYMBOL');
{? +_buf.SYMBOL>_dl
|| _validate.msg_length('SYMBOL',_dl)
?};
_dl:=MS.fld_len(SLO_TYP,'OPIS');
{? +_buf.OPIS>_dl
|| _validate.msg_length('OPIS',_dl)
?};
{? int(_buf.DL_KOD)<>_buf.DL_KOD
|| _validate.msg_int('DL_KOD')
?};
{? int(_buf.DL_NAZ)<>_buf.DL_NAZ
|| _validate.msg_int('DL_NAZ')
?};

:: Wybór akcji
{? SLO_TYP.find_key(_buf.SYMBOL,)
|| {? _update
   || {? SLO_TYP.OPIS<>_buf.OPIS | SLO_TYP.DL_KOD<>_buf.DL_KOD | SLO_TYP.DL_NAZ<>_buf.DL_NAZ
      || _validate.ACTION:='put'
      || _validate.ACTION:='~put'
      ?}
   || _validate.RESULT:=0
   ?}
|| _validate.ACTION:='add'
?};

~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła odpowiedzialna za utworzenie lub aktualizację rekordu.
::   WE: _a [OBJECT] - Obiekt z polami odczytami z Excela.
::       _b [NUMBER] - Tryb aktualizacji: 0 - nie (dodawane są tylko nowe rekordy) / 1 - tak.
::       _c [OBJECT] - Obiekt (RESULT, ACTION, OBJ) - wynik walidacji.
::   WY: Status operacji: 0/1
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_update:=_b;
_validate:=_c;

_przypisz:="
   _buf:=_a;
   _update:=_b;
   _validate:=_c;

   SLO_TYP.SYMBOL:=_buf.SYMBOL;
   SLO_TYP.OPIS:=_buf.OPIS;
   SLO_TYP.DL_KOD:=_buf.DL_KOD;
   SLO_TYP.DL_NAZ:=_buf.DL_NAZ;
   ~~
";

_key:=_buf.SYMBOL;

_ret:=1;

{? _validate.ACTION='add'
|| SLO_TYP.blank();
   _przypisz(_buf,_update,_validate);
   {? ~SLO_TYP.add(1)
   || _ret:=0;
      _validate.msg_insert(_key)
   ?}
|? _validate.ACTION='put'
|| _przypisz(_buf,_update,_validate);
   {? ~SLO_TYP.put(1)
   || _ret:=0;
      _validate.msg_update(_key)
   ?}
?};

_ret

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 51d9e693de35dd02d96fc39660bb86cc2eda839cc6616a6b681787ed5d21a0fccfe094186791114052c19e31ef5dad72c4389248d180ccc4b1030d807ec299c19807e7159cba1badd81e75dc001fe25a430fce86633ef153615a3d547a751679bcb55e00252bce93f7355e0097d8b6009ae2c399afd15e5d5149b5500fb6bcc2

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_a_okrka.fml
:: Utworzony: 18.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu kalendarzy w okresie
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='A_OKRKA';
_def.FUNPAR:='ZWS_PAR_PNOR';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Kalendarze w okresie=1,1';
_def.NAME:='Kalendarze w okresie'@;
_def.DESC:='Kalendarze w okresie';

: źródło danych
_def.TABLE:="A_OKRKA";

: prolog
_def.BEFORE:="
   KAL_NAZW.cntx_psh();
   KAL_NAZW.prefix();
   A_OKRN.cntx_psh();
   A_OKRN.prefix();
   A_OKRKA.cntx_psh();
   A_OKRKA.index('A_OKRKA');
   A_OKRKA.prefix();
   ~~
";

: epilog
_def.AFTER:="
   A_OKRKA.cntx_pop();
   A_OKRN.cntx_pop();
   KAL_NAZW.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('NAZ',   MS.name(A_OKRKA,'NAZ'),   0,,MS.comment(A_OKRKA,'NAZ'),type_of(''));
   _env.define('KALEND',MS.name(A_OKRKA,'KALEND'),0,,MS.comment(A_OKRKA,'KALEND'),type_of(''));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.NAZ.VALUE:=A_OKRKA.NAZ().NAZ;
   _buf.KALEND.VALUE:=A_OKRKA.KALEND().NAZWA;
   _env.write_async('A_OKRN',A_OKRKA.NAZ);
   _env.write_async('KAL_NAZW',A_OKRKA.KALEND);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_a_okrka',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_a_okrka',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.NAZ=''    || 'NAZ'
   |? _buf.KALEND='' || 'KALEND'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(A_OKRN,'NAZ');
{? +_buf.NAZ>_max
|| _val.msg_length('NAZ',_max);
   _buf.NAZ:=_max+_buf.NAZ
?};
_max:=MS.fld_len(KAL_NAZW,'NAZWA');
{? +_buf.KALEND>_max
|| _val.msg_length('KALEND',_max);
   _buf.KALEND:=_max+_buf.KALEND
?};

: weryfikacja spójności referencyjnej
{? ~A_OKRN.find_tab(,'NAZ',,'=',_buf.NAZ)
|| _val.msg_norec('NAZ');
   _val.RESULT:=0;
   return()
|| _okr:=A_OKRN.ref()
?};
{? ~KAL_NAZW.find_tab(,'FIRMA',,'=',REF.FIRMA,'NAZWA',,'=',_buf.KALEND)
|| _val.msg_norec('KALEND');
   _val.RESULT:=0;
   return()
|| _kal:=KAL_NAZW.ref()
?};

_ref:=null;
{? A_OKRKA.find_tab(,'KALEND',,'=',_kal)
|| _ref:=A_OKRKA.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1, %2'[_a.NAZ,_a.KALEND]",
   'NAZ',_okr,
   'KALEND',_kal
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,A_OKRKA,"
   _a.NAZ:=_c.NAZ;
   _a.KALEND:=_c.KALEND;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:46 e0ce977dbf2da0cd948a2015da32222c4c651140f26324423c88d56290fd9f843a72369736418539933eafd3b80b9aa56a7c946eebb4f8276c211d4577a34d424804358788095f83b8acf05b33116c1d9752726672a822a394c2626be71a3794b1dae08c04d3e81159e65aa08a9ac5b27c51218ec52e9901530755c136805973

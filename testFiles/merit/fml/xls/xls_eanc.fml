:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_eanc.fml
:: Utworzony: 30.07.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji urządzeń mobilnych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='EANC';
_def.DOMAIN:=exec('name','#b_domain','LMO');
_def.FUNPAR:='ZWS_MOBI_DEF';

_def.FILE:='magazyn_mobilny.xlsx';
_def.SHEET:='Urządzenia mobilne=1,1';
_def.NAME:='Urządzenia mobilne'@;
_def.DESC:='Urządzenia mobilne';

_def.PREFIX:="exec('prefix','xls_eanc')";
_def.TABLE:="exec('table','xls_eanc')";
_def.FIELDS:="exec('fields','xls_eanc',_a)";

_def.BEFORE:="EANC.cntx_psh()";
_def.AFTER:="EANC.cntx_pop()";
_def.SELECT:="exec('select','xls_eanc',_a)";

_def.EXPORT:="exec('export','xls_eanc',_a,_b)";
_def.IMPORT:="exec('import','xls_eanc',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_eanc',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
EANC.index('NRC');
EANC.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
EANC


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NRC','Numer',0,,'Numer czytnika.');
_def.define('OPI','Opis',1,,'Numer czytnika.');
_def.define('AKT','Czy aktywny?',1,,'Czy aktywny? - (T/N)');
_def.define('ODDZ','Oddział',1,,'Kod oddziału - 1 znakowy');
_def.define('IDTERM','Id urządzenia',0,,'Identyfikator urządzenia (sprzętowy)');
_def.define('INTERVAL','Interwał czasowy',1,,'Interwał czasowy - czas odświeżenia');
_def.define('SYSTEM','System operacyjny',1,,
'System operacyjny na urządzeniu:
Windows CE 5
Windows CE 6
Windows CE 7
Windows Mobile 6
Windows Mobile 7
');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

EANC.index('NRC');
EANC.prefix();
{? _validate.ACTION='add'
||
   EANC.blank();
   EANC.NRC:=_obj.NRC
?};

EANC.OPI:=_obj.OPI;
EANC.AKT:={? _obj.IDTERM='' || 'N' || _obj.AKT ?};
EANC.ODDZ:=_validate.OBJ.ODDZ;
EANC.IDTERM:=_obj.IDTERM;
EANC.INTERVAL:=_obj.INTERVAL;
EANC.SYSTEM:=_validate.OBJ.SYSTEM;
EANC.VERSYS:=_validate.OBJ.VERSYS;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.NRC.VALUE:=EANC.NRC;
_table.OPI.VALUE:=EANC.OPI;
_table.ODDZ.VALUE:=EANC.ODDZ().KOD; _excel.write_async('ODDZ',EANC.ODDZ);
_table.IDTERM.VALUE:=EANC.IDTERM;
_table.INTERVAL.VALUE:=EANC.INTERVAL;
_table.SYSTEM.VALUE:={? EANC.SYSTEM<>'' || EANC.SYSTEM+' '+$EANC.VERSYS || '' ?};
_table.AKT.VALUE:=EANC.AKT;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('ODDZ','SYSTEM','VERSYS');

_tabsys:=tab_tmp(1,'SYSTEM','STRING[100]','');
{! _ii:=5..7 |! _tabsys.blank(); _tabsys.SYSTEM:='Windows CE '+$_ii; _tabsys.add(1) !};
{! _ii:=6..7 |! _tabsys.blank(); _tabsys.SYSTEM:='Windows Mobile '+$_ii; _tabsys.add(1) !};
_tabsys.SYSTEM:='Android 6'; _tabsys.add(1);

_result.OBJ.ODDZ:={? _obj.ODDZ='' || null() || exec('FindInSet','#table','ODDZ','KOD2',_obj.ODDZ,,,1) ?};
{? _result.OBJ.ODDZ=null()
|| _result.MSG:='Nie znaleziono oddziału %1.'@[_obj.ODDZ];
   _result.RESULT:=0
?};

{? _obj.SYSTEM<>''
|| _tabsys.clear();
   _tabsys.prefix(_obj.SYSTEM,);
   {? _tabsys.first()
   || _result.OBJ.SYSTEM:=form(_tabsys.SYSTEM-1);
      _result.OBJ.VERSYS:=#(_tabsys.SYSTEM+1)
   || FUN.info('Podany system %1 nie jest dostępny dla aplikacji - usunięto przypisanie.'@[_obj.SYSTEM]);
      _result.OBJ.SYSTEM:='';
      _result.OBJ.VERSYS:=0
   ?}
|| _result.OBJ.SYSTEM:='';
   _result.OBJ.VERSYS:=0
?};
obj_del(_tabsys);

{? _result.RESULT=1
||
   EANC.index('NRC');
   EANC.prefix(_obj.NRC);
   {? EANC.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_eanc',_obj,_mode,_result);
         {? exec('chk_eanc','magazyn_mob',_result.ACTION)<>'' || _result.RESULT:=0 ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_eanc',_obj,_mode,_result);
      {? exec('chk_eanc','magazyn_mob',_result.ACTION)<>'' || _result.RESULT:=0 ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=EANC.add(1)
   |? _validate.ACTION='put'
   || _result:=EANC.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:={? _validate.ACTION='add'
                  || 'Dodanie urządzenia o numerze %1 nie powiodło się.'@[EANC.NRC]
                  || 'Modyfikacja urządzenia o numerze %1 nie powiodła się.'@[EANC.NRC]
                  ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'NRC','INTEGER','Numer czytnika'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OPI','STRING[120]','Opis'
   ,'ODDZ','STRING[1]','Kod oddziału'
   ,'IDTERM','STRING[40]','Id urządzenia'
   ,'SYSTEM','STRING[40]','System');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NRC',,);

EANC.cntx_psh();
EANC.index('NRC');
EANC.prefix();
{? EANC.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$EANC.ref();
      _selected.prefix($EANC.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NRC:=EANC.NRC;
      _tab.OPI:=EANC.OPI;
      _tab.ODDZ:=EANC.ODDZ().KOD;
      _tab.IDTERM:=EANC.IDTERM;
      _tab.SYSTEM:={? EANC.SYSTEM<>'' || EANC.SYSTEM+' '+$EANC.VERSYS || '' ?};
      _tab.add();
      EANC.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NRC,OPI,ODDZ,IDTERM,SYSTEM',15,'Wybór urządzeń mobilnych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
EANC.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2020/04/03 17:14:07 ba11e17da00096e007416f2aaec8fc6fa2b7ce20fdd2866259e518f730bd38cdfb323cc6d356bac784018f312e556c36778e8eee5ed8a5969bc86c3ededf2f9449af93b1def1ce12ff30265db15a570d7b99e5061d4f0412844361f437187999250b4ce3ea0abea02cbf67f071b6a9f0bc65d753b23f3bb0c70b7c26f61bb3ff

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kal_ud.fml
:: Utworzony: 07.08.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu kalendarzy wydziałów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KAL_UD';
_def.DOMAIN:=exec('name','#b_domain','TPP');
_def.FUNPAR:='ZWS_PAR_TOKA';

_def.FILE:='produkcja_planowanie.xlsx';
_def.SHEET:='Kalendarze wydziałów'@+'=1,1';
_def.NAME:='Kalendarze wydziałów'@;
_def.DESC:='Kalendarze wydziałów'@;

_def.PREFIX:="KAL_UD.index('RORWYD'); KAL_UD.prefix('W')";
_def.TABLE:="KAL_UD";
_def.FIELDS:="exec('fields','xls_kal_ud',_a)";

_def.BEFORE:="KAL_UD.cntx_psh()";
_def.AFTER:="KAL_UD.cntx_pop()";
_def.SELECT:="exec('select','xls_kal_ud',_a)";

_def.EXPORT:="exec('export','xls_kal_ud',_a,_b)";
_def.VALIDATE:="exec('validate','xls_kal_ud',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kal_ud',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('UD_SKL',MS.name(KAL_UD,'UD_SKL'),0,,MS.comment(KAL_UD,'UD_SKL'));
_def.define('KAL',MS.name(KAL_UD,'KAL'),1,,
   MS.comment(KAL_UD,'KAL')+' '+'(max. %1 znaków)'@[$MS.fld_len(KAL_NAZW,'NAZWA')]
);
_def.define('PL_WZOR',MS.name(KAL_UD,'PL_WZOR'),1,,
   MS.comment(KAL_UD,'PL_WZOR')+' '+'(max. %1 znaków)'@[$MS.fld_len(PL_WZOR,'NAZWA')]
);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
KAL_UD.prefix();
{? _validate.ACTION='add'
|| KAL_UD.blank();
   KAL_UD.UD_SKL:=_validate.OBJ.UD_SKL;
   KAL_UD.RESORWYD:='W';
   KAL_UD.PL_RODZ:='W'
?};
KAL_UD.KAL:=_validate.OBJ.KAL;
KAL_UD.PL_WZOR:=_validate.OBJ.PL_WZOR;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.UD_SKL.VALUE:=KAL_UD.UD_SKL().SYMBOL;  _excel.write_async('UD_SKL',KAL_UD.UD_SKL);
_table.KAL.VALUE:=KAL_UD.KAL().NAZWA;         _excel.write_async('KAL_NAZW',KAL_UD.KAL);
_table.PL_WZOR.VALUE:=KAL_UD.PL_WZOR().NAZWA; _excel.write_async('PL_WZOR',KAL_UD.PL_WZOR);
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('UD_SKL','KAL','PL_WZOR');
_ud_typ:=null();
UD_TYP.cntx_psh();
UD_TYP.index('SYMBOL');
UD_TYP.prefix('PODZORG');

{? UD_TYP.first() || _ud_typ:=UD_TYP.ref() ?};
UD_TYP.cntx_pop();
_result.OBJ.UD_SKL:=exec('FindInSet','#table','UD_SKL','SYMBOL',_obj.UD_SKL,_ud_typ,"UD_SKL.ref()",1);
_result.OBJ.KAL:=exec('FindInSet','#table','KAL_NAZW','KAL_NAZW',_obj.KAL,REF.FIRMA,"KAL_NAZW.ref()",1);
_result.OBJ.PL_WZOR:=exec('FindInSet','#table','PL_WZOR','NAZWA',_obj.PL_WZOR,,"PL_WZOR.ref()",1);

:: Błędy
{? _result.OBJ.UD_SKL=null()
|| _result.msg_norec('UD_SKL');
   _result.RESULT:=0
?};
{? _obj.KAL<>'' & _result.OBJ.KAL=null()
|| _result.msg_norec('KAL');
   _result.RESULT:=0
?};
{? _obj.PL_WZOR<>'' & _result.OBJ.PL_WZOR=null()
|| _result.msg_norec('PL_WZOR');
   _result.RESULT:=0
?};

_chk:="''";

:: OK
{? _result.RESULT>0
|| KAL_UD.index('RESORWYD');
   KAL_UD.prefix(_result.OBJ.UD_SKL,'W');
   {? KAL_UD.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_kal_ud',_obj,_mode,_result);
         {? _chk(1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_kal_ud',_obj,_mode,_result);
      {? _chk(0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KAL_UD.add(1)
   |? _validate.ACTION='put'
   || _result:=KAL_UD.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie kalendarza wydziału %1 nie powiodło się.'@[_obj.UD_SKL]
   || _validate.MSG:='Poprawa kalendarza wydziału %1 nie powiodła się.'@[_obj.UD_SKL]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'UD_SKL','STRING[16]','Wydział'@
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'KAL','STRING[20]','Kalendarz'@
   ,'PL_WZOR','STRING[15]','Wzorzec'@
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'UD_SKL',,);

KAL_UD.cntx_psh();
KAL_UD.index('RORWYD');
KAL_UD.prefix('W');
{? KAL_UD.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$KAL_UD.ref();
      _selected.prefix($KAL_UD.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.UD_SKL:=KAL_UD.UD_SKL().SYMBOL;
      _tab.KAL:=KAL_UD.KAL().NAZWA;
      _tab.PL_WZOR:=KAL_UD.PL_WZOR().NAZWA;
      _tab.add();
      KAL_UD.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'UD_SKL,KAL,PL_WZOR',20,'Wybór kalendarzy wydziałów do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KAL_UD.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2021/04/09 15:28:02 720c8b8df552bc66fb7ee98055d9e0abd645333ee29e09936758121790478942c5301798059cbcef9c1b02272788b1ed973ec037ff6e434b982263056583577293ec2b8155091e8fbbb9a461d3ec5037a06d93a65f8a3700ccda3f923bdfdbf079e0618839ef6f422d7b014387d68e63132385277c5a5cffb72062098bfbed7b

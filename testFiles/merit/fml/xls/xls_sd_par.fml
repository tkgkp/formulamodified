:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_sd_par.fml
:: Utworzony: 13.03.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='SD_PAR';
_def.FUNPAR:='ZWS_PAR_ASDD';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_sd_def_xlsx','xls__init');
_def.SHEET:='Parametry szablonów=1,1';
_def.NAME:='Parametry szablonów dokumentów'@;
_def.DESC:='Paramatry szablonów dokumentów';
_def.ADD_ROWS:=0;
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="SD_PAR";

: prolog
_def.BEFORE:="
   SD_PAR.cntx_psh();
   SD_PAR.f_clear();
   SD_PAR.index('KOD');
   SD_PAR.clear();
   SD_DOK.cntx_psh();
   SD_DOK.index('KOD');
   SD_DOK.prefix();
   ~~
";

: epilog
_def.AFTER:="
   SD_DOK.cntx_pop();
   SD_PAR.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYSTEM',  MS.name(SD_PAR,'SYSTEM'),0,,  MS.comment(SD_PAR,'SYSTEM'));
   _env.define('KOD',     MS.name(SD_PAR,'KOD'),0,,     MS.comment(SD_PAR,'KOD'));
   _env.define('SD_DOK',  MS.name(SD_PAR,'SD_DOK'),0,,  MS.comment(SD_PAR,'SD_DOK'),type_of(''));
   _env.define('NUMER',   MS.name(SD_PAR,'NUMER'),0,,   MS.comment(SD_PAR,'NUMER'),,0);
   _env.define('SYMBOL',  MS.name(SD_PAR,'SYMBOL'),0,,  MS.comment(SD_PAR,'SYMBOL'));
   _env.define('NAZWA',   MS.name(SD_PAR,'NAZWA'),0,,   MS.comment(SD_PAR,'NAZWA'));
   _env.define('OPIS',    MS.name(SD_PAR,'OPIS'),0,,    MS.comment(SD_PAR,'OPIS'));
   _env.define('WIDOCZNY',MS.name(SD_PAR,'WIDOCZNY'),0,,MS.comment(SD_PAR,'WIDOCZNY'));
   _env.define('WYMAGANY',MS.name(SD_PAR,'WYMAGANY'),0,,MS.comment(SD_PAR,'WYMAGANY'));
   _env.define('TYP',     MS.name(SD_PAR,'TYP'),0,,     MS.comment(SD_PAR,'TYP'));
   _env.define('DLUGOSC', MS.name(SD_PAR,'DLUGOSC'),0,, MS.comment(SD_PAR,'DLUGOSC'),,0);
   _env.define('PRECYZJA',MS.name(SD_PAR,'PRECYZJA'),0,,MS.comment(SD_PAR,'PRECYZJA'),,0);
   _env.define('OKIENKO', MS.name(SD_PAR,'OKIENKO'),0,, MS.comment(SD_PAR,'OKIENKO'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_sd_par',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_sd_par',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_sd_par',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_buf:=_b;
_buf.SYSTEM.VALUE:=SD_PAR.SYSTEM;
_buf.KOD.VALUE:=SD_PAR.KOD;
_buf.SD_DOK.VALUE:=SD_PAR.SD_DOK().KOD;
_buf.NUMER.VALUE:=SD_PAR.NUMER;
_buf.SYMBOL.VALUE:=SD_PAR.SYMBOL;
_buf.NAZWA.VALUE:=SD_PAR.NAZWA;
_buf.OPIS.VALUE:=SD_PAR.memo_txt(,1,'OPIS');
_buf.WIDOCZNY.VALUE:=SD_PAR.WIDOCZNY;
_buf.WYMAGANY.VALUE:=SD_PAR.WYMAGANY;
_buf.TYP.VALUE:=SD_PAR.TYP;
_buf.DLUGOSC.VALUE:=SD_PAR.DLUGOSC;
_buf.PRECYZJA.VALUE:=SD_PAR.PRECYZJA;
_buf.OKIENKO.VALUE:=SD_PAR.memo_txt(,1,'OKIENKO');

:: formuły parametru
SD_FML.cntx_psh();
SD_FML.index('NUMER');
SD_FML.prefix(SD_PAR.ref());
_loop:=SD_FML.first();
{!
|? _loop
|! _env.write_async('SD_FML',SD_FML.ref());
   _loop:=SD_FML.next()
!};
SD_FML.cntx_pop();
1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYSTEM=''   || 'SYSTEM'
   |? _buf.KOD=''      || 'KOD'
   |? _buf.SD_DOK=''   || 'SD_DOK'
   |? _buf.SYMBOL=''   || 'SYMBOL'
   |? _buf.NAZWA=''    || 'NAZWA'
   |? _buf.WIDOCZNY='' || 'WIDOCZNY'
   |? _buf.WYMAGANY='' || 'WYMAGANY'
   |? _buf.TYP=''      || 'TYP'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.SYSTEM))=0
|| _val.msg_inset('SYSTEM','T','N');
   _val.RESULT:=0;
   return()
?};
{? ('TN'*(1+_buf.WIDOCZNY))=0
|| _val.msg_inset('WIDOCZNY','T','N');
   _val.RESULT:=0;
   return()
?};
{? ('TN'*(1+_buf.WYMAGANY))=0
|| _val.msg_inset('WYMAGANY','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(SD_PAR,'SYSTEM');
{? +_buf.SYSTEM>_max
|| _val.msg_length('SYSTEM',_max);
   _buf.SYSTEM:=_max+_buf.SYSTEM
?};
_max:=MS.fld_len(SD_PAR,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(SD_DOK,'KOD');
{? +_buf.SD_DOK>_max
|| _val.msg_length('SD_DOK',_max);
   _buf.SD_DOK:=_max+_buf.SD_DOK
?};
_max:=MS.fld_len(SD_PAR,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(SD_PAR,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};
_max:=MS.fld_len(SD_PAR,'WIDOCZNY');
{? +_buf.WIDOCZNY>_max
|| _val.msg_length('WIDOCZNY',_max);
   _buf.WIDOCZNY:=_max+_buf.WIDOCZNY
?};
_max:=MS.fld_len(SD_PAR,'WYMAGANY');
{? +_buf.WYMAGANY>_max
|| _val.msg_length('WYMAGANY',_max);
   _buf.WYMAGANY:=_max+_buf.WYMAGANY
?};

: weryfikacja spójności referencyjnej
_dok:=null;
{? SD_DOK.find_key(_buf.SD_DOK,)
|| _dok:=SD_DOK.ref()
|| _val.msg_norec('SD_WREK');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? (SD_PAR.index('KOD'); SD_PAR.find_key(_buf.KOD,)) |
   (SD_PAR.index('SYMBOL'); SD_PAR.find_key(_dok,_buf.SYMBOL,))
|| _ref:=SD_PAR.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[_a.KOD]",
   'SD_DOK',_dok
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,SD_PAR,"
   _a.SYSTEM:=_b.SYSTEM;
   _a.KOD:=_b.KOD;
   _a.SD_DOK:=_c.SD_DOK;
   _a.NUMER:=_b.NUMER;
   _a.SYMBOL:=_b.SYMBOL;
   _a.NAZWA:=_b.NAZWA;
   _a.memo_set(_b.OPIS,'OPIS');
   _a.WIDOCZNY:=_b.WIDOCZNY;
   _a.WYMAGANY:=_b.WYMAGANY;
   _a.TYP:=_b.TYP;
   _a.DLUGOSC:=_b.DLUGOSC;
   _a.PRECYZJA:=_b.PRECYZJA;
   _a.memo_set(_b.OKIENKO,'OKIENKO');
   ~~
",'OPIS,OKIENKO');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 3dc6c9cee857af7c288d309cf072844e38deec6bda1f072c1163abe560549c9c8e52246feb818e2142764fb4bafa2b40bc16ea934eb1a35a0b1a53f1e7f4d99b338aef3fc250303baffb66d666f65cdf476fe17de59a21f6ea5750b29a20ca51bd7262387187fffb2f6988252079cc0a4dd2817b8eafb832dba7a41631265979

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kst_map.fml
:: Utworzony: 3.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu mapy stałych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='KST_MAP';
_def.FUNPAR:='ZWS_PAR_KST_ADM';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.NAME:='Stałe systemu - mapa'@;
_def.FILE:=exec('def_kst_xlsx','xls__init');
_def.SHEET:='Mapa stałych=1,1';
_def.DESC:='Mapa stałych';
_def.ADD_ROWS:=0;
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="KST_MAP";

: prolog
_def.BEFORE:="
   KST_ZES.cntx_psh();
   KST_ZES.prefix();
   KST_DEF.cntx_psh();
   KST_DEF.prefix();
   KST_MAP.cntx_psh();
   KST_MAP.f_clear();
   KST_MAP.index('KST_MAP');
   KST_MAP.prefix();
   ~~
";

: epilog
_def.AFTER:="
   KST_MAP.cntx_pop();
   KST_DEF.cntx_pop();
   KST_ZES.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('KST_ZES',MS.name(KST_MAP,'KST_ZES'),0,,MS.comment(KST_MAP,'KST_ZES'));
   _env.define('KST_DEF',MS.name(KST_MAP,'KST_DEF'),0,,MS.comment(KST_MAP,'KST_DEF'));
   _env.define('SYMBOL', MS.name(KST_MAP,'SYMBOL'), 0,,MS.comment(KST_MAP,'SYMBOL'));
   _env.define('OPIS',   MS.name(KST_MAP,'OPIS'),   0,,MS.comment(KST_MAP,'OPIS'));
   _env.define('INFO',   MS.name(KST_MAP,'INFO'),   0,,MS.comment(KST_MAP,'INFO'));
   _env.define('SYSTEM', MS.name(KST_MAP,'SYSTEM'), 0,,MS.comment(KST_MAP,'SYSTEM'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.KST_ZES.VALUE:=KST_MAP.KST_ZES().SYMBOL;
   _buf.KST_DEF.VALUE:=KST_MAP.KST_DEF().SYMBOL;
   _buf.SYMBOL.VALUE:=KST_MAP.SYMBOL;
   _buf.OPIS.VALUE:=KST_MAP.OPIS;
   _buf.INFO.VALUE:=KST_MAP.memo_txt(,1,'INFO');
   _buf.SYSTEM.VALUE:=KST_MAP.SYSTEM;
   _env.write_async('KST_ZES',KST_MAP.KST_ZES);
   _env.write_async('KST_DEF',KST_MAP.KST_DEF);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_kst_map',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kst_map',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.KST_ZES='' || 'KST_ZES'
   |? _buf.KST_DEF='' || 'KST_DEF'
   |? _buf.SYMBOL=''  || 'SYMBOL'
   |? _buf.SYSTEM=''  || 'SYSTEM'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('TN'*_buf.SYSTEM)=0
|| _val.msg_inset('SYSTEM','T','N');
   _val.SYSTEM:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(KST_ZES,'SYMBOL');
{? +_buf.KST_ZES>_max
|| _val.msg_length('KST_ZES',_max);
   _buf.KST_ZES:=_max+_buf.KST_ZES
?};
_max:=MS.fld_len(KST_DEF,'SYMBOL');
{? +_buf.KST_DEF>_max
|| _val.msg_length('KST_DEF',_max);
   _buf.KST_DEF:=_max+_buf.KST_DEF
?};
_max:=MS.fld_len(KST_MAP,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(KST_MAP,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

: weryfikacja spójności referencyjnej
{? ~KST_ZES.find_tab(,'SYMBOL',,'=',_buf.KST_ZES)
|| _val.msg_norec('KST_ZES');
   _val.RESULT:=0;
   return()
?};
{? ~KST_DEF.find_tab(,'SYMBOL',,'=',_buf.KST_DEF)
|| _val.msg_norec('KST_DEF');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
: odszukaj aktualizowany wiersz
{? KST_MAP.find_tab(,
      'KST_ZES',,'=',KST_ZES.ref(),
      'SYMBOL',,'=',_buf.SYMBOL
   )
|| _ref:=KST_MAP.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1: %2 - %3'[_a.KST_ZES,_a.SYMBOL,_a.OPIS]",
   'KST_ZES',KST_ZES.ref(),
   'KST_DEF',KST_DEF.ref()
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,KST_MAP,"
   _a.KST_ZES:=_c.KST_ZES;
   _a.KST_DEF:=_c.KST_DEF;
   _a.SYMBOL:=_b.SYMBOL;
   _a.OPIS:=_b.OPIS;
   _a.memo_set(_b.INFO,'INFO');
   _a.SYSTEM:=_b.SYSTEM;
   ~~
",'INFO');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 d05d0f0a8e3c5648d020e26e6822e20a83c3a3b46f553bb0f33c549bee419a43fd7f87e77aec5fd2bd9afa2dd99901d422622de8c17b5653a4cd73623b20c9a5e6bcb536a9d3003f5e74033662a496ea18732a4b991f6d11ef33167883baf9c67362e11d6cdb8feb2e0ec4bb65593e6673f86e290f91ee415fd6f976cad1f785

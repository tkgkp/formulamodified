:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_jks_upr.fml
:: Utworzony: 16.09.2019
:: Autor: Katarzyna Niziołek
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu i importu uprawnień do danych - tabela USERSDEP
::======================================================================================================================
\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel uprawnień do jednostek księgowych
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USRUPR';
_def.ID:='UPR_JKS';

_def.FILE:='uprawnienia.xlsx';
_def.SHEET:='Jednostki księgowe=1,1';
_def.NAME:='Uprawnienia do jednostek księgowych'@;
_def.DESC:='Uprawnienia do jednostek księgowych';

_def.TABLE:="exec('table','xls_jks_upr')";
_def.FIELDS:="exec('fields','xls_jks_upr',_a)";

_def.TAB_IMP:="USERSDEP";

_def.BEFORE:="USERSDEP.cntx_psh()";
_def.AFTER:="USERSDEP.cntx_pop()";

_def.PREFIX:="exec('prefix','xls_jks_upr')";
_def.EXPORT:="exec('export','xls_jks_upr',_a,_b)";

_def.VALIDATE:="exec('validate','xls_jks_upr',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_jks_upr',_a,_b,_c)";

_def.SELECT:="exec('select','xls_jks_upr',_a,_b)";

exec('SelUsersTab','xls_users_up');

~~


\TabJKsUpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [Katarzyna Niziołek] [19.42]
:: OPIS: Tymczasowa tabela uprawnień do jednostek księgowych
::   WY: uchwyt tabeli
::----------------------------------------------------------------------------------------------------------------------

_wyn:=tab_tmp(5
   ,'USER_KOD','STRING[10]','User kod'
   ,'USER_REF','STRING[16]','User ref'
   ,'JK_REF','STRING[16]','Ref jednostki księgowej'
   ,'JK_SKROT','STRING[8]','Skrót jednostki księgowej'
   ,'ACCESS','STRING[1]','Uprawnienie'
   ,'PELNE','STRING[1]','Czy pelne uprawnienia'
   ,'UIDREF','STRING[100]','Identyfikator rekordu'
   );

_add:="_wyn:=params_get().wyn;
       _wyn.blank();
       _wyn.USER_REF:=_a;
       _wyn.USER_KOD:=_b;
       _wyn.JK_REF:=_c;
       _wyn.JK_SKROT:=_d;
       _wyn.ACCESS:=_e;
       _wyn.PELNE:=_f;
       _wyn.UIDREF:=_g;
       _wyn.add(1)
      ";

USERS.cntx_psh();
FIRMA.cntx_psh();
ODD.cntx_psh();
USERSDEP.cntx_psh();

USERS.f_clear();
USERS.index('USR_AKOD');
USERS.prefix('T');

USERSDEP.index(USERSDEP.ndx_tmp(,,'USERS',,,'DEPT',,));
ODD.index('ODDZIALY');
ODD.prefix(REF.FIRMA);
{? USERS.first()
|| {!
   |? _user_ref:=USERS.ref();
      _user_kod:=USERS.KOD;
      _moz_dostep:=exec('user_upraw_dostep','xls_jks_upr','FJKS',_user_ref);
      {? _moz_dostep
      || USERSDEP.prefix(_user_ref);
         {? ODD.first()
         || {!
            |? {? pelne_upr='T'
               || params_set('wyn',_wyn);
                  (_add)($_user_ref,_user_kod,'','','T','T',USERS.uidref()+ODD.uidref());
                  ODD.last()
               || _access:={? _moz_dostep & USERSDEP.find_key(ODD.ref()) || 'T' || 'N' ?};
                  params_set('wyn',_wyn);
                  (_add)($_user_ref,_user_kod,$ODD.ref(),ODD.OD,_access,'N',USERS.uidref()+ODD.uidref())
               ?};
               ODD.next()
            !}
         ?}
      ?};
      USERS.next()
   !}
?};
:: _wyn.win_sel(_wyn.mk_sel(,,1));
:: _wyn.select()

USERS.cntx_pop();
FIRMA.cntx_pop();
ODD.cntx_pop();
USERSDEP.cntx_pop();

_wyn


\user_upraw_dostep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Sprawdza w tabeli B_PERM_U czy dany użytkownik może mieć dany rodzaj uprawnienia
::   WE: _a - rodzaj uprawnienia (FJKS)
::       _b - ref użytkownika
::   WY: 1 - użytkownik może mieć uprawnienie
::       0 - użytkownik nie może mieć uprawnienia
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | (_a<>'FJKS')
|| _a:='ODDZ'
?};
{? var_pres('_b')<>type_of(null()) | _b=null()
|| return(0)
?};

_rodz:=_a;
_user_ref:=_b;
_bperm_ref:=exec('bperm_ref','xls_jks_upr',_rodz);

B_PERM_U.cntx_psh();
B_PERM_U.index('USER');
B_PERM_U.prefix(REF.FIRMA,_user_ref,_bperm_ref);
_dostep:=(B_PERM_U.first());
pelne_upr:=B_PERM_U.FULL;
B_PERM_U.cntx_pop();

_dostep


\bperm_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW:
:: OPIS: Zwraca ref B_PERM dla danego rodzaju uprawnienia
::   WE: _a - rodzaj uprawnienia (ODDZ, MG, STS, STZ, ZAM, ZAW, TARS, TARD, ZAM, ZAW)
::   WY: ref B_PERM
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | (_a<>'FJKS')
|| _a:='ODDZ'
?};

_rodz:=_a;

_bperm_ref:=null();
B_PERM.cntx_psh();
B_PERM.index('NAME');
B_PERM.prefix(_rodz);
{? B_PERM.first()
|| _bperm_ref:=B_PERM.ref()
?};
B_PERM.cntx_pop();

_bperm_ref


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
__SelUsers.prefix('FJKS');
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

exec('TabJKsUpr','xls_jks_upr')


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_tooltip:=MS.name(USERSDEP,'USERS')+' '+'(max. %1 znaków). '@[$MS.fld_len(USERS,'KOD')];
_tooltip+='Pole wymagane'@;
_def.define('USER','Użytkownik'@,1,,_tooltip);

_tooltip:=MS.name(USERSDEP,'DEPT')+' '+'(max. %1 znaków). '@[$MS.fld_len(ODD,'OD')];
_def.define('ODD','Jednostka księgowa'@,1,,_tooltip);

_tooltip:='Uprawniony (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('DOSTEP','Czy uprawniony?'@,1,,_tooltip);

_tooltip:='Pełne uprawnienia (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('PELNE','Czy pełne uprawnienia?'@,1,,_tooltip);

~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------

_excel:=_a;
_obj:=_b;
_tab:=_excel.table();

{? __SelUsers.first()
|| _result:=__SelUsers.find_key(_tab.USER_REF)
|| _result:=1
?};

{? _result
|| _obj.USER.VALUE:=_tab.USER_KOD;
   _obj.ODD.VALUE:=_tab.JK_SKROT;
   _obj.DOSTEP.VALUE:=_tab.ACCESS;
   _obj.PELNE.VALUE:=_tab.PELNE
?};

_result


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------

_def:=_a;
_obj:=_a;
_mode:=_b;
_validate:=_c;

USERSDEP.prefix();
{? _validate.ACTION='add'
|| USERSDEP.blank()
?};
USERSDEP.USERS:=_validate.OBJ.USER;
USERSDEP.DEPT:=_validate.OBJ.JKS;


~~

\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - obj_new - środowisko mechanizmu
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_excel:=_b;

exec('select','xls_users_up',_selected,_excel,'FJKS')


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER   - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obj_new() - obiekt z resultem, wynik działania exec('args_valid','#excel_imex').
::                        Obiekt ten jest przekazywany do formuły na IMPORT. Pole RESULT decyduje czy formuła
::                        na import się wykona. Pole MSG służy to odpisania komunikatu
::----------------------------------------------------------------------------------------------------------------------

_obj:=_a;
_mode:=_b;
_result:=_c;

_can_continue:=1;
:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _obj.USER='' & _can_continue
|| _can_continue:=0;
   _result.msg_empty('USER')
?};
{? _obj.DOSTEP='' & _can_continue
|| _can_continue:=0;
   _result.msg_empty('DOSTEP')
?};
{? _obj.PELNE='' & _can_continue
|| _can_continue:=0;
   _result.msg_empty('PELNE')
?};

_user:=null();
{? _can_continue
|| _user:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1);
   {? _user=null()
   || _can_continue:=0;
      _result.msg_norec('USER')
   ?}
?};

_jks:=null();
{? _can_continue
|| ODD.index('ODDZIALY');
   ODD.prefix(REF.FIRMA,_obj.ODD);
   ODD.first();
   _jks:=ODD.ref();
   {? _jks=null()
   || _can_continue:=0;
      _result.msg_norec('ODD')
   ?}
?};

{? _obj.DOSTEP<>'T' & _obj.DOSTEP<>'N' & _can_continue
|| _can_continue:=0;
   _result.msg_inset('DOSTEP','T','N')
?};

{? _obj.PELNE<>'T' & _obj.PELNE<>'N' & _can_continue
|| _can_continue:=0;
   _result.msg_inset('PELNE','T','N')
?};

{? _can_continue & _obj.DOSTEP='T'
|| {? ~exec('user_upraw_dostep','xls_jks_upr','FJKS',_user)
   || _can_continue:=0;
      _result.msg_value('DOSTEP','Użytkownik nie może mieć uprawnienia, nie ma odpowiedniej roli.'@)
   ?}
?};

_result.RESULT:=_can_continue;

:: OK

{? _result.RESULT & _obj.PELNE='T' & _mode=1
|| USERSDEP.index('UNIK');
   USERSDEP.prefix(REF.FIRMA, _user);
   _bperm_ref:=exec('bperm_ref','xls_jks_upr','FJKS');
   B_PERM_U.index('USER');
   B_PERM_U.prefix(REF.FIRMA,_user, _bperm_ref);
   {? USERSDEP.first() | (B_PERM_U.first(); B_PERM_U.FULL='N')
   || _result.ACTION:='pdel';
      _result.OBJ:=obj_new('USER','PELNE');
      _result.OBJ.USER:=_user;
      _result.OBJ.PELNE:='T'
   ?}
?};

{? _result.RESULT & _obj.PELNE='N' & (_mode=1 | _obj.DOSTEP='T')
|| USERSDEP.index('UNIK');
   USERSDEP.prefix(REF.FIRMA,_user,_jks);
   _bperm_ref:=exec('bperm_ref','xls_jks_upr','FJKS');
   B_PERM_U.index('USER');
   B_PERM_U.prefix(REF.FIRMA,_user, _bperm_ref);
   {? USERSDEP.first()
   || {? _obj.DOSTEP='N'
      || _result.ACTION:='del'
      ?}
   || {? _obj.DOSTEP='T'
      || _result.ACTION:='add';
         _result.OBJ:=obj_new('USER','JKS');
         _result.OBJ.USER:=_user;
         _result.OBJ.JKS:=_jks;
         exec('record','xls_jks_upr',_obj,_mode,_result)
      ?}
   ?}
?};

~~

\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziołek
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------

_obj:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| _user:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1);
   _bperm_ref:=exec('bperm_ref','xls_jks_upr','FJKS');
   B_PERM_U.index('USER');
   B_PERM_U.prefix(REF.FIRMA, _user, _bperm_ref);
   {? _validate.ACTION='add'
   || {? B_PERM_U.first()
      || {? B_PERM_U.FULL='T' | B_PERM_U.ACCESS='N'
         || B_PERM_U.FULL:='N';
            B_PERM_U.ACCESS:='T';
            B_PERM_U.put()
         ?};
         {? USERSDEP.add(1) & B_PERM_U.FULL='N' & B_PERM_U.ACCESS='T'
         || _result:=1
         ?}
      ?}
   |? _validate.ACTION='pdel'
   || {? USERSDEP.first()
      || {! |? USERSDEP.del() !}
      ?};
      {? B_PERM_U.first() & (B_PERM_U.FULL='N' | B_PERM_U.ACCESS='N')
      || B_PERM_U.FULL:='T';
         B_PERM_U.ACCESS:='T';
         B_PERM_U.put()
      ?};
      {? ~USERSDEP.first() & B_PERM_U.FULL='T' & B_PERM_U.ACCESS='T' || _result:=1 ?}
   |? _validate.ACTION='del'
   || {? B_PERM_U.first()
      || {? B_PERM_U.FULL='T'
         || B_PERM_U.FULL:='N';
            B_PERM_U.put()
         ?};
         {? B_PERM_U.FULL='N' & USERSDEP.count()=0 & USERSDEP.del(1,1)<>0
         || _result:=1
         ?}
      ?}
   ?}
?};

{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do jednostki księgowej %2 nie powiodło się.'@
                        [_obj.USER,_obj.ODD]

   |? _validate.ACTION='del'
   || _validate.MSG:='Usunięcie uprawnienia użytkownika %1 do jednostki księgowej %2 nie powiodło się.'@
                        [_obj.USER,_obj.ODD]
   |? _validate.ACTION='pdel'
   || _validate.MSG:='Nadanie pełnych uprawnień do jednostek księgowych użytkownikowi %1 nie powiodło się.'@
                        [_obj.USER]
   ?}
?};

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 207ebcf7fb71fb29383efbd04e0f4490b408a8e5dfc8aef2fe7c98d5a1e2d874bd0c63bf1c0503a7fe9e930e28abbc82505843056620e13b586d434563e15e7e7853211da9ea1c694a9afd4095a9371a9df59dc56740044b35cc1a94568f36d168cd027ef86f5018db7bbdf0a1c97eaf4622714294ce72ba75049fb2c8758fdf

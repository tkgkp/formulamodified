:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_istdefs.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu Strukture Intrastat
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ISTDEFS';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_DEF_IST';
_def.MULTIFIR:='T';

_def.HIDDEN:='T';
_def.FILE:='intrastat.xlsx';
_def.SHEET:='Struktura Intrastat=1,1';
_def.NAME:='Struktura Intrastat'@;
_def.DESC:='Struktura Intrastat';
_def.TABLE:="exec('table','xls_istdefs')";
_def.FIELDS:="exec('fields','xls_istdefs',_a)";
_def.EXPORT:="exec('export','xls_istdefs',_a,_b)";
_def.IMPORT:="exec('import','xls_istdefs',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_istdefs',_a,_b,_c)";
_def.PREFIX:="ISTDEFS.index('XLS'); ISTDEFS.prefix('','I')";
_def.BEFORE:="ISTDEFS.cntx_psh()";
_def.AFTER:="ISTDEFS.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TM_STAMP','Wersja definicji TM_STAM',0,,'Wskazanie na strukturę deklaracji');
_def.define('LP','Lp.',1,,'Liczba porządkowa');
_def.define('OPIS','Nazwa elementu',1,,'Nazwa elementu - do 200 znaków');
_def.define('FORMULA','Formuła',1,,'Formuła na wyznaczanie wartości atrybutów elementu - do 255 znaków');
_def.define('FORM_XML','Formuła xml',1,,'Formuła wykorzystywana przy eksporcie elementów do pliku - do 255 znaków');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
ISTDEFS.prefix();
{? _validate.ACTION='add'
||
   ISTDEFS.blank();
   ISTDEFS.ISTDEF:=_validate.OBJ.ISTDEF
?};

ISTDEFS.LP:=_obj.LP;
ISTDEFS.OPIS:=_obj.OPIS;
ISTDEFS.FORMULA:=_obj.FORMULA;
ISTDEFS.FORM_XML:=_obj.FORM_XML;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.TM_STAMP.VALUE:=$ISTDEFS.ISTDEF().TM_STAM;
::_excel.write_async('ISTDEF',ISTDEFS.ISTDEF);

_table.LP.VALUE:=ISTDEFS.LP;
_table.OPIS.VALUE:=ISTDEFS.OPIS;
_table.FORMULA.VALUE:=ISTDEFS.FORMULA;
_table.FORM_XML.VALUE:=ISTDEFS.FORM_XML;

::ISTDEFI.cntx_psh();
::ISTDEFI.index('LP');
::ISTDEFI.prefix(ISTDEFS.ref(),);
::{? ISTDEFI.first()
::||
::   _excel.write_async('ISTDEFI',ISTDEFI.ref())
::?};
::ISTDEFI.cntx_pop();

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('ISTDEF');

ISTDEF.cntx_psh();
ISTDEF.index('LP');
ISTDEF.prefix('','','I',#_obj.TM_STAMP);
_result.OBJ.ISTDEF:={? ISTDEF.first() || ISTDEF.ref() || null() ?};
ISTDEF.cntx_pop();
{? _result.OBJ.ISTDEF=null()
||
   _result.MSG:='Nie znaleziono definicji Intrastat o znaczniku czasowym %1.'@[_obj.TM_STAMP];
   _result.RESULT:=0;
   return(~~)
?};

{? _result.RESULT = 1
||
   ISTDEFS.index('LP');
   ISTDEFS.prefix(_result.OBJ.ISTDEF, _obj.LP);
   {? ISTDEFS.first()
   ||
      {? _mode=0
      || _result.RESULT:=0
      ||
         _result.ACTION:='put';
         exec('record','xls_istdefs',_obj,_mode,_result);
         {? exec('chk_opis','edi_defxml','P')<>''
         ||
            _result.RESULT:=0
         ?}
      ?}
   ||
      _result.ACTION:='add';
      exec('record','xls_istdefs',_obj,_mode,_result);
      {? exec('chk_opis','edi_defxml','D')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=ISTDEFS.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=ISTDEFS.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie pozycji %1 struktury definicji Intrastat %2 nie powiodło się.'@[$_obj.LP,_obj.TM_STAMP])
   ||
      FUN.info('Poprawa pozycji %1 struktury definicji Intrastat %2 nie powiodła się.'@[$_obj.LP,_obj.TM_STAMP])
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 154eb15713fa977104850f69506f7b9567769bd2471e422a09c39211bc2a33b0a6e4cc179004c1e44a49488b092eb030e061702c60c3f7e00551edb4f9a00e3e26aac6b5fe6d9b3f53d4f5c55aacfb09943eed867039ba44f3fbd52b5b54fcf36c6639c1c4137fda2551f9b7fa5a52e527da19e6200462d520e393e60eef81d9

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_px_rules.fml
:: Utworzony: 08.08.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu formuł planu strategicznego
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PX_RULES';
_def.DOMAIN:=exec('name','#b_domain','TPP');
_def.FUNPAR:='ZWS_PAR_DFOR';

_def.FILE:='produkcja_planowanie.xlsx';
_def.SHEET:='Formuły planu'@+'=1,1';
_def.NAME:='Formuły planu strategicznego'@;
_def.DESC:='Formuły planu strategicznego'@;

_def.PREFIX:="PX_RULES.index('TYP'); PX_RULES.prefix()";
_def.TABLE:="PX_RULES";
_def.FIELDS:="exec('fields','xls_px_rules',_a)";

_def.BEFORE:="PX_RULES.cntx_psh()";
_def.AFTER:="PX_RULES.cntx_pop()";
_def.SELECT:="exec('select','xls_px_rules',_a)";

_def.EXPORT:="exec('export','xls_px_rules',_a,_b)";
_def.VALIDATE:="exec('validate','xls_px_rules',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_px_rules',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('SYMBOL',MS.name(PX_RULES,'SYMBOL'),0,,
   MS.name(PX_RULES,'SYMBOL')+' '+'(max. %1 znaków)'@[$MS.fld_len(PX_RULES,'SYMBOL')]
);
_def.define('NAZWA',MS.name(PX_RULES,'NAZWA'),1,,
   MS.name(PX_RULES,'NAZWA')+' '+'(max. %1 znaków)'@[$MS.fld_len(PX_RULES,'NAZWA')]
);
_def.define('TYP',MS.name(PX_RULES,'TYP'),1,,
   'Typ formuły:\n%1 - na pojemnik\n%2 - po wlaniu\n%3 - po wylaniu\n%4 - na pojemność\n%5 - na przepis'@
   ['CUP','IN','OUT','CAP','TEX']
);
_def.define('RULE',MS.name(PX_RULES,'RULE'),1,,
   MS.name(PX_RULES,'RULE')+' '+'(max. %1 znaków)'@[$MS.fld_len(PX_RULES,'RULE')]
);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PX_RULES.prefix();
{? _validate.ACTION='add'
|| PX_RULES.blank();
   PX_RULES.SYMBOL:=_obj.SYMBOL;
   PX_RULES.TYP:=_obj.TYP
?};
PX_RULES.NAZWA:=_obj.NAZWA;
PX_RULES.RULE:=_obj.RULE;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.SYMBOL.VALUE:=PX_RULES.SYMBOL;
_table.NAZWA.VALUE:=PX_RULES.NAZWA;
_table.TYP.VALUE:=PX_RULES.TYP;
_table.RULE.VALUE:=PX_RULES.RULE;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

exec('mainversion_chk','px_ver');

:: Ostrzeżenia
_max:=MS.fld_len(PX_RULES,'SYMBOL');
{? +_obj.SYMBOL>_max
|| _result.msg_length('SYMBOL',_max)
?};
_max:=MS.fld_len(PX_RULES,'NAZWA');
{? +_obj.NAZWA>_max
|| _result.msg_length('NAZWA',_max)
?};

:: Błędy
{? +_obj.TYP=0 | 'CUP;IN;OUT;CAP;TEX;'*(_obj.TYP+';')=0
|| _result.msg_inset('TYP','CUP','IN','OUT','CAP','TEX');
   _result.RESULT:=0
?};
_max:=MS.fld_len(PX_RULES,'RULE');
{? +_obj.RULE>_max
|| _result.msg_length('RULE',_max);
   _result.RESULT:=0
?};

_chk:="''";

:: OK
{? _result.RESULT>0
|| PX_RULES.index('TYP');
   PX_RULES.prefix(_obj.TYP,_obj.SYMBOL,);
   {? PX_RULES.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_px_rules',_obj,_mode,_result);
         {? _chk(1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_px_rules',_obj,_mode,_result);
      {? _chk(0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=PX_RULES.add(1)
   |? _validate.ACTION='put'
   || _result:=PX_RULES.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie formuły %1 nie powiodło się.'@[$_obj.SYMBOL]
   || _validate.MSG:='Poprawa formuły %1 nie powiodła się.'@[$_obj.SYMBOL]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'SYMBOL','STRING[60]','Symbol'@
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'@
   ,'TYP','STRING[10]','Typ'@
   ,'NAZWA','STRING[80]','Nazwa'@
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'SYMBOL',,);

PX_RULES.cntx_psh();
PX_RULES.index('TYP');
PX_RULES.prefix();
{? PX_RULES.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$PX_RULES.ref();
      _selected.prefix($PX_RULES.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.SYMBOL:=PX_RULES.SYMBOL;
      _tab.TYP:=PX_RULES.TYP;
      _tab.NAZWA:=PX_RULES.NAZWA;
      _tab.add();
      PX_RULES.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'TYP,SYMBOL,NAZWA',20,'Wybór formuł planu strategicznego do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PX_RULES.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 c8bc477c17d4ab972542a95e54434e60a8777a5e639441ad361596e206688823edd7ed3939ec4d1a76dcc3f403dc6109b6f9c739a6a56726b6b2e541e799f2f750923f2ad5dfba697e78696b8eaf81e87e58446555e81bd742075d7d6cd115c23199f5681125b4468427d0f92f3f3850ebfd593ef28a1cda6eb227b21f5853e9

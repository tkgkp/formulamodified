:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_xr_tmp.fml
:: Utworzony: 23.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu szablonów raportów zewnętrznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='XR_TMP';
_def.FUNPAR:='ZWS_PKD_XRDEF';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_xlsx','xls__init');
_def.SHEET:='Szablony raportów=1,1';
_def.NAME:='Szablony raportów'@;
_def.DESC:='Szablony raportów';

: źródło danych
_def.TABLE:="XR_TMP";

: prolog
_def.BEFORE:="
   XR_DEF.cntx_psh();
   XR_DEF.prefix();
   XR_TMP.cntx_psh();
   XR_TMP.f_clear();
   XR_TMP.index('PLIK');
   XR_TMP.prefix();
   ~~
";

: epilog
_def.AFTER:="
   XR_TMP.cntx_pop();
   XR_DEF.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('RAPORT',MS.name(XR_TMP,'RAPORT'),0,,MS.comment(XR_TMP,'RAPORT'));
   _env.define('PLIK',  MS.name(XR_TMP,'PLIK'),  1,,MS.comment(XR_TMP,'PLIK'));
   _env.define('OPIS',  MS.name(XR_TMP,'OPIS'),  1,,MS.comment(XR_TMP,'OPIS'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.RAPORT.VALUE:=XR_TMP.RAPORT().SYMBOL;
   _buf.PLIK.VALUE:=XR_TMP.PLIK;
   _buf.OPIS.VALUE:=XR_TMP.OPIS;
   _env.write_async('XR_DEF',XR_TMP.RAPORT);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_xr_tmp',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_xr_tmp',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.RAPORT='' || 'RAPORT'
   |? _buf.PLIK=''   || 'PLIK'
   |? _buf.OPIS=''   || 'OPIS'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(XR_DEF,'SYMBOL');
{? +_buf.RAPORT>_max
|| _val.msg_length('RAPORT',_max);
   _buf.RAPORT:=_max+_buf.RAPORT
?};
_max:=MS.fld_len(XR_TMP,'PLIK');
{? +_buf.PLIK>_max
|| _val.msg_length('PLIK',_max);
   _buf.PLIK:=_max+_buf.PLIK
?};
_max:=MS.fld_len(XR_TMP,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

: weryfikacja spójności referencyjnej
{? ~XR_DEF.find_tab(,'SYMBOL',,'=',_buf.RAPORT)
|| _val.msg_norec('RAPORT');
   _val.RESULT:=0;
   return()
|| _def:=XR_DEF.ref()
?};

_rzP:={? XR_TMP.find_tab(,'RAPORT',,'=',_def,'PLIK',,'=',_buf.PLIK) || XR_TMP.ref() || null ?};
_rzO:={? XR_TMP.find_tab(,'RAPORT',,'=',_def,'OPIS',,'=',_buf.OPIS) || XR_TMP.ref() || null ?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=exec('write_obj','xls__util',_rzP,
   "'%1: %2 - %3'[_a.RAPORT,_a.PLIK,_a.OPIS]",
   'RAPORT',_def
);

{? _rzP=null
|| {? _rzO=null
   || _val.ACTION:='add'
   || _val.msg_unique(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
|| {? _rzO=null | _rzO=_rzP
   || _val.ACTION:='put'
   || _val.msg_unique(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,XR_TMP,"
   _a.RAPORT:=_c.RAPORT;
   _a.PLIK:=_b.PLIK;
   _a.OPIS:=_b.OPIS;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:52 283d8c346238c2650c9790de7213b94750fa7d1e64352e4eb37a30fc10d2fe6407ef5c15a232a2cf60b24c00a6b09b22882723aa300d8250040031c329ef4282b171f58060361a75a875dba00fbec1ac743a7ef4e0d3a2d4f9122befeac9e081b49aeeca9e0521e412a93889f1167e3b638aa26d77d708b9b8c20a516f30815a

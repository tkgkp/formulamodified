:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_pl_opis.fml
:: Utworzony: 22.08.2018
:: Autor: GZ
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu widoków planu operacyjnego
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PL_OPIS';
_def.DOMAIN:=exec('name','#b_domain','TPP');
_def.FUNPAR:='ZWS_PAR_TOWZ';

_def.FILE:='produkcja_planowanie.xlsx';
_def.SHEET:='Zasoby widoków planu'@+'=1,1';
_def.NAME:='Zasoby widoków planu'@;
_def.DESC:='Zasoby widoków planu'@;

_def.PREFIX:="PL_OPIS.index('PL_OPIS'); PL_OPIS.prefix()";
_def.TABLE:="PL_OPIS";
_def.FIELDS:="exec('fields','xls_pl_opis',_a)";

_def.BEFORE:="PL_OPIS.cntx_psh()";
_def.AFTER:="PL_OPIS.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_pl_opis',_a,_b)";
_def.VALIDATE:="exec('validate','xls_pl_opis',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_pl_opis',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('WZORZEC',MS.name(PL_OPIS,'WZORZEC'),1,,
   MS.name(PL_OPIS,'WZORZEC')+' '+'(max. %1 znaków)'@[$MS.fld_len(PL_WZOR,'NAZWA')]
);
_def.define('NUMER',MS.name(PL_OPIS,'NUMER'),1,,MS.comment(PL_OPIS,'NUMER')+' '+'(liczba naturalna)'@,,0);
_def.define('PL_RES',MS.name(PL_OPIS,'PL_RES'),1,,
   MS.name(PL_OPIS,'PL_RES')+' '+'(max. %1 znaków)'@[$MS.fld_len(PL_RES,'SYM')]
);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PL_OPIS.prefix();
{? _validate.ACTION='add'
|| PL_OPIS.blank();
   PL_OPIS.WZORZEC:=_validate.OBJ.WZORZEC;
   PL_OPIS.POZIOM:=1;
   PL_OPIS.NUMER:=_obj.NUMER;
   PL_OPIS.RODZAJ:='R';
   PL_OPIS.LDNI:=0;
   PL_OPIS.PL_RES:=_validate.OBJ.PL_RES;
   PL_OPIS.OPIS:=PL_OPIS.PL_RES().SYM
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.WZORZEC.VALUE:=PL_OPIS.WZORZEC().NAZWA; _excel.write_async('PL_WZOR',PL_OPIS.WZORZEC);
_table.NUMER.VALUE:=PL_OPIS.NUMER;
_table.PL_RES.VALUE:=PL_OPIS.PL_RES().SYM; _excel.write_async('PL_RES',PL_OPIS.PL_RES);
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('WZORZEC','PL_RES');

_result.OBJ.WZORZEC:=exec('FindInSet','#table','PL_WZOR','NAZWA',_obj.WZORZEC,,"PL_WZOR.ref()",1);
_result.OBJ.PL_RES:=exec('FindInSet','#table','PL_RES','SYMSYM',_obj.PL_RES,,"PL_RES.ref()",1);

:: Błędy
{? _result.OBJ.WZORZEC=null()
|| _result.msg_norec('WZORZEC');
   _result.RESULT:=0
?};
{? _result.OBJ.PL_RES=null()
|| _result.msg_norec('PL_RES');
   _result.RESULT:=0
?};
{? _obj.NUMER<=0
|| _result.msg_range('NUMER',0);
   _result.RESULT:=0
?};

_chk:="''";

:: OK
{? _result.RESULT>0
|| PL_OPIS.index('PL_POZ');
   PL_OPIS.prefix(_result.OBJ.WZORZEC,1,_obj.NUMER);
   {? PL_OPIS.first()
   ||
:: nie ma czego poprawiać
      ~~
   || _result.ACTION:='add';
      exec('record','xls_pl_opis',_obj,_mode,_result);
      {? _chk(0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=PL_OPIS.add(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie zasobu %1 do widoku %2 nie powiodło się.'@[_obj.PL_RES,_obj.WZORZEC]
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2021/04/09 15:28:03 0a54a94bc0ab986db10806b91a2f04ed20fd4eb5dfe36dc3acc2280a4b3247f3d45e7d7868456ea2af01689d252f0f152991562ccd5ec9ef4315c16a77503c8089336dbad34ff1f92141916a702e703f8c3bb277cf7960a9b19dde0003a1126239a5a40c992ab1a96c2d29ccbed480a43c2d450cb6eac35b330a86b049575bc0

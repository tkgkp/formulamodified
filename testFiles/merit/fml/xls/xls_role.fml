:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_users.fml
:: Utworzony: 02.07.2018
:: Autor: WH
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - obiekt z definicją, który należy zasilić - wynik działania exec('args_init','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='B_ROLE';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USERS';
_def.FILE:='users.xlsx';
_def.SHEET:='Role=1,3';
_def.NAME:='Role'@;
_def.DESC:='Role systemu';

_def.PREFIX:="B_ROLE.index('UNIK');B_ROLE.prefix(REF.FIRMA)";
_def.TABLE:="B_ROLE";
_def.FIELDS:="exec('fields','xls_role',_a)";

_def.BEFORE:="B_ROLE.f_clear();B_ROLE.cntx_psh()";
_def.AFTER:="B_ROLE.cntx_pop()";
_def.SELECT:="exec('select','xls_role',_a)";

_def.EXPORT:="exec('export','xls_role',_a,_b)";
_def.VALIDATE:="exec('validate','xls_role',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_role',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Zwraca eksportowane pola tabeli i ich atrybuty w excel
::   WE: _a - obiekt z definicją pól który należy zasilić - wynik działania exec('args_fields','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_tooltip:=MS.name(B_ROLE,'NAME')+' '+'(max. %1 znaków).'@[$MS.fld_len(B_ROLE,'NAME')];
_tooltip+='Wymagane.'@;
_def.define('NAME','Nazwa'@,1,,_tooltip);
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól tabeli MacroBASE
::                      każdy obiekt ma taką strukturę:
::                        _obj_bridge.NAZWA_POLA.VALUE - wartość która będzie zapisana w EXCEL
::                        _obj_bridge.NAZWA_POLA.EDITABLE - czy komórka będzie edytowalna
::                        _obj_bridge.NAZWA_POLA.COLOR - kolor w formacie html czyli #ffffff
::                      Aktualny wewnętrzny obiekt można podejrzeć tu: exec('filler_obj_core','#excel_imex')
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_obj_bridge:=_b;
_result:=1;
_obj_bridge.NAME.VALUE:=B_ROLE.NAME;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER   - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obj_new() - obiekt z resultem, wynik działania exec('args_valid','#excel_imex').
::                        Obiekt ten jest przekazywany do formuły na IMPORT. Pole RESULT decyduje czy formuła
::                        na import się wykona. Pole MSG służy to odpisania komunikatu
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_can_continue:=1;
:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _obj.NAME=''
|| _can_continue:=0;
   _result.msg_empty('NAME')
?};
{? _can_continue>0
|| _max:=MS.fld_len(B_ROLE,'NAME');
   {? +_obj.NAME>_max
   || _result.msg_length('NAME',_max);
      _obj.NAME:=_max+_obj.NAME
   ?}
?};

{? _can_continue>0
||
   B_ROLE.index('UNIK');
   B_ROLE.prefix(REF.FIRMA,_obj.NAME);
   {? B_ROLE.first()=0
   || B_ROLE.blank();
      B_ROLE.NAME:=_obj.NAME;
      B_ROLE.FIRMA:=REF.FIRMA;
      _result.ACTION:='add'
   ?}
?};
{? _can_continue=0
|| _result.RESULT:=0
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt z resultem walidatora i resultem importu, wynik działania exec('args_valid','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

{? _result.ACTION='add'
||
   _result.RESULT:=B_ROLE.add();
   {? _result.RESULT=0
   || _result.msg_insert('Rola %1'@[_obj.NAME])
   ?}
?};
::_result.env.XLS.XLS.erorTest();
~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'NAME','STRING[20]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAME',,);

B_ROLE.cntx_psh();
B_ROLE.index('UNIK');
B_ROLE.prefix(REF.FIRMA);
{? B_ROLE.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$B_ROLE.ref();
      _selected.prefix($B_ROLE.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NAME:=B_ROLE.NAME;
      _tab.add();
      B_ROLE.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'NAME',30,'Wybór ról do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
B_ROLE.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 ae743b1922e1b0316b9b9847bd82a120933410573581c636578e79a0b7afe955399c94e971298ccde7065327b06cf30f6ffa697b265bb755659d624b81bbc255e9b04fc18304773e15db63f7b3c47399653b8ec2691faff45ed689da2f7d7fca93a9cf4dcd80a41ccf949503fe6a4a44687306c3c67a7807ff3db28799b95d10

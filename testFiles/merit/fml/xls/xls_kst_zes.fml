:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kst_zes.fml
:: Utworzony: 31.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu zestawów stałych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='KST_ZES';
_def.FUNPAR:='ZWS_PAR_KST_ADM';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.NAME:='Stałe systemu - zestawy'@;
_def.FILE:=exec('def_kst_xlsx','xls__init');
_def.SHEET:='Zestawy stałych=1,1';
_def.DESC:='Zestawy stałych';
_def.ADD_ROWS:=0;
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="KST_ZES";

: prolog
_def.BEFORE:="
   KST_ZES.cntx_psh();
   KST_ZES.f_clear();
   KST_ZES.index('SYMBOL');
   KST_ZES.prefix();
   ~~
";

: epilog
_def.AFTER:="
   KST_ZES.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYMBOL',MS.name(KST_ZES,'SYMBOL'),0,,MS.comment(KST_ZES,'SYMBOL'));
   _env.define('OPIS',  MS.name(KST_ZES,'OPIS'),  0,,MS.comment(KST_ZES,'OPIS'));
   _env.define('INFO',  MS.name(KST_ZES,'INFO'),  0,,MS.comment(KST_ZES,'INFO'));
   _env.define('SYSTEM',MS.name(KST_ZES,'SYSTEM'),0,,MS.comment(KST_ZES,'SYSTEM'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.SYMBOL.VALUE:=KST_ZES.SYMBOL;
   _buf.OPIS.VALUE:=KST_ZES.OPIS;
   _buf.INFO.VALUE:=KST_ZES.memo_txt(,1,'INFO');
   _buf.SYSTEM.VALUE:=KST_ZES.SYSTEM;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_kst_zes',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kst_zes',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYMBOL='' || 'SYMBOL'
   |? _buf.OPIS=''   || 'OPIS'
   |? _buf.SYSTEM='' || 'SYSTEM'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('TN'*_buf.SYSTEM)=0
|| _val.msg_inset('SYSTEM','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(KST_ZES,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(KST_ZES,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

_ref:=null;
: odszukaj aktualizowany wiersz
{? KST_ZES.find_tab(,'SYMBOL',,'=',_buf.SYMBOL)
|| _ref:=KST_ZES.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2'[_a.SYMBOL,_a.OPIS]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,KST_ZES,"
   _a.SYMBOL:=_b.SYMBOL;
   _a.OPIS:=_b.OPIS;
   _a.memo_set(_b.INFO,'INFO');
   _a.SYSTEM:=_b.SYSTEM;
   ~~
",'INFO');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 2704760ab32bbcad58db1011167702c1d2ca4a62135d266660e6053c83d176cc9f0abe31bbb117b7d860ae0ae6a628829d675bdb0baf142d9220b200548a79aa48d89c4b2b2158c770dc8468712742cb24dabab4e4cb94c40a3f61e198c8fbd08165111cc9a5f5dfb11cba4057e4a9c5353f8891c7241ceb8b0b69ef62c68bce

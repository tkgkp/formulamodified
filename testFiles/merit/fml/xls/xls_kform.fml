:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kform.fml
:: Utworzony: 19.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu formuł kalkulacji kart technologicznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KFORM';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTKT';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Formuły kalkulacyjne'@+'=1,1';
_def.NAME:='Formuły kalkulacyjne'@;
_def.DESC:='Formuły kalkulacyjne do wyliczania TKW na podstawie kart technologicznych'@;

_def.PREFIX:="KFORM.index('WR'); KFORM.prefix()";
_def.TABLE:="KFORM";
_def.FIELDS:="exec('fields','xls_kform',_a)";

_def.BEFORE:="KFORM.cntx_psh()";
_def.AFTER:="KFORM.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_kform',_a,_b)";
_def.VALIDATE:="exec('validate','xls_kform',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kform',_a,_b,_c)";
_def.SELECT:="exec('select','xls_kform',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('OPC','Wariant kalkulacji'@,0,,MS.comment(KFORM,'OPC')+' '+'(max. %1 znaków)'@[$MS.fld_len(KWAR,'NAZ')]);
_def.define('NRUB','Rubryka'@,0,,MS.comment(KFORM,'NRUB')+' '+'(liczba naturalna)'@,type_of(0),0);
_def.define('FRM','Wyliczanie wartości pozycji'@,1,,'Treść formuły (max. %1 znaków)'@['255']);
_def.define('FRS','Wyświetlanie szczegółów pozycji'@,1,,'Treść formuły (max. %1 znaków)'@['255']);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
KFORM.prefix();
{? _validate.ACTION='add'
|| KFORM.blank();
   KFORM.OPC:=_validate.OBJ.KWAR;
   KFORM.NRUB:=_validate.OBJ.KRUB
?};
KFORM.FRM:=_obj.FRM;
KFORM.FRS:=_obj.FRS;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.OPC.VALUE:=KFORM.OPC().NAZ; _excel.write_async('KWAR',KFORM.OPC);
_table.NRUB.VALUE:=KFORM.NRUB().NR; _excel.write_async('KRUB',KFORM.NRUB);
_table.FRM.VALUE:=KFORM.FRM;
_table.FRS.VALUE:=KFORM.FRS;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('KWAR','KRUB');
_result.OBJ.KWAR:=exec('FindInSet','#table','KWAR','NA',_obj.OPC,,"KWAR.ref()",1);
_result.OBJ.KRUB:=exec('FindInSet','#table','KRUB','NR',_obj.NRUB,,"KRUB.ref()");

:: Błędy
{? _result.OBJ.KWAR=null()
|| _result.msg_norec('OPC');
   _result.RESULT:=0
?};
{? _result.OBJ.KRUB=null()
|| _result.msg_norec('NRUB');
   _result.RESULT:=0
?};
{? +_obj.FRM>255
|| _result.msg_length('FRM',255);
   _result.RESULT:=0
?};
{? +_obj.FRS>255
|| _result.msg_length('FRS',255);
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| KFORM.index('WR');
   KFORM.prefix(_result.OBJ.KWAR,_obj.NRUB);
   {? KFORM.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_kform',_obj,_mode,_result);
         {? exec('chk_kform','tech_kalk',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_kform',_obj,_mode,_result);
      {? exec('chk_kform','tech_kalk',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KFORM.add(1)
   |? _validate.ACTION='put'
   || _result:=KFORM.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie formuły kalkulacji dla rubryki %1 wariantu %2 nie powiodło się.'@[_obj.NRUB,_obj.OPC]
   || _validate.MSG:='Poprawa formuły kalkulacji dla rubryki %1 wariantu %2 nie powiodła się.'@[_obj.NRUB,_obj.OPC]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'OPC','STRING[40]','Wariant kalkulacji'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NRUB','INTEGER','Rubryka');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'OPC',,);

KFORM.cntx_psh();
KFORM.index('WR');
KFORM.prefix();
{? KFORM.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$KFORM.ref();
      _selected.prefix($KFORM.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.OPC:=KFORM.OPC().NAZ;
      _tab.NRUB:=KFORM.NRUB().NR;
      _tab.add();
      KFORM.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'OPC,NRUB',20,'Wybór formuł kalkulacyjnych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KFORM.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:48 090756abd6899a95f13018187e6c81ecc039ffc6c1dc4e1c7bbcecccaffc09e272c02504f573279daf2a7f8ccc51fe510de920a0218fff62b00f36f1434717cc4560acdf390b5404ca99962a261cf4a66bbac6374414e9caa1b86e200d38487de16a7fa2f5f5185be11d3ffc729cc875732cbdf6be19e76e9d7f39fceb9d8635

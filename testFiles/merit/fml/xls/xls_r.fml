:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_r.fml
:: Utworzony: 26.07.2018
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu rubryk płacowych.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Inicjowanie mechanizmu.
::   WE: _a [OBJECT] - Środowisko mechanizmu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

:: ID nie może być jednoznakowe, dlatego zamiast naturalnego R przyjmiemy RUBRYKI.
_def.ID:='RUBRYKI';
_def.FUNPAR:='ZWS_PAR_PSKL';
_def.FILE:='personel_place.xlsx';
_def.NAME:='Rubryki płacowe (v.2)'@;
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.SHEET:='Rubryki płacowe (v.2)'@+'=1,1';
_def.DESC:='Definicja rubryk płacowych (v.2)'@;
_def.MULTIFIR:='T';

_def.TABLE:="R";
_def.BEFORE:="
   R.f_clear();
   R.index('RUBLP');
   R.prefix();
   {? R.first()
   || R.prefix(R.LP)
   || R.prefix(1)
   ?}
";
_def.AFTER:="R.prefix()";
_def.PREFIX:="";

_def.FIELDS:="
   _fld:=_a;

   _fld.define('FILE','Nazwa pliku',0,,'Nazwa pliku zawierającego definicje rubryk płacowych',type_of(''));
   ~~
";

_def.SELECT:="";

_def.EXPORT:="
   _excel:=_a;
   _buf:=_b;

   _buf.FILE.VALUE:='rubryki_placowe.txt';

   _arr:=obj_new(2);
   _arr[1]:='2';
   _arr[2]:={? _excel.DIR_ON_SERVER || '' || '@' ?}+_excel.DIR
            +exec('sep','#file',{? _excel.DIR_ON_SERVER || 2 || 0 ?})+_buf.FILE.VALUE;
   exec('export','rubryki',_arr)=''
";

_def.VALIDATE:="exec('validate','xls_r',_a,_b,_c)";
_def.IMPORT:="1";
::_def.IMPORT:="exec('import','xls_r',_a,_b,_c)";

~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła sprawdza, czy można poprawić/dodać rekord.
::   WE: _a [OBJECT] - Obiekt z polami odczytami z Excela.
::       _b [NUMBER] - Tryb aktualizacji: 0 - nie (dodawane są tylko nowe rekordy) / 1 - tak.
::       _c [OBJECT] - Obiekt (RESULT, ACTION, OBJ) - wynik walidacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
_update:=_b;
_validate:=_c;


_validate.OBJ:=obj_new('FN');

{? _buf.FILE=''
|| _validate.msg_empty('FILE');
   _validate.RESULT:=0
|? _fn:={? _validate.env.DIR_ON_SERVER || '' || '@' ?}+_validate.env.DIR+exec('sep','#file')+_buf.FILE;
   fexists(_fn)<>1
|| _validate.msg_value('FILE','Brak dostępu do pliku %1.'[1-_fn]);
   _validate.RESULT:=0
|? _arr:=obj_new(2);
   _arr[1]:='2';
   _arr[2]:=_fn;
   {? _update=0
::    Jeżeli został wybrany tryb 'Dodaj brakujące', to nie ma potrzeby komunikowania, że istnieją rubryki, które
::    wymagają aktualizacji.
   || _excl:='req_rub_akt'
   || _excl:=''
   ?};
   _ret:=exec('import','rubryki',_arr,'*',_update,0,_excl);
   _ret<>''
|| _validate.MSG:=_ret;
   _validate.RESULT:=0
?};

~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 99ebd808f278e1e2f2d2fff8a7865b6f924b322395d8c1d0906ea76c75df51abb38c0f6ec2bcff4ec8f821ce8dcebb44897ac8fa0212fdd29b9ff1fade1e4c6f140ab31fd821028b42a8ee3ae22914395978b12da65811853252741d13910346e7a6e164eeccd08be50d3a8ff61485be44213a3e7ec1341013ccc2d269efc8aa

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_rt_konf.fml
:: Utworzony: 14.05.2020
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_def:=_a;

_def.ID:='RT_KONF';
_def.FUNPAR:='ZWS_PAR_RRTL';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_rt_xlsx','xls__init');
_def.SHEET:='Ustawienia rejestru=1,1';
_def.NAME:='Ustawienia rejestru'@;
_def.DESC:='Ustawienia rejestru';
_def.ADD_ROWS:=0;
_def.HIDDEN:='N';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="RT_KONF";

: prolog
_def.BEFORE:="
   RT_KONF.cntx_psh();
   RT_KONF.index('UNIQUE');
   RT_KONF.prefix();
   ~~
";

: epilog
_def.AFTER:="
   RT_KONF.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('TABELA', MS.name(RT_KONF,'TABELA'),0,, MS.comment(RT_KONF,'TABELA'));
   _env.define('POLE',   MS.name(RT_KONF,'POLE'),0,,   MS.comment(RT_KONF,'POLE'));
   _env.define('WARTOSC',MS.name(RT_KONF,'WARTOSC'),0,,MS.comment(RT_KONF,'WARTOSC'));
   _env.define('FORMULA',MS.name(RT_KONF,'FORMULA'),0,,MS.comment(RT_KONF,'FORMULA'));
   _env.define('AUTO',   MS.name(RT_KONF,'AUTO'),0,,   MS.comment(RT_KONF,'AUTO'));
   _env.define('OPIS',   MS.name(RT_KONF,'OPIS'),0,,   MS.comment(RT_KONF,'OPIS'));
   _env.define('DL_SPR', MS.name(RT_KONF,'DL_SPR'),0,, MS.comment(RT_KONF,'DL_SPR'));
   _env.define('DL_OBCY',MS.name(RT_KONF,'DL_OBCY'),0,,MS.comment(RT_KONF,'DL_OBCY'),type_of(0),0);
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_rt_konf',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_rt_konf',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rt_konf',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_env:=_a;
_buf:=_b;

_buf.TABELA.VALUE:=RT_KONF.TABELA;
_buf.POLE.VALUE:=RT_KONF.POLE;
_buf.WARTOSC.VALUE:=RT_KONF.WARTOSC;
_buf.FORMULA.VALUE:=RT_KONF.memo_txt(,1,'FORMULA');
_buf.AUTO.VALUE:=RT_KONF.AUTO;
_buf.OPIS.VALUE:=RT_KONF.OPIS;
_buf.DL_SPR.VALUE:=RT_KONF.DL_SPR;
_buf.DL_OBCY.VALUE:=RT_KONF.DL_OBCY;

:: języki tłumaczeń
RT_JEZYK.cntx_psh();
RT_JEZYK.index('KOD');
RT_JEZYK.prefix();
_loop:=RT_JEZYK.first();
{!
|? _loop
|! _env.write_async('RT_JEZYK',RT_JEZYK.ref());
   _loop:=RT_JEZYK.next()
!};
RT_JEZYK.cntx_pop();

1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.TABELA='' || 'TABELA'
   |? _buf.POLE=''   || 'POLE'
   |? _buf.AUTO=''   || 'AUTO'
   |? _buf.DL_SPR='' || 'DL_SPR'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.AUTO))=0
|| _val.msg_inset('AUTO','T','N');
   _val.RESULT:=0;
   return()
?};
{? ('N?T'*(1+_buf.DL_SPR))=0
|| _val.msg_inset('DL_SPR','N','?','T');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(RT_KONF,'TABELA');
{? +_buf.TABELA>_max
|| _val.msg_length('TABELA',_max);
   _buf.TABELA:=_max+_buf.TABELA
?};
_max:=MS.fld_len(RT_KONF,'POLE');
{? +_buf.POLE>_max
|| _val.msg_length('POLE',_max);
   _buf.POLE:=_max+_buf.POLE
?};
_max:=MS.fld_len(RT_KONF,'WARTOSC');
{? +_buf.WARTOSC>_max
|| _val.msg_length('WARTOSC',_max);
   _buf.WARTOSC:=_max+_buf.WARTOSC
?};
_max:=MS.fld_len(RT_KONF,'AUTO');
{? +_buf.AUTO>_max
|| _val.msg_length('AUTO',_max);
   _buf.AUTO:=_max+_buf.AUTO
?};
_max:=MS.fld_len(RT_KONF,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

_ref:=null;
{? RT_KONF.find_key(_buf.TABELA,_buf.POLE,_buf.WARTOSC,)
|| _ref:=RT_KONF.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1, %2%3'[_a.TABELA,_a.POLE,{? _a.WARTOSC<>'' || ' (%1)'[_a.WARTOSC] || '' ?}]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,RT_KONF,"
   _a.TABELA:=_b.TABELA;
   _a.POLE:=_b.POLE;
   _a.WARTOSC:=_b.WARTOSC;
   _a.memo_set(_b.FORMULA,'FORMULA');
   _a.AUTO:=_b.AUTO;
   _a.OPIS:=_b.OPIS;
   _a.DL_SPR:=_b.DL_SPR;
   _a.DL_OBCY:=_b.DL_OBCY;
   ~~
",'FORMULA');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 a12d35d7d9cd81ddcaf7317ac297c597a7dc35c88d533be0595477b405d0efaf35052a1b0446cef8e4c03277b34487c52725f2572d05419481caeced604409140c22a959d40a26d94b5d04b154865881d2669a9b476eb13926c34364d4a6e9445f146425906d5dcf3c219ab8ff39a6f0143519a29284bddb018b77a7bf0a627c

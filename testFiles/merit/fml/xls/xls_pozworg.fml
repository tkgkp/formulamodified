:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_pozworg.fml
:: Utworzony: 16.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu pozycji w organizacji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='POZWORG';
_def.FUNPAR:='ZWS_PAR_PSTO';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Pozycje w organizacji=1,1';
_def.NAME:='Pozycje w organizacji'@;
_def.DESC:='Pozycje w organizacji';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="POZWORG";

: prolog
_def.BEFORE:="
   POZWORG.cntx_psh();
   POZWORG.f_clear();
   POZWORG.index('KOD');
   POZWORG.prefix(REF.FIRMA);
   ~~
";

: epilog
_def.AFTER:="
   POZWORG.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('KOD', MS.name(POZWORG,'KOD'), 0,,MS.comment(POZWORG,'KOD'));
   _env.define('OPIS',MS.name(POZWORG,'OPIS'),1,,MS.comment(POZWORG,'OPIS'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.KOD.VALUE:=POZWORG.KOD;
   _buf.OPIS.VALUE:=POZWORG.OPIS;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_pozworg',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_pozworg',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.KOD=''  || 'KOD'
   |? _buf.OPIS='' || 'OPIS'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(POZWORG,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(POZWORG,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

_ref:=null;
{? POZWORG.find_tab(,'KOD',,'=',_buf.KOD)
|| _ref:=POZWORG.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('POZWORG','to_str');
_obj.to_str:="'%1 - %2'[_a.KOD,_a.OPIS]";
_obj.POZWORG:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.KOD:=_b.KOD;
   _a.OPIS:=_b.OPIS;
   _a.FIRMA:=REF.FIRMA;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? POZWORG.seek(_obj.POZWORG)
   || _set(POZWORG,_buf);
      {? ~POZWORG.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,POZWORG.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| POZWORG.blank();
   _set(POZWORG,_buf);
   {? ~POZWORG.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 21b8b90a1c403395f10915780a3cdede9e133e3d32e69217934806597eab452ab3d074d4c2c22574d3f9f6775b8211aef30ed447d1cfc47dc7143315342b31edb7fa84a5865452fe81a973718a7230186559a4b09f5a815d0b83c28dbf7d9f6bbbd69ef0c3a89e69316fa3830d1a1047ff05d50b47585ccac12341bdb90a4344

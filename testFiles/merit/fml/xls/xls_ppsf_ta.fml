:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ppsf_ta.fml
:: Utworzony: 20.04.2023
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu typów adresów pracy zdalnej
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='PPSF_TA';
_def.FUNPAR:='ZWS_PAR_PPTA';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Typy adresów pracy zdalnej=1,1';
_def.NAME:='Typy adresów pracy zdalnej'@;
_def.DESC:='Typy adresów pracy zdalnej';

: źródło danych
_def.TABLE:="PPSF_TA";

: prolog
_def.BEFORE:="
   PPSF_TA.cntx_psh();
   PPSF_TA.index('KOD');
   PPSF_TA.prefix();
   ~~
";

: epilog
_def.AFTER:="
   PPSF_TA.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _tStr:=type_of('');
   _env.define('KOD',    MS.name(PPSF_TA,'KOD'),1,,    MS.comment(PPSF_TA,'KOD'),_tStr);
   _env.define('NAZWA',  MS.name(PPSF_TA,'NAZWA'),1,,  MS.comment(PPSF_TA,'NAZWA'),_tStr);
   _env.define('SYSTEM', MS.name(PPSF_TA,'SYSTEM'),1,, MS.comment(PPSF_TA,'SYSTEM'),_tStr);
   _env.define('K',      MS.name(PPSF_TA,'K'),1,,      MS.comment(PPSF_TA,'K'),_tStr);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.KOD.VALUE:=PPSF_TA.KOD;
   _buf.NAZWA.VALUE:=PPSF_TA.NAZWA;
   _buf.SYSTEM.VALUE:=PPSF_TA.SYSTEM;
   _buf.K.VALUE:=PPSF_TA.K;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_ppsf_ta',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ppsf_ta',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.KOD=''    || 'KOD'
   |? _buf.NAZWA=''  || 'NAZWA'
   |? _buf.SYSTEM='' || 'SYSTEM'
   |? _buf.K=''      || 'K'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('NT'*_buf.SYSTEM)=0
|| _val.msg_inset('SYSTEM','N','T');
   _val.RESULT:=0;
   return()
|? ('NT'*_buf.K)=0
|| _val.msg_inset('K','N','T');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(PPSF_TA,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(PPSF_TA,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};
{? PPSF_TA.find_tab(,'KOD',,'=',_buf.KOD,'NAZWA',,'<>',_buf.NAZWA) |
   PPSF_TA.find_tab(,'KOD',,'<>',_buf.KOD,'NAZWA',,'=',_buf.NAZWA)
|| _val.msg_unique();
   _val.RESULT:=0;
   return()
?};
: weryfikacja spójności referencyjnej
_ref:=null;
{? PPSF_TA.find_tab(,
      'KOD',,'=',_buf.KOD
   )
|| _ref:=PPSF_TA.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 %2'[_a.KOD,_a.NAZWA]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,PPSF_TA,"
   _a.KOD:=_b.KOD;
   _a.NAZWA:=_b.NAZWA;
   _a.SYSTEM:=_b.SYSTEM;
   _a.K:=_b.K;
   ~~
");
~~

:Sign Version 2.0 jowisz:1045 2023/04/28 15:59:47 bf952f83ff487c6ad014c982f01e50ea4f75e7c6b5294afa349f0209dc405662374ba7bd17554901532ad0ba8087a8c96c881d3c4850aecd8b013cbefa330ba374304fd84b0e9ae5b3896cb7523c6f9c2925ee03eb84edc02a500be1612dd7c3b1fef578e255e0d712ce31fb13c13c72e7f7c7515e96913c669672bcba62fad4

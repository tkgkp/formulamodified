:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_slu.fml
:: Utworzony: 20.07.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu słowników użytkownika
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='SLU';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_KSLO';

_def.FILE:='slowniki_uzytkownika.xlsx';
_def.SHEET:='Słowniki użytkownika=1,1';
_def.NAME:='Słowniki użytkownika'@;
_def.DESC:='Słowniki użytkownika';
_def.MULTIFIR:='T';

_def.PREFIX:="SLU.index('NAZ'); SLU.prefix()";
_def.TABLE:="SLU";
_def.FIELDS:="exec('fields','xls_slu',_a)";

_def.BEFORE:="SLU.cntx_psh()";
_def.AFTER:="SLU.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_slu',_a,_b)";
_def.VALIDATE:="exec('validate','xls_slu',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_slu',_a,_b,_c)";
_def.SELECT:="exec('select','xls_slu',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NAZ','Nazwa słownika',1,,'20 znakowa nazwa');
_def.define('WZ','Wzorzec',1,,'20 znakowy wzorzec');
_def.define('DL','Długość kodu',1,,'Liczba określająca długość kodu pozycji w słowniku');
_def.define('OP','Opis słownika',1,,'60 znakowy opis');
_def.define('SYSTEM','Słownik systemowy',1,,'T/N');
_def.define('POM','Słownik pomocniczy',1,,'T/N');
_def.define('KDK','Kontrola długości kodu',1,,'T/N',type_of(''));
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
{? SLU.SYSTEM='T'
|| _result:=0
|| _table.NAZ.VALUE:=SLU.NAZ;
   _table.WZ.VALUE:=SLU.WZ;
   _table.DL.VALUE:=SLU.DL;
   _table.OP.VALUE:=SLU.OP;
   _table.SYSTEM.VALUE:=SLU.SYSTEM;
   _table.POM.VALUE:=SLU.POM;
   _table.KDK.VALUE:={? SLU.KDK || 'N' || 'T' ?};
   {? (SLU.WZ='prosty' | (1+SLU.WZ)='~' | SLU.WZ='Bank')
   || SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(SLU.ref(),);
      _loop:=SLO.first();
      {!
      |? _loop
      |! _excel.write_async('SLO',SLO.ref());
         _loop:=SLO.next()
      !};
      SLO.cntx_pop()
   ?}
?};
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
SLU.index('NAZ');
SLU.prefix(_obj.NAZ,);
{? SLU.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT=1
|| {? _result.ACTION='add' || SLU.blank() ?};
   SLU.NAZ:=_obj.NAZ;
   SLU.WZ:=_obj.WZ;
   SLU.DL:=_obj.DL;
   SLU.OP:=_obj.OP;
   SLU.SYSTEM:=_obj.SYSTEM;
   SLU.POM:=_obj.POM;
   SLU.KDK:=_obj.KDK='N';
   {? exec('slu_chk','slo_slu')<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=SLU.add(1);
      {? _result
      || SLUAPPL.cntx_psh();
         SLUAPPL.index('NAZ'); SLUAPPL.prefix('F',SLU.NAZ,);
         {? ~SLUAPPL.first()
         || SLUAPPL.blank(1);
            SLUAPPL.GDZIE:='F';
            SLUAPPL.SLU:=SLU.ref();
            SLUAPPL.add()
         ?};
         SLUAPPL.cntx_pop()
      ?}

   |? _validate.ACTION='put'
   ||
      _result:=SLU.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie słownika użytkownika %1 nie powiodło się.'@[_obj.NAZ]
   ||
      _validate.MSG:='Poprawa słownika użytkownika %1 nie powiodła się.'@[_obj.NAZ]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'NAZ','STRING[20]','Słownik użytkownika'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZ',,);

SLU.cntx_psh();
SLU.index('NAZ');
SLU.prefix();
{? SLU.first()
|| {! |?
      {? SLU.SYSTEM='N'
      || _tab.blank();
         _tab.REF:=$SLU.ref();
         _selected.prefix($SLU.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.NAZ:=SLU.NAZ;
         _tab.add()
      ?};
      SLU.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NAZ',20,'Wybór słowników użytkownika do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
SLU.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 48a56f704efbea0c27a621315cc564daa9a585573d793aa5661328072e4ee18bd4978df0de0011cbdbc4fae0c28d79b58017e3c4ae813ea2cc40c9bb0bb6280c9d9ac59672144df0bd53d2c1bf4d4b68db53b1ce54f7d7097102398cacb1a26f36e6bb0d80e14a0716948835451549fec4908b94dd15e14ef4ab47a8a1d0b8dc

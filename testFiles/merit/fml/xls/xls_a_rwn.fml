:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_a_rwn.fml
:: Utworzony: 20.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu grup ruchomego czasu pracy
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='A_RWN';
_def.FUNPAR:='ZWS_PAR_PGRC';
_def.DOMAIN:=exec('name','#b_domain','PRC');
_def.FILE:=exec('def_per_prc_xlsx','xls__init');
_def.SHEET:='Grupy ruchomego czasu pracy=1,1';
_def.NAME:='Grupy ruchomego czasu pracy'@;
_def.DESC:='Grupy ruchomego czasu pracy';

: źródło danych
_def.TABLE:="A_RWN";

: prolog
_def.BEFORE:="
   A_RWN.cntx_psh();
   A_RWN.f_clear();
   A_RWN.index('WN_FIRMA');
   A_RWN.prefix('T');
   ~~
";

: epilog
_def.AFTER:="
   A_RWN.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('OD',  MS.name(A_RWN,'OD'),  1,,MS.comment(A_RWN,'OD'));
   _env.define('DO',  MS.name(A_RWN,'DO'),  1,,MS.comment(A_RWN,'DO'));
   _env.define('OPIS',MS.name(A_RWN,'OPIS'),1,,MS.comment(A_RWN,'OPIS'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.OD.VALUE:=A_RWN.OD;
   _buf.DO.VALUE:=A_RWN.DO;
   _buf.OPIS.VALUE:=A_RWN.OPIS;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_a_rwn',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_a_rwn',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.OD=date(0,0,0) || 'OD'
   |? _buf.DO=date(0,0,0) || 'DO'
   |? _buf.OPIS=''        || 'OPIS'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? _buf.DO<_buf.OD
|| _val.MSG:='Data "%1" nie może być wcześniejsza od daty "%2".'@
      [MS.name(A_RWN,'DO'),MS.name(A_RWN,'OD')];
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(A_RWN,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

_ref:=null;
{? A_RWN.find_tab(,
      'P',,'=',null,
      'OD',,'=',_buf.OD,
      'WP',,'=','N',
      'OPIS',,'=',_buf.OPIS
   )
|| _ref:=A_RWN.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 - %2: %3'[$_a.OD,$_a.DO,_a.OPIS]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,A_RWN,"
   _a.A_RWN:=0;
   _a.P:=null;
   _a.OD:=_b.OD;
   _a.DO:=_b.DO;
   _a.WP:='N';
   _a.F:='T';
   _a.OPIS:=_b.OPIS;
   _a.GROUP:='';
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:46 3b11acf4c905c279be71edca9c1305d3fe00f3362a3516aae068afcaecbf14da2d4dec2f5aa1e59a88d88152a597aa2e9d66151251619e1c5e3fa51f03d0dab3c213cd0b7003b7fb69d44c89db0f2cdfd7c94323d968db737ca5123303122f6adb562641f274db683afe4a8fbf0f8e7909ed6da86aa301db8e6dec37b4f29459

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ek_slop.fml
:: Utworzony: 31.08.2022
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi słownika problemów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='EK_SLOP';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZPR';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Słownik problemów'@+'=1,1';
_def.NAME:='Słownik problemów'@;
_def.DESC:='Słownik problemów'@;

_def.PREFIX:="EK_SLOP.index('KOD'); EK_SLOP.prefix()";
_def.TABLE:="EK_SLOP";
_def.FIELDS:="exec('fields','xls_ek_slop',_a)";

_def.BEFORE:="EK_SLOP.cntx_psh()";
_def.AFTER:="EK_SLOP.cntx_pop()";

_def.EXPORT:="exec('export','xls_ek_slop',_a,_b)";
_def.VALIDATE:="exec('validate','xls_ek_slop',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ek_slop',_a,_b,_c)";
_def.SELECT:="exec('select','xls_ek_slop',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić - wynik działania exec('args_fields','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod'@,1);
_def.define('NAZ','Nazwa'@,1);
_def.define('KLASA','Klasa'@,1);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
EK_SLOP.prefix();
{? _validate.ACTION='add'
|| EK_SLOP.blank();
   EK_SLOP.KOD:=_obj.KOD
?};
EK_SLOP.NAZ:=_obj.NAZ;
EK_SLOP.KLASA:=_obj.KLASA;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=EK_SLOP.KOD;
_table.NAZ.VALUE:=EK_SLOP.NAZ;
_table.KLASA.VALUE:=EK_SLOP.KLASA;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_can_continue:=1;

:: Błędy
{? _obj.KOD=''
|| _can_continue:=0;
   _result.msg_empty('KOD')
?};
{? _obj.NAZ=''
|| _can_continue:=0;
   _result.msg_empty('NAZ')
?};
{? _obj.KLASA=''
|| _can_continue:=0;
   _result.msg_empty('KLASA')
|? _obj.KLASA<>exec('prob_class_information','zl_wkj') &
   _obj.KLASA<>exec('prob_class_suspensing','zl_wkj') &
   _obj.KLASA<>exec('prob_class_blocking','zl_wkj')
|| _can_continue:=0;
   _result.msg_inset('KLASA'
      ,exec('prob_class_information','zl_wkj')
      ,exec('prob_class_suspensing','zl_wkj')
      ,exec('prob_class_blocking','zl_wkj')
   )
?};

:: Ostrzeżenia
{? +_obj.KOD>8
|| _result.msg_length('KOD',8)
?};
{? +_obj.NAZ>80
|| _result.msg_length('NAZ',80)
?};

:: OK
{? _can_continue>0
|| EK_SLOP.index('KOD');
   EK_SLOP.prefix(_obj.KOD,);
   {? EK_SLOP.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_ek_slop',_obj,_mode,_result);
         {? exec('chk_ek_slop','zl_wkj',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_ek_slop',_obj,_mode,_result);
      {? exec('chk_ek_slop','zl_wkj',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
|| _result.RESULT:=0
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=EK_SLOP.add(1)
   |? _validate.ACTION='put'
   || _result:=EK_SLOP.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie pozycji słownika problemów %1 nie powiodło się.'@[_obj.KOD]
   || _validate.MSG:='Poprawa pozycji słownika problemów %1 nie powiodła się.'@[_obj.KOD]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'KOD','STRING[8]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[80]','Nazwa'
   ,'KLASA','STRING[25]','Klasa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

EK_SLOP.cntx_psh();
EK_SLOP.index('KOD');
EK_SLOP.prefix();
{? EK_SLOP.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$EK_SLOP.ref();
      _selected.prefix($EK_SLOP.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=EK_SLOP.KOD;
      _tab.NAZ:=EK_SLOP.NAZ;
      _tab.KLASA:=EK_SLOP.KLASA;
      _tab.add();
      EK_SLOP.next()
   !}
?};
_result:=exec('select_action','#table',_tab,
   'KOD,NAZ,KLASA',20,'Wybór pozycji słownika problemów do eksportu'@,1
);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
EK_SLOP.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 13f1c321306965be09347a9ed81e5ec2ee19b9161eed045c73da78ed958838f38e6b60536cd99244c2e5f757be13547e4d7659d8e2f5b2335a02263eb0c27e8d35025390fe64ceeb281c416af32505b2a06b0d58cfb13d3b7999212be95944c2ea3c0fdd1392a6a70b3dc6a2a5a41390b631dc56e26db5f157fe459e6737da33

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_rem_katg.fml
:: Utworzony: 04.03.2019
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi kategorii zgłoszeń remontowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='REM_KATG';
_def.DOMAIN:=exec('name','#b_domain','TRE');
_def.FUNPAR:='ZWS_PAR_TRKG';

_def.FILE:='remonty.xlsx';
_def.SHEET:='Kategorie zgłoszeń'@+'=1,1';
_def.NAME:='Kategorie zgłoszeń'@;
_def.DESC:='Kategorie zgłoszeń remontowych'@;

_def.PREFIX:="REM_KATG.index('SYMBOL'); REM_KATG.prefix()";
_def.TABLE:="REM_KATG";
_def.FIELDS:="exec('fields','xls_rem_katg',_a)";

_def.BEFORE:="REM_KATG.cntx_psh()";
_def.AFTER:="REM_KATG.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_rem_katg',_a,_b)";
_def.VALIDATE:="exec('validate','xls_rem_katg',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rem_katg',_a,_b,_c)";
_def.SELECT:="exec('select','xls_rem_katg',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('SYMBOL','Symbol'@,1);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
REM_KATG.prefix();
{? _validate.ACTION='add'
|| REM_KATG.blank();
   REM_KATG.SYMBOL:=_obj.SYMBOL
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.SYMBOL.VALUE:=REM_KATG.SYMBOL;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

:: Ostrzeżenia
{? +_obj.SYMBOL>100
|| _result.msg_length('SYMBOL',100)
?};

:: OK
REM_KATG.index('SYMBOL');
REM_KATG.prefix(_obj.SYMBOL,);
{? ~REM_KATG.first()
|| _result.ACTION:='add';
   exec('record','xls_rem_katg',_obj,_mode,_result);
   {? exec('chk_rem_katg','remonty_zgloszenia',0)<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=REM_KATG.add(1)
   ?}
?};
{? _result=0
|| _validate.MSG:='Dodanie kategorii zgłoszenia remontowego %1 nie powiodło się.'@[_obj.SYMBOL]
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'SYMBOL','STRING[100]','Symbol'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'SYMBOL',,);

REM_KATG.cntx_psh();
REM_KATG.index('SYMBOL');
REM_KATG.prefix();
{? REM_KATG.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$REM_KATG.ref();
      _selected.prefix($REM_KATG.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.SYMBOL:=REM_KATG.SYMBOL;
      _tab.add();
      REM_KATG.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'SYMBOL',25,'Wybór kategorii zgłoszeń do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
REM_KATG.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:50 14b4caef5494e1dd28183085c3338b1f643ff02ab8424be1d536d6f2df90a7bfa11e5801e1f5889cac754b39ad59d92c9a6ec54268226b0d8d0dabff11ab1fe1b38f0e508c08a8fd7191aad8b41d3378d707ce479a7bb40d053435a08212bcacb7bfeab3c039e16b17c22b3a7fa820dc57fb964d78648223fdb9de55f0918ccb

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_o_dpar.fml
:: Utworzony: 02.11.2020
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu konfiguracji.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_def:=_a;

_def.ID:='O_DPAR';
_def.FUNPAR:='ZWS_PAR_PALS';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_o_par','xls__init');
_def.SHEET:='Definicje parametrów=1,1';
_def.NAME:='Definicje parametrów'@;
_def.DESC:='Definicje parametrów';
_def.ADD_ROWS:=0;
_def.HIDDEN:='N';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="O_DPAR";

: prolog
_def.BEFORE:="
   O_DPAR.cntx_psh();
   O_DPAR.index('KOD');
   O_DPAR.prefix();
   ~~
";

: epilog
_def.AFTER:="
   O_DPAR.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('SYSTEM',  MS.name(O_DPAR,'SYSTEM'),0,,  MS.comment(O_DPAR,'SYSTEM'));
   _env.define('KOD',     MS.name(O_DPAR,'KOD'),0,,     MS.comment(O_DPAR,'KOD'));
   _env.define('SYMBOL',  MS.name(O_DPAR,'SYMBOL'),0,,  MS.comment(O_DPAR,'SYMBOL'));
   _env.define('NAZWA',   MS.name(O_DPAR,'NAZWA'),0,,   MS.comment(O_DPAR,'NAZWA'));
   _env.define('OPIS',    MS.name(O_DPAR,'OPIS'),0,,    MS.comment(O_DPAR,'OPIS'));
   _env.define('TYP',     MS.name(O_DPAR,'TYP'),0,,     MS.comment(O_DPAR,'TYP'));
   _env.define('WYM_JEST',MS.name(O_DPAR,'WYM_JEST'),0,,MS.comment(O_DPAR,'WYM_JEST'));
   _env.define('WYM_DOST',MS.name(O_DPAR,'WYM_DOST'),0,,MS.comment(O_DPAR,'WYM_DOST'));
   _env.define('FML_BL',  MS.name(O_DPAR,'FML_BL'),0,,  MS.comment(O_DPAR,'FML_BL'));
   _env.define('FML_AE',  MS.name(O_DPAR,'FML_AE'),0,,  MS.comment(O_DPAR,'FML_AE'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="exec('export','xls_o_dpar',_a,_b)";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_o_dpar',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_o_dpar',_a,_b,_c)";
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Ustawia wartości w polach bufora danych i asynchronicznie zapisuje powiązane dane.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_env:=_a;
_buf:=_b;

_buf.SYSTEM.VALUE:=O_DPAR.SYSTEM;
_buf.KOD.VALUE:=O_DPAR.KOD;
_buf.SYMBOL.VALUE:=O_DPAR.SYMBOL;
_buf.NAZWA.VALUE:=O_DPAR.NAZWA;
_buf.OPIS.VALUE:=O_DPAR.memo_txt(,1,'OPIS');
_buf.TYP.VALUE:=O_DPAR.TYP;
_buf.WYM_JEST.VALUE:=O_DPAR.WYM_JEST;
_buf.WYM_DOST.VALUE:=O_DPAR.WYM_DOST;
_buf.FML_BL.VALUE:=O_DPAR.memo_txt(,1,'FML_BL');
_buf.FML_AE.VALUE:=O_DPAR.memo_txt(,1,'FML_AE');

:: parametry w firmie
{? exec('runtime_dro','#system')
|| return(1)
?};
O_FPAR.cntx_psh();
O_FPAR.index('SYMBOL');
O_FPAR.prefix(exec('ref_firma','ustawienia'));
{? O_FPAR.find_key(O_DPAR.SYMBOL,)
|| _env.write_async('O_FPAR',O_FPAR.ref())
?};
O_FPAR.cntx_pop();

1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.SYSTEM=''   || 'SYSTEM'
   |? _buf.KOD=''      || 'KOD'
   |? _buf.SYMBOL=''   || 'SYMBOL'
   |? _buf.NAZWA=''    || 'NAZWA'
   |? _buf.TYP=''      || 'TYP'
   |? _buf.WYM_JEST='' || 'WYM_JEST'
   |? _buf.WYM_DOST='' || 'WYM_DOST'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

:: błędne wartości
{? ('TN'*(1+_buf.SYSTEM))=0
|| _val.msg_inset('SYSTEM','T','N');
   _val.RESULT:=0;
   return()
|? ('ZR'*(1+_buf.TYP))=0
|| _val.msg_inset('TYP','Z','R');
   _val.RESULT:=0;
   return()
|? ('TN'*(1+_buf.WYM_JEST))=0
|| _val.msg_inset('WYM_JEST','T','N');
   _val.RESULT:=0;
   return()
|? ('TN'*(1+_buf.WYM_DOST))=0
|| _val.msg_inset('WYM_DOST','T','N');
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(O_DPAR,'SYSTEM');
{? +_buf.SYSTEM>_max
|| _val.msg_length('SYSTEM',_max);
   _buf.SYSTEM:=_max+_buf.SYSTEM
?};
_max:=MS.fld_len(O_DPAR,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(O_DPAR,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('SYMBOL',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(O_DPAR,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};

_ref:=null;
{? O_DPAR.find_key(_buf.KOD,)
|| _ref:=O_DPAR.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1'[_a.KOD]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,O_DPAR,"
   _a.SYSTEM:=_b.SYSTEM;
   _a.KOD:=_b.KOD;
   _a.SYMBOL:=_b.SYMBOL;
   _a.NAZWA:=_b.NAZWA;
   _a.memo_set(_b.OPIS,'OPIS');
   _a.TYP:=_b.TYP;
   _a.WYM_JEST:=_b.WYM_JEST;
   _a.WYM_DOST:=_b.WYM_DOST;
   _a.memo_set(_b.FML_BL,'FML_BL');
   _a.memo_set(_b.FML_AE,'FML_AE');
   ~~
",'OPIS,FML_BL,FML_AE');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 cbdd0603cc05244826d6943b575f5e75088bca6b06dca0ba38dd9a7bc4fe9289cbf01dee223159115ac7aac73c34638f9747a78319945f0cbc1a13f250095c833965ee5613e48b497fb047da788eb1d32f975e43ae30bef9ea4e2e55afc6721f0bc259112975c12e875d7c78d1e4a499bbb3acee03d00019793111ad71d9464e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_istdefsl.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu Intrastat słowniki
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ISTDEFSL';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_DEF_IST';
_def.MULTIFIR:='T';

_def.FILE:='intrastat.xlsx';
_def.SHEET:='Intrastat słowniki=1,1';
_def.NAME:='Intrastat słowniki'@;
_def.DESC:='Intrastat słowniki';
_def.TABLE:="exec('table','xls_istdefsl')";
_def.FIELDS:="exec('fields','xls_istdefsl',_a)";
_def.EXPORT:="exec('export','xls_istdefsl',_a,_b)";
_def.IMPORT:="exec('import','xls_istdefsl',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_istdefsl',_a,_b,_c)";
_def.SELECT:="exec('select','xls_istdefsl',_a)";
_def.PREFIX:="ISTDEFSL.index('SL'); ISTDEFSL.prefix()";
_def.BEFORE:="ISTDEFSL.cntx_psh()";
_def.AFTER:="ISTDEFSL.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ISTDEFSL


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('SL_K','Kod słownika',0,,'Kod słownika INTRASTAT');
_def.define('T','Nagłówek / Pozycja',0,,'N-nagłówek, P-pozycje');
_def.define('K','Kod',0,,'Kod');
_def.define('O','Opis',1,,'Opis');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
ISTDEFSL.prefix();
{? _validate.ACTION='add'
||
   ISTDEFSL.blank();
   ISTDEFSL.SL_K:=_obj.SL_K;
   ISTDEFSL.T:=_obj.T;
   ISTDEFSL.K:=_obj.K
?};

ISTDEFSL.O:=_obj.O;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.SL_K.VALUE:=ISTDEFSL.SL_K;
_table.T.VALUE:=ISTDEFSL.T;
_table.K.VALUE:=ISTDEFSL.K;
_table.O.VALUE:=ISTDEFSL.O;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? _result.RESULT = 1
||
   ISTDEFSL.index('SL');
   ISTDEFSL.prefix(_obj.SL_K, _obj.T, _obj.K);
   {? ISTDEFSL.first()
   ||
      {? _mode=0
      || _result.RESULT:=0
      ||
         _result.ACTION:='put';
         exec('record','xls_istdefsl',_obj,_mode,_result);
         {? __CHK.record(ISTDEFSL,,'SL_K','O')<>''
         ||
            _result.RESULT:=0
         ?}
      ?}
   ||
      _result.ACTION:='add';
      exec('record','xls_istdefsl',_obj,_mode,_result);
      {? __CHK.record(ISTDEFSL,,'SL_K','O')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=ISTDEFSL.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=ISTDEFSL.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie pozycji słownika o kodzie %1 nie powiodło się.'@[$_obj.KOD])
   ||
      FUN.info('Poprawa pozycji słownika o kodzie %1 nie powiodła się.'@[$_obj.KOD])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'K','STRING[8]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'O','STRING[255]','Opis');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'O',,);

ISTDEFSL.cntx_psh();
ISTDEFSL.index('SL');
ISTDEFSL.prefix();
{? ISTDEFSL.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$ISTDEFSL.ref();
      _selected.prefix($ISTDEFSL.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.K:=ISTDEFSL.K;
      _tab.O:=ISTDEFSL.O;
      _tab.add();
      ISTDEFSL.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'K,O',20,'Wybór słowniki Intrastat do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
ISTDEFSL.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 db3f86330a036d8fde773b75f4f13e6a8e8c35537fada5abd21955f904b07be4dda94d5a9edef2230bb8e21d5a3dbe3298e0e1a27da98d07db27330ad1092d5a4789380090161fe380bd76b2169fdb30bf45f48c3147a8a1458a4051e26c36d318bb90599adc429b585dd50d2096a15be97da549f7e2f8455c0ac6fe66c45746

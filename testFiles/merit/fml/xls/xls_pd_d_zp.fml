:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_pd_d_zp.fml
:: Utworzony: 25.01.2021
:: Autor: AKUL
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu nagłówków zestawów formuł planu dostaw
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PD_D_ZP';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PLAN_DST';

_def.FILE:='plan_dostaw.xlsx';
_def.SHEET:='Pozycje zestawów formuł=1,1';
_def.NAME:='Pozycje zestawów formuł'@;
_def.DESC:='Pozycje zestawów formuł';
_def.TABLE:="PD_D_ZP";
_def.FIELDS:="exec('fields','xls_pd_d_zp',_a)";
_def.EXPORT:="exec('export','xls_pd_d_zp',_a,_b)";
_def.IMPORT:="exec('import','xls_pd_d_zp',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_pd_d_zp',_a,_b,_c)";
_def.SELECT:="exec('select','xls_pd_d_zp',_a)";
_def.PREFIX:="PD_D_ZP.index('ZES_FORM'); PD_D_ZP.prefix()";
_def.BEFORE:="PD_D_ZP.cntx_psh()";
_def.AFTER:="PD_D_ZP.cntx_pop()";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('PD_D_ZN','Zestaw',0);
_def.define('PD_D','Formuła',0,,'Formuła generacji planu dostaw');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PD_D_ZP.prefix();
{? _validate.ACTION='add'
|| PD_D_ZP.blank();
   PD_D_ZP.PD_D:=_validate.OBJ.PD_D;
   PD_D_ZP.PD_D_ZN:=_validate.OBJ.PD_D_ZN
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.PD_D_ZN.VALUE:=PD_D_ZP.PD_D_ZN().NAZ; _excel.write_async('PD_D_ZN',PD_D_ZP.PD_D_ZN);
_table.PD_D.VALUE:=PD_D_ZP.PD_D().KOD;
{? PD_D_ZP.PD_D().TYP='S'
|| _excel.write_async('PD_D_asm',PD_D_ZP.PD_D)
|? PD_D_ZP.PD_D().TYP='C'
|| _excel.write_async('PD_D_acs',PD_D_ZP.PD_D)
|? PD_D_ZP.PD_D().TYP='U'
|| _excel.write_async('PD_D_us',PD_D_ZP.PD_D)
?};

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('PD_D_ZN','PD_D');

::Błędy
_result.OBJ.PD_D_ZN:=exec('FindInSet','#table','PD_D_ZN','NAZ',_obj.PD_D_ZN,,,1,,null());
{? _result.OBJ.PD_D_ZN=null()
|| _result.MSG:='Nie znaleziono nagłówka zestawu formuł o nazwie %1.'@[_obj.PD_D_ZN];
   _result.RESULT:=0;
   return(~~)
?};
_result.OBJ.PD_D:=exec('FindInSet','#table','PD_D','KOD',_obj.PD_D,,,1,,null());
{? _result.OBJ.PD_D=null()
|| _result.MSG:='Nie znaleziono formuły o kodzie %1.'@[_obj.PD_D];
   _result.RESULT:=0;
   return(~~)
?};

{? _result.RESULT=1
|| PD_D_ZP.index('ZES_FORM');
   PD_D_ZP.prefix(_result.OBJ.PD_D_ZN,_result.OBJ.PD_D);
   {? PD_D_ZP.first()
   ||
      {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_pd_d_zp',_obj,_mode,_result);
         {? __CHK.record(PD_D_ZP,,'PD_D_ZN','PD_D')<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_pd_d_zp',_obj,_mode,_result);
      {? __CHK.record(PD_D_ZP,,'PD_D_ZN','PD_D')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=PD_D_ZP.add(1)
   |? _validate.ACTION='put'
   || _result:=PD_D_ZP.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || FUN.info('Dodanie pozycji zestawu formuł (zestaw: %1, formuła: %2) nie powiodło się.'@[_obj.PD_D_ZN,_obj.PD_D])
   || FUN.info('Poprawa pozycji zestawu formuł (Zestaw: %1, formuła: %2) powiodła się.'@[_obj.PD_D_ZN,_obj.PD_D])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'ZN','STRING[100]','Zestaw'
   ,'SORT','STRING[10]','Kolejność'
   ,'KOD','STRING[10]','Kod'
   ,'SKROT','STRING[8]','Skrót'
   ,'NAZWA','STRING[127]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'ZN',,,'SORT',,);

PD_D_ZP.cntx_psh();
PD_D_ZP.index('ZES_FORM');
PD_D_ZP.prefix();
{? PD_D_ZP.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$PD_D_ZP.ref();
      _selected.prefix($PD_D_ZP.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      PD_D_ZP.PD_D();
      _tab.ZN:=PD_D_ZP.PD_D_ZN().NAZ;
      _tab.SORT:=PD_D.SORT;
      _tab.KOD:=PD_D.KOD;
      _tab.SKROT:=PD_D.FORMULA().SKROT;
      _tab.NAZWA:=PD_D.FORMULA().NAZWA;
      _tab.add();
      PD_D_ZP.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'ZN,SORT,KOD,SKROT,NAZWA',20,'Wybór pozycji zestawów formuł planu dostaw do eksportu'@,1);
{? _result>0
|| _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PD_D_ZP.cntx_pop();

_result

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:15 4268014d338cfd33fda6adf1396f3e6ed716adb8f93e5174f0e21349e93cb4a4d33f0ed063ebfc5bba199e1353e81d0a9757726237b9fa41b89a151ab7ed324d1310abe9565cd24f51e067a61e84818ded6be961dc2e3d3011725813db11a8ec856fab5ba8d367d4a740798a556d33a01b2dde2b102b8b6525f069856dc1f547

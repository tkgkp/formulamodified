:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_is_ogr.fml
:: Utworzony: 23.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu ograniczanych świadczeń
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='IS_OGR';
_def.FUNPAR:='ZWS_PAR_PSWI';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Ograniczane świadczenia=1,1';
_def.NAME:='Ograniczane świadczenia'@;
_def.DESC:='Ograniczane świadczenia';

: źródło danych
_def.TABLE:="IS_OGR";

: prolog
_def.BEFORE:="
   IS_DEF.cntx_psh();
   IS_DEF.prefix();
   IS_LIM.cntx_psh();
   IS_LIM.prefix();
   IS_OGR.cntx_psh();
   IS_OGR.f_clear();
   IS_OGR.index('UNIQUE');
   IS_OGR.prefix();
   ~~
";

: epilog
_def.AFTER:="
   IS_OGR.cntx_pop();
   IS_LIM.cntx_pop();
   IS_DEF.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('OPIS',    MS.name(IS_DEF,'OPIS'),  1,,MS.comment(IS_DEF,'OPIS'));
   _env.define('RODZ_DEF',MS.name(IS_DEF,'RODZAJ'),1,,MS.comment(IS_DEF,'RODZAJ')+' [Z-ZFŚS, P-pozostałe]');
   _env.define('OD',      MS.name(IS_LIM,'OD'),    1,,MS.comment(IS_LIM,'OD'),type_of(date()));
   _env.define('DO',      MS.name(IS_LIM,'DO'),    1,,MS.comment(IS_LIM,'DO'),type_of(date()));
   _env.define('MIES',    MS.name(IS_LIM,'MIES'),  1,,MS.comment(IS_LIM,'MIES'));
   _env.define('RODZ_LIM',MS.name(IS_LIM,'RODZAJ'),1,,MS.comment(IS_LIM,'RODZAJ'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.OPIS.VALUE:=IS_OGR.IS_DEF().OPIS;
   _buf.RODZ_DEF.VALUE:=IS_OGR.IS_DEF().RODZAJ;
   _buf.OD.VALUE:=IS_OGR.IS_LIM().OD;
   _buf.DO.VALUE:=IS_OGR.IS_LIM().DO;
   _buf.MIES.VALUE:=IS_OGR.IS_LIM().MIES;
   _buf.RODZ_LIM.VALUE:=IS_OGR.IS_LIM().RODZAJ;
   _env.write_async('IS_DEF',IS_OGR.IS_DEF);
   _env.write_async('IS_LIM',IS_OGR.IS_LIM);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_is_ogr',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_is_ogr',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.OPIS=''        || 'OPIS'
   |? _buf.RODZ_DEF=''    || 'RODZ_DEF'
   |? _buf.OD=date(0,0,0) || 'OD'
   |? _buf.DO=date(0,0,0) || 'DO'
   |? _buf.MIES=''        || 'MIES'
   |? _buf.RODZ_LIM=''    || 'RODZ_LIM'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? ('PZ'*_buf.RODZ_DEF)=0
|| _val.msg_inset('RODZ_DEF','P','Z');
   _val.RESULT:=0;
   return()

|? ('TN'*_buf.MIES)=0
|| _val.msg_inset('MIES','T','N');
   _val.RESULT:=0;
   return()

|? ('PU'*_buf.RODZ_LIM)=0
|| _val.msg_inset('RODZ_LIM','P','U');
   _val.RESULT:=0;
   return()

|? _buf.DO<_buf.OD
|| _val.MSG:='Data "%1" nie może być wcześniejsza od daty "%2".'@
      [MS.name(IS_LIM,'DO'),MS.name(IS_LIM,'OD')];
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(IS_DEF,'OPIS');
{? +_buf.OPIS>_max
|| _val.msg_length('OPIS',_max);
   _buf.OPIS:=_max+_buf.OPIS
?};

: weryfikacja spójności referencyjnej
{? ~IS_DEF.find_tab(,
      'OPIS',,'=',_buf.OPIS,
      'RODZAJ',,'=',_buf.RODZ_DEF
   )
|| _val.msg_norec('IS_DEF');
   _val.RESULT:=0;
   return()
?};
{? ~IS_LIM.find_tab(,
      'OD',,'=',_buf.OD,
      'DO',,'=',_buf.DO,
      'MIES',,'=',_buf.MIES,
      'RODZAJ',,'=',_buf.RODZ_LIM
   )
|| _val.msg_norec('IS_LIM');
   _val.RESULT:=0;
   return()
?};


_ref:=null;
{? IS_OGR.find_tab(,
      'IS_DEF',,'=',IS_DEF.ref(),
      'IS_LIM',,'=',IS_LIM.ref()
   )
|| _ref:=IS_OGR.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 (%2), %3 - %4 (%5)'[_a.OPIS,_a.RODZ_DEF,$_a.OD,$_a.DO,_a.RODZ_LIM]",
   'IS_DEF',IS_DEF.ref(),
   'IS_LIM',IS_LIM.ref()
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,IS_OGR,"
   _a.IS_DEF:=_c.IS_DEF;
   _a.IS_LIM:=_c.IS_LIM;
   ~~
");
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:48 1688c68828d692120df89f290969f49c0ba560a9f90da9b15fc182485ea6324a484f329bf8a26ba102601cc1d98b4acf1500e2ff21759a488f9136a59b78fe31486b5d6733505dbcb20549856539681ad0c748332b6fcd5a274376796898766cae0d1d08549945cfbe06ba8e1ceddf4001c88657c3ba4614b6d1c773336814e8

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ttxcause.fml
:: Utworzony: 19.10.2018
:: Autor:
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu przyczyn zmian w technologii
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TTXCAUSE';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTKT';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Przyczyny zmian w technologii'@+'=1,1';
_def.NAME:='Przyczyny zmian w technologii'@;
_def.DESC:='Przyczyny zmian w technologii'@;

_def.PREFIX:="exec('prefix','xls_ttxcause')";
_def.TABLE:="exec('table','xls_ttxcause')";
_def.FIELDS:="exec('fields','xls_ttxcause',_a)";

_def.BEFORE:="TTXCAUSE.cntx_psh()";
_def.AFTER:="TTXCAUSE.cntx_pop()";
_def.SELECT:="exec('select','xls_ttxcause',_a)";

_def.EXPORT:="exec('export','xls_ttxcause',_a,_b)";
_def.VALIDATE:="exec('validate','xls_ttxcause',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ttxcause',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TTXCAUSE.index('NAZ');
TTXCAUSE.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
TTXCAUSE


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NAZ','Nazwa',1,,'60 znakowa Nazwa');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TTXCAUSE.prefix();
{? _validate.ACTION='add'
||
   TTXCAUSE.blank()
?};
TTXCAUSE.NAZ:=_obj.NAZ;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.NAZ.VALUE:=TTXCAUSE.NAZ;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? (+_obj.NAZ)>60 || _result.msg_length('NAZ',60); _result.RESULT:=0 ?};

TTXCAUSE.index('NAZ');
TTXCAUSE.prefix(_obj.NAZ,);
{? TTXCAUSE.first()
||
   {? _mode=0
   || _result.RESULT:=0
:: jest tylko 1 pole, więc nie ma po co poprawiać, gdy pozycja już istnieje
   ||
      _result.ACTION:='put';
      exec('record','xls_ttxcause',_obj,_mode,_result);
      {? exec('chk_ttxcause','tech_common',1)<>''
      ||
         _result.RESULT:=0
      ?}
   ?}
||
   _result.ACTION:='add';
   exec('record','xls_ttxcause',_obj,_mode,_result);
   {? exec('chk_ttxcause','tech_common',0)<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=TTXCAUSE.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=TTXCAUSE.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie przyczyny zmiany w technologii "%1" nie powiodło się.'@[_obj.NAZ]
   ||
      _validate.MSG:=''
::      _validate.MSG:='Poprawa przyczyny korekt technologii "%1" nie powiodła się.'@[_obj.NAZ]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[60]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZ',,);

TTXCAUSE.cntx_psh();
TTXCAUSE.index('NAZ');
TTXCAUSE.prefix();
{? TTXCAUSE.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$TTXCAUSE.ref();
      _selected.prefix($TTXCAUSE.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NAZ:=TTXCAUSE.NAZ;
      _tab.add();
      TTXCAUSE.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NAZ',20,'Wybór przyczyn zmian w technologii do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TTXCAUSE.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 bddb29b0ed85ade8a75e1b38b60f9ff733ea9f9a35f3b01c844511fd4d2d7c124d1dad391a2e8f691984254030b94b7c51bdbd4612c3079af6144fa2346bfcd748da4dbb12ec7c221a11d3a3e2be5401297fb9cfeb02a41ea2a8f4b9c7dbea9343234484c079e5234d69af9398c219fca7ae14adb854ad878cf67b95f7ab8a49

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_istdefi.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu Atrybuty Intrastat
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ISTDEFI';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_DEF_IST';
_def.MULTIFIR:='T';

_def.HIDDEN:='T';
_def.FILE:='intrastat.xlsx';
_def.SHEET:='Atrybuty Intrastat=1,1';
_def.NAME:='Atrybuty Intrastat'@;
_def.DESC:='Atrybuty Intrastat';
_def.TABLE:="exec('table','xls_istdefi')";
_def.FIELDS:="exec('fields','xls_istdefi',_a)";
_def.EXPORT:="exec('export','xls_istdefi',_a,_b)";
_def.IMPORT:="exec('import','xls_istdefi',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_istdefi',_a,_b,_c)";
_def.PREFIX:="ISTDEFI.index('XLS'); ISTDEFI.prefix()";
_def.BEFORE:="ISTDEF.cntx_psh(); ISTDEFS.cntx_psh(); ISTDEFI.cntx_psh()";
_def.AFTER:="ISTDEF.cntx_pop(); ISTDEFS.cntx_pop(); ISTDEFI.cntx_pop()";
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ISTDEFI


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TM_STAMP','Wersja definicji TM_STAM',0,,'Wskazanie na strukturę deklaracji');
_def.define('ISTDEFS','Struktura',0,,'Element struktury Intrastat lub EDI');
_def.define('LP','Lp.',1,,'Liczba porządkowa');
_def.define('NAZ','Nazwa',1,,'Nazwa atrybutu');
_def.define('NRFP','Nr pola',1,,'Nr pola formularza papierowego Intrastat');
_def.define('WYM','Wymagalność',1,,'Reguły wymagalności wypełnienia pola');
_def.define('TYP','Typ atrybutu',1,,'Typ wartości atrybutu');
_def.define('SLOWNIK','Słownik',1,,'Numer słownika dla pola');
_def.define('REGULY','Reguły',1,,'Reguły wypełnienie pola');
_def.define('NAZFLD','Akronim pola',1,,'Akronim pola w tabeli repozytorium');
_def.define('OPIS_FLD','Etykieta pola',1,,'Nazwa pola widoczna w okienkach wertowania i redagowania');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
ISTDEFI.prefix();
{? _validate.ACTION='add'
||
   ISTDEFI.blank();
   ISTDEFI.ISTDEFS:=_validate.OBJ.ISTDEFS
?};

ISTDEFI.LP:=_obj.LP;
ISTDEFI.NAZ:=_obj.NAZ;
ISTDEFI.NRFP:=_obj.NRFP;
ISTDEFI.WYM:=_obj.WYM;
ISTDEFI.TYP:=_obj.TYP;
ISTDEFI.SLOWNIK:=_obj.SLOWNIK;
ISTDEFI.NAZFLD:=_obj.NAZFLD;
ISTDEFI.REGULY:=_obj.REGULY;
ISTDEFI.OPIS_FLD:=_obj.OPIS_FLD;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_TABLE:=_excel.table();

_result:=ISTDEFI.ISTDEFS().ISTDEF().IST_ISTK='I';

{? _result
||
   _table.ISTDEFS.VALUE:=$ISTDEFI.ISTDEFS().LP;
::   _excel.write_async('ISTDEFS',ISTDEFI.ISTDEFS);

   _table.TM_STAMP.VALUE:=$ISTDEFS.ISTDEF().TM_STAM;
::   _excel.write_async('ISTDEF',ISTDEFS.ISTDEF);

   _table.LP.VALUE:=ISTDEFI.LP;
   _table.NAZ.VALUE:=ISTDEFI.NAZ;
   _table.NRFP.VALUE:=ISTDEFI.NRFP;
   _table.WYM.VALUE:=ISTDEFI.WYM;
   _table.TYP.VALUE:=ISTDEFI.TYP;
   _table.SLOWNIK.VALUE:=ISTDEFI.SLOWNIK;
   _table.NAZFLD.VALUE:=ISTDEFI.NAZFLD;
   _table.REGULY.VALUE:=ISTDEFI.REGULY;
   _table.OPIS_FLD.VALUE:=ISTDEFI.OPIS_FLD
?};

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_result.OBJ:=obj_new('ISTDEFS', 'ISTDEF');

ISTDEF.cntx_psh();
ISTDEF.index('LP');
ISTDEF.prefix('','','I',#_obj.TM_STAMP);
_result.OBJ.ISTDEF:={? ISTDEF.first() || ISTDEF.ref() || null() ?};
ISTDEF.cntx_pop();
{? _result.OBJ.ISTDEF=null()
||
   _result.MSG:='Nie znaleziono definicji Intrastat o znaczniku czasowym %1.'@[_obj.TM_STAMP];
   _result.RESULT:=0;
   return(~~)
?};

_result.OBJ.ISTDEFS:=exec('FindInSet','#table','ISTDEFS','LP',#_obj.ISTDEFS, _result.OBJ.ISTDEF,,,,null());
{? _result.OBJ.ISTDEFS=null()
||
   _result.MSG:='Nie znaleziono wersje deklaracji o lp %1.'@[_obj.ISTDEFS];
   _result.RESULT:=0;
   return(~~)
?};

{? _result.RESULT = 1
||
   ISTDEFI.index('LP');
   ISTDEFI.prefix(_result.OBJ.ISTDEFS, _obj.LP);
   {? ISTDEFI.first() & ISTDEFI.LP = _obj.LP
   ||
      {? _mode=0
      || _result.RESULT:=0
      ||
         _result.ACTION:='put';
         exec('record','xls_istdefi',_obj,_mode,_result)
::         {? exec('spr_ISTDEFI','ISTDEFI_slu')<>''
::         ||
::            _result.RESULT:=0
::         ?}
      ?}
   ||
      _result.ACTION:='add';
      exec('record','xls_istdefi',_obj,_mode,_result)
::      {? exec('spr_ISTDEFI','ISTDEFI_slu')<>''
::      || _result.RESULT:=0
::      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=ISTDEFI.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=ISTDEFI.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      FUN.info('Dodanie atrybutu %1 struktury %2 definicji Intrastat %3 nie powiodło się.'@[$_obj.NAZ,_obj.ISTDEFS,_obj.TM_STAMP])
   ||
      FUN.info('Poprawa atrybutu %1 struktury %2 definicji Intrastat %3 nie powiodła się.'@[$_obj.NAZ,_obj.ISTDEFS,_obj.TM_STAMP])
   ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 0db3d1e8f4afde9b30d86ddf5129c4b97a7fd6f49ec5c38e7710e07778f15871dfd061a4d1b85e5223ee20bbbd3469fff993596e860a7827cc01585c77e07dbc25250a2b766e322ae791b97e230d16c1c1e74e01553b1af383bf94c7a5cc149f8bb424ba48eace7c242173dff144e10843faabe7d1d58451fbee709d4eaee81e

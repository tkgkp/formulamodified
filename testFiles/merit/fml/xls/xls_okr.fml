:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_okr.fml
:: Utworzony: 19.07.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu okresy obrachunkowe
::======================================================================================================================


\init_uc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='OKR_UC';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_DEF_IST';
_def.FILE:='okresy.xlsx';
_def.SHEET:='Urząd celny=1,1';
_def.NAME:='Urząd celny do Intrastat'@;
_def.DESC:='Urząd celny do Intrastat';
_def.TABLE:="OKR";
_def.FIELDS:="exec('fields_uc','xls_okr',_a)";
_def.EXPORT:="exec('export_uc','xls_okr',_a,_b)";
_def.IMPORT:="exec('import_uc','xls_okr',_a,_b,_c)";
_def.VALIDATE:="exec('validate_uc','xls_okr',_a,_b,_c)";
_def.PREFIX:="OKR.index('MC'); OKR.prefix(REF.FIRMA,1,)";
_def.BEFORE:="OKR.cntx_psh()";
_def.AFTER:="OKR.cntx_pop()";
~~


\fields_uc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('ROK','Rok',0,,'Rok',type_of(''),0);
_def.define('UC','Urząd celny',1,,'Atrybut Intrastat - Urząd celny');
~~


\export_uc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.ROK.VALUE:=form(OKR.ROK,,0,'99');
_table.UC.VALUE:=OKR.UC;

_result


\validate_uc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

{? type_of(_obj.ROK)<>type_of('') | #_obj.ROK>9999 | #_obj.ROK<0 | int(#_obj.ROK)<>#_obj.ROK
||
   _result.MSG:='Format roku jest nieprawidłowy. '@;
   _result.RESULT:=0
?};

{? _result.RESULT = 1
||
   OKR.index('OKR');
   OKR.prefix(REF.FIRMA,#_obj.ROK,1);
   {? OKR.first()
   ||
      {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   ||
      _result.MSG:='Nie zdefiniowano roku %1.'@[_obj.ROK];
      _result.RESULT:=0
   ?}
?};

{? _result.RESULT=1
||
   OKR.UC:=_obj.UC;
   {? exec('info_uc_po','intrastat')=0
   ||
      _result.RESULT:=0
   ?}
?};
_result


\import_uc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='put'
   ||
      _result:=OKR.put(1)
   ?}
?};
{? _result=0
||
   FUN.info('Zmiana urzędu celnego %1 w roku %2 nie powiodło się.'@[_obj.UC,_obj.ROK])
?};
_result


\OKR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='OKR';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_AOKR';
_def.FILE:='okresy.xlsx';
_def.SHEET:='Okresy logistyczne=1,1';
_def.NAME:='Okresy logistyczne'@;
_def.DESC:='Okresy logistyczne';
_def.ADD_ROWS:=0;
_def.TABLE:="OKR";
_def.FIELDS:="exec('fields','xls_okr',_a)";
_def.EXPORT:="exec('export','xls_okr',_a,_b)";
_def.IMPORT:="exec('import','xls_okr',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_okr',_a,_b,_c)";
_def.PREFIX:="OKR.index('OKR'); OKR.prefix(REF.FIRMA,)";
_def.BEFORE:="OKR.cntx_psh()";
_def.AFTER:="OKR.cntx_pop()";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('ROK','Rok',0,,'Rok',,0);
_def.define('MC','Miesiąc',0,,'Miesiąc',,0);
_def.define('NAZ','Nazwa',1,,'Nazwa okresu');
_def.define('ISTPP','INTRASTAT-próg w przywozie',1,,'Intrastat - próg w przywozie');
_def.define('ISTPW','INTRASTAT-próg w wywozie',1,,'Intrastat - próg w wywozie');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
OKR.prefix();
{? _validate.ACTION='add'
||
   OKR.blank();
   OKR.FIRMA:=REF.FIRMA
?};

OKR.ROK:=_obj.ROK;
OKR.MC:=_obj.MC;
OKR.NAZ:=_obj.NAZ;
OKR.ISTPP:=_obj.ISTPP;
OKR.ISTPW:=_obj.ISTPW;
::OKR.UC:=_obj.UC;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.ROK.VALUE:=OKR.ROK;
_table.MC.VALUE:=OKR.MC;
_table.NAZ.VALUE:=OKR.NAZ;
_table.ISTPP.VALUE:=OKR.ISTPP;
_table.ISTPW.VALUE:=OKR.ISTPW;
::_table.UC.VALUE:=OKR.UC;

_result


\validate_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj - tablica nazwana z polami odczytanymi z excel
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_result:='';

{? type_of(_obj.ROK)<>type_of(0) | _obj.ROK>9999 | _obj.ROK<0 | int(_obj.ROK)<>_obj.ROK
||
   _result+='Format roku jest nieprawidłowy. '@
?};

{? type_of(_obj.MC)<>type_of(0) | _obj.MC>12 | _obj.MC<1 | int(_obj.MC)<>_obj.MC
||
   _result+='Format miesiąca jest nieprawidłowy. '@
?};


_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_validate:=exec('validate_obj','xls_okr', _obj);
{? _validate<>''
||
   _result.MSG:=_validate;
   _result.RESULT:=0
?};

{? _result.RESULT = 1
||
   OKRO.cntx_psh();
   OKRO.index('UNIK');
   OKRO.prefix(REF.FIRMA,_obj.ROK,_obj.MC);
   {? OKRO.first() & OKRO.NR_OKR = _obj.MC
   ||
      _result.RESULT:=1
   ||
      _rok:={? type_of(_obj.ROK)<>type_of('') || $_obj.ROK || _obj.ROK ?};
      _mc:={? type_of(_obj.MC)<>type_of('') || $_obj.MC || _obj.MC ?};
      _result.MSG:='Nie znaleziono roku %1 i miesiąca %2 w okresach całego pakietu.'@[_rok, _mc];
      _result.RESULT:=0;
      OKRO.cntx_pop();
      return(~~)
   ?};
   OKRO.cntx_pop()
?};

{? _result.RESULT = 1
||
   OKR.index('OKR');
   OKR.prefix(REF.FIRMA, _obj.ROK, _obj.MC);
   {? OKR.first()
   ||
      {? _mode=0
      || _result.RESULT:=0
      ||
         _result.ACTION:='put';
         exec('record','xls_okr',_obj,_mode,_result)
::         {? exec('validate_obj','xls_okr', _obj)
::         ||
::            _result.RESULT:=0
::         ?}
      ?}
   ||
      _result.ACTION:='add';
      exec('record','xls_okr',_obj,_mode,_result)
::      {? exec('validate_obj','xls_okr', _obj)
::      ||
::         _result.RESULT:=0
::      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=OKR.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=OKR.put(1)
   ?}
?};
{? _result=0
||
   {? _validate.ACTION='add'
   ||
      _rok:={? type_of(_obj.ROK)<>type_of('') || $_obj.ROK || _obj.ROK ?};
      _mc:={? type_of(_obj.MC)<>type_of('') || $_obj.MC || _obj.MC ?};
      FUN.info('Dodanie pozycji z roku: %1 miesiąca: %2 nie powiodło się.'@[_rok, _mc])
   ||
      FUN.info('Poprawa pozycji z roku: %1 miesiąca: %2 nie powiodło się.'@[_rok, _mc])
   ?}
?};
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:42 65d69fdce31554dd522dcdf49e94176c4d91c710b5eef0dc393aae690cb806ba23230c37f6bc38727375cd7e517df1247d7c04b55a3e6b3c2cc824e611a3d819ca5239193a412ebfb574107957e0ae314bd37a2e0e0aae0127f0b71fb52eb2b72df3bca1e938479f775e8ac753ee4b870fa99c91fa8a98c49879a91749a042be

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_psto.fml
:: Utworzony: 16.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu pozycji w organizacji dla stanowisk w jednostkach organizacyjnych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='PSTO';
_def.FUNPAR:='ZWS_PAR_PSTO';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Pozycje w org. dla stanowisk=1,1';
_def.NAME:='Pozycje w org. dla stanowisk'@;
_def.DESC:='Pozycje w org. dla stanowisk';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="PSTO";

: prolog
_def.BEFORE:="
   UD_SKL.cntx_psh();
   UD_SKL.prefix();
   STN.cntx_psh();
   STN.prefix();
   PSTO.cntx_psh();
   PSTO.f_clear();
   PSTO.index('POZKOD');
   PSTO.prefix();
   ~~
";

: epilog
_def.AFTER:="
   PSTO.cntx_pop();
   STN.cntx_pop();
   UD_SKL.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('UD_SKL', MS.name(STO,'UD_SKL'),  1,,MS.comment(STO,'UD_SKL'));
   _env.define('STN',    MS.name(STO,'STN'),     1,,MS.comment(STO,'STN'));
   _env.define('POZWORG',MS.name(PSTO,'POZWORG'),1,,MS.comment(PSTO,'POZWORG'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.UD_SKL.VALUE:=PSTO.STO().UD_SKL().SYMBOL;
   _buf.STN.VALUE:=PSTO.STO().STN().ST;
   _buf.POZWORG.VALUE:=PSTO.POZWORG().KOD;
   _env.write_async('STO',PSTO.STO);
   _env.write_async('POZWORG',PSTO.POZWORG);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_psto',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_psto',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.UD_SKL=''  || 'UD_SKL'
   |? _buf.STN=''     || 'STN'
   |? _buf.POZWORG='' || 'POZWORG'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(UD_SKL,'SYMBOL');
{? +_buf.UD_SKL>_max
|| _val.msg_length('UD_SKL',_max);
   _buf.UD_SKL:=_max+_buf.UD_SKL
?};
_max:=MS.fld_len(STN,'ST');
{? +_buf.STN>_max
|| _val.msg_length('STN',_max);
   _buf.STN:=_max+_buf.STN
?};
_max:=MS.fld_len(POZWORG,'KOD');
{? +_buf.POZWORG>_max
|| _val.msg_length('POZWORG',_max);
   _buf.POZWORG:=_max+_buf.POZWORG
?};

: weryfikacja spójności referencyjnej
{? (_skl:=exec('szukaj_ud_skl','schemat','PODZORG',_buf.UD_SKL))=null
|| _val.msg_norec('UD_SKL');
   _val.RESULT:=0;
   return()
?};
{? ~STN.find_tab(,'ST',,'=',_buf.STN)
|| _val.msg_norec('STN');
   _val.RESULT:=0;
   return()
|| _stn:=STN.ref()
?};
{? ~STO.find_tab(,'UD_SKL',,'=',_skl,'STN',,'=',_stn)
|| _val.msg_norec(,MS.name(PSTO,'STO'));
   _val.RESULT:=0;
   return()
|| _sto:=STO.ref()
?};
{? ~POZWORG.find_tab(,'FIRMA',,'=',REF.FIRMA,'KOD',,'=',_buf.POZWORG)
|| _val.msg_norec('POZWORG');
   _val.RESULT:=0;
   return()
|| _poz:=POZWORG.ref()
?};

_ref:=null;
{? PSTO.find_tab(,'STO',,'=',_sto,'POZWORG',,'=',_poz)
|| _ref:=PSTO.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1/%2: %3'[_a.UD_SKL,_a.STN,_a.POZWORG]",
   'STO',_sto,
   'POZWORG',_poz
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~l
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,PSTO,"
   _a.STO:=_c.STO;
   _a.POZWORG:=_c.POZWORG;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 6a57fc036380377c1f89062ea0f375283357280696f3035af3169589da0b0f71ff9771502d39e091d530a3f8ba6b7d19bd3eaec32ac3971ac26493bf854c6b092c2ba444cace54cd2c09977e160b6ee8cce62b906c677d1295db8fb27f9280faae101060b17b53c7544c85a4c18d721b84dbbc459c51d18fc1976260cce90eb6

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kst_lst.fml
:: Utworzony: 25.09.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu widoków stałej
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='KST_LST';
_def.FUNPAR:='ZWS_PAR_KST_ADM';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.NAME:='Stałe systemu - widok stałej'@;
_def.FILE:=exec('def_kst_xlsx','xls__init');
_def.SHEET:='Widok stałej=1,1';
_def.DESC:='Widok stałej';
_def.ADD_ROWS:=0;
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="KST_LST";

: prolog
_def.BEFORE:="
   KST_MAP.cntx_psh();
   KST_MAP.f_clear();
   KST_MAP.prefix();

   KST_DOM.cntx_psh();
   KST_DOM.f_clear();
   KST_DOM.prefix();

   KST_LST.cntx_psh();
   KST_LST.f_clear();
   KST_LST.index('NUMER');
   KST_LST.prefix();
   ~~
";

: epilog
_def.AFTER:="
   KST_LST.cntx_pop();
   KST_DOM.cntx_pop();
   KST_MAP.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('B_DOMAIN',MS.name(KST_DOM,'B_DOMAIN'),0,,MS.comment(KST_DOM,'B_DOMAIN'));
   _env.define('KST_ZES', MS.name(KST_DOM,'KST_ZES'), 0,,MS.comment(KST_DOM,'KST_ZES'));
   _env.define('SYMBOL',  MS.name(KST_MAP,'SYMBOL'),  0,,MS.comment(KST_MAP,'SYMBOL'));
   _env.define('KST_DEF', MS.name(KST_MAP,'KST_DEF'), 0,,MS.comment(KST_MAP,'KST_DEF'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.B_DOMAIN.VALUE:=KST_LST.KST_DOM().B_DOMAIN().SYMBOL;
   _buf.KST_ZES.VALUE:=KST_LST.KST_DOM().KST_ZES().SYMBOL;
   _buf.SYMBOL.VALUE:=KST_LST.KST_MAP().SYMBOL;
   _buf.KST_DEF.VALUE:=KST_LST.KST_MAP().KST_DEF().SYMBOL;
   _env.write_async('KST_DOM',KST_LST.KST_DOM);
   _env.write_async('KST_MAP',KST_LST.KST_MAP);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_kst_lst',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kst_lst',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.B_DOMAIN='' || 'B_DOMAIN'
   |? _buf.KST_ZES=''  || 'KST_ZES'
   |? _buf.SYMBOL=''   || 'SYBMOL'
   |? _buf.KST_DEF=''  || 'KST_DEF'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(B_DOMAIN,'SYMBOL');
{? +_buf.B_DOMAIN>_max
|| _val.msg_length('B_DOMAIN',_max);
   _buf.B_DOMAIN:=_max+_buf.B_DOMAIN
?};
_max:=MS.fld_len(KST_ZES,'SYMBOL');
{? +_buf.KST_ZES>_max
|| _val.msg_length('KST_ZES',_max);
   _buf.KST_ZES:=_max+_buf.KST_ZES
?};
_max:=MS.fld_len(KST_MAP,'SYMBOL');
{? +_buf.SYMBOL>_max
|| _val.msg_length('KST_DEF',_max);
   _buf.SYMBOL:=_max+_buf.SYMBOL
?};
_max:=MS.fld_len(KST_DEF,'SYMBOL');
{? +_buf.KST_DEF>_max
|| _val.msg_length('KST_DEF',_max);
   _buf.KST_DEF:=_max+_buf.KST_DEF
?};

: weryfikacja spójności referencyjnej
{? ~KST_DOM.find_tab(,
      'B_DOMAIN','SYMBOL','=',_buf.B_DOMAIN,
      'KST_ZES','SYMBOL','=',_buf.KST_ZES
   )
|| _val.msg_norec('KST_ZES');
   _val.RESULT:=0;
   return()
?};
{? ~KST_MAP.find_tab(,
      'KST_ZES','SYMBOL','=',_buf.KST_ZES,
      'KST_DEF','SYMBOL','=',_buf.KST_DEF,
      'SYMBOL',,'=',_buf.SYMBOL
   )
|| _val.msg_norec('SYMBOL');
   _val.RESULT:=0;
   return()
?};

_ref:=null;
_numer:=0;
: odszukaj aktualizowany wiersz
{? KST_LST.find_tab(,
      'KST_DOM',,'=',KST_DOM.ref(),
      'KST_MAP',,'=',KST_MAP.ref()
   )
|| _ref:=KST_LST.ref();
   _val.ACTION:='put';
   _numer:=1
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1, %2, %3, %4'[_a.B_DOMAIN,_a.KST_ZES,_a.KST_DEF,_a.SYMBOL]",
   'KST_DOM',KST_DOM.ref(),
   'KST_MAP',KST_MAP.ref(),
   'NUMER',_numer
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,KST_LST,"
   _a.KST_DOM:=_c.KST_DOM;
   _a.KST_MAP:=_c.KST_MAP;
   {? _c.NUMER=0
   || _numer:=1;
      _a.cntx_psh();
      _a.index('NUMER');
      _a.prefix(_c.KST_DOM);
      {? _a.last()
      || _numer:=_a.NUMER+1
      ?};
      _a.cntx_pop();
      _a.NUMER:=_numer
   ?};
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 54f7f5043649e9aa0bc7dcb99706f8b2f2b761a6def636174dc067c10f540699c0c4b5581fe136e4a62f25250afb737ce3e897ac7321c183f191b599f0a74976c573cbfd543f372218f1107b59d349a1dcb907c6eb58acf1d18ca6f58275ae17585b5f2c7674cc7fd8c33d7362f8c343a02b6f63d1c75b0da95f0c4008a7df9d

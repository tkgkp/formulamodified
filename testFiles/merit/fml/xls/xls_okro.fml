:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_okro.fml
:: Utworzony: 22.08.2018
:: Autor: AMK
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi wspólnych okresów
::======================================================================================================================


\OKRO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='OKRO';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_AOKR';
_def.ADD_ROWS:=0;
_def.FILE:=exec('filename','xls_okro');
_def.SHEET:='Okresy pracy w systemie=1,1';
_def.NAME:='Okresy pracy w systemie'@;
_def.DESC:='Okresy pracy w systemie'@;

_def.PREFIX:="OKRO.index('UNIK'); OKRO.prefix(REF.FIRMA)";
_def.TABLE:="OKRO";
_def.FIELDS:="exec('OKRO_fld','xls_okro',_a)";

_def.BEFORE:="OKRO.cntx_psh()";
_def.AFTER:="OKRO.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('OKRO_exp','xls_okro',_a,_b)";
_def.VALIDATE:="exec('OKRO_valid','xls_okro',_a,_b,_c)";
_def.IMPORT:="exec('OKRO_imp','xls_okro',_a,_b,_c)";
_def.SELECT:="exec('OKRO_sel','xls_okro',_a)";
~~


\filename
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Nazwa pliku do eksportu okresów
::----------------------------------------------------------------------------------------------------------------------
'okresy.xlsx'


\OKRO_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('ROK','Rok',0,,'Numer roku',,0);
_def.define('NR_OKR','Numer okresu',0,,'Numer okresu',,0);
_def.define('NAZWA','Nazwa okresu',0,,'12 znakowa nazwa okresu');
_def.define('DATA_OD','Data początku okresu',0,,'Data początkowa okresu',type_of(date()));
_def.define('DATA_DO','Data końca okresu',0,,'Data końcowa okresu',type_of(date()));
~~


\OKRO_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.ROK.VALUE:=OKRO.ROK;
_table.NR_OKR.VALUE:=OKRO.NR_OKR;
_table.NAZWA.VALUE:=OKRO.NAZWA;
_table.DATA_OD.VALUE:=OKRO.DATA_OD;
_table.DATA_DO.VALUE:=OKRO.DATA_DO;

:: eksport powiązań okresów z obszarami
OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref());
{? OKR_OBSZ.first()
|| {! |?
      _excel.write_async('OKR_OBSZ',OKR_OBSZ.ref());
      OKR_OBSZ.next()
   !}
?};
OKR_OBSZ.cntx_pop();

_result


\OKRO_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? _table.ROK<=0
|| _result.MSG:='Nie podano numeru roku wspólnego %1.'@[_table.NAZWA];
   _result.RESULT:=0
?};
{? _result.RESULT & |_table.NAZWA=''
|| _result.MSG:='Nie podano nazwy okresu.'@;
   _result.RESULT:=0
?};
{? _result.RESULT & (_table.NR_OKR<1 | _table.NR_OKR>12)
|| _result.MSG:='Numer okresu musi być z przedziału 1-12 (%1 %2).'@[_table.NAZWA,$_table.ROK];
   _result.RESULT:=0
?};
{? _result.RESULT & (_table.DATA_OD=date(0,0,0) | _table.DATA_DO=date(0,0,0))
|| _result.MSG:='Okres nie może mieć zerowej daty zakończenia i końca (%1 %2).'@[_table.NAZWA,$_table.ROK];
   _result.RESULT:=0
?};
{? _result.RESULT & (_table.DATA_OD>_table.DATA_DO)
|| _result.MSG:='Data końca okresu nie może być mniejsza niż początku (%1 %2).'@[_table.NAZWA,$_table.ROK];
   _result.RESULT:=0
?};
{? _result.RESULT
|| OKRO.cntx_psh(); OKRO.index('UNIK'); OKRO.prefix(REF.FIRMA,_table.ROK);
   {? ~OKRO.first()
   || OKRO.prefix(REF.FIRMA);
      {? OKRO.first()
      || OKRO.prefix(REF.FIRMA,_table.ROK-1);
         _jest:=OKRO.first();
         {? ~_jest
         || OKRO.prefix(REF.FIRMA,_table.ROK+1);
            _jest:=OKRO.first()
         ?};
         {? ~_jest
         || _result.MSG:='Okres %2 / %1 brak roku poprzedniego lub następnego.'@[$_table.ROK,$_table.NR_OKR];
            _result.RESULT:=0
         ?}
      ?}
   ?};
   OKRO.cntx_pop()
?};
{? _result.RESULT
|| OKRO.index('UNIK'); OKRO.prefix(REF.FIRMA,_table.ROK,_table.NR_OKR);
   {? OKRO.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || OKRO.blank() ?};
      OKRO.FIRMA:=REF.FIRMA;
      OKRO.ROK:=_table.ROK;
      OKRO.NR_OKR:=_table.NR_OKR;
      OKRO.NAZWA:=_table.NAZWA;
      OKRO.DATA_OD:=_table.DATA_OD;
      OKRO.DATA_DO:=_table.DATA_DO
   ?}
?};
~~


\OKRO_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=OKRO.add(1)
   |? _validate.ACTION='put'
   || _result:=OKRO.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie wspólnego okresu %1 %2 nie powiodło się.'@[$_table.NR_OKR,$_table.ROK]
   || _validate.MSG:='Poprawa wspólnego okresu %1 %2 nie powiodło się.'@[$_table.NR_OKR,$_table.ROK]
   ?}
?};
_result


\OKRO_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=sql('select distinct ROK, cast(null as DATE_TYPE) as DATA_OD, cast(null as DATE_TYPE) as DATA_DO,'+
          ' cast(\'N\' as STRING_TYPE) as SELECTED, \'    \' as REF from :_a where :_a.FIRMA=:_b order by 1',
          OKRO,REF.FIRMA);
OKRO.cntx_psh(); OKRO.index('UNIK');
{? _tab.first()
|| {! |?
      _tab.REF:=$_tab.ROK;
      OKRO.prefix(REF.FIRMA,_tab.ROK);
      {? OKRO.first() || _tab.DATA_OD:=OKRO.DATA_OD ?};
      {? OKRO.last() || _tab.DATA_DO:=OKRO.DATA_DO ?};
      _tab.put();
      _tab.next()
   !}
?};
OKRO.cntx_pop();
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'ROK',,);

_result:=exec('select_action','#table',_tab,'ROK,DATA_OD,DATA_DO',
              20,'Wybór lat do eksportu'@,1);
{? _result>0
|| _selected.erase();
   _tab.index(_ndx);
   OKRO.cntx_psh(); OKRO.index('UNIK'); OKRO.prefix(REF.FIRMA);
   {? OKRO.first()
   || {! |?
         _tab.prefix('T',OKRO.ROK);
         {? _tab.first()
         || _selected.prefix($OKRO.ref());
            {? ~_selected.first()
            || _selected.blank();
               _selected.REF:=$OKRO.ref();
               _selected.add()
            ?}
         ?};
         OKRO.next()
      !}
   ?};
   OKRO.cntx_pop()
?};
_result


\OKR_OBSZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='OKR_OBSZ';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_AOKR';
_def.HIDDEN:='T';
_def.ADD_ROWS:=0;
_def.FILE:=exec('filename','xls_okro');
_def.SHEET:='Okresy i obszary=1,1';
_def.NAME:='Powiązania okresów z obszarami'@;
_def.DESC:='Powiązania okresów z obszarami'@;

_def.PREFIX:="OKR_OBSZ.index('UNIK1'); OKR_OBSZ.prefix(REF.FIRMA)";
_def.TABLE:="OKR_OBSZ";
_def.FIELDS:="exec('OKR_OBSZ_fld','xls_okro',_a)";

_def.BEFORE:="OKR_OBSZ.cntx_psh()";
_def.AFTER:="OKR_OBSZ.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('OKR_OBSZ_exp','xls_okro',_a,_b)";
_def.VALIDATE:="exec('OKR_OBSZ_valid','xls_okro',_a,_b,_c)";
_def.IMPORT:="exec('OKR_OBSZ_imp','xls_okro',_a,_b,_c)";
~~


\OKR_OBSZ_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('ROK','Rok',0,,'Okres wspólny - numer roku',type_of(0),0);
_def.define('NR_OKR','Numer okresu',0,,'Okres wspólny - numer okresu',type_of(0),0);
_def.define('B_DOMAIN','Obszar',0,,'Symbol dziedziny');
_def.define('OKRO_FR','Okres obrachunkowy - nazwa roku',0,,'Okres obrachunkowy - nazwa roku');
_def.define('OKRO_FN','Okres obrachunkowy - numer okresu',0,,'Okres obrachunkowy - numer okresu',type_of(0),0);
_def.define('OKR_LR','Okres logistyczny - numer roku',0,,'Okres logistyczny - numer roku',type_of(0),0);
_def.define('OKR_LN','Okres logistyczny - numer miesiąca',0,,'Okres logistyczny - numer miesiąca',type_of(0),0);
~~


\OKR_OBSZ_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_result:=1;

:: eksport lat bilansowych, okresów obrachunkowych i okresów raportowania
{? OKR_OBSZ.OKRO_F
|| ROK_F.cntx_psh(); OKRO_F.cntx_psh(); OSPR.cntx_psh();
   OKRO_F.prefix(); _excel.write_async('ROK_F',OKR_OBSZ.OKRO_F().ROK);
   ROK_F.cntx_pop(); OKRO_F.cntx_pop(); OSPR.cntx_pop()
?};

_excel.write_async('OKR',OKR_OBSZ.OKR);

_table.ROK.VALUE:=OKR_OBSZ.OKRO().ROK;
_table.NR_OKR.VALUE:=OKR_OBSZ.OKRO().NR_OKR;
_table.B_DOMAIN.VALUE:=OKR_OBSZ.B_DOMAIN().SYMBOL;
_table.OKRO_FR.VALUE:={? OKR_OBSZ.OKRO_F || OKR_OBSZ.OKRO_F().ROK().NAZ || '' ?};
_table.OKRO_FN.VALUE:={? OKR_OBSZ.OKRO_F || OKR_OBSZ.OKRO_F().NR || 0 ?};
_table.OKR_LR.VALUE:={? OKR_OBSZ.OKR || OKR_OBSZ.OKR().ROK || 0 ?};
_table.OKR_LN.VALUE:={? OKR_OBSZ.OKR || OKR_OBSZ.OKR().MC || 0 ?};
_result


\OKR_OBSZ_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_okro:=_b_dom:=_rok_f:=_okro_f:=_okr:=null;
{? _table.ROK<=0
|| _result.MSG:='Nie podano numeru roku wspólnego.'@;
   _result.RESULT:=0
?};
{? _result.RESULT & (_table.NR_OKR<1 | _table.NR_OKR>12)
|| _result.MSG:='Numer okresu musi być z przedziału 1-12.'@;
   _result.RESULT:=0
|| OKRO.index('UNIK'); OKRO.prefix(REF.FIRMA,_table.ROK,_table.NR_OKR);
   {? OKRO.first()
   || _okro:=OKRO.ref()
   || _result.MSG:='Nie znaleziono okresu wspólnego %1 %2.'@[$_table.NR_OKR,$_table.ROK];
      _result.RESULT:=0
   ?}
?};
{? _result.RESULT
|| B_DOMAIN.index('SYMBOL'); B_DOMAIN.prefix(_table.B_DOMAIN,);
   {? ~B_DOMAIN.first()
   || _result.MSG:='Nie znaleziono dziedziny o symbolu: %1.'@[_table.B_DOMAIN];
      _result.RESULT:=0
   || _b_dom:=B_DOMAIN.ref()
   ?}
?};
{? _result.RESULT & _table.OKRO_FR<>''
|| ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,_table.OKRO_FR,);
   {? ~ROK_F.first()
   || _result.MSG:='Nie znaleziono roku bilansowego o nazwie: %1.'@[_table.OKRO_FR];
      _result.RESULT:=0
   || _rok_f:=ROK_F.ref()
   ?}
?};
{? _result.RESULT & _rok_f & (_table.OKRO_FN<0 | _table.OKRO_FN>23)
|| _result.MSG:='Numer okresu obrachunkowego musi być z przedziału 0-23.'@;
   _result.RESULT:=0
?};
{? _result.RESULT & _rok_f
|| OKRO_F.index('ROK'); OKRO_F.prefix(_rok_f,_table.OKRO_FN);
   {? OKRO_F.first()
   || _okro_f:=OKRO_F.ref()
   || _result.MSG:='Nie znaleziono okresu obrachunkowego: %1 %2.'@[$_table.OKRO_FN,_table.OKRO_FR];
      _result.RESULT:=0
   ?}
?};
{? _result.RESULT & _table.OKR_LR<0
|| _result.MSG:='Numer roku logistycznego musi być większy od zera.'@;
   _result.RESULT:=011
?};
{? _result.RESULT & _table.OKR_LR & (_table.OKR_LN<1 | _table.OKR_LN>12)
|| _result.MSG:='Numer okresu logistycznego musi być z przedziału 0-12.'@;
   _result.RESULT:=0
?};
{? _result.RESULT & _table.OKR_LR
|| OKR.index('OKR'); OKR.prefix(REF.FIRMA,_table.OKR_LR,_table.OKR_LN);
   {? OKR.first()
   || _okr:=OKR.ref()
   || _result.MSG:='Nie znaleziono okresu logistycznego: %1 %2.'@[$_table.OKR_LN,$_table.OKR_LR];
      _result.RESULT:=0
   ?}
?};
{? _result.RESULT
|| OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(_okro,_b_dom,_okro_f);
   {? OKR_OBSZ.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || OKR_OBSZ.blank() ?};
      OKR_OBSZ.OKRO:=_okro;
      OKR_OBSZ.B_DOMAIN:=_b_dom;
      OKR_OBSZ.OKRO_F:=_okro_f;
      OKR_OBSZ.OKR:=_okr
   ?}
?};
~~


\OKR_OBSZ_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=OKR_OBSZ.add(1)
   |? _validate.ACTION='put'
   || _result:=OKR_OBSZ.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie powiązania okresu wspolnego z okresami dziedzinowymi nie powiodło się.'
   || _validate.MSG:='Poprawienie powiązania okresu wspolnego z okresami dziedzinowymi nie powiodło się.'
   ?}
?};
_result


\ROK_F
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ROK_F';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_AOKR';
_def.HIDDEN:='T';
_def.ADD_ROWS:=0;
_def.FILE:=exec('filename','xls_okro');
_def.SHEET:='Lata bilansowe=1,1';
_def.NAME:='Lata bilansowe'@;
_def.DESC:='Lata bilansowe'@;

_def.PREFIX:="ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA)";
_def.TABLE:="ROK_F";
_def.FIELDS:="exec('ROK_F_fld','xls_okro',_a)";

_def.BEFORE:="ROK_F.cntx_psh()";
_def.AFTER:="ROK_F.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('ROK_F_exp','xls_okro',_a,_b)";
_def.VALIDATE:="exec('ROK_F_valid','xls_okro',_a,_b,_c)";
_def.IMPORT:="exec('ROK_F_imp','xls_okro',_a,_b,_c)";
~~


\ROK_F_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('NAZ','Nazwa',0,,'Nazwa roku bilansowego');
_def.define('LOBR','Liczba okresów obrachunkowych',0,,'Liczba okresów w roku bilansowego',,0);
_def.define('SYNT','Długość syntetyki',0,,'Długość syntetyki konta w roku bilansowego',,0);
_def.define('SEP','Znak separatora',0,,'Znak separatora w roku bilansowego (znak "," to brak sep.)');
_def.define('PD','Podatek dochodowy',0,,'Stawka podatku dochodowego od osób prawnych');
_def.define('PROC_VAT','% struktury VAT',0,,'Procent struktury VAT');
_def.define('ROZL_VAT','Rozliczenie VAT',0,,'Tryb rozliczenia VAT, (M-miesięczne, K-kwartalne)');
_def.define('POCZ_ROK','Początek roku',0,,'Początek roku bilansowego - data',type_of(date()));
_def.define('KON_ROK','Koniec roku',0,,'Koniec roku bilansowego - data',type_of(date()));
_def.define('PLAN_GR','Plan kont jak dla grupy',0,,'Znacznik, czy plan kont taki jak grupy T/N');
_def.define('PREWSK','% prewskaźnika VAT',0,,'Prewskaźnik dla celów odliczeń VAT');
_def.define('ZK','Zmiana klasyfikacji środków',0,,'Czy na początku roku wykonano zmianę klasyfikacji środków? T/N');
_def.define('AKT_ROZ','Aktualizacja rozrachunków',0,,'Czy aktualizować rozrachunki w roku bilansowym? T/N');
~~


\ROK_F_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;


:: eksport lat bilansowych, okresów obrachunkowych i okresów raportowania
ROK_F.cntx_psh(); OSPR.cntx_psh();
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
{? OKRO_F.first()
|| {! |?
      _excel.write_async('OKRO_F',OKRO_F.ref());
      OKRO_F.next()
   !}
?};
OSPR.index('ROK1'); OSPR.prefix(ROK_F.ref());
{? OSPR.first()
|| {! |?
      _excel.write_async('OSPR',OSPR.ref());
      OSPR.next()
   !}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop(); OSPR.cntx_pop();

_result:=1;
_table.NAZ.VALUE:=ROK_F.NAZ;
_table.LOBR.VALUE:=ROK_F.LOBR;
_table.PLAN_GR.VALUE:={? ROK_F.PLAN_GR='T' | ROK_F.PLAN_GR='N' || ROK_F.PLAN_GR || 'N' ?};
_table.ZK.VALUE:={? ROK_F.ZK='T' | ROK_F.ZK='N' || ROK_F.ZK || 'N' ?};
_table.AKT_ROZ.VALUE:={? ROK_F.AKT_ROZ='T' | ROK_F.AKT_ROZ='N' || ROK_F.AKT_ROZ || 'N' ?};
_table.SYNT.VALUE:=ROK_F.SYNT;
_table.SEP.VALUE:=ROK_F.SEP;
_table.PD.VALUE:=ROK_F.PD;
_table.PROC_VAT.VALUE:=ROK_F.PROC_VAT;
_table.ROZL_VAT.VALUE:=ROK_F.ROZL_VAT;
_table.POCZ_ROK.VALUE:=ROK_F.POCZ_ROK;
_table.KON_ROK.VALUE:=ROK_F.KON_ROK;
_table.PREWSK.VALUE:=ROK_F.PREWSK;

_result


\ROK_F_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1;
exec('ini_rokf','okresy');
{? _result.RESULT & _table.NAZ=''
|| _result.MSG:='Nie wprowadzono nazwy roku bilansowego.';
   _result.RESULT:=0
?};
{? _result.RESULT & _table.LOBR<1 | _table.LOBR>23
|| _result.MSG:='Rok obrachunkowy może mieć liczbę okresów od 1 do 23 (%1).'@[_table.NAZ];
   _result.RESULT:=0
?};
{? _result.RESULT & (_table.POCZ_ROK=date(0,0,0) | _table.KON_ROK=date(0,0,0))
|| _result.MSG:='Rok obrachunkowy nie może mieć zerowej daty zakończenia i końca (%1).'@[_table.NAZ];
   _result.RESULT:=0
?};
{? _result.RESULT & (_table.POCZ_ROK>_table.KON_ROK)
|| _result.MSG:='Data końca roku nie może być mniejsza niż początku roku (%1).'@[_table.NAZ];
   _result.RESULT:=0
?};
{? _result.RESULT
|| ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,_table.NAZ,);
   {? ROK_F.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || ROK_F.blank() ?};
      ROK_F.NAZ:=_table.NAZ;
      ROK_F.LOBR:=_table.LOBR;
      ROK_F.SYNT:=_table.SYNT;
      ROK_F.SEP:=_table.SEP;
      ROK_F.PD:=_table.PD;
      ROK_F.PROC_VAT:=_table.PROC_VAT;
      ROK_F.ROZL_VAT:=_table.ROZL_VAT;
      ROK_F.POCZ_ROK:=_table.POCZ_ROK;
      ROK_F.KON_ROK:=_table.KON_ROK;
      ROK_F.FIRMA:=REF.FIRMA;
      ROK_F.PLAN_GR:={? _table.PLAN_GR='T' | _table.PLAN_GR='N' || _table.PLAN_GR || 'N' ?};
      ROK_F.PREWSK:=_table.PREWSK;
      ROK_F.ZK:={? ROK_F.ZK='T' | ROK_F.ZK='N' || _table.ZK || 'N' ?};
      ROK_F.AKT_ROZ:={? ROK_F.AKT_ROZ='T' | ROK_F.AKT_ROZ='N' || _table.AKT_ROZ || 'N' ?}
   ?}
?};
~~


\ROK_F_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || ROK_F_K.cntx_psh(); ROK_F_K.index('LP'); ROK_F_K.prefix(null);
      _ok:={? ROK_F_K.first()
           || ROK_F.KOD:=ROK_F_K.KOD;
              _result:=ROK_F.add(1);
              {? _result
              || ROK_F_K.prefix();
                 ROK_F_K.ROK_F:=ROK_F.ref();
                 ROK_F_K.put()
              ?}
           || FUN.info('Wyczerpano dwuznakowe kody lat.\nDodanie roku niemożliwe'@); 0
           ?};
      ROK_F_K.cntx_pop()
   |? _validate.ACTION='put'
   || _result:=ROK_F.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie roku bilansowego %1 nie powiodło się.'@[_table.NAZ]
   || _validate.MSG:='Poprawa roku bilansowego %1 nie powiodła się.'@[_table.NAZ]
   ?}
|? _validate.ACTION='add'
|| exec('add_bobz','rejestry',ROK_F.ref());
   exec('add_rok_view','rejestry')
?};
_result


\OKRO_F
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='OKRO_F';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_AOKR';
_def.HIDDEN:='T';
_def.ADD_ROWS:=0;
_def.FILE:=exec('filename','xls_okro');
_def.SHEET:='Okresy obrachunkowe=1,1';
_def.NAME:='Okresy obrachunkowe';
_def.DESC:='Okresy obrachunkowe';

_def.PREFIX:="OKRO_F.index('FIRMA_NR'); OKRO_F.prefix()";
_def.TABLE:="OKRO_F";
_def.FIELDS:="exec('OKRO_F_fld','xls_okro',_a)";

_def.BEFORE:="OKRO_F.cntx_psh()";
_def.AFTER:="OKRO_F.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('OKRO_F_exp','xls_okro',_a,_b)";
_def.VALIDATE:="exec('OKRO_F_valid','xls_okro',_a,_b,_c)";
_def.IMPORT:="exec('OKRO_F_imp','xls_okro',_a,_b,_c)";
~~


\OKRO_F_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('ROK','Rok bilansowy',0,,'Nazwa roku bilansowego');
_def.define('KOD','Kod',0,,'Kod okresu obrachunkowego');
_def.define('NR','Nr okresu',0,,'Kolejny numer okresu obrachunkowego',,0);
_def.define('NAZ','Nazwa',0,,'Nazwa okresu obrachunkowego');
_def.define('POCZ','Początek okresu',0,,'Data początku okresu obrachunkowego',type_of(date()));
_def.define('KON','Koniec okresu',0,,'Data końca okresu obrachunkowego',type_of(date()));
_def.define('POLROCZE','Półrocze',0,,'Numer półrocza (1 lub 2) którego dotyczy dany okres obrachunkowy',,0);
_def.define('KWARTAL','Kwartał',0,,'Numer kwartału (1-4), którego dotyczy dany okres obrachunkowy',,0);
_def.define('OES','Numer okresu podatkowego',0,,'Numer okresu dla środków trwałych',,0);
_def.define('RES','Numer roku podatkowego',0,,'Numer roku dla środków trwałych',,0);
_def.define('TAB_KRST','Klasyfikacja środków trwałych',0,,'Obowiązująca w okresie klasyfikacja środków trwałych');
~~


\OKRO_F_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_result:=1;
OKRO_F.cntx_psh();
_excel.write_async('TAB_KRST',OKRO_F.TAB_KRST);
OKRO_F.cntx_pop();
_table.ROK.VALUE:=OKRO_F.ROK().NAZ;
_table.KOD.VALUE:=OKRO_F.KOD;
_table.NR.VALUE:=OKRO_F.NR;
_table.NAZ.VALUE:=OKRO_F.NAZ;
_table.POCZ.VALUE:=OKRO_F.POCZ;
_table.KON.VALUE:=OKRO_F.KON;
_table.POLROCZE.VALUE:=OKRO_F.POLROCZE;
_table.KWARTAL.VALUE:=OKRO_F.KWARTAL;
_table.OES.VALUE:=OKRO_F.OES;
_table.RES.VALUE:=OKRO_F.RES;
_table.TAB_KRST.VALUE:={? OKRO_F.TAB_KRST || OKRO_F.TAB_KRST().KST || '' ?};

_result


\OKRO_F_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;
_result.RESULT:=1; _tab_krs:=null;

{? _table.ROK=''
|| _result.MSG:='Nie podano nazwy roku bilansowego.'@;
   _result.RESULT:=0
?};
{? _result.RESULT
|| ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,_table.ROK,);
   {? ~ROK_F.first()
   || _result.MSG:='Nie znaleziono roku bilansowego o nazwie: %1.'@[_table.ROK];
      _result.RESULT:=0
   ?}
?};
{? _result.RESULT & _table.TAB_KRST<>''
|| TAB_KRST.index('TAB_KRST'); TAB_KRST.prefix(_table.TAB_KRST,);
   {? ~TAB_KRST.first()
   || _result.MSG:='Nie znaleziono tabeli klasyfikacji środków trwałych o symbolu: %1.'@[_table.TAB_KRST];
      _result.RESULT:=0
   || _tab_krs:=TAB_KRST.ref()
   ?}
?};
{? _result.RESULT & +_table.KOD<>24
|| _result.MSG:='Nieprawidłowa długość kodu okresu obrachunkowego: %1.'@[_table.KOD];
   _result.RESULT:=0
?};
{? _result.RESULT & (_table.NR<0 | _table.NR>23)
|| _result.MSG:='Numer okresu musi być z przedziału 0-23.'@;
   _result.RESULT:=0
?};
{? _result.RESULT & |_table.NAZ=''
|| _result.MSG:='Nie wprowadzono nazwy okresu obrachunkowego: %1 %2.'@[$_table.NR,_table.ROK];
   _result.RESULT:=0
?};
{? _result.RESULT & OKRO_F.POCZ<>date(0,0,0) & OKRO_F.KON<>date(0,0,0) & (_table.POCZ>_table.KON)
|| _result.MSG:='Okres obrachunkowy: %1 %2 Data końca okresu nie może być mniejsza niż początku okresu.'@[$_table.NR,_table.ROK];
   _result.RESULT:=0
?};
{? _result.RESULT & ~(_table.POLROCZE=1 | _table.POLROCZE=2)
|| _result.MSG:='Okres obrachunkowy: %1 %2 - pole półrocze może mieć wartośc 1 lub 2.'@[$_table.NR,_table.ROK];
   _result.RESULT:=0
?};
{? _result.RESULT & ~(_table.KWARTAL>=1 & _table.KWARTAL<=4)
|| _result.MSG:='Okres obrachunkowy: %1 %2 - pole kwartał może mieć wartośc 1 - 4.'@[$_table.NR,_table.ROK];
   _result.RESULT:=0
?};
{? _result.RESULT
|| OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref(),_table.NR);
   {? OKRO_F.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || OKRO_F.blank() ?};
      OKRO_F.ROK:=ROK_F.ref();
      OKRO_F.KOD:=_table.KOD;
      OKRO_F.NR:=_table.NR;
      OKRO_F.NAZ:=_table.NAZ;
      OKRO_F.DOK:=OKRO_F.DOK_H:='N';
      {? 6+OKRO_F.NAZ='Bilans'
      || OKRO_F.POCZ:=OKRO_F.KON:=date(0,0,0)
      || OKRO_F.POCZ:=_table.POCZ;
         OKRO_F.KON:=_table.KON
      ?};
      OKRO_F.POLROCZE:=_table.POLROCZE;
      OKRO_F.KWARTAL:=_table.KWARTAL;
      OKRO_F.OES:=_table.OES;
      OKRO_F.RES:=_table.RES;
      OKRO_F.TAB_KRST:=_tab_krs
   ?}
?};
~~


\OKRO_F_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=OKRO_F.add(1)
   |? _validate.ACTION='put'
   || _result:=OKRO_F.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie okresu obrachunkowego %1 %2 nie powiodło się.'@[_table.NAZ,_table.ROK]
   || _validate.MSG:='Poprawa okresu obrachunkowego %1 %2 nie powiodła się.'@[_table.NAZ,_table.ROK]
   ?}
?};
_result


\OSPR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='OSPR';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_AOKR';
_def.HIDDEN:='T';
_def.ADD_ROWS:=0;
_def.FILE:=exec('filename','xls_okro');
_def.SHEET:='Okresy raportowania=1,1';
_def.NAME:='Okresy raportowania';
_def.DESC:='Okresy raportowania';

_def.PREFIX:="OSPR.index('OKRES9'); OSPR.prefix(REF.FIRMA)";
_def.TABLE:="OSPR";
_def.FIELDS:="exec('OSPR_fld','xls_okro',_a)";

_def.BEFORE:="OSPR.cntx_psh()";
_def.AFTER:="OSPR.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('OSPR_exp','xls_okro',_a,_b)";
_def.VALIDATE:="exec('OSPR_valid','xls_okro',_a,_b,_c)";
_def.IMPORT:="exec('OSPR_imp','xls_okro',_a,_b,_c)";
~~


\OSPR_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.define('ROK','Rok',0,,'Nazwa roku bilansowego');
_def.define('ROK_OD','Rok od',0,,'Rok bilansowy rozpoczynający okres raportowania');
_def.define('OKRES_OD','Okres od',0,,'Okres obrachunkowy rozpoczynający okres raportowania',type_of(0),0);
_def.define('ROK_DO','Rok do',0,,'Rok bilansowy kończący okres raportowania');
_def.define('OKRES_DO','Okres do',0,,'Okres obrachunkowy kończący okres raportowania',type_of(0),0);
_def.define('NAZWA','Nazwa okresu',0,,'Nazwa okresu raportowania');
_def.define('TYP','Typ okresu',0,,'Typ okresu raportowania - "R"ok, "P"ółrocze, "K"wartał, "M"iesiąc, "I"nny');
_def.define('SKROT','Skrót',0,,'Skrót nazwy okresu raportowania');
_def.define('OKRES','Okres',0,,'Typ okresu np. M01 (miesiąc 1), K01 (kwartał 1), P01 (pierwsze półrocze)');
~~


\OSPR_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_excel.write_async('SLO',OSPR.OKRES);
_table.ROK.VALUE:=OSPR.ROK().NAZ;
_table.ROK_OD.VALUE:=OSPR.OKRES_OD().ROK().NAZ;
_table.OKRES_OD.VALUE:=OSPR.OKRES_OD().NR;
_table.ROK_DO.VALUE:=OSPR.OKRES_DO().ROK().NAZ;
_table.OKRES_DO.VALUE:=OSPR.OKRES_DO().NR;
_table.NAZWA.VALUE:=OSPR.NAZWA;
_table.TYP.VALUE:=OSPR.TYP;
_table.SKROT.VALUE:=OSPR.SKROT;
_table.OKRES.VALUE:=OSPR.OKRES().KOD;
_result


\OSPR_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_rok:=_rok_od:=_rok_do:=_okr_od:=_okr_do:=null;
{? |_table.NAZWA=''
|| _result.MSG:='Nie wprowadzono nazwy okresu raportowania.'@;
   _result.RESULT:=0
?};
{? _result.RESULT & |_table.TYP=''
|| _result.MSG:='Nie wprowadzono typu okresu raportowania: %1.'@[_table.NAZWA];
   _result.RESULT:=0
?};
{? _result.RESULT & |_table.SKROT=''
|| _result.MSG:='Nie wprowadzono skróconej nazwy okresu raportowania: %1.'@[_table.NAZWA];
   _result.RESULT:=0
?};
{? _result.RESULT & _table.ROK=''
|| _result.MSG:='Nie podano nazwy roku bilansowego okresu raportowania: %1.'@[_table.NAZWA];
   _result.RESULT:=0
?};
{? _result.RESULT
|| ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,_table.ROK,);
   {? ~ROK_F.first()
   || _result.MSG:='Nie znaleziono roku bilansowego o nazwie: %1.'@[_table.ROK];
      _result.RESULT:=0
   || _rok:=ROK_F.ref()
   ?}
?};
{? _result.RESULT & _table.ROK_OD=''
|| _result.MSG:='Nie podano nazwy roku bilansowego rozpoczynającego okres raportowania: %1.'@[_table.NAZWA];
   _result.RESULT:=0
?};
{? _result.RESULT
|| ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,_table.ROK_OD,);
   {? ~ROK_F.first()
   || _result.MSG:='Nie znaleziono roku bilansowego o nazwie: %1.'@[_table.ROK_OD];
      _result.RESULT:=0
   || _rok_od:=ROK_F.ref()
   ?}
?};
{? _result.RESULT
|| OKRO_F.index('ROK'); OKRO_F.prefix(_rok_od,_table.OKRES_OD);
   {? ~OKRO_F.first()
   || _result.MSG:='Nie znaleziono okresu obrachunkowego rozpoczynającego okres raportowania: %1.'@[_table.NAZWA];
      _result.RESULT:=0
   || _okr_od:=OKRO_F.ref()
   ?}
?};
{? _result.RESULT & _table.ROK_DO=''
|| _result.MSG:='Nie podano nazwy roku bilansowego kończącego okres raportowania: %1.'@[_table.NAZWA];
   _result.RESULT:=0
?};
{? _result.RESULT
|| ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,_table.ROK_DO,);
   {? ~ROK_F.first()
   || _result.MSG:='Nie znaleziono roku bilansowego o nazwie: %1.'@[_table.ROK_DO];
      _result.RESULT:=0
   || _rok_do:=ROK_F.ref()
   ?}
?};
{? _result.RESULT
|| OKRO_F.index('ROK'); OKRO_F.prefix(_rok_do,_table.OKRES_DO);
   {? ~OKRO_F.first()
   || _result.MSG:='Nie znaleziono okresu obrachunkowego kończącego okres raportowania: %1.'@[_table.NAZWA];
      _result.RESULT:=0
   || _okr_do:=OKRO_F.ref()
   ?}
?};

OSPR.index('OKRES3'); OSPR.prefix(REF.FIRMA,_rok,_okr_od,_okr_do);
{? OSPR.first()
|| {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put'
   ?}
|| _result.ACTION:='add'
?};
{? _result.RESULT
|| {? _result.ACTION='add' || OSPR.blank() ?};
   OSPR.ROK:=_rok;
   OSPR.OKRES_OD:=_okr_od;
   OSPR.OKRES_DO:=_okr_do;
   OSPR.NAZWA:=_table.NAZWA;
   OSPR.TYP:=_table.TYP;
   OSPR.SKROT:=_table.SKROT;
   OSPR.OKRES:=null;
   {? _table.OKRES<>''
   || SLU.index('WZORZEC'); SLU.prefix('prosty','~TYPY OKRESÓW');
      SLO.index('SL'); SLO.prefix(SLU.ref(),_table.OKRES);
      {? SLO.first() || OSPR.OKRES:=SLO.ref() ?}
   ?};
   OSPR.FIRMA:=REF.FIRMA
?};
~~


\OSPR_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_table:=_a;
_mode:=_b;
_validate:=_c;
_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=OSPR.add(1)
   |? _validate.ACTION='put'
   || _result:=OSPR.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie okresu raportowania %1 nie powiodło się.'@[_table.NAZWA]
   || _validate.MSG:='Poprawa okresu raportowania %1 nie powiodła się.'@[_table.NAZWA]
   ?}
?};
_result

:Sign Version 2.0 jowisz:1045 2020/04/03 17:14:08 5e2f1d7606aaa8b0b056c656ca3ec3ee89a5b7a3ffa778b86727e9f2738b608e2158d2d8e06797949628e71e1bd873ef81cdc865b2c915671c5593a48f5ba4984088f761c6e19ad1fd8614f298cf2c484ca50bfc4d943a4302567d8b1e37b5f173e878d02cc69a3a8c1260ee9dcf32affe00d891398e4d78907f179a6747e43e

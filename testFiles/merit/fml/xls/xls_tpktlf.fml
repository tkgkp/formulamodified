:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_tpktlf.fml
:: Utworzony: 20.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu wariantów kalkulacji dla typów kart technologicznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TPKTLF';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTKT';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Warianty kalk. dla typów tech.'@+'=1,1';
_def.NAME:='Warianty kalkulacji dla typów kart tech.'@;
_def.DESC:='Przypisanie wariantów kalkulacji do typów kart technologicznych'@;

_def.PREFIX:="TPKTLF.index('TO'); TPKTLF.prefix()";
_def.TABLE:="TPKTLF";
_def.FIELDS:="exec('fields','xls_tpktlf',_a)";

_def.BEFORE:="TPKTLF.cntx_psh()";
_def.AFTER:="TPKTLF.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_tpktlf',_a,_b)";
_def.VALIDATE:="exec('validate','xls_tpktlf',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_tpktlf',_a,_b,_c)";
_def.SELECT:="exec('select','xls_tpktlf',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TYP','Typ'@,1,,MS.comment(TPKTLF,'TYP')+' '+'(max. %1 znaków)'@[$MS.fld_len(TPKTL,'TYP')]);
_def.define('OPC','Wariant'@,1,,MS.comment(TPKTLF,'OPC')+' '+'(max. %1 znaków)'@[$MS.fld_len(KWAR,'NAZ')]);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TPKTLF.prefix();
{? _validate.ACTION='add'
|| TPKTLF.blank();
   TPKTLF.TYP:=_validate.OBJ.TPKTL;
   TPKTLF.OPC:=_validate.OBJ.KWAR
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.TYP.VALUE:=TPKTLF.TYP().TYP; _excel.write_async('TPKTL',TPKTLF.TYP);
_table.OPC.VALUE:=TPKTLF.OPC().NAZ; _excel.write_async('KWAR',TPKTLF.OPC);
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('TPKTL','KWAR');
_result.OBJ.TPKTL:=exec('FindInSet','#table','TPKTL','TYP',_obj.TYP,,"TPKTL.ref()",1);
_result.OBJ.KWAR:=exec('FindInSet','#table','KWAR','NA',_obj.OPC,,"KWAR.ref()",1);

:: Błędy
{? _result.OBJ.TPKTL=null()
|| _result.msg_norec('TYP');
   _result.RESULT:=0
?};
{? _result.OBJ.KWAR=null()
|| _result.msg_norec('OPC');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| TPKTLF.index('TO');
   TPKTLF.prefix(_result.OBJ.TPKTL,_result.OBJ.KWAR);
   {? TPKTLF.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_tpktlf',_obj,_mode,_result);
         {? exec('chk_tpktlf','tech_common',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_tpktlf',_obj,_mode,_result);
      {? exec('chk_tpktlf','tech_common',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TPKTLF.add(1)
   |? _validate.ACTION='put'
   || _result:=TPKTLF.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie wariantu %1 dla typu %2 nie powiodło się.'@[_obj.OPC,_obj.TYP]
   || _validate.MSG:='Poprawa wariantu %1 dla typu %2 nie powiodła się.'@[_obj.OPC,_obj.TYP]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'TYP','STRING[3]','Typ'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OPC','STRING[40]','Wariant');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'TYP',,);

TPKTLF.cntx_psh();
TPKTLF.index('TO');
TPKTLF.prefix();
{? TPKTLF.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$TPKTLF.ref();
      _selected.prefix($TPKTLF.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.TYP:=TPKTLF.TYP().TYP;
      _tab.OPC:=TPKTLF.OPC().NAZ;
      _tab.add();
      TPKTLF.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'TYP,OPC',20,'Wybór wariantów kalk. dla typów tech do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TPKTLF.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:51 7e3530d1ea1dc7974cc540d9ee92304daef2bb2c6b006998a2b0aa735e0a72512e3f1b9df5e900ae28a909655f546e0b828b230e22aa941450e323ab8a2224d2aae7ba5e3e536f5d68c662fbd06431bcdf1570530d82366c5f490e41b412f19e380720844907f91ba53d0894da71a89a7905a6c238cb06c118b993bf8d15739e

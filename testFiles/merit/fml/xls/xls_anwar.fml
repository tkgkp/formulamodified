:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_anwar.fml
:: Utworzony: 23.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu wariantów kalkulacji/analizy zleceń
::======================================================================================================================


\init_0
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - dla wariantów kalkulacji zleceń
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ANWAR0';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZLE';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Warianty kalkulacji zleceń'@+'=1,1';
_def.NAME:='Warianty kalkulacji zleceń'@;
_def.DESC:='Warianty kalkulacji zleceń'@;

_def.PREFIX:="ANWAR.index('NA'); ANWAR.prefix('0')";
_def.TABLE:="ANWAR";
_def.FIELDS:="exec('fields','xls_anwar',_a)";

_def.BEFORE:="ANWAR.cntx_psh()";
_def.AFTER:="ANWAR.cntx_pop()";
_def.SELECT:="exec('select','xls_anwar',_a,'0')";

_def.EXPORT:="exec('export','xls_anwar',_a,_b,'0')";
_def.VALIDATE:="exec('validate','xls_anwar',_a,_b,_c,'0')";
_def.IMPORT:="exec('import','xls_anwar',_a,_b,_c,'0')";
~~


\init_1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel - dla wariantów analiz zleceń
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='ANWAR1';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZLE';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Warianty analizy zleceń'@+'=1,1';
_def.NAME:='Warianty analizy zleceń'@;
_def.DESC:='Warianty analizy zleceń'@;

_def.PREFIX:="ANWAR.index('NA'); ANWAR.prefix('1')";
_def.TABLE:="ANWAR";
_def.FIELDS:="exec('fields','xls_anwar',_a)";

_def.BEFORE:="ANWAR.cntx_psh()";
_def.AFTER:="ANWAR.cntx_pop()";
_def.SELECT:="exec('select','xls_anwar',_a,'1')";

_def.EXPORT:="exec('export','xls_anwar',_a,_b,'1')";
_def.VALIDATE:="exec('validate','xls_anwar',_a,_b,_c,'1')";
_def.IMPORT:="exec('import','xls_anwar',_a,_b,_c,'1')";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NA','Nazwa'@,1,,MS.comment(ANWAR,'NA')+' '+'(max. %1 znaków)'@[$MS.fld_len(ANWAR,'NA')]);
_def.define('WST','Okresy wstecz'@,1,,MS.comment(ANWAR,'WST')+' '+'(liczba naturalna)'@,,0);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - '0'/'1' - kalkulacja/analiza
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_a01:=_d;

ANWAR.prefix();
{? _validate.ACTION='add'
|| ANWAR.blank();
   ANWAR.NA:=_obj.NA;
   ANWAR.A01:=_a01
?};
ANWAR.WST:=_obj.WST;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::       _c - '0'/'1' - kalkulacja/analiza
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;
_a01:=_c;

_result:=1;
_table.NA.VALUE:=ANWAR.NA;
_table.WST.VALUE:=ANWAR.WST;

:: Zapisanie formuł dla bieżącego wariantu
::AFORM.cntx_psh();
::AFORM.index('OR');
::AFORM.prefix(ANWAR.ref());
::{? AFORM.first()
::|| {!
::   |? _excel.write_async('AFORM'+_a01,AFORM.ref());
::      AFORM.next()
::   !}
::?};
::AFORM.cntx_pop();

:: Zapisanie stałych dla bieżącego wariantu
::ANKON.cntx_psh();
::ANKON.index('OR');
::ANKON.prefix(ANWAR.ref());
::{? ANKON.first()
::|| {!
::   |? _excel.write_async('ANKON'+_a01,ANKON.ref());
::      ANKON.next()
::   !}
::?};
::ANKON.cntx_pop();

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::       _d - '0'/'1' - kalkulacja/analiza
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;
_a01:=_d;

_result.RESULT:=1;

_int:=int(_obj.WST);

:: Błędy
{? _int<exec('minint','#math') | _int>exec('maxint','#math')
|| _result.msg_range('WST',exec('minint','#math'),exec('maxint','#math'));
   _result.RESULT:=0
?};

:: Ostrzeżenia
{? +_obj.NA>40
|| _result.msg_length('NA',40)
?};
{? _int<>_obj.WST
|| _result.msg_int('WST')
?};

:: OK
{? _result.RESULT>0
|| ANWAR.index('NA');
   ANWAR.prefix(_a01,_obj.NA,);
   {? ANWAR.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_anwar',_obj,_mode,_result,_a01);
         {? exec('chk_anwar','zl_kalk',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_anwar',_obj,_mode,_result,_a01);
      {? exec('chk_anwar','zl_kalk',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - '0'/'1' - kalkulacja/analiza
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
_a01:=_d;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=ANWAR.add(1)
   |? _validate.ACTION='put'
   || _result:=ANWAR.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || {? _a01='0'
      || _validate.MSG:='Dodanie wariantu kalkulacji \'%1\' nie powiodło się.'@[_obj.NAZ]
      || _validate.MSG:='Dodanie wariantu analizy \'%1\' nie powiodło się.'@[_obj.NAZ]
      ?}
   || {? _a01='0'
      || _validate.MSG:='Poprawa wariantu kalkulacji \'%1\' nie powiodła się.'@[_obj.NAZ]
      || _validate.MSG:='Poprawa wariantu analizy \'%1\' nie powiodła się.'@[_obj.NAZ]
      ?}
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;
_a01:=_b;

_result:=0;

_tab:=tab_tmp(1,
   'NA','STRING[40]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NA',,);

ANWAR.cntx_psh();
ANWAR.index('NA');
ANWAR.prefix(_a01);
{? ANWAR.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$ANWAR.ref();
      _selected.prefix($ANWAR.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NA:=ANWAR.NA;
      _tab.add();
      ANWAR.next()
   !}
?};
_hdr:={? _a01='0' || 'Wybór wariantów kalkulacji do eksportu'@ || 'Wybór wariantów analizy do eksportu'@ ?};
_result:=exec('select_action','#table',_tab,'NA',30,_hdr);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
ANWAR.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:46 4ca3aa4410f1fa1ec09e298d7b51b02e4c9905fdf4ff18bca2fd0ed98114f9f1148213d4650160ee7f4726fe18b2ca52e7ef7c687b90c22c326aea6cb71d7e551035faa524db1c21396a1366da616aa203dbf355e93fdae6134dcb3b9eacb018f5dd8a29fe14926616e0f41896d1aabfc7ad2c513579e6fd5a21ea9d125da231

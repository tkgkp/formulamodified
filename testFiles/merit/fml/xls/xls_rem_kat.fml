:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_rem_kat.fml
:: Utworzony: 31.01.2019
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi kategorii zasobów remontowych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='REM_KAT';
_def.DOMAIN:=exec('name','#b_domain','TRE');
_def.FUNPAR:='ZWS_PAR_TRKZ';

_def.FILE:='remonty.xlsx';
_def.SHEET:='Kategorie zasobów'@+'=1,1';
_def.NAME:='Kategorie zasobów'@;
_def.DESC:='Kategorie zasobów remontowych'@;

_def.PREFIX:="REM_KAT.index('SYMBOL'); REM_KAT.prefix()";
_def.TABLE:="REM_KAT";
_def.FIELDS:="exec('fields','xls_rem_kat',_a)";

_def.BEFORE:="REM_KAT.cntx_psh()";
_def.AFTER:="REM_KAT.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_rem_kat',_a,_b)";
_def.VALIDATE:="exec('validate','xls_rem_kat',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_rem_kat',_a,_b,_c)";
_def.SELECT:="exec('select','xls_rem_kat',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('SYMBOL','Symbol'@,1);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
REM_KAT.prefix();
{? _validate.ACTION='add'
|| REM_KAT.blank();
   REM_KAT.SYMBOL:=_obj.SYMBOL
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.SYMBOL.VALUE:=REM_KAT.SYMBOL;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

:: Ostrzeżenia
{? +_obj.SYMBOL>100
|| _result.msg_length('SYMBOL',100)
?};

:: OK
REM_KAT.index('SYMBOL');
REM_KAT.prefix(_obj.SYMBOL,);
{? ~REM_KAT.first()
|| _result.ACTION:='add';
   exec('record','xls_rem_kat',_obj,_mode,_result);
   {? exec('chk_rem_kat','zasoby',0)<>''
   || _result.RESULT:=0
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=REM_KAT.add(1);
      {? _result=0
      || _validate.MSG:='Dodanie kategorii zasobu remontowego %1 nie powiodło się.'@[_obj.SYMBOL]
      ?}
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1
   ,'SYMBOL','STRING[100]','Symbol'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'SYMBOL',,);

REM_KAT.cntx_psh();
REM_KAT.index('SYMBOL');
REM_KAT.prefix();
{? REM_KAT.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$REM_KAT.ref();
      _selected.prefix($REM_KAT.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.SYMBOL:=REM_KAT.SYMBOL;
      _tab.add();
      REM_KAT.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'SYMBOL',25,'Wybór kategorii zasobów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
REM_KAT.cntx_pop();

_result

:Sign Version 2.0 jowisz:1028 2019/10/14 09:22:01 1e1d1a2856087a3461cc42760a101ca20895ba3a13ff8d7cf571bd1171e72c3b975e70085d2df5953a6269953adabcaefa4adfe4fe4e39d7ada22c9826d8aa5c29c38c631ac881b7f641612cf58eb1e0f39f48502449b15be3ab663dda8f4de1f7e5c171a251dc08f84f90abc02638d151500ae6243bf8c7bb3dabb38dcce478

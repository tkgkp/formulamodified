:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kh_msc.fml
:: Utworzony: 26.09.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu rodzaje dostaw
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TR_ZEWN';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_LRTR';
_def.MULTIFIR:='T';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Rodzaje dostaw=1,1';
_def.NAME:='Rodzaje dostaw'@;
_def.DESC:='Rodzaje dostaw';

_def.PREFIX:="TR_ZEWN.index('KOD'); TR_ZEWN.prefix()";
_def.TABLE:="TR_ZEWN";
_def.FIELDS:="exec('fields','xls_tr_zewn',_a)";

_def.BEFORE:="TR_ZEWN.cntx_psh()";
_def.AFTER:="TR_ZEWN.cntx_pop()";

_def.EXPORT:="exec('export','xls_tr_zewn',_a,_b)";
_def.VALIDATE:="exec('validate','xls_tr_zewn',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_tr_zewn',_a,_b,_c)";
_def.SELECT:="exec('select','xls_tr_zewn',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('KOD','Kod',1,,'Kod');
_def.define('NAZ','Nazwa',1,,'Nazwa');
_def.define('WYB_MSC','Zdefiniowane miejsca dostaw',1,,'Czy obsługa miejsc dostaw? (1/0)');
_def.define('API','WebAppi',1,,'Czy obsługa WebAppi? (T/N)');
_def.define('COURIER','Kod kuriera',1,,'Kod kuriera do 30 znaków');
_def.define('MULTI','Kilka paczek',1,,'Czy obsługa kilku paczek? (1/0)');
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.KOD.VALUE:=TR_ZEWN.KOD;
_table.NAZ.VALUE:=TR_ZEWN.NAZ;
_table.WYB_MSC.VALUE:=TR_ZEWN.WYB_MSC;
_table.API.VALUE:=TR_ZEWN.API;
_table.COURIER.VALUE:=TR_ZEWN.COURIER;
_table.MULTI.VALUE:=TR_ZEWN.MULTI;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

:: wychwyć błędy krytyczne
_result.RESULT:=1;

{? (+form(_obj.KOD))=0      || _result.msg_empty('KOD'); _result.RESULT:=0
|? _obj.WYB_MSC<>0
 & _obj.WYB_MSC<>1          || _result.msg_inset('WYB_MSC','1','0'); _result.RESULT:=0
|? (+form(_obj.API))<>1     || _result.msg_empty('API'); _result.RESULT:=0
|? ~((';NT'*_obj.API)>1)    || _result.msg_inset('API','T','N'); _result.RESULT:=0
|? _obj.MULTI<>0
 & _obj.MULTI<>1            || _result.msg_empty('MULTI','1','0'); _result.RESULT:=0
?};

{? _result.RESULT=1
||
   TR_ZEWN.index('KOD');
   TR_ZEWN.prefix(_obj.KOD,);
   {? TR_ZEWN.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put'
      ?}
   || _result.ACTION:='add'
   ?};
   {? _result.RESULT=1
   || {? _result.ACTION='add' || TR_ZEWN.blank() ?};

      TR_ZEWN.KOD:=_obj.KOD;
      TR_ZEWN.NAZ:=_obj.NAZ;
      TR_ZEWN.WYB_MSC:=_obj.WYB_MSC;
      TR_ZEWN.API:=_obj.API;
      TR_ZEWN.COURIER:=_obj.COURIER;
      TR_ZEWN.MULTI:=_obj.MULTI
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TR_ZEWN.add(1)
   |? _validate.ACTION='put'
   || _result:=TR_ZEWN.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie rodzaju dostawy nie powiodło się.'@
   || _validate.MSG:='Modyfikacja rodzaju dostawy nie powiodła się.'@
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'KOD','STRING[30]','Kod'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'NAZ','STRING[60]','Nazwa');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'KOD',,);

TR_ZEWN.cntx_psh();
TR_ZEWN.index('KOD');
TR_ZEWN.prefix();
{? TR_ZEWN.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$TR_ZEWN.ref();
      _selected.prefix($TR_ZEWN.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.KOD:=TR_ZEWN.KOD;
      _tab.NAZ:=TR_ZEWN.NAZ;
      _tab.add();
      TR_ZEWN.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'KOD,NAZ',20,'Wybór rodzajów dostaw do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TR_ZEWN.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 89de3482d7ce6be4dffb8b6527f6d86767e73821398d941849acf1d7260263a71ac42fd9bfa6b13503ddfb751a0fa9f91473ad976084e3d21ceadd033cf68a820104d849bde516d4066afaa763598e4b1f4947de3b06581b73a6e7d678d8f40174f2a988b031bcc5ea89f963e5b29a03f08305f92aa5521fdae69ad810559d09

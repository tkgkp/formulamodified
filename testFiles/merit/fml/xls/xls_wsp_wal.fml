:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_wsp_wal.fml
:: Utworzony: 11.01.2019
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu współczynników waloryzacji
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='WSP_WAL';
_def.FUNPAR:='ZWS_PAR_PPPL';
_def.DOMAIN:=exec('name','#b_domain','PPL');
_def.FILE:='personel_stale.xlsx';
_def.SHEET:='Współczynniki waloryzacji=1,1';
_def.NAME:='Współczynniki waloryzacji'@;
_def.DESC:='Współczynniki waloryzacji';
_def.HIDDEN:='N';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="WSP_WAL";

: prolog
_def.BEFORE:="
   WSP_WAL.cntx_psh();
   WSP_WAL.f_clear();
   WSP_WAL.index('WSP_WAL');
   WSP_WAL.prefix()
";

: epilog
_def.AFTER:="
   WSP_WAL.cntx_pop()
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('ROK',    MS.name(WSP_WAL,'ROK'), 1,,   MS.comment(WSP_WAL,'ROK'),,0);
   _env.define('KWARTAL',MS.name(WSP_WAL,'KWARTAL'),1,,MS.comment(WSP_WAL,'KWARTAL'),,0);
   _env.define('WSP',    MS.name(WSP_WAL,'WSP'),1,,    MS.comment(WSP_WAL,'WSP'),,2);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.ROK.VALUE:=WSP_WAL.ROK;
   _buf.KWARTAL.VALUE:=WSP_WAL.KWARTAL;
   _buf.WSP.VALUE:=WSP_WAL.WSP;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_wsp_wal',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_wsp_wal',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.ROK=0     || 'ROK'
   |? _buf.KWARTAL=0 || 'KWARTAL'
   |? _buf.WSP=0     || 'WSP'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? _buf.ROK<0
|| _val.msg_range('ROK',0);
   _val.RESULT:=0;
   return()

|? _buf.KWARTAL<0 | _buf.KWARTAL>4
|| _val.msg_range('PR',1,4);
   _val.RESULT:=0;
   return()

|? _buf.WSP<0
|| _val.msg_range('WSP',0);
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? WSP_WAL.find_tab(,'ROK',,'=',_buf.ROK,'KWARTAL',,'=',_buf.KWARTAL)
|| _ref:=WSP_WAL.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1, %2'[$_a.ROK,$_a.KWARTAL]"
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,WSP_WAL,"
   _a.ROK:=_b.ROK;
   _a.KWARTAL:=_b.KWARTAL;
   _a.WSP:=_b.WSP;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 c6e1b501f3c9cd0f3e217dc714fd74589b225694d1b6adee28b0f9f39087e3ab7f1852012ee9b1edd35fb3e6048534f761f12d0c7277b422a2854f1b1f5a36408dfdddfe38c704aea5ef9573a61bc68a941734f3571e45819c73b4ee1a04b3944a2f741ceabf62864634057d7055105196dd829b0647941c6090f8f6dff1de99

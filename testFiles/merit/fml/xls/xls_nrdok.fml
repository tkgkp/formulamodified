:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_nrdok.fml
:: Utworzony: 12.07.2018
:: Autor: Mario
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu numeracji dokumentów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='NRDOK';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_PAR_DOK_NUM';

_def.FILE:='parametryzacja.xlsx';
_def.SHEET:='Numeracje dokumentów=1,1';
_def.NAME:='Numeracje dokumentów'@;
_def.DESC:='Numeracje dokumentów';

_def.PREFIX:="exec('prefix','xls_nrdok')";
_def.TABLE:="NRDOK";
_def.FIELDS:="exec('fields','xls_nrdok',_a)";

_def.BEFORE:="NRDOK.cntx_psh()";
_def.AFTER:="NRDOK.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_nrdok',_a,_b)";
_def.VALIDATE:="exec('validate','xls_nrdok',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_nrdok',_a,_b,_c)";
_def.SELECT:="exec('select','xls_nrdok',_a)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
NRDOK.index('NRDOK');
NRDOK.prefix();
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TYP','Symbol numeracji',1,,'3 znakowy unikalny symbol');
_def.define('OP','Opis',1,,'40 znakowy opis');
_def.define('AB','Akronim numeracji',1,,'8 znakowy akronim tabeli dla której definiujemy numerację');
_def.define('WOL_NR','Czy wykorzystywać wolne nr?',1,,'Wartości T/N');
_def.define('MAXN','O ile numerów',1,,'O ile numerów można zwiększyć numerację');
_def.define('P1','Pole klucza numeracji',1,,'');
_def.define('P2','Pole klucza numeracji',1,,'');
_def.define('P3','Pole klucza numeracji',1,,'');
_def.define('P4','Pole klucza numeracji',1,,'');
_def.define('P5','Pole klucza numeracji',1,,'');
_def.define('P6','Pole klucza numeracji',1,,'');
_def.define('F','Formuła symbol dokumenu',1,,'');
_def.define('FT','Formuła symbolu tymcz.',1,,'Formuła symbolu tymczasowego dokumentu');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

NRDOK.prefix();
{? _validate.ACTION='add'
||
   NRDOK.blank();
   NRDOK.TYP:=_obj.TYP
?};

NRDOK.OP:=_obj.OP;
NRDOK.AB:=_obj.AB;
NRDOK.F:=_obj.F;
NRDOK.MAXN:=_obj.MAXN;
NRDOK.P1:=_obj.P1;
NRDOK.P2:=_obj.P2;
NRDOK.P3:=_obj.P3;
NRDOK.P4:=_obj.P4;
NRDOK.P5:=_obj.P5;
NRDOK.P6:=_obj.P6;
NRDOK.FT:=_obj.FT;
NRDOK.WOL_NR:=_obj.WOL_NR;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.TYP.VALUE:=NRDOK.TYP;
_table.OP.VALUE:=NRDOK.OP;
_table.AB.VALUE:=NRDOK.AB;
_table.F.VALUE:=NRDOK.F;
_table.MAXN.VALUE:=NRDOK.MAXN;
_table.P1.VALUE:=NRDOK.P1;
_table.P2.VALUE:=NRDOK.P2;
_table.P3.VALUE:=NRDOK.P3;
_table.P4.VALUE:=NRDOK.P4;
_table.P5.VALUE:=NRDOK.P5;
_table.P6.VALUE:=NRDOK.P6;
_table.FT.VALUE:=NRDOK.FT;
_table.WOL_NR.VALUE:=NRDOK.WOL_NR;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

NRDOK.index('NRDOK');
NRDOK.prefix(_obj.TYP,);
{? NRDOK.first()
||
   {? _mode=0
   || _result.RESULT:=0
   || _result.ACTION:='put';
      _moz:=1;
      {? NRDOK.AB<>_obj.AB | NRDOK.P1<>_obj.P1 | NRDOK.P2<>_obj.P2 | NRDOK.P3<>_obj.P3
         | NRDOK.P4<>_obj.P4 | NRDOK.P5<>_obj.P5 | NRDOK.P6<>_obj.P6
      || _moz:=~exec('nrtyp_uzywany','numery',NRDOK.TYP);
         {? ~_moz
         || _result.msg_update();
            _result.MSG+=' Numeracja %1 jest używana, nie można zmienić akronimu ani klucza.'@[NRDOK.TYP]
         ?}
      ?};
      {? _moz
      || exec('record','xls_nrdok',_obj,_mode,_result);
         {? exec('chk_nrdo','numery','put')<>''
         || _result.RESULT:=0
         ?}
      || _result.RESULT:=0
      ?}
   ?}
||
   _result.ACTION:='add';
   exec('record','xls_nrdok',_obj,_mode,_result);
   {? exec('chk_nrdo','numery','add')<>''
   || _result.RESULT:=0
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
||
   {? _validate.ACTION='add'
   ||
      _result:=NRDOK.add(1)
   |? _validate.ACTION='put'
   ||
      _result:=NRDOK.put(1)
   ?}
?};
{? _result=0
||

   {? _validate.ACTION='add'
   ||
      _validate.MSG:='Dodanie numeracji o kodzie %1 nie powiodło się.'@[_obj.TYP]
   ||
      _validate.MSG:='Poprawa numeracji o kodzie %1 nie powiodło się.'@[_obj.TYP]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'TYP','STRING[3]','Symbol numeracji'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'OP','STRING[40]','Opis');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'TYP',,);

NRDOK.cntx_psh();
NRDOK.index('NRDOK');
NRDOK.prefix();
{? NRDOK.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$NRDOK.ref();
      _selected.prefix($NRDOK.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.TYP:=NRDOK.TYP;
      _tab.OP:=NRDOK.OP;
      _tab.add();
      NRDOK.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'TYP,OP',20,'Wybór numeracji dokumentów do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
NRDOK.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2020/10/16 15:26:06 832e6090688e28db7307f4bafec43662bdff0e1c4e17855fdf9397b84006af86ca38a38e7692e67f4d0ba258f8df24a3cb660085dc24ba0441ca6d3f1c286a7d9c47a07df69f2949caf69baf9810281d8a5213d7cdab4aefc7b3f2772cdb4063fa3c70f557239602863ea03e28797d684d694b936292133d33b4dfd4fa1f7d23

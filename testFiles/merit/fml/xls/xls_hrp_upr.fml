:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_hrp_upr.fml
:: Utworzony: 16.09.2019
:: Autor: KN
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu i importu uprawnień do danych - tabela UPR
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel uprawnień do rodzajów przelewów
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

_def:=_a;
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FUNPAR:='ZWS_USRUPR';
_def.ID:='UPR_HRP';

_def.FILE:='uprawnienia.xlsx';
_def.SHEET:='Rodzaje przelewów=1,1';
_def.NAME:='Uprawnienia do rodzajów przelewów'@;
_def.DESC:='Uprawnienia do rodzajów przelewów';

_def.TABLE:="exec('table','xls_hrp_upr')";
_def.FIELDS:="exec('fields','xls_hrp_upr',_a)";

_def.TAB_IMP:="UPR";

_def.BEFORE:="UPR.cntx_psh()";
_def.AFTER:="UPR.cntx_pop()";

_def.PREFIX:="exec('prefix','xls_hrp_upr')";
_def.EXPORT:="exec('export','xls_hrp_upr',_a,_b)";

_def.VALIDATE:="exec('validate','xls_hrp_upr',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_hrp_upr',_a,_b,_c)";

_def.SELECT:="exec('select','xls_hrp_upr',_a,_b)";

exec('SelUsersTab','xls_users_up');
exec('tzl','dane_startowe');

~~


\TabRpUpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Tymczasowa tabela uprawnień do rodzajów przelewów
::   WY: uchwyt tabeli
::----------------------------------------------------------------------------------------------------------------------

_wyn:=tab_tmp(5
   ,'USER_KOD','STRING[10]','User kod'
   ,'USER_REF','STRING[16]','User ref'
   ,'RP_REF','STRING[16]','Ref rodzaju przelewu'
   ,'RP_KOD','STRING[20]','Kod rodzaju przelewu'
   ,'RP_OPIS','STRING[50]','Opis rodzaju przelewu'
   ,'ACCESS','STRING[1]','Uprawnienie'
   ,'PELNE','STRING[1]','Czy pelne uprawnienia'
   ,'UIDREF','STRING[100]','Identyfikator rekordu'
   );

_add:="_wyn:=params_get().wyn;
       _wyn.blank();
       _wyn.USER_REF:=_a;
       _wyn.USER_KOD:=_b;
       _wyn.RP_REF:=_c;
       _wyn.RP_KOD:=_d;
       _wyn.RP_OPIS:=_e;
       _wyn.ACCESS:=_f;
       _wyn.PELNE:=_g;
       _wyn.UIDREF:=_h;
       _wyn.add(1)
      ";

USERS.cntx_psh();
TZL.cntx_psh();
UPR.cntx_psh();

USERS.f_clear();
USERS.index('USR_AKOD');
USERS.prefix('T');

UPR.index('REFKOD');
TZL.index('KOD');
TZL.prefix();

{? USERS.first()
|| {!
   |? _user_ref:=USERS.ref();
      _user_kod:=USERS.KOD;
      _moz_dostep:=exec('user_upraw_dostep','xls_hrp_upr','HRP',_user_ref);
      {? _moz_dostep
      || UPR.prefix(_user_ref);
         {? TZL.first()
         || {!
            |? {? pelne_upr='T'
               || params_set('wyn',_wyn);
                  (_add)($_user_ref,_user_kod,'','','','T','T',USERS.uidref()+TZL.uidref());
                  TZL.last()
               || _access:={? _moz_dostep & UPR.find_key(TZL.KOD) || 'T' || 'N' ?};
                  params_set('wyn',_wyn);
                  (_add)($_user_ref,_user_kod,$TZL.ref(),TZL.KOD,TZL.OPIS,_access,'N',USERS.uidref()+TZL.uidref())
               ?};
               TZL.next()
            !}
         ?}
      ?};
      USERS.next()
   !}
?};
::_wyn.win_sel(_wyn.mk_sel(,,1));
::_wyn.select();

USERS.cntx_pop();
TZL.cntx_pop();
UPR.cntx_pop();

_wyn


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

exec('TabRpUpr','xls_hrp_upr')


\user_upraw_dostep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Sprawdza w tabeli B_PERM_U czy dany użytkownik może mieć dany rodzaj uprawnienia
::   WE: _a - rodzaj uprawnienia (HRP)
::       _b - ref użytkownika
::   WY: 1 - użytkownik może mieć uprawnienie
::       0 - użytkownik nie może mieć uprawnienia
::----------------------------------------------------------------------------------------------------------------------

{? var_pres('_a')<>type_of('') | (_a<>'HRP')
|| _a:='ODDZ'
?};
{? var_pres('_b')<>type_of(null()) | _b=null()
|| return(0)
?};

_rodz:=_a;
_user_ref:=_b;
_bperm_ref:=exec('bperm_ref','xls_hrp_upr',_rodz);

B_PERM_U.cntx_psh();
B_PERM_U.index('USER');
B_PERM_U.prefix(REF.FIRMA,_user_ref,_bperm_ref);
_dostep:=(B_PERM_U.first());
pelne_upr:=B_PERM_U.FULL;
B_PERM_U.cntx_pop();

_dostep


\bperm_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Zwraca ref B_PERM dla danego rodzaju uprawnienia
::   WE: _a - rodzaj uprawnienia (HRP)
::   WY: ref B_PERM
::----------------------------------------------------------------------------------------------------------------------

{? var_pres('_a')<>type_of('') | (_a<>'HRP')
|| _a:='ODDZ'
?};

_rodz:=_a;

_bperm_ref:=null();
B_PERM.cntx_psh();
B_PERM.index('NAME');
B_PERM.prefix(_rodz);
{? B_PERM.first()
|| _bperm_ref:=B_PERM.ref()
?};
B_PERM.cntx_pop();

_bperm_ref


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

__SelUsers.prefix('HRP');
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

_def:=_a;

_tooltip:=MS.name(UPR,'RR')+' '+'(max. %1 znaków). '@[$MS.fld_len(USERS,'KOD')];
_tooltip+='Pole wymagane'@;
_def.define('USER','Użytkownik'@,1,,_tooltip);

_tooltip:=MS.name(UPR,'TP')+' '+'(max. %1 znaków). '@[$MS.fld_len(TZL,'OPIS')];
_def.define('RP','Opis rodzaju przelewu'@,1,,_tooltip);

_tooltip:='Uprawniony (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('DOSTEP','Czy uprawniony?'@,1,,_tooltip);

_tooltip:='Pełne uprawnienia (max. 1 znak). '@;
_tooltip+='Dozwolone wartości: T/N'@;
_def.define('PELNE','Czy pełne uprawnienia?'@,1,,_tooltip);

~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------

_excel:=_a;
_obj:=_b;
_tab:=_excel.table();

{? __SelUsers.first()
|| _result:=__SelUsers.find_key(_tab.USER_REF)
|| _result:=1
?};

{? _result
|| _obj.USER.VALUE:=_tab.USER_KOD;
   _obj.RP.VALUE:=_tab.RP_OPIS;
   _obj.DOSTEP.VALUE:=_tab.ACCESS;
   _obj.PELNE.VALUE:=_tab.PELNE
?};

_result


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------

_def:=_a;
_obj:=_a;
_mode:=_b;
_validate:=_c;

UPR.prefix();
{? _validate.ACTION='add'
|| UPR.blank(1)
?};
UPR.RR:=_validate.OBJ.USER;
UPR.TP:=_validate.OBJ.RP;

~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - obj_new - środowisko mechanizmu
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

_selected:=_a;
_excel:=_b;

exec('select','xls_users_up',_selected,_excel,'HRP')


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER   - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obj_new() - obiekt z resultem, wynik działania exec('args_valid','#excel_imex').
::                        Obiekt ten jest przekazywany do formuły na IMPORT. Pole RESULT decyduje czy formuła
::                        na import się wykona. Pole MSG służy to odpisania komunikatu
::----------------------------------------------------------------------------------------------------------------------

_obj:=_a;
_mode:=_b;
_result:=_c;

_can_continue:=1;
:: Walidacja wartości z Excel, czy są w nim pola których się spodziewam i czy mają dobre wartości
{? _obj.USER='' & _can_continue
|| _can_continue:=0;
   _result.msg_empty('USER')
?};
{? _obj.DOSTEP='' & _can_continue
|| _can_continue:=0;
   _result.msg_empty('DOSTEP')
?};
{? _obj.PELNE='' & _can_continue
|| _can_continue:=0;
   _result.msg_empty('PELNE')
?};

_user:=null();
{? _can_continue
|| _user:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1);
   {? _user=null()
   || _can_continue:=0;
      _result.msg_norec('USER')
   ?}
?};

_rp:=null();
{? _can_continue
|| TZL.index('TZL_OPIS');
   TZL.prefix(_obj.RP);
   {? TZL.first() || _rp:=TZL.ref() ?};
   {? _rp=null()
   || _can_continue:=0;
      _result.msg_norec('RP')
   ?}
?};

{? _obj.DOSTEP<>'T' & _obj.DOSTEP<>'N' & _can_continue
|| _can_continue:=0;
   _result.msg_inset('DOSTEP','T','N')
?};

{? _obj.PELNE<>'T' & _obj.PELNE<>'N' & _can_continue
|| _can_continue:=0;
   _result.msg_inset('PELNE','T','N')
?};

{? _can_continue & _obj.DOSTEP='T'
|| {? ~exec('user_upraw_dostep','xls_hrp_upr','HRP',_user)
   || _can_continue:=0;
      _result.msg_value('DOSTEP','Użytkownik nie może mieć uprawnienia, nie ma odpowiedniej roli.'@)
   ?}
?};

_result.RESULT:=_can_continue;

:: OK

{? _result.RESULT & _obj.PELNE='T' & _mode=1
|| UPR.index('REFTYP');
   UPR.prefix(_user);
   _bperm_ref:=exec('bperm_ref','xls_hrp_upr','HRP');
   B_PERM_U.index('USER');
   B_PERM_U.prefix(REF.FIRMA,_user, _bperm_ref);
   {? UPR.first() | (B_PERM_U.first(); B_PERM_U.FULL='N')
   || _result.ACTION:='pdel';
      _result.OBJ:=obj_new('USER','PELNE');
      _result.OBJ.USER:=_user;
      _result.OBJ.PELNE:='T'
   ?}
?};

{? _result.RESULT & _obj.PELNE='N' & (_mode=1 | _obj.DOSTEP='T')
|| UPR.index('REFTYP');
   UPR.prefix(_user,_obj.RP);
   _bperm_ref:=exec('bperm_ref','xls_hrp_upr','HRP');
   B_PERM_U.index('USER');
   B_PERM_U.prefix(REF.FIRMA,_user, _bperm_ref);
   {? UPR.first()
   || {? _obj.DOSTEP='N'
      || _result.ACTION:='del'
      ?}
   || {? _obj.DOSTEP='T'
      || _result.ACTION:='add';
         _result.OBJ:=obj_new('USER','RP');
         _result.OBJ.USER:=_user;
         _result.OBJ.RP:=_rp;
         exec('record','xls_hrp_upr',_obj,_mode,_result)
      ?}
   ?}
?};

~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------

_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| _user:=exec('FindInSet','#table','USERS','USR_KKOD',_obj.USER,,,1);
   _bperm_ref:=exec('bperm_ref','xls_hrp_upr','HRP');
   B_PERM_U.index('USER');
   B_PERM_U.prefix(REF.FIRMA, _user, _bperm_ref);
   {? _validate.ACTION='add'
   || {? B_PERM_U.first()
      || {? B_PERM_U.FULL='T' | B_PERM_U.ACCESS='N'
         || B_PERM_U.FULL:='N';
            B_PERM_U.ACCESS:='T';
            B_PERM_U.put()
         ?};
         {? UPR.add(1) & B_PERM_U.FULL='N' & B_PERM_U.ACCESS='T'
         || _result:=1
         ?}
      ?}
   |? _validate.ACTION='pdel'
   || {? UPR.first()
      || {! |? UPR.del() !}
      ?};
      {? B_PERM_U.first() & (B_PERM_U.FULL='N' | B_PERM_U.ACCESS='N')
       || B_PERM_U.FULL:='T';
          B_PERM_U.ACCESS:='T';
          B_PERM_U.put()
      ?};
      {? ~UPR.first() & B_PERM_U.FULL='T' & B_PERM_U.ACCESS='T' || _result:=1 ?}
   |? _validate.ACTION='del'
   || {? B_PERM_U.first()
      || {? B_PERM_U.FULL='T'
         || B_PERM_U.FULL:='N';
            B_PERM_U.put()
         ?};
         {? B_PERM_U.FULL='N' & UPR.count()=0 & UPR.del(1,1)<>0
         || _result:=1
         ?}
      ?}
   ?}
?};

{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie uprawnienia użytkownika %1 do rodzaju przelewu %2 nie powiodło się.'@
                        [_obj.USER,_obj.RP]

   |? _validate.ACTION='del'
   || _validate.MSG:='Usunięcie uprawnienia użytkownika %1 do rodzaju przelewu %2 nie powiodło się.'@
                        [_obj.USER,_obj.RP]
   |? _validate.ACTION='pdel'
   || _validate.MSG:='Nadanie pełnych uprawnień do rodzajów przelewów użytkownikowi %1 nie powiodło się.'@
                        [_obj.USER]
   ?}
?};

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 30fac7f34e130eea25ecf0122d1ed7bcd6199ee94bb305976b92f9e27203f8f8d78e1e1f8ff3df70997a2d118d5dc0bdf9f2d28cb4c54a5725dd69799fe82a96d7a987f2ffe23e01ac3fdbcd329e2cbcb27a49a74340c12a0908b0f4256a4ceb7f7e6466f2f838caee500d0da53fb5045c2a65a85b8c8013c08d7fd8bc14ef98

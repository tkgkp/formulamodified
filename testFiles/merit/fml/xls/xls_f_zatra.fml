:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_f_zatra.fml
:: Utworzony: 09.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu czynności dla form współpracy
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='F_ZATRA';
_def.FUNPAR:='ZWS_UPR_RCFZ';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:=exec('def_per_dict_xlsx','xls__init');
_def.SHEET:='Czynności dla form współpracy=1,1';
_def.NAME:='Czynności dla form współpracy'@;
_def.DESC:='Czynności dla form współpracy';

: źródło danych
_def.TABLE:="F_ZATRA";

: prolog
_def.BEFORE:="
   B_ACTION.cntx_psh();
   B_ACTION.prefix();
   F_ZATR.cntx_psh();
   F_ZATR.prefix();
   F_ZATRA.cntx_psh();
   F_ZATRA.f_clear();
   F_ZATRA.index('ACT_SYM');
   F_ZATRA.prefix();
   ~~
";

: epilog
_def.AFTER:="
   F_ZATRA.cntx_pop();
   F_ZATR.cntx_pop();
   B_ACTION.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('F_ZATR',  MS.name(F_ZATRA,'F_ZATR'),  0,,MS.comment(F_ZATRA,'F_ZATR'));
   _env.define('B_ACTION',MS.name(F_ZATRA,'B_ACTION'),0,,MS.comment(F_ZATRA,'B_ACTION'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.F_ZATR.VALUE:=F_ZATRA.F_ZATR().KOD;
   _buf.B_ACTION.VALUE:=F_ZATRA.B_ACTION().UID;
   _env.write_async('F_ZATR',F_ZATRA.F_ZATR);
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_f_zatra',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_f_zatra',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.F_ZATR=''   || 'F_ZATR'
   |? _buf.B_ACTION='' || 'B_ACTION'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(F_ZATR,'KOD');
{? +_buf.F_ZATR>_max
|| _val.msg_length('F_ZATR',_max);
   _buf.F_ZATR:=_max+_buf.F_ZATR
?};
_max:=MS.fld_len(B_ACTION,'UID');
{? +_buf.B_ACTION>_max
|| _val.msg_length('B_ACTION',_max);
   _buf.B_ACTION:=_max+_buf.B_ACTION
?};

: weryfikacja spójności referencyjnej
{? ~F_ZATR.find_tab(,'KOD',,'=',_buf.F_ZATR)
|| _val.msg_norec('F_ZATR');
   _val.RESULT:=0;
   return()
|| _f_zatr:=F_ZATR.ref()
?};
{? ~B_ACTION.find_tab(,'UID',,'=',_buf.B_ACTION)
|| _val.msg_norec('B_ACTION');
   _val.RESULT:=0;
   return()
|| _action:=B_ACTION.ref()
?};

_ref:=null;
{? F_ZATRA.find_tab(,'F_ZATR',,'=',_f_zatr,'B_ACTION',,'=',_action)
|| _ref:=F_ZATRA.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('F_ZATRA','F_ZATR','B_ACTION','to_str');
_obj.to_str:="'%1 - %2'[_a.F_ZATR,_a.B_ACTION]";
_obj.F_ZATRA:=_ref;
_obj.F_ZATR:=_f_zatr;
_obj.B_ACTION:=_action;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.F_ZATR:=_b.F_ZATR;
   _a.B_ACTION:=_b.B_ACTION;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? F_ZATRA.seek(_obj.F_ZATRA)
   || _set(F_ZATRA,_obj);
      {? ~F_ZATRA.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,F_ZATRA.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| F_ZATRA.blank();
   _set(F_ZATRA,_obj);
   {? ~F_ZATRA.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:47 53088c88ccb6e094bb47c523243158f0055f860e0b88114116079e56468a73da7befa3c9cda7c396d615aeb5ff2a8a2d0c6f20373e92f08a6e6e8200f32197a9d5eee9641e15fdaddb9640e4e2e277c2b0ba2a5cec347d15e01f11176b9253b76c0c113beadb74ad4a58f62a5170ec8d3e4270ddc63faa96a794d123bbdbb8f2

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kst_url.fml
:: Utworzony: 09.08.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu wymiarów urlopów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.ID:='KST_URL';
_def.FUNPAR:='ZWS_PAR_PWUR';
_def.DOMAIN:=exec('name','#b_domain','PKD');
_def.FILE:='personel_stale.xlsx';
_def.SHEET:='Wymiary urlopów=1,1';
_def.NAME:='Wymiary urlopów'@;
_def.DESC:='Wymiary urlopów';

: źródło danych
_def.TABLE:="KST_URL";

: prolog
_def.BEFORE:="
   KST_URL.cntx_psh();
   KST_URL.f_clear();
   KST_URL.index('KST_URL');
   KST_URL.prefix()
";

: epilog
_def.AFTER:="
   KST_URL.cntx_pop()
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('ROK',  MS.name(KST_URL,'ROK'),  1,,MS.comment(KST_URL,'ROK'),,0);
   _env.define('STAZ', MS.name(KST_URL,'STAZ'), 1,,MS.comment(KST_URL,'STAZ'),,0);
   _env.define('LIMIT',MS.name(KST_URL,'LIMIT'),1,,MS.comment(KST_URL,'LIMIT'),,0);
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.ROK.VALUE:=KST_URL.ROK;
   _buf.STAZ.VALUE:=KST_URL.STAZ;
   _buf.LIMIT.VALUE:=KST_URL.LIMIT;
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_kst_url',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kst_url',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.ROK=0 || 'ROK'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: błędne wartości
{? _buf.STAZ<0
|| _val.msg_range('STAZ',0);
   _val.RESULT:=0;
   return()

|? _buf.LIMIT<0
|| _val.msg_range('LIMIT',0);
   _val.RESULT:=0;
   return()
?};

_ref:=null;
{? KST_URL.find_tab(,'ROK',,'=',_buf.ROK,'STAZ',,'=',_buf.STAZ)
|| _ref:=KST_URL.ref()
?};

: optymalizacja importu i standaryzacja kodu
_obj:=_val.OBJ:=obj_new('KST_URL','to_str');
_obj.to_str:="'%1: %2 - %3'[$_a.ROK,$_a.STAZ,$_a.LIMIT]";
_obj.KST_URL:=_ref;

{? _ref=null
|| _val.ACTION:='add'
|| _val.ACTION:='put'
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;
_obj:=_c.OBJ;

_set:="
   _a.ROK:=_b.ROK;
   _a.STAZ:=_b.STAZ;
   _a.LIMIT:=_b.LIMIT;
   ~~
";

{? _val.ACTION='put' & _mod
:  włączono tryb nadpisywania
|| {? KST_URL.seek(_obj.KST_URL)
   || _set(KST_URL,_buf);
      {? ~KST_URL.put(1)
      || _val.msg_update(_obj.to_str(_buf));
         _val.RESULT:=0
      ?}
   || _val.msg_norec(,KST_URL.comment());
      _val.RESULT:=0
   ?}

|? _val.ACTION='add'
:  brak ograniczeń
|| KST_URL.blank();
   _set(KST_URL,_buf);
   {? ~KST_URL.add(1)
   || _val.msg_insert(_obj.to_str(_buf));
      _val.RESULT:=0
   ?}
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:48 807657de762fe9984f0e43a294bbe2c67e7a7636e9da1deef25dbbb89d7382c96f5bb63153516a4ddaf05852e726c2ebeb8d299ff64ece20ca47c364f38e47563f7f19c41673db1f78d95bd20ed96e94600cce89c77c263e246a62ed19c82439250ccdcc22ebde765773ba4f06756fa22a3b6c6131ef75fd76e0729bf5c8e227

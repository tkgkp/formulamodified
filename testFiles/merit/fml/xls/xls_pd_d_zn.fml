:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_pd_d_zn.fml
:: Utworzony: 25.01.2021
:: Autor: AKUL
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu nagłówków zestawów formuł planu dostaw
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='PD_D_ZN';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PLAN_DST';

_def.FILE:='plan_dostaw.xlsx';
_def.SHEET:='Nagłówki zestawów formuł=1,1';
_def.NAME:='Nagłówki zestawów formuł'@;
_def.DESC:='Nagłówki zestawów formuł';
_def.TABLE:="PD_D_ZN";
_def.FIELDS:="exec('fields','xls_pd_d_zn',_a)";
_def.EXPORT:="exec('export','xls_pd_d_zn',_a,_b)";
_def.IMPORT:="exec('import','xls_pd_d_zn',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_pd_d_zn',_a,_b,_c)";
_def.SELECT:="exec('select','xls_pd_d_zn',_a)";
_def.PREFIX:="PD_D_ZN.index('NAZ'); PD_D_ZN.prefix()";
_def.BEFORE:="PD_D_ZN.cntx_psh()";
_def.AFTER:="PD_D_ZN.cntx_pop()";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NAZ','Nazwa',0);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
PD_D_ZN.prefix();
{? _validate.ACTION='add'
|| PD_D_ZN.blank();
   PD_D_ZN.NAZ:=_obj.NAZ
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;

_table.NAZ.VALUE:=PD_D_ZN.NAZ;

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? _result.RESULT=1
||  PD_D_ZN.index('NAZ');
   PD_D_ZN.prefix(_obj.NAZ,);
   {? PD_D_ZN.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_pd_d_zn',_obj,_mode,_result);
         {? __CHK.record(PD_D_ZN,,'NAZ')<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_pd_d_zn',_obj,_mode,_result);
      {? __CHK.record(PD_D_ZN,,'NAZ')<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
_result


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=PD_D_ZN.add(1)
   |? _validate.ACTION='put'
   || _result:=PD_D_ZN.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || FUN.info('Dodanie nagłówka zestawu formuł o nazwie %1 nie powiodło się.'@[_obj.NAZ])
   || FUN.info('Poprawa nagłówka zestawu formuł o nazwie %1 nie powiodła się.'@[_obj.NAZ])
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'NAZ','STRING[10]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZ',,);

PD_D_ZN.cntx_psh();
PD_D_ZN.index('NAZ');
PD_D_ZN.prefix();
{? PD_D_ZN.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$PD_D_ZN.ref();
      _selected.prefix($PD_D_ZN.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NAZ:=PD_D_ZN.NAZ;
      _tab.add();
      PD_D_ZN.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'NAZ',20,'Wybór nagłówków zestawów formuł planu dostaw do eksportu'@,1);
{? _result>0
|| _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
PD_D_ZN.cntx_pop();

_result

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:14 1198884adeaaa9de5b64a2dc613fc8a252d57f1fa96378ee22dd9c916541cf3bc6d707c58b5881afe43eb4c6415babe81b1e58956690c256f115e2292a5e338e342325e05f57d580143825031e5dd427f30529f27b495989e17d2fa1a21d4960d837a1b3e34d27656c7cb3b98d9b0caf83ae8a6434bdce7561620ec86ffcc9b2

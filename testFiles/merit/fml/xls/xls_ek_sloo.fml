:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_ek_sloo.fml
:: Utworzony: 15.09.2022
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi słownika opisów problemów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='EK_SLOO';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZPR';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Słownik opisów problemów'@+'=1,1';
_def.NAME:='Słownik opisów problemów'@;
_def.DESC:='Słownik opisów problemów'@;

_def.PREFIX:="EK_SLOO.index('DESC'); EK_SLOO.prefix()";
_def.TABLE:="EK_SLOO";
_def.FIELDS:="exec('fields','xls_ek_sloo',_a)";

_def.BEFORE:="EK_SLOO.cntx_psh()";
_def.AFTER:="EK_SLOO.cntx_pop()";

_def.EXPORT:="exec('export','xls_ek_sloo',_a,_b)";
_def.VALIDATE:="exec('validate','xls_ek_sloo',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_ek_sloo',_a,_b,_c)";
_def.SELECT:="exec('select','xls_ek_sloo',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić - wynik działania exec('args_fields','#excel_imex')
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('TYP','Typ'@,1);
_def.define('DESC','Opis'@,1);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
EK_SLOO.prefix();
{? _validate.ACTION='add'
|| EK_SLOO.blank();
   EK_SLOO.TYP:=_obj.TYP;
   EK_SLOO.DESC:=_obj.DESC
?};
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.TYP.VALUE:=EK_SLOO.TYP;
_table.DESC.VALUE:=EK_SLOO.DESC;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_can_continue:=1;

:: Błędy
{? _obj.TYP=''
|| _can_continue:=0;
   _result.msg_empty('TYP')
|? _obj.TYP<>'R' & _obj.TYP<>'Z'
|| _can_continue:=0;
   _result.msg_inset('TYP','R','Z')
?};
{? _obj.DESC=''
|| _can_continue:=0;
   _result.msg_empty('DESC')
?};

:: Ostrzeżenia
{? +_obj.DESC>255
|| _result.msg_length('DESC',255)
?};

:: OK
{? _can_continue>0
|| EK_SLOO.index('DESC');
   EK_SLOO.prefix(_obj.TYP,_obj.DESC,);
   {? ~EK_SLOO.first()
   || _result.ACTION:='add';
      exec('record','xls_ek_sloo',_obj,_mode,_result)
   ?}
|| _result.RESULT:=0
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=EK_SLOO.add(1)
   |? _validate.ACTION='put'
   || _result:=EK_SLOO.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie pozycji słownika opisów problemów %1/%2 nie powiodło się.'@[_obj.TYP,_obj.DESC]
   || _validate.MSG:='Poprawa pozycji słownika opisów problemów %1/%2 nie powiodła się.'@[_obj.TYP,_obj.DESC]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(2
   ,'KOD','STRING[1]','Typ'
   ,'DESC','STRING[255]','Opis'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'TYP',,,'DESC',,);

EK_SLOO.cntx_psh();
EK_SLOO.index('DESC');
EK_SLOO.prefix();
{? EK_SLOO.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$EK_SLOO.ref();
      _selected.prefix($EK_SLOO.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.TYP:=EK_SLOO.TYP;
      _tab.DESC:=EK_SLOO.DESC;
      _tab.add();
      EK_SLOO.next()
   !}
?};
_result:=exec('select_action','#table',_tab
   ,'TYP,DESC',20,'Wybór pozycji słownika opisów problemów do eksportu'@,1
);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
EK_SLOO.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:11 4889d51043e6823b80f8f254a29a8fc142189295bc8e1e61995519b76b682d45e6a16e60c88e134a16c360fd3a1dc4a63909ca70a826548e56993aca4840319d5b8c6fd35e0831a88e7138e9ca1098229ae51430a9a1596a902c14e245a224062b4d7baadbe8a6f718eb4a7fc0d4075d930f6895fb31593ba2ab0d9b51819be2

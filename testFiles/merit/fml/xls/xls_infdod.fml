:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_infdod.fml
:: Utworzony: 07.08.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu informacji dodatkowych dla dokumentów
::======================================================================================================================


\init_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='INFD_MAG';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='ZWS_PAR_LTDM';

_def.FILE:='magazyn.xlsx';
_def.SHEET:='Inf.dod. dok.magazynowych=1,1';
_def.NAME:='Informacje dodatkowe dla dokumentów magazynowych'@;
_def.DESC:='Informacje dodatkowe dla dokumentów magazynowych';

_def.PREFIX:="exec('prefixT','xls_infdod','M')";
_def.TABLE:="exec('tableT','xls_infdod','M')";
_def.FIELDS:="exec('fieldsT','xls_infdod',_a,'M')";

_def.BEFORE:="FAKSO.cntx_psh(); FAKSO.use('faktodef')";
_def.AFTER:="FAKSO.cntx_pop(); FAKSO.f_clear()";
_def.SELECT:="exec('selectT','xls_infdod',_a,'M')";

_def.EXPORT:="exec('exportT','xls_infdod',_a,_b,'M')";
_def.IMPORT:="exec('importT','xls_infdod',_a,_b,_c,'M')";
_def.VALIDATE:="exec('validateT','xls_infdod',_a,_b,_c,'M')";
~~


\init_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='INFD_SPR';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.FUNPAR:='ZWS_PAR_LTDS';

_def.FILE:='sprzedaz.xlsx';
_def.SHEET:='Inf.dod. dok.sprzedaży=1,1';
_def.NAME:='Informacje dodatkowe dla dokumentów sprzedaży'@;
_def.DESC:='Informacje dodatkowe dla dokumentów sprzedaży';

_def.PREFIX:="exec('prefixT','xls_infdod','S')";
_def.TABLE:="exec('tableT','xls_infdod','S')";
_def.FIELDS:="exec('fieldsT','xls_infdod',_a,'S')";

_def.BEFORE:="FAKSO.cntx_psh(); FAKSO.use('faktodef')";
_def.AFTER:="FAKSO.cntx_pop(); FAKSO.f_clear()";
_def.SELECT:="exec('selectT','xls_infdod',_a,'S')";

_def.EXPORT:="exec('exportT','xls_infdod',_a,_b,'S')";
_def.IMPORT:="exec('importT','xls_infdod',_a,_b,_c,'S')";
_def.VALIDATE:="exec('validateT','xls_infdod',_a,_b,_c,'S')";
~~


\init_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='INFD_ZAK';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PAR_LTDZ';

_def.FILE:='zakupy.xlsx';
_def.SHEET:='Inf.dod. dok.zakupu=1,1';
_def.NAME:='Informacje dodatkowe dla dokumentów zakupu'@;
_def.DESC:='Informacje dodatkowe dla dokumentów zakupu';

_def.PREFIX:="exec('prefixT','xls_infdod','Z')";
_def.TABLE:="exec('tableT','xls_infdod','Z')";
_def.FIELDS:="exec('fieldsT','xls_infdod',_a,'Z')";

_def.BEFORE:="FAKSO.cntx_psh(); FAKSO.use('faktodef')";
_def.AFTER:="FAKSO.cntx_pop(); FAKSO.f_clear()";
_def.SELECT:="exec('selectT','xls_infdod',_a,'Z')";

_def.EXPORT:="exec('exportT','xls_infdod',_a,_b,'Z')";
_def.IMPORT:="exec('importT','xls_infdod',_a,_b,_c,'Z')";
_def.VALIDATE:="exec('validateT','xls_infdod',_a,_b,_c,'Z')";
~~


\init_zsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='INFD_ZSP';
_def.DOMAIN:=exec('name','#b_domain','LSP');
_def.FUNPAR:='ZWS_PAR_LTZK';

_def.FILE:='sprzedaz.xlsx';
_def.SHEET:='Inf.dod. zam.sprzedaży=1,1';
_def.NAME:='Informacje dodatkowe dla zamówień sprzedaży'@;
_def.DESC:='Informacje dodatkowe dla zamówień sprzedaży';

_def.PREFIX:="exec('prefixT','xls_infdod','K')";
_def.TABLE:="exec('tableT','xls_infdod','K')";
_def.FIELDS:="exec('fieldsT','xls_infdod',_a,'K')";

_def.BEFORE:="FAKSO.cntx_psh(); FAKSO.use('faktodef')";
_def.AFTER:="FAKSO.cntx_pop(); FAKSO.f_clear()";
_def.SELECT:="exec('selectT','xls_infdod',_a,'K')";

_def.EXPORT:="exec('exportT','xls_infdod',_a,_b,'K')";
_def.IMPORT:="exec('importT','xls_infdod',_a,_b,_c,'K')";
_def.VALIDATE:="exec('validateT','xls_infdod',_a,_b,_c,'K')";
~~


\init_zwe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='INFD_ZWE';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='ZWS_PAR_LTZW';

_def.FILE:='zakupy.xlsx';
_def.SHEET:='Inf.dod. zam.wewnętrznych=1,1';
_def.NAME:='Informacje dodatkowe dla zamówień wewnętrznych'@;
_def.DESC:='Informacje dodatkowe dla zamówień wewnętrznych';

_def.PREFIX:="exec('prefixT','xls_infdod','W')";
_def.TABLE:="exec('tableT','xls_infdod','W')";
_def.FIELDS:="exec('fieldsT','xls_infdod',_a,'W')";

_def.BEFORE:="FAKSO.cntx_psh(); FAKSO.use('faktodef')";
_def.AFTER:="FAKSO.cntx_pop(); FAKSO.f_clear()";
_def.SELECT:="exec('selectT','xls_infdod',_a,'W')";

_def.EXPORT:="exec('exportT','xls_infdod',_a,_b,'W')";
_def.IMPORT:="exec('importT','xls_infdod',_a,_b,_c,'W')";
_def.VALIDATE:="exec('validateT','xls_infdod',_a,_b,_c,'W')";
~~


\init_zds
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='INFD_ZDS';
_def.DOMAIN:=exec('name','#b_domain','LZK');
_def.FUNPAR:='ZWS_PAR_LTZD';

_def.FILE:='zakupy.xlsx';
_def.SHEET:='Inf.dod. zam.dostaw=1,1';
_def.NAME:='Informacje dodatkowe dla zamówień dostaw'@;
_def.DESC:='Informacje dodatkowe dla zamówień dostaw';

_def.PREFIX:="exec('prefixT','xls_infdod','D')";
_def.TABLE:="exec('tableT','xls_infdod','D')";
_def.FIELDS:="exec('fieldsT','xls_infdod',_a,'D')";

_def.BEFORE:="FAKSO.cntx_psh(); FAKSO.use('faktodef')";
_def.AFTER:="FAKSO.cntx_pop(); FAKSO.f_clear()";
_def.SELECT:="exec('selectT','xls_infdod',_a,'D')";

_def.EXPORT:="exec('exportT','xls_infdod',_a,_b,'D')";
_def.IMPORT:="exec('importT','xls_infdod',_a,_b,_c,'D')";
_def.VALIDATE:="exec('validateT','xls_infdod',_a,_b,_c,'D')";
~~


\prefixT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::   WE: _a - 'M'-magazyn, 'S'-sprzedaż, 'Z'-zakup, 'K'-zam.sprzedaży, 'W'-zam.wewnętrzne, 'D'-zam.dostaw
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
FAKSO.prefix();
FAKSO.f_clear();
{? _a='M' || FAKSO.f_set('TYPYDOK(T),LP','join TYPYDOK')
|? _a='S' || FAKSO.f_set('TYPYSP(T),LP','join TYPYSP','TYPYSP.ZAK=\'N\'')
|? _a='Z' || FAKSO.f_set('TYPYSP(T),LP','join TYPYSP','TYPYSP.ZAK=\'T\'')
|? _a='K' || FAKSO.f_set('TYPYZAM(T),LP','join TYPYZAM','TYPYZAM.R=\'Z\'')
|? _a='W' || FAKSO.f_set('TYPYZAM(T),LP','join TYPYZAM','TYPYZAM.R=\'W\'')
|? _a='D' || FAKSO.f_set('TYPYZAM(T),LP','join TYPYZAM','TYPYZAM.R=\'D\'')
?};
~~


\tableT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
FAKSO


\fieldsT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::       _b - 'M'-magazyn, 'S'-sprzedaż, 'Z'-zakup, 'K'-zam.sprzedaży, 'W'-zam.wewnętrzne, 'D'-zam.dostaw
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

{? (';MSZ'*_b)>1
|| _def.define('TYP','Typ dokumentu',1,,'Typ dokumentu - 8 znaków')
|| _def.define('TYP','Typ zamówienia',1,,'Typ zamówienia - 8 znaków')
?};
_def.define('LP','Lp',1,,'Kolejność informacji',1,0);
_def.define('T','Tytuł',1,,'Tytuł informacji - 40 znaków');
_def.define('O','Opis',1,,'Opis informacji');
_def.define('MW','Miejsce wywołania',1,,
'Miejsce wywołania:
(N) - nagłówek
(P) - pozycje
');
_def.define('PRN','Czy drukować?',1,,
'Umieszczać informację na wydruku?
(T) - tak (opis)
(A) - tak (tytuł+opis)
(N) - nie umieszczać
');
_def.define('LOG','Warunek',1,,'Formuła określająca czy dodawać informację na dokumencie');
_def.define('GD','Miejsce wydruku',1,,
'Miejsce wydruku na dokumencie - dotyczy danych nagłówkowych:
(D1) Dół  - 1 kolumna
(D2) Dół  - 2 kolumny
(D3) Dół  - 3 kolumny
(D4) Dół  - 4 kolumny
(G1) Góra - 1 kolumna
(G2) Góra - 2 kolumny
(G3) Góra - 3 kolumny
(G4) Góra - 4 kolumny
');
_def.define('OR','Rodzaj wartości',1,,
'Typ informacji:
(S) - stała
(F) - formuła
(W) - słownik
');
_def.define('SLU','Słownik',1,,'Nazwa słownika - dla rodzaju S');
_def.define('DOMSLO','Domyślna wartość',1,,'Kod domyślnej wartości ze słownika (20 znaków) - dla rodzaju S');
_def.define('K_OR_TR','Tryb podpowiedzi',1,,
'Co podpowiadać ze słownika - dla rodzaju S:
(0) - KOD - wartość domyślna
(1) - TREŚĆ
',1,0);
_def.define('MODIFY','Modyfikacja?',1,,'Czy można modyfikować informację po zaakceptowaniu dokumentu? - (T/N)');
~~


\recordT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - 'M'-magazyn, 'S'-sprzedaż, 'Z'-zakup, 'K'-zam.sprzedaży, 'W'-zam.wewnętrzne, 'D'-zam.dostaw
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

{? _d='M'
|| FAKSO.index('TYM');
   FAKSO.prefix();
   {? _validate.ACTION='add'
   ||
      FAKSO.blank();
      FAKSO.TYPYDOK:=_validate.OBJ.TYP;
      FAKSO.LP:=_obj.LP
   ?}
|? (';SZ'*_d)>1
|| FAKSO.index('TYP');
   FAKSO.prefix();
   {? _validate.ACTION='add'
   ||
      FAKSO.blank();
      FAKSO.TYPYSP:=_validate.OBJ.TYP;
      FAKSO.LP:=_obj.LP
   ?}
|? (';KWD'*_d)>1
|| FAKSO.index('TYZ');
   FAKSO.prefix();
   {? _validate.ACTION='add'
   ||
      FAKSO.blank();
      FAKSO.TYPYZAM:=_validate.OBJ.TYP;
      FAKSO.LP:=_obj.LP
   ?}
?};

FAKSO.T:=_obj.T;
FAKSO.O:=_obj.O;
FAKSO.MW:=_obj.MW;
FAKSO.PRN:=_obj.PRN;
FAKSO.LOG:=_obj.LOG;
FAKSO.GD:=_obj.GD;
FAKSO.OR:=_obj.OR;
FAKSO.SLU:=_validate.OBJ.SLU;
FAKSO.DOMSLO:=_obj.DOMSLO;
FAKSO.K_OR_TR:=_obj.K_OR_TR;
FAKSO.MODIFY:=_obj.MODIFY;
~~


\exportT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::       _c - 'M'-magazyn, 'S'-sprzedaż, 'Z'-zakup, 'K'-zam.sprzedaży, 'W'-zam.wewnętrzne, 'D'-zam.dostaw
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
{? _c='M' & FAKSO.TYPYDOK<>null()
|| _table.TYP.VALUE:=FAKSO.TYPYDOK().T; _excel.write_async('TYPYDOK',FAKSO.TYPYDOK)
|? _c='S' & FAKSO.TYPYSP<>null() & FAKSO.TYPYSP().ZAK='N'
|| _table.TYP.VALUE:=FAKSO.TYPYSP().T; _excel.write_async('TYPYSP',FAKSO.TYPYSP)
|? _c='Z' & FAKSO.TYPYSP<>null() & FAKSO.TYPYSP().ZAK='T'
|| _table.TYP.VALUE:=FAKSO.TYPYSP().T; _excel.write_async('TYPYZAK',FAKSO.TYPYSP)
|? _c='K' & FAKSO.TYPYZAM<>null() & FAKSO.TYPYZAM().R='Z'
|| _table.TYP.VALUE:=FAKSO.TYPYZAM().T; _excel.write_async('TYPYZAMZ',FAKSO.TYPYZAM)
|? _c='W' & FAKSO.TYPYZAM<>null() & FAKSO.TYPYZAM().R='W'
|| _table.TYP.VALUE:=FAKSO.TYPYZAM().T; _excel.write_async('TYPYZAMW',FAKSO.TYPYZAM)
|? _c='D' & FAKSO.TYPYZAM<>null() & FAKSO.TYPYZAM().R='D'
|| _table.TYP.VALUE:=FAKSO.TYPYZAM().T; _excel.write_async('TYPYZAMD',FAKSO.TYPYZAM)
|| _result:=0
?};

{? _result
|| _table.LP.VALUE:=FAKSO.LP;
   _table.T.VALUE:=FAKSO.T;
   _table.O.VALUE:=FAKSO.O;
   _table.MW.VALUE:=FAKSO.MW;
   _table.PRN.VALUE:=FAKSO.PRN;
   _table.LOG.VALUE:=FAKSO.LOG;
   _table.GD.VALUE:=FAKSO.GD;
   _table.OR.VALUE:=FAKSO.OR;
   _table.SLU.VALUE:=FAKSO.SLU().NAZ; _excel.write_async('SLU',FAKSO.SLU);
   _table.DOMSLO.VALUE:=FAKSO.DOMSLO;
   _table.K_OR_TR.VALUE:=FAKSO.K_OR_TR;
   _table.MODIFY.VALUE:=FAKSO.MODIFY
?};
_result


\validateT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::       _d - 'M'-magazyn, 'S'-sprzedaż, 'Z'-zakup, 'K'-zam.sprzedaży, 'W'-zam.wewnętrzne, 'D'-zam.dostaw
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? _obj.LP<=0 || _result.msg_range('LP',,0); _result.RESULT:=0 ?};
{? ~(+_obj.T) || _result.msg_empty('T'); _result.RESULT:=0 ?};
{? ~((';NP'*_obj.MW)>1) || _result.msg_inset('MW','N','P'); _result.RESULT:=0 ?};
{? ~((';TNA'*_obj.PRN)>1) || _result.msg_inset('PRN','T','N','A'); _result.RESULT:=0 ?};
{? _obj.MW='N' & _obj.PRN='T' & ~(('.;D1;D2;D3;D4;G1;G2;G3;G4;'*(';'+_obj.GD+';'))>1)
|| _result.msg_inset('GD','D1','D2','D3','D4','G1','G2','G3','G4'); _result.RESULT:=0
?};
{? ~((';SFW'*_obj.OR)>1) || _result.msg_inset('OR','S','F','W'); _result.RESULT:=0 ?};
{? _obj.K_OR_TR<>0 & _obj.K_OR_TR<>1 || _result.msg_inset('K_OR_TR','0','1'); _result.RESULT:=0 ?};
{? ~((';TN'*_obj.MODIFY)>1) || _result.msg_inset('MODIFY','T','N'); _result.RESULT:=0 ?};

_result.OBJ:=obj_new('TYP','SLU');

{? _d='M'
|| _result.OBJ.TYP:={? _obj.TYP='' || null() || exec('FindInSet','#table','TYPYDOK','TYP',_obj.TYP,,,1) ?};
   {? _result.OBJ.TYP=null()
   || _result.MSG:={? _obj.TYP=''
                   || 'Nie podano typu dokumentu magazynowego'
                   || 'Nie znaleziono typu dokumentu magazynowego %1.'@[_obj.TYP]
                   ?};
      _result.RESULT:=0
   ?}
|? (';SZ'*_d)>1
|| _result.OBJ.TYP:={? _obj.TYP='' || null() || exec('FindInSet','#table','TYPYSP','TYPYKOD',_obj.TYP,,,1) ?};
   {? _result.OBJ.TYP=null()
   || _result.MSG:={? _obj.TYP=''
                   || 'Nie podano typu dokumentu %1'@[{? _d='S' || 'sprzedaży'@ || 'zakupu'@ ?}]
                   || 'Nie znaleziono typu dokumentu %2 %1.'@[_obj.TYP,{? _d='S' || 'sprzedaży'@ || 'zakupu'@ ?}]
                   ?};
      _result.RESULT:=0
   ?}
|? (';KWD'*_d)>1
|| _rodzaj:={? _d='K' || 'Z' || _d ?};
   _result.OBJ.TYP:={? _obj.TYP='' || null() || exec('FindInSet','#table','TYPYZAM','TYP',_obj.TYP,_rodzaj,,1) ?};
   {? _result.OBJ.TYP=null()
   || _result.MSG:={? _obj.TYP=''
                   || 'Nie podano typu zamówienia %1'@[{? _d='K' || 'sprzedaży'@ |? _d='W' || 'wewnętrznego'@ || 'dostaw'@ ?}]
                   || 'Nie znaleziono typu zamówienia %2 %1.'@[_obj.TYP,{? _d='K' || 'sprzedaży'@ |? _d='W' || 'wewnętrznego'@ || 'dostaw'@ ?}]
                   ?};
      _result.RESULT:=0
   ?}
?};

_res:=0;
_result.OBJ.SLU:={? _obj.SLU='' || null() || _res:=1; exec('FindInSet','#table','SLU','NAZ',_obj.SLU,,,1) ?};
{? _res=1 & _result.OBJ.SLU=null()
|| _result.MSG:='Nie znaleziono słownika %1.'@[_obj.SLU];
   _result.RESULT:=0
?};

{? _result.RESULT=1
||
   {? _d='M'
   || FAKSO.index('TYM');
      FAKSO.prefix(_result.OBJ.TYP,_obj.LP)
   |? (';SZ'*_d)>1
   || FAKSO.index('TYP');
      FAKSO.prefix(_result.OBJ.TYP,_obj.LP)
   |? (';KWD'*_d)>1
   || FAKSO.index('TYZ');
      FAKSO.prefix(_result.OBJ.TYP,_obj.LP)
   ?};
   {? FAKSO.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('recordT','xls_infdod',_obj,_mode,_result,_d)
      ?}
   || _result.ACTION:='add';
      exec('recordT','xls_infdod',_obj,_mode,_result,_d)
   ?}
?};
~~


\importT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::       _d - 'M'-magazyn, 'S'-sprzedaż, 'Z'-zakup, 'K'-zam.sprzedaży, 'W'-zam.wewnętrzne, 'D'-zam.dostaw
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=FAKSO.add(1)
   |? _validate.ACTION='put'
   || _result:=FAKSO.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:={? _validate.ACTION='add'
                  || 'Dodanie definicji informacji dodatkowej %1 nie powiodło się.'@[FAKSO.T]
                  || 'Modyfikacja definicji informacji dodatkowej %1 nie powiodła się.'@[FAKSO.T]
                  ?}
?};
_result


\selectT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - 'M'-magazyn, 'S'-sprzedaż, 'Z'-zakup, 'K'-zam.sprzedaży, 'W'-zam.wewnętrzne, 'D'-zam.dostaw
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'TYP','STRING[10]', {? (';MSZ'*_b)>1 || 'Typ dokumentu' || 'Typ zamówienia' ?}
     ,'REF','STRING[16]','SQL ref'
     ,'SELECTED','STRING[1]','Czy wybrano'
     ,'LP','INTEGER','Lp'
     ,'T','STRING[40]','Tytuł'
     ,'O','STRING[255]','Opis'
     ,'MW','STRING[1]','Miejsce');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'TYP',,,'LP',,);

SLU.cntx_psh();
FAKSO.cntx_psh();
FAKSO.use('faktodef');
FAKSO.index('TYP');
FAKSO.prefix();
{? FAKSO.first()
|| {!
   |?
      {? (_b='M' & FAKSO.TYPYDOK<>null())
       | (_b='S' & FAKSO.TYPYSP<>null() & FAKSO.TYPYSP().ZAK='N')
       | (_b='Z' & FAKSO.TYPYSP<>null() & FAKSO.TYPYSP().ZAK='T')
       | (_b='K' & FAKSO.TYPYZAM<>null() & FAKSO.TYPYZAM().R='Z')
       | (_b='W' & FAKSO.TYPYZAM<>null() & FAKSO.TYPYZAM().R='W')
       | (_b='D' & FAKSO.TYPYZAM<>null() & FAKSO.TYPYZAM().R='D')
      || _tab.blank();
         _tab.REF:=$FAKSO.ref();
         _selected.prefix($FAKSO.ref());
         {? _selected.first()
         || _tab.SELECTED:='T'
         || _tab.SELECTED:='N'
         ?};
         _tab.TYP:={? _b='M' || FAKSO.TYPYDOK().T
                   |? (';SZ'*_b)>1 || FAKSO.TYPYSP().T
                   || FAKSO.TYPYZAM().T
                   ?};
         _tab.LP:=FAKSO.LP;
         _tab.T:=FAKSO.T;
         _tab.O:=FAKSO.O;
         _tab.MW:=FAKSO.MW;
         _tab.add()
      ?};
      FAKSO.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'TYP,LP,T,O,MW',20,'Wybór definicji informacji dodatkowych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
SLU.cntx_pop();
FAKSO.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:14 dec7681a750d775e92d46d854324c64116e2b0c39a5657fc67a9d1348c851b9ea3b12380f480e1aea4d3c7a609b1273daeeb65262ce1b075f40d57fd31081ce97620876c21660940dd69cd0d18257fa78fff78e9245d248e9f1c01e276e92cf37e180427dccbbdfce7c13ea02f2f28c9d93a175d0830f26274fc3f8c3331ccc2

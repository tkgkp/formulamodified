:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_stats.fml
:: Utworzony: 25.07.2018
:: Autor: [rr]
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu definicji statusów dostaw
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='STATS';
_def.DOMAIN:=exec('name','#b_domain','LMG');
_def.FUNPAR:='LMG_KDOST_BAD';

_def.FILE:='logistyka_badania.xlsx';
_def.SHEET:='Statusy dostaw=1,1';
_def.NAME:='Definicja statusów dostawy'@;
_def.DESC:='Definicja statusów dostawy';

_def.PREFIX:="exec('prefix','xls_stats')";
_def.TABLE:="exec('table','xls_stats')";
_def.FIELDS:="exec('fields','xls_stats',_a)";

_def.BEFORE:="STATS.cntx_psh()";
_def.AFTER:="STATS.cntx_pop()";
_def.SELECT:="exec('select','xls_stats',_a)";

_def.EXPORT:="exec('export','xls_stats',_a,_b)";
_def.IMPORT:="exec('import','xls_stats',_a,_b,_c)";
_def.VALIDATE:="exec('validate','xls_stats',_a,_b,_c)";
~~


\prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa zakres eksportowanych danych
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
STATS.index('NN');
STATS.prefix();
~~


\table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa uchwyt do tabeli
::   WY: uchwyt do eksportowanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('nowedane','statexam');
STATS


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('N','Nazwa',0,,'Nazwa statusu - 40 znaków.');
_def.define('KIND','Rodzaj statusu',1,,
'Rodzaj statusu:
(Z) - zgodny
(N) - niezgodny
(O) - nieokreślony
');
_def.define('ICON','Ikona',1,,'Ikona statusu');
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

STATS.index('NN');
STATS.prefix();
{? _validate.ACTION='add'
||
   STATS.blank();
   STATS.N:=_obj.N;
   STATS.KIND:=_obj.KIND
|? _validate.ACTION='put'
||
:: domyślne systemowe statusy - nie zmieniamy rodzajów żeby się nie podupiło
   _can_continue:=1;
   {? _obj.N='~Zgodny'
   || _can_continue:=0
   |? _obj.N='~Niezgodny'
   || _can_continue:=0
   |? _obj.N='~Nieokreślony'
   || _can_continue:=0
   ?};
   {? _can_continue>0
   || STATS.KIND:=_obj.KIND
   ?}
?};
STATS.ICON:=_obj.ICON;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.N.VALUE:=STATS.N;
_table.KIND.VALUE:=STATS.KIND;
_table.ICON.VALUE:=STATS.ICON;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

{? (+_obj.N)>40 || _result.msg_length('N',40); _result.RESULT:=0 ?};
{? (+_obj.KIND)<>1 | ~((';ZNO'*_obj.KIND)>1) || _result.msg_inset('KIND','Z','N','O'); _result.RESULT:=0 ?};

{? _result.RESULT=1
||
   STATS.index('NN');
   STATS.prefix(_obj.N,);
   {? STATS.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_stats',_obj,_mode,_result);
         {? ~exec('stats_afe','statexam',2) || _result.RESULT:=0 ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_stats',_obj,_mode,_result);
      {? ~exec('stats_afe','statexam',1) || _result.RESULT:=0 ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=STATS.add(1)
   |? _validate.ACTION='put'
   || _result:=STATS.put(1)
   ?}
?};
{? _result=0
|| _validate.MSG:='Dodanie definicji statusu dostawy %1 nie powiodło się.'@[STATS.N]
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::       _b - typ atrybutów: 'B'-indeksu, 'C'-dostaw
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'N','STRING[40]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'KIND','STRING[1]','Typ'
   ,'ICON','STRING[20]','Ikona');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'N',,);

STATS.cntx_psh();
STATS.index('NN');
STATS.prefix();
{? STATS.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$STATS.ref();
      _selected.prefix($STATS.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.N:=STATS.N;
      _tab.KIND:=STATS.KIND;
      _tab.ICON:=STATS.ICON;
      _tab.add();
      STATS.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'N,KIND,ICON',20,'Wybór statusów dostaw do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
STATS.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/10/14 09:22:01 2fe3d009f7eb98defdfbcc690bc8e27dea341f87b0ed52bb00cdeb03fc80c86851271167c03b48e489cdfbd37a21ffd7a5ce6bf8d0c0c1a7b4a737b453a824a0925e814d272a29f0e7734bd5ec1bbd471fb2a2119cf2e1b5a9597701b7d2533ab94ce6fde8a871aa17b9139fddfa4714eef314ed31925b8a4961526f4b537012

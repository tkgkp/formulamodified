:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_powiaty.fml
:: Utworzony: 15.01.2019
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu powiatów
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu.
::   WE: _a [OBJECT] - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_def:=_a;

_def.ID:='POWIATY';
_def.FUNPAR:='';
_def.DOMAIN:=exec('name','#b_domain','ZWS');
_def.FILE:=exec('def_pak_xlsx','xls__init');
_def.SHEET:='Powiaty=1,1';
_def.NAME:='Powiaty'@;
_def.DESC:='Powiaty';
_def.HIDDEN:='T';
_def.MULTIFIR:='T';

: źródło danych
_def.TABLE:="POWIATY";

: prolog
_def.BEFORE:="
   POWIATY.cntx_psh();
   POWIATY.f_clear();
   POWIATY.index('P');
   POWIATY.prefix();
   INW_LOK.cntx_psh();
   INW_LOK.index('S');
   INW_LOK.prefix();
   WOJEWODZ.cntx_psh();
   WOJEWODZ.index('WOJNAZWA');
   WOJEWODZ.prefix();
   ~~
";

: epilog
_def.AFTER:="
   WOJEWODZ.cntx_pop();
   INW_LOK.cntx_pop();
   POWIATY.cntx_pop();
   ~~
";

: definicja kolumn
_def.FIELDS:="
   _env:=_a;
   _env.define('NAZWA',MS.name(POWIATY,'NAZWA'),1,,MS.comment(POWIATY,'NAZWA'));
   _env.define('KOD',MS.name(POWIATY,'KOD'),1,,MS.comment(POWIATY,'KOD'));
   _env.define('WOJ',MS.name(POWIATY,'WOJ'),1,,MS.comment(POWIATY,'WOJ'));
   _env.define('WOJEWODZ',MS.name(POWIATY,'WOJEWODZ'),1,,MS.comment(POWIATY,'WOJEWODZ'));
   _env.define('TP',MS.name(POWIATY,'TP'),1,,MS.comment(POWIATY,'TP'));
   ~~
";

: zawartość wiersza
_def.EXPORT:="
   _env:=_a;
   _buf:=_b;
   _buf.NAZWA.VALUE:=POWIATY.NAZWA;
   _buf.KOD.VALUE:=POWIATY.KOD;
   _buf.WOJ.VALUE:=POWIATY.WOJ().S;
   _buf.WOJEWODZ.VALUE:=POWIATY.WOJEWODZ().NAZWA;
   _buf.TP.VALUE:=POWIATY.TP;
   _env.write_async('INW_LOK',POWIATY.WOJ);
:  gminy powiatu
   GMINY.cntx_psh();
   GMINY.index('GP');
   GMINY.prefix(POWIATY.ref());
   _loop:=GMINY.first();
   {!
   |? _loop
   |! _env.write_async('GMINY',GMINY.ref());
      _loop:=GMINY.next()
   !};
   GMINY.cntx_pop();
   1
";

: weryfikacja i zapis wiersza odczytanego z pliku wymiany
_def.VALIDATE:="exec('validate','xls_powiaty',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_powiaty',_a,_b,_c)";
~~


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Weryfikuje poprawność wiersza odczytanego z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - obiekt z resultem, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_buf:=_a;
_mod:=_b;
_val:=_c;

: wychwyć błędy krytyczne
_fld:=
   {? _buf.NAZWA=''    || 'NAZWA'
   |? _buf.WOJEWODZ='' || 'WOJEWODZ'
   || ''
   ?};
{? _fld<>''
:  katastrofa
|| _val.msg_empty(_fld);
   _val.RESULT:=0;
   return()
?};

: ostrzeżenia
_max:=MS.fld_len(POWIATY,'NAZWA');
{? +_buf.NAZWA>_max
|| _val.msg_length('NAZWA',_max);
   _buf.NAZWA:=_max+_buf.NAZWA
?};
_max:=MS.fld_len(POWIATY,'KOD');
{? +_buf.KOD>_max
|| _val.msg_length('KOD',_max);
   _buf.KOD:=_max+_buf.KOD
?};
_max:=MS.fld_len(INW_LOK,'S');
{? +_buf.WOJ>_max
|| _val.msg_length('WOJ',_max);
   _buf.WOJ:=_max+_buf.WOJ
?};
_max:=MS.fld_len(WOJEWODZ,'NAZWA');
{? +_buf.WOJEWODZ>_max
|| _val.msg_length('WOJEWODZ',_max);
   _buf.WOJEWODZ:=_max+_buf.WOJEWODZ
?};
_max:=MS.fld_len(POWIATY,'TP');
{? +_buf.TP>_max
|| _val.msg_length('TP',_max);
   _buf.TP:=_max+_buf.TP
?};

: błędne wartości
{? _buf.TP<>'' & ('012'*_buf.TP)=0
|| _val.msg_inset('0','1','2');
   _val.RESULT:=0;
   return()
?};

: weryfikacja spójności referencyjnej
_inw_lok:=null;
{? _buf.WOJ<>''
|| {? ~INW_LOK.find_tab(,'S',,'=',_buf.WOJ)
   || _val.msg_norec('WOJ');
      _val.RESULT:=0;
      return()
   || _inw_lok:=INW_LOK.ref()
   ?}
?};
_wojewodz:=null;
{? ~WOJEWODZ.find_tab(,'NAZWA',,'=',_buf.WOJEWODZ)
|| _val.msg_norec('WOJEWODZ');
   _val.RESULT:=0;
   return()
|| _wojewodz:=WOJEWODZ.ref()
?};

_ref:=null;
{? POWIATY.find_tab(,
      'WOJEWODZ',,'=',_wojewodz,
      'NAZWA',,'=',_buf.NAZWA
   )
|| _ref:=POWIATY.ref();
   _val.ACTION:='put'
|| _val.ACTION:='add'
?};

: optymalizacja importu i standaryzacja kodu
_val.OBJ:=exec('write_obj','xls__util',_ref,
   "'%1 (%2)'[_a.NAZWA,_a.WOJEWODZ]",
   'WOJ',_inw_lok,
   'WOJEWODZ',_wojewodz
);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy lub modyfikuje wiersze na podstawie wartości odczytanych z pliku wymiany.
::   WE: _a [OBJECT] - tablica nazwana z wartościami pól wiersza pliku wymiany
::       _b [INTEGER] - tryb pracy: 0 - zachowaj istniejące, 1 - nadpisywać istniejące
::       _c [OBJECT] - rezultat walidacji i importu, patrz exec('args_valid','#excel_imex')
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('write','xls__util',_a,_b,_c,POWIATY,"
   _a.NAZWA:=_b.NAZWA;
   _a.KOD:=_b.KOD;
   _a.WOJ:=_c.WOJ;
   _a.WOJEWODZ:=_c.WOJEWODZ;
   _a.TP:=_b.TP;
   ~~
");
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:17:12 24d417a838e5466870b36a838171e7519fff2bcc510ca58816f4f2f0cd030af257b6b5e406922dc0454aca71a3aec61bbdc1e9f11d7af2b3e8e5d5b9d6e2f63548c801fd9d39266ad5a465de545b6e8f0673acc9f26b3365ea4e46bb69cf38b70342db7fc885cab79accfd308c1111b471c3907be620f1b21afe6b49850bba97

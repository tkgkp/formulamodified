:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_kwar.fml
:: Utworzony: 19.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu wariantów kalkulacji kart technologicznych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='KWAR';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TTKT';

_def.FILE:='produkcja_kalkulacje.xlsx';
_def.SHEET:='Warianty kalkulacyjne'@+'=1,1';
_def.NAME:='Warianty kalkulacyjne'@;
_def.DESC:='Warianty kalkulacji kart technologicznych'@;

_def.PREFIX:="KWAR.index('NA'); KWAR.prefix()";
_def.TABLE:="KWAR";
_def.FIELDS:="exec('fields','xls_kwar',_a)";

_def.BEFORE:="KWAR.cntx_psh()";
_def.AFTER:="KWAR.cntx_pop()";
_def.SELECT:="exec('select','xls_kwar',_a)";

_def.EXPORT:="exec('export','xls_kwar',_a,_b)";
_def.VALIDATE:="exec('validate','xls_kwar',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_kwar',_a,_b,_c)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_def.define('NAZ','Nazwa'@,1,,MS.comment(KWAR,'NAZ')+' '+'(max. %1 znaków)'@[$MS.fld_len(KWAR,'NAZ')]);
_def.define('DW','Dni ważności'@,1,,MS.comment(KWAR,'DW')+' '+'(liczba naturalna)'@,,0);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
KWAR.prefix();
{? _validate.ACTION='add'
|| KWAR.blank();
   KWAR.NAZ:=_obj.NAZ
?};
KWAR.DW:=_obj.DW;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.NAZ.VALUE:=KWAR.NAZ;
_table.DW.VALUE:=KWAR.DW;

:: Zapisanie formuł dla bieżącego wariantu
::KFORM.cntx_psh();
::KFORM.index('WR');
::KFORM.prefix(KWAR.ref());
::{? KFORM.first()
::|| {!
::   |? _excel.write_async('KFORM',KFORM.ref());
::      KFORM.next()
::   !}
::?};
::KFORM.cntx_pop();

:: Zapisanie stałych dla bieżącego wariantu
::KKS.cntx_psh();
::KKS.index('WR');
::KKS.prefix(KWAR.ref());
::{? KKS.first()
::|| {!
::   |? _excel.write_async('KKS',KKS.ref());
::      KKS.next()
::   !}
::?};
::KKS.cntx_pop();

_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;

_int:=int(_obj.DW);

:: Błędy
{? _int<exec('minint','#math') | _int>exec('maxint','#math')
|| _result.msg_range('DW',exec('minint','#math'),exec('maxint','#math'));
   _result.RESULT:=0
?};

:: Ostrzeżenia
{? +_obj.NAZ>40
|| _result.msg_length('NAZ',40)
?};
{? _int<>_obj.DW
|| _result.msg_int('DW')
?};

:: OK
{? _result.RESULT>0
|| KWAR.index('NA');
   KWAR.prefix(_obj.NAZ,);
   {? KWAR.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_kwar',_obj,_mode,_result);
         {? exec('chk_kwar','tech_kalk',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_kwar',_obj,_mode,_result);
      {? exec('chk_kwar','tech_kalk',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=KWAR.add(1)
   |? _validate.ACTION='put'
   || _result:=KWAR.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie wariantu kalkulacji \'%1\' nie powiodło się.'@[_obj.NAZ]
   || _validate.MSG:='Poprawa wariantu kalkulacji \'%1\' nie powiodła się.'@[_obj.NAZ]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,
   'NAZ','STRING[40]','Nazwa'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'NAZ',,);

KWAR.cntx_psh();
KWAR.index('NA');
KWAR.prefix();
{? KWAR.first()
|| {!
   |? _tab.blank();
      _tab.REF:=$KWAR.ref();
      _selected.prefix($KWAR.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.NAZ:=KWAR.NAZ;
      _tab.add();
      KWAR.next()
   !}
?};
_result:=exec('select_action','#table',_tab,'NAZ',30,'Wybór wariantów kalkulacji do eksportu'@);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |? _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
KWAR.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:48 04953137f87aa5338d642c8d7b11e7a3fab7892c7d2ee42b1e4268208466ddd7ed4f78ea150da90ebf70d05d0e09c690983d3eda3204282b592762bf06acd0d2c13ec25a220a2898bf75b4a704fdd059a98590391a0c28759ed2d5fca3775d7f83aa64cc854ce94bc1979b4a77cbf8f19fc99b1ea49abd04fbb39d8aa5fcb078

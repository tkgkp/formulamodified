:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xls_twrkzpo.fml
:: Utworzony: 26.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi eksportu/importu elementów gniazd
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Inicjalizuje mechanizm importu/eksportu do Excel
::   WE: _a - obj_new - środowisko mechanizmu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;
_def.ID:='TWRKZPO';
_def.DOMAIN:=exec('name','#b_domain','TTE');
_def.FUNPAR:='ZWS_PAR_TZAS';

_def.FILE:='produkcja.xlsx';
_def.SHEET:='Elemety gniazd'@+'=1,1';
_def.NAME:='Elementy gniazd'@;
_def.DESC:='Elemetny gniazd produkcyjnych'@;

_def.PREFIX:="TWRKZPO.index('GRSTAN'); TWRKZPO.prefix()";
_def.TABLE:="TWRKZPO";
_def.FIELDS:="exec('fields','xls_twrkzpo',_a)";

_def.BEFORE:="TWRKZPO.cntx_psh()";
_def.AFTER:="TWRKZPO.cntx_pop()";
_def.SELECT:="";

_def.EXPORT:="exec('export','xls_twrkzpo',_a,_b)";
_def.VALIDATE:="exec('validate','xls_twrkzpo',_a,_b,_c)";
_def.IMPORT:="exec('import','xls_twrkzpo',_a,_b,_c)";
_def.SELECT:="exec('select','xls_twrkzpo',_a)";
~~


\fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Określa pola tabeli
::   WE: _a - obiekt z definicją pól który należy zasilić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_def:=_a;

_define:=$(
   '_a.define(_b,'
   '{? var_pres(\'_d\')=type_of(\'\') || _d || exec(\'fldLabel\',\'#field\',TWRKZPO,_b) ?},_c,,'
   '{? var_pres(\'_e\')=type_of(\'\') || _e || exec(\'fldComment\',\'#field\',TWRKZPO,_b) ?})'
);
_define(_def,'GRUPA',0);
_define(_def,'ELEMENT',0);
_define(_def,'DEFAULT',1);
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Uzupełnia bufor tabeli wartościami obiektu
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;
TWRKZPO.prefix();
{? _validate.ACTION='add'
|| TWRKZPO.blank();
   TWRKZPO.GRUPA:=_validate.OBJ.GRUPA;
   TWRKZPO.ELEMENT:=_validate.OBJ.ELEMENT;
   TWRKPLC.cntx_psh();SLO.cntx_psh();
   TWRKZPO.KOD_:=TWRKZPO.ELEMENT().KOD;
   TWRKZPO.NAZ_:=TWRKZPO.ELEMENT().NA;
   TWRKZPO.WYD_:=TWRKZPO.ELEMENT().WYD().KOD;
   TWRKPLC.cntx_pop();SLO.cntx_pop()
?};
TWRKZPO.DEFAULT:=_obj.DEFAULT;
~~


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Wypełnia obiekt który zapisze w Excel wiersz danymi z rekordu
::       Użycie: export
::   WE: _a - obj_new - środowisko mechanizmu
::       _b - obj_new - obiekt który zasilić wartościami z pól
::   WY: ANY - dowolny wynik (zwracany przez _excel.write_async())
::----------------------------------------------------------------------------------------------------------------------
_excel:=_a;
_table:=_b;

_result:=1;
_table.GRUPA.VALUE:=TWRKZPO.GRUPA().SYMBOL; _excel.write_async('TWRKZBR',TWRKZPO.GRUPA);
_table.ELEMENT.VALUE:=TWRKZPO.ELEMENT().KOD; _excel.write_async('TWRKPLC',TWRKZPO.ELEMENT);
_table.DEFAULT.VALUE:=TWRKZPO.DEFAULT;
_result


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Waliduje czy można poprawić/dodać rekord
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów do przekazania dla import)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_result:=_c;

_result.RESULT:=1;
_result.OBJ:=obj_new('GRUPA','ELEMENT');
_result.OBJ.GRUPA:=exec('FindInSet','#table','TWRKZBR','SYM',_obj.GRUPA,,"TWRKZBR.ref()",1);
_result.OBJ.ELEMENT:=exec('FindInSet','#table','TWRKPLC','KOD',_obj.ELEMENT,,"TWRKPLC.ref()",1);

:: Błędy
{? _result.OBJ.GRUPA=null()
|| _result.msg_norec('GRUPA');
   _result.RESULT:=0
?};
{? _result.OBJ.ELEMENT=null()
|| _result.msg_norec('ELEMENT');
   _result.RESULT:=0
?};

:: OK
{? _result.RESULT>0
|| TWRKZPO.index('GRUPA');
   TWRKZPO.prefix(_result.OBJ.GRUPA,_result.OBJ.ELEMENT);
   {? TWRKZPO.first()
   || {? _mode=0
      || _result.RESULT:=0
      || _result.ACTION:='put';
         exec('record','xls_twrkzpo',_obj,_mode,_result);
         {? exec('chk_twrkzpo','zasoby',1)<>''
         || _result.RESULT:=0
         ?}
      ?}
   || _result.ACTION:='add';
      exec('record','xls_twrkzpo',_obj,_mode,_result);
      {? exec('chk_twrkzpo','zasoby',0)<>''
      || _result.RESULT:=0
      ?}
   ?}
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Tworzy lub modyfikuje rekord na podstawie wartości odczytanych z Excel
::       Użycie: import
::   WE: _a - obj_new() - tablica nazwana z polami odczytanymi z excel
::       _b - INTEGER - tryb pracy: 0 - nie zastępować istniejących wartości, 1 - zastępować istniejące
::       _c - obiekt Result, zawiera .RESULT, .ACTION, .OBJ (własna tablica parametrów)
::   WY: 0 - porażka
::       1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_mode:=_b;
_validate:=_c;

_result:=0;
{? _validate.RESULT=1
|| {? _validate.ACTION='add'
   || _result:=TWRKZPO.add(1)
   |? _validate.ACTION='put'
   || _result:=TWRKZPO.put(1)
   ?}
?};
{? _result=0
|| {? _validate.ACTION='add'
   || _validate.MSG:='Dodanie stanowiska %1 do gniazda %2 nie powiodło się.'@[_obj.ELEMENT,_obj.GRUPA]
   || _validate.MSG:='Poprawa stanowiska %1 gniazda %2 nie powiodła się.'@[_obj.ELEMENT,_obj.GRUPA]
   ?}
?};
_result


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Umożliwia wybór rekordów do eksportu
::   WE: _a - tab_tmp - tabela tymczasowa z polem REF którą należy zasilić
::   WY: 0 - użytkownik zrezygnował z wyboru
::       1 - użytkownik potwierdził wybór
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_selected:=_a;

_result:=0;

_tab:=tab_tmp(1,'GRUPA','STRING[60]','Gniazdo produkcyjne'
   ,'REF','STRING[16]','SQL ref'
   ,'SELECTED','STRING[1]','Czy wybrano'
   ,'ELEMENT','STRING[60]','Stanowisko');

_ndx:=_tab.ndx_tmp(,,'SELECTED',,,'GRUPA',,);

TWRKZPO.cntx_psh();
TWRKZPO.index('GRUPA');
TWRKZPO.prefix();
{? TWRKZPO.first()
|| {!
   |?
      _tab.blank();
      _tab.REF:=$TWRKZPO.ref();
      _selected.prefix($TWRKZPO.ref());
      {? _selected.first()
      || _tab.SELECTED:='T'
      || _tab.SELECTED:='N'
      ?};
      _tab.GRUPA:=TWRKZPO.GRUPA().NAZWA;
      _tab.ELEMENT:=TWRKZPO.ELEMENT().NA;
      _tab.add();
      TWRKZPO.next()
   !}
?};
_result:=exec('select_action','#table',_tab
 ,'GRUPA,ELEMENT',20,'Wybór elementów gniazd produkcyjnych do eksportu'@,1);
{? _result>0
||
   _selected.erase();

   _tab.index(_ndx);
   _tab.prefix('T');
   {? _tab.first()
   || {!
      |?
         _selected.prefix(_tab.REF);
         {? _selected.first()=0
         || _selected.blank();
            _selected.REF:=_tab.REF;
            _selected.add()
         ?};
         _tab.next()
      !}
   ?}
?};
TWRKZPO.cntx_pop();

_result

:Sign Version 2.0 jowisz:1048 2021/04/09 15:28:03 4457738938309cb3e137d146d251f81f3709b5d9928fe97788cb88c45d12f5b8c792b70775ae4f688e59769ae936bbef9b219312af3097dfe1fc7913f9d79fd9525db5e5e17dac2c6ac977c3353d57e6dc31668547f3624a851a5ea334b6ddf178a6fad9c5bb599972ce23921a1fa74bbedaa3fe0905e88e6771fc7f15dbb52e
